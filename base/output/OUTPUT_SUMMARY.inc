*Create output file

SET CATEGORY /COSTS,INCOME,PROFIT, Market_Value/;
SET SUBCATEGORY /ELECTRICITY_SALE,HEAT_SALE,H2_SALE,BIOMETHANE_SALE,GENERATION_CAPITAL_COSTS,GENERATION_FIXED_COSTS,
                  GENERATION_OPERATIONAL_COSTS,GENERATION_FUEL_COSTS,GENERATION_TAXES,GENERATION_GRID_TARIFFS,TRANSMISSION_CAPITAL_COSTS,TRANSMISSION_OPERATIONAL_COSTS,
                  TRANSMISSION_TRADE_INCOME,TRANSMISSION_TRADE_COSTS,HEAT_TRANSMISSION_CAPITAL_COSTS,HEAT_TRANSMISSION_OPERATIONAL_COSTS,
                  H2_TRANSMISSION_CAPITAL_COSTS,H2_TRANSMISSION_OPERATIONAL_COSTS,GENERATION_CO2_TAX,GENERATION_CO2_TRANSPORT,GENERATION_UC_COSTS,
                  GENERATION_OTHER_EMI_TAX,GENERATION_OPPORTUNITY_COSTS,HYDRO_PROFILE,TAXES,GRID_TARIFFS,TOTAL_PROFIT,ENERGY_SPECIFIC_PROFIT,
                  OPTIFLOW_INVESTMENT,OPTIFLOW_FIXED,OPTIFLOW_FUEL/;
SET COMMODITY /ELECTRICITY,HEAT,HYDROGEN,BIOMETHANE/;
SET TECH_TYPE /CONDENSING,CHP-BACK-PRESSURE,CHP-EXTRACTION,BOILERS,ELECT-TO-HEAT,HEATPUMP,INTERSEASONAL-HEAT-STORAGE,INTERSEASONAL-ELECT-STORAGE,INTRASEASONAL-HEAT-STORAGE,INTRASEASONAL-ELECT-STORAGE,HYDRO-RESERVOIRS,HYDRO-RUN-OF-RIVER,WIND-ON,WIND-OFF,SOLAR-PV,SOLAR-HEATING,HYDRO-WAVE,HEAT-PUMP,EL-BOILER,FUELCELL,ELECTROLYZER,H2-STORAGE,BIOMETH-DAC,HUB-OFF,STEAMREFORMING,OPTIFLOW/;
SET GTECH_TYPE(GGG,TECH_TYPE);
SET PRICE_CATEGORY /AVERAGE,AVERAGE_WEIGHTED_BY_CONSUMPTION,AVERAGE_WEIGHTED_BY_PRODUCTION/;
SET VARIABLE_CATEGORY /EXOGENOUS,EXO_TRANS,ENDOGENOUS,DECOMMISSIONING,ENDOGENOUS_ELECT2HEAT,ENDO_INTRASTO,ENDO_INTERSTO,ENDO_EV,ENDO_HEATPUMP,ENDO_ELBOILER,ENDO_OTHERTRANS,DIST_LOSSES,TRANS_LOSSES,ENDO_CCS,ENDO_H2,ENDO_BIOMETHANE,ENDO_FUELCELL,ENDO_OPTIFLOW/;
SET EL_BAL_TYPE /CURTAILMENT,NETEXPORT,PRICE,DEMAND_EXO,DEMAND_EXOTRANS,DEMAND_LOSS,DEMAND_INTERSTO,DEMAND_INTRASTO,DEMAND_P2H,DEMAND_EV,CONDENSING,CHP-BACK-PRESSURE,CHP-EXTRACTION,ELECT-TO-HEAT,INTER-STO,INTRA-STO,HYDRO-RESERVOIRS,HYDRO-RUN-OF-RIVER,WIND-ON,WIND-OFF,SOLAR-PV,HYDRO-WAVE,FUELCELL,DEMAND_OTHERTRANS,DEMAND_DISTLOSSES,DEMAND_CCS,DEMAND_P2G/;
SET H_BAL_TYPE /CURTAILMENT,NETEXPORT,PRICE,DEMAND_EXO,DEMAND_LOSS,DEMAND_INTERSTO,DEMAND_INTRASTO,BOILERS,CHP-BACK-PRESSURE,CHP-EXTRACTION,INTER-STO,INTRA-STO,SOLAR-HEATING,P2H,FUELCELL,DEMAND_P2G,DEMAND_DISTLOSSES/;
SET UNITS /GW,TWh,MWh,Money_per_MWh,Mmoney,kton,GWh/;

$ifi not %TRANSPORT%==yes $goto NO_TRANSPORT_SET;
SET IDEUSER_TRANS(DEUSER)           "Subset of electricity users containing trains, busses, and simple EVs";
IDEUSER_TRANS('TRANS_TRAINS') = YES;
IDEUSER_TRANS('TRANS_BUS') = YES;
IDEUSER_TRANS('TRANS_EV') = YES;
$label NO_TRANSPORT_SET;

   GTECH_TYPE(G,'CONDENSING')$IGCND(G)                                                    = YES;
   GTECH_TYPE(G,'CHP-BACK-PRESSURE')$IGBPR(G)                                             = YES;
   GTECH_TYPE(G,'CHP-EXTRACTION')$IGEXT(G)                                                = YES;
   GTECH_TYPE(G,'BOILERS')$IGHOB(G)                                                       = YES;
   GTECH_TYPE(G,'ELECT-TO-HEAT')$IGETOH(G)                                                = YES;
   GTECH_TYPE(G,'INTRASEASONAL-HEAT-STORAGE')$(GDATA(G,'GDTYPE') EQ GHSTO)                = YES;
   GTECH_TYPE(G,'INTERSEASONAL-HEAT-STORAGE')$(GDATA(G,'GDTYPE') EQ GHSTOS)               = YES;
   GTECH_TYPE(G,'INTRASEASONAL-ELECT-STORAGE')$(GDATA(G,'GDTYPE') EQ GESTO)               = YES;
   GTECH_TYPE(G,'INTERSEASONAL-ELECT-STORAGE')$(GDATA(G,'GDTYPE') EQ GESTOS)              = YES;
   GTECH_TYPE(G,'HYDRO-RESERVOIRS')$IGHYRS(G)                                             = YES;
   GTECH_TYPE(G,'HYDRO-RUN-OF-RIVER')$IGHYRR(G)                                           = YES;
   GTECH_TYPE(G,'WIND-ON')$(IGWND(G) AND GDATA(G,'GDTECHGROUP')  EQ WINDTURBINE_ONSHORE)  = YES;
   GTECH_TYPE(G,'WIND-OFF')$(IGWND(G) AND GDATA(G,'GDTECHGROUP') EQ WINDTURBINE_OFFSHORE) = YES;
   GTECH_TYPE(G,'SOLAR-PV')$IGSOLE(G)                                                     = YES;
   GTECH_TYPE(G,'SOLAR-HEATING')$IGSOLH(G)                                                = YES;
   GTECH_TYPE(G,'HYDRO-WAVE')$IGWAVE(G)                                                   = YES;
$ifi %HYDROGEN%==yes GTECH_TYPE(G,'FUELCELL')$(GDATA(G,'GDTECHGROUP') EQ FUELCELL)                = YES;
$ifi %HYDROGEN%==yes GTECH_TYPE(G,'ELECTROLYZER')$(GDATA(G,'GDTECHGROUP') EQ ELECTROLYZER)        = YES;
$ifi %HYDROGEN%==yes GTECH_TYPE(G,'ELECTROLYZER')$(GDATA(G,'GDTECHGROUP') EQ BACKUP_HYDROGEN)     = YES;
$ifi %HYDROGEN%==yes GTECH_TYPE(G,'H2-STORAGE')$(GDATA(G,'GDTYPE') EQ HYDROGEN_GH2STO)            = YES;
$ifi %HYDROGEN%==yes GTECH_TYPE(G,'BIOMETH-DAC')$(GDATA(G,'GDTYPE') EQ HYDROGEN_GH2TOBIOMETH)     = YES;
$ifi %HYDROGEN%==yes GTECH_TYPE(G,'BIOGASUPGRADING')$(GDATA(G,'GDTYPE') EQ HYDROGEN_GBIOGASUPGRADING)     = YES;
$ifi %HYDROGEN%==yes GTECH_TYPE(G,'BIOGASMETHANATION')$(GDATA(G,'GDTYPE') EQ HYDROGEN_GBIOGASMETHANATION)     = YES;
$ifi %HYDROGEN%==yes GTECH_TYPE(G,'STEAMREFORMING')$(GDATA(G,'GDTYPE') EQ HYDROGEN_GCH4TOH2)      = YES;
$ifi %OFFSHOREGRID%==yes GTECH_TYPE(G,'HUB-OFF')$(GDATA(G,'GDTYPE') EQ HUB_OFF)     = YES;

PARAMETER G_CAP_YCRAF(Y,C,RRR,AAA,G,FFF,COMMODITY,TECH_TYPE,VARIABLE_CATEGORY,UNITS) "Generation capacity for each year, country, region, area, technology, fuel, commodity, technology type and variable type (GW)";
PARAMETER G_STO_YCRAF(Y,C,RRR,AAA,G,FFF,COMMODITY,TECH_TYPE,VARIABLE_CATEGORY,UNITS) "Generation storage for each year, country, region, area, technology, fuel, commodity, technology type and variable type (GWh)";
PARAMETER PRO_YCRAGFST(Y,C,RRR,AAA,G,FFF,SSS,TTT,COMMODITY,TECH_TYPE,UNITS) "Energy Production for each year, country, region, area, technology, fuel, season, hour, commodity and technology type (MWh)" ;
PARAMETER PRO_YCRAGF(Y,C,RRR,AAA,G,FFF,COMMODITY,TECH_TYPE,UNITS) "Annual Energy Production for each year, country, region, area, technology, fuel, commodity and technology type (TWh)";
PARAMETER F_CONS_YCRAST(Y,C,RRR,AAA,G,FFF,SSS,TTT,TECH_TYPE,UNITS) "Fuel consumption for each year, country, region, area, technology, fuel, season, hour and technology type (MWh)";
PARAMETER F_CONS_YCRA(Y,C,RRR,AAA,G,FFF,TECH_TYPE,UNITS) "Fuel consumption for each year, country, region, area, technology, fuel and technology type (TWh)";
PARAMETER X_CAP_YCR(Y,C,IRRRE,IRRRI,VARIABLE_CATEGORY,UNITS) "Transmission capacity for each year, country, from region to region (GW)";
PARAMETER X_FLOW_YCRST(Y,C,IRRRE,IRRRI,SSS,TTT,UNITS) "Transmission flow for each year, country, from region to region, for each season and hour (MWh)";
PARAMETER X_FLOW_YCR(Y,C,IRRRE,IRRRI,UNITS) "Transmission flow for each year, country, from region to region (TWh)";
PARAMETER ECO_G_YCRAG(Y,C,RRR,AAA,G,FFF,TECH_TYPE,CATEGORY,SUBCATEGORY,UNITS) "Generation Economic Output for each year, country, region, fuel, technology type, area and technology (Mmoney)";
PARAMETER ECO_X_YCR(Y,C,RRR,IRRRI,CATEGORY,SUBCATEGORY,UNITS) "Electric Transmission Economic Output for each year, country and region (Mmoney)";
PARAMETER OBJ_YCR(Y,C,RRR,SUBCATEGORY,UNITS) "Objective function (Mmoney)";
PARAMETER EL_PRICE_YCR(Y,C,RRR,PRICE_CATEGORY,UNITS) "Average Electricity Prices for each region (money/MWh)";
PARAMETER EL_PRICE_YCRST(Y,C,RRR,SSS,TTT,UNITS) "Hourly Electricity Prices for each region (money/MWh)";
PARAMETER H_PRICE_YCRA(Y,C,RRR,AAA,PRICE_CATEGORY,UNITS) "Average heating Prices for each area(money/MWh)";
PARAMETER H_PRICE_YCRAST(Y,C,RRR,AAA,SSS,TTT,UNITS) "Hourly heating Prices for each area (money(MWh)";
PARAMETER EL_DEMAND_YCRST(Y,C,RRR,SSS,TTT,VARIABLE_CATEGORY,UNITS) "Aggregated Hourly Electricity Demand (MWh)";
PARAMETER EL_DEMAND_YCR(Y,C,RRR,VARIABLE_CATEGORY,UNITS) "Aggregated annual Electricity Demand (TWh)";
PARAMETER H_DEMAND_YCRAST(Y,C,RRR,AAA,SSS,TTT,VARIABLE_CATEGORY,UNITS) "Aggregated Hourly heat Demand (MWh)";
PARAMETER H_DEMAND_YCRA(Y,C,RRR,AAA,VARIABLE_CATEGORY,UNITS) "Aggregated Annual heat Demand (MWh)";
PARAMETER EMI_YCRAG(Y,C,RRR,AAA,G,FFF,TECH_TYPE,UNITS) "Annual CO2 emissions(ktons)";
PARAMETER CC_YCRAG(Y,C,RRR,AAA,G,FFF,TECH_TYPE,UNITS) "Annual captured CO2 (ktons)";
PARAMETER CURT_YCRAGFST(Y,C,RRR,AAA,G,FFF,SSS,TTT,COMMODITY,TECH_TYPE,UNITS) "Hourly energy curtailment per country, region, area, technology, fuel, hour and technology type (MWh)";
PARAMETER CURT_YCRAGF(Y,C,RRR,AAA,G,FFF,COMMODITY,TECH_TYPE,UNITS) "Hourly energy curtailment per country, region, area, technology, fuel, hour and technology type (MWh)";
PARAMETER EL_BALANCE_YCRST(Y,C,RRR,EL_BAL_TYPE,SSS,TTT,UNITS) "Hourly electricity balance per country, region, and type (MWh)";
PARAMETER H_BALANCE_YCRAST(Y,C,RRR,AAA,H_BAL_TYPE,SSS,TTT,UNITS) "Hourly heat balance per country, region, area and type (MWh)";
$ifi %HYDROGEN%==yes PARAMETER H2_PRICE_YCR(Y,C,RRR,PRICE_CATEGORY,UNITS) "Average Hydrogen Prices for each region (money/MWh)";
$ifi %HYDROGEN%==yes PARAMETER H2_PRICE_YCRST(Y,C,RRR,SSS,TTT,UNITS) "Hourly hydrogen Prices for each region (money/MWh)";
$ifi %HYDROGEN%==yes PARAMETER BIOMETH_PRICE_YST(Y,SSS,TTT,UNITS) "Hourly biomethane Prices for each region (money/MWh)";
$ifi %HYDROGEN%==yes PARAMETER XH2_CAP_YCR(Y,C,IRRRE,IRRRI,VARIABLE_CATEGORY,UNITS) "H2 Transmission capacity for each year, country, from region to region (GW)";
$ifi %HYDROGEN%==yes PARAMETER XH2_FLOW_YCRST(Y,C,IRRRE,IRRRI,SSS,TTT,UNITS) "H2 Transmission flow for each year, country, from region to region, for each season and hour (MWh)";
$ifi %HYDROGEN%==yes PARAMETER XH2_FLOW_YCR(Y,C,IRRRE,IRRRI,UNITS) "H2 Transmission flow for each year, country, from region to region (TWh)";
$ifi %HYDROGEN%==yes PARAMETER ECO_XH2_YCR(Y,C,RRR,IRRRI,CATEGORY,SUBCATEGORY,UNITS) "H2 Transmission Economic Output for each year, country and region (Mmoney)";
$ifi %HYDROGEN%==yes PARAMETER H2_DEMAND_YCRST(Y,C,RRR,SSS,TTT,VARIABLE_CATEGORY,UNITS) "Aggregated Hourly H2 Demand (MWh)";
$ifi %HYDROGEN%==yes PARAMETER H2_DEMAND_YCR(Y,C,RRR,VARIABLE_CATEGORY,UNITS) "Aggregated annual H2 Demand (TWh)";


**Additional Addon output
*HEATRANS
$ifi %HEATTRANS%==yes PARAMETER XH_CAP_YCA(Y,C,IAAAE,IAAAI,VARIABLE_CATEGORY,UNITS) "Heat Transmission capacity for each year, country, from area to area (GW)";
$ifi %HEATTRANS%==yes PARAMETER XH_FLOW_YCAST(Y,C,IAAAE,IAAAI,SSS,TTT,UNITS) "Heat Transmission flow for each year, country, from areas to area, for each season and hour (MWh)";
$ifi %HEATTRANS%==yes PARAMETER XH_FLOW_YCA(Y,C,IAAAE,IAAAI,UNITS) "Heat Transmission flow for each year, country, from area to area (TWh)";
$ifi %HEATTRANS%==yes PARAMETER ECO_XH_YCRA(Y,C,RRR,AAA,IAAAI,CATEGORY,SUBCATEGORY,UNITS) "Heat Transmission Economic Output for each year, country and region and area (Mmoney)";


$ifi not %stointers%==all $goto NO_STOINTERS
*Modification of STORAGE sets so calculation are done correcting its type:
IGHSTO(G)   = 0;
IGHSTOS(G)  = 0;
IGESTO(G)   = 0;
IGESTOS(G)  = 0;
IGHSTO(G)   = YES$(GDATA(G,'GDTYPE') EQ GHSTO);
IGHSTOS(G)  = YES$(GDATA(G,'GDTYPE') EQ GHSTOS);
IGESTO(G)   = YES$(GDATA(G,'GDTYPE') EQ GESTO);
IGESTOS(G)  = YES$(GDATA(G,'GDTYPE') EQ GESTOS);
$label NO_STOINTERS

*Modification of IHOURSINST
$ifi %RollingSeasons%==yes PARAMETER IHOURSINTROLLINGSEASON(S,T) "Length of time segment (hours) used in rolling season mode";
$ifi %RollingSeasons%==yes IWEIGHSUMS=SUM(SSS$(ORD(SSS) LE ROLLINGSEASONSNUMBER), WEIGHT_S(SSS));
$ifi %RollingSeasons%==yes IHOURSINTROLLINGSEASON(S,T)=IOF8760*WEIGHT_S(S)*WEIGHT_T(T)/(IWEIGHSUMS*SUM(ITALIAS, WEIGHT_T(ITALIAS)))/IHOURFRAC;

IWEIGHSUMS = SUM(S, WEIGHT_S(S));
IWEIGHSUMT = SUM(T, WEIGHT_T(T));
IHOURSINST(SSS,TTT)=0;
IHOURSINST(S,T)=IOF8760*WEIGHT_S(S)*WEIGHT_T(T)/(IWEIGHSUMS*IWEIGHSUMT)/IHOURFRAC;



* -------------------- ENERGY CAPACITY ---------------------

* ------------- ELECTRICITY CAPACITY --------------

** EXOGENOUS ELECTRICITY CAPACITY

G_CAP_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'EXOGENOUS','GW')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND NOT(IHYDROGEN(IGE)))=
IOF0001*GKFX(Y,IA,IGE)/(1$((NOT IGESTO(IGE)) AND (NOT IGESTOS(IGE)))+GDATA(IGE,'GDSTOHUNLD')$((IGESTO(IGE)) OR IGESTOS(IGE)))
;


** ENDOGENOUS ELECTRIC CAPACITY ACCUMULATED INVESTMENTS
G_CAP_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'ENDOGENOUS','GW')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND NOT(IHYDROGEN(IGE)))=
IOF0001*VGKNACCUMNET.L(Y,IA,IGE)/(1$((NOT IGESTO(IGE)) AND (NOT IGESTOS(IGE)))+GDATA(IGE,'GDSTOHUNLD')$((IGESTO(IGE)) OR IGESTOS(IGE)))
;

** ENDOGENOUS ELECTRICITY CAPACITY ACCUMULATED DECOMMISSIONING
$ifi %DECOM%==yes G_CAP_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'DECOMMISSIONING','GW')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,IGE) AND NOT(IHYDROGEN(IGE)))=
$ifi %DECOM%==yes -IOF0001*VDECOM_EXO_ACCUM.L(Y,IA,IGE)/(1$((NOT IGESTO(IGE)) AND (NOT IGESTOS(IGE)))+GDATA(IGE,'GDSTOHUNLD')$((IGESTO(IGE)) OR IGESTOS(IGE)))
;


* ------------- END OF ELECTRICITY CAPACITY --------------

* ------------- HEAT CAPACITY --------------

** EXOGENOUS HEAT CAPACITY

G_CAP_YCRAF(Y,C,IR,IA,IGH,FFF,'HEAT',TECH_TYPE,'EXOGENOUS','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE))=
(IOF0001*GKFX(Y,IA,IGH)/((1$((NOT IGHSTO(IGH)) AND (NOT IGHSTOS(IGH)))+GDATA(IGH,'GDSTOHUNLD')$((IGHSTO(IGH)) OR IGHSTOS(IGH)))*(GDATA(IGH,'GDCB')$IGBPR(IGH)+ (GDATA(IGH,'GDCB')+GDATA(IGH,'GDCV'))$IGEXT(IGH)+1$(NOT (IGBPR(IGH) OR IGEXT(IGH))))))$(NOT IGBYPASS(IGH) AND NOT(IHYDROGEN(IGH)))
+(IOF0001*GKFX(Y,IA,IGH)*(((GDATA(IGH,'GDCB')/GDATA(IGH,'GDBYPASSC')+1)/(GDATA(IGH,'GDCB')+GDATA(IGH,'GDCV')))$IGEXT(IGH) + (1/GDATA(IGH,'GDBYPASSC')+1/GDATA(IGH,'GDCB'))$IGBPR(IGH)))$(IGBYPASS(IGH) AND NOT(IHYDROGEN(IGH)))
$ifi %HYDROGEN%==yes +(IOF0001*GKFX(Y,IA,IGH)/GDATA(IGH,'GDCB'))$(IHYDROGEN_GETOHH2(IGH) OR IHYDROGEN_GH2TOEH(IGH))
;

** ENDOGENOUS HEAT CAPACITY ACCUMULATED INVESTMENTS
G_CAP_YCRAF(Y,C,IR,IA,IGH,FFF,'HEAT',TECH_TYPE,'ENDOGENOUS','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE))=
(IOF0001*VGKNACCUMNET.L(Y,IA,IGH)/((1$((NOT IGHSTO(IGH)) AND (NOT IGHSTOS(IGH)))+GDATA(IGH,'GDSTOHUNLD')$((IGHSTO(IGH)) OR IGHSTOS(IGH)))*(GDATA(IGH,'GDCB')$IGBPR(IGH)+ (GDATA(IGH,'GDCB')+GDATA(IGH,'GDCV'))$IGEXT(IGH)+1$(NOT (IGBPR(IGH) OR IGEXT(IGH))))))$(NOT IGBYPASS(IGH) AND NOT(IHYDROGEN(IGH)))
+(IOF0001*VGKNACCUMNET.L(Y,IA,IGH)*(((GDATA(IGH,'GDCB')/GDATA(IGH,'GDBYPASSC')+1)/(GDATA(IGH,'GDCB')+GDATA(IGH,'GDCV')))$IGEXT(IGH) + (1/GDATA(IGH,'GDBYPASSC')+1/GDATA(IGH,'GDCB'))$IGBPR(IGH)))$(IGBYPASS(IGH) AND NOT(IHYDROGEN(IGH)))
$ifi %HYDROGEN%==yes +(IOF0001*VGKNACCUMNET.L(Y,IA,IGH)/GDATA(IGH,'GDCB'))$(IHYDROGEN_GETOHH2(IGH) OR IHYDROGEN_GH2TOEH(IGH))
;

** ENDOGENOUS HEAT CAPACITY ACCUMULATED DECOMMISSIONING
$ifi %DECOM%==yes G_CAP_YCRAF(Y,C,IR,IA,IGH,FFF,'HEAT',TECH_TYPE,'DECOMMISSIONING','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,IGH))=
$ifi %DECOM%==yes -(IOF0001*VDECOM_EXO_ACCUM.L(Y,IA,IGH)/((1$((NOT IGHSTO(IGH)) AND (NOT IGHSTOS(IGH)))+GDATA(IGH,'GDSTOHUNLD')$((IGHSTO(IGH)) OR IGHSTOS(IGH)))*(GDATA(IGH,'GDCB')$IGBPR(IGH)+ (GDATA(IGH,'GDCB')+GDATA(IGH,'GDCV'))$IGEXT(IGH)+1$(NOT (IGBPR(IGH) OR IGEXT(IGH))))))$(NOT IGBYPASS(IGH) AND NOT(IHYDROGEN(IGH)))
$ifi %DECOM%==yes -(IOF0001*VDECOM_EXO_ACCUM.L(Y,IA,IGH)*(((GDATA(IGH,'GDCB')/GDATA(IGH,'GDBYPASSC')+1)/(GDATA(IGH,'GDCB')+GDATA(IGH,'GDCV')))$IGEXT(IGH) + (1/GDATA(IGH,'GDBYPASSC')+1/GDATA(IGH,'GDCB'))$IGBPR(IGH)))$(IGBYPASS(IGH) AND NOT(IHYDROGEN(IGH)))
$ifi %DECOM%==yes $ifi %HYDROGEN%==yes -(IOF0001*VDECOM_EXO_ACCUM.L(Y,IA,IGH)/GDATA(IGH,'GDCB'))$(IHYDROGEN_GETOHH2(IGH) OR IHYDROGEN_GH2TOEH(IGH))
;

* ------------- END OF HEAT CAPACITY --------------

$ifi not %HYDROGEN%==yes  $goto NO_HYDROGEN_CAPACITY
* ------------- HYDROGEN CAPACITY --------------

** EXOGENOUS HYDROGEN CAPACITY

G_CAP_YCRAF(Y,C,IR,IA,G,FFF,'HYDROGEN',TECH_TYPE,'EXOGENOUS','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) AND IHYDROGEN(G))=
IOF0001*GKFX(Y,IA,G)*(1$IHYDROGEN_GH2STO(G)+GDATA(G,'GDFE')$(NOT(IHYDROGEN_GH2STO(G))))/(1$(NOT IHYDROGEN_GH2STO(G))+GDATA(G,'GDSTOHUNLD')$IHYDROGEN_GH2STO(G))
;


** ENDOGENOUS HYDROGEN CAPACITY ACCUMULATED INVESTMENTS
G_CAP_YCRAF(Y,C,IR,IA,G,FFF,'HYDROGEN',TECH_TYPE,'ENDOGENOUS','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) AND IHYDROGEN(G))=
IOF0001*VGKNACCUMNET.L(Y,IA,G)*(1$IHYDROGEN_GH2STO(G)+GDATA(G,'GDFE')$(NOT(IHYDROGEN_GH2STO(G))))/(1$(NOT IHYDROGEN_GH2STO(G))+GDATA(G,'GDSTOHUNLD')$IHYDROGEN_GH2STO(G))
;

** ENDOGENOUS HYDROGEN CAPACITY ACCUMULATED DECOMMISSIONING
$ifi %DECOM%==yes G_CAP_YCRAF(Y,C,IR,IA,G,FFF,'HYDROGEN',TECH_TYPE,'DECOMMISSIONING','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,G) AND IHYDROGEN(G) AND NOT(IHYDROGEN_GH2TOBIOMETH(G)))=
$ifi %DECOM%==yes -IOF0001*VDECOM_EXO_ACCUM.L(Y,IA,G)*(1$IHYDROGEN_GH2STO(G)+GDATA(G,'GDFE')$(NOT(IHYDROGEN_GH2STO(G))))/(1$(NOT IHYDROGEN_GH2STO(G))+GDATA(G,'GDSTOHUNLD')$IHYDROGEN_GH2STO(G))
;

* ------------- END OF HYDROGEN CAPACITY --------------

* ------------- BIOMETHANE CAPACITY --------------

** EXOGENOUS BIOMETHANE CAPACITY

G_CAP_YCRAF(Y,C,IR,IA,G,FFF,'BIOMETHANE',TECH_TYPE,'EXOGENOUS','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) AND (IHYDROGEN_GH2TOBIOMETH(G) OR IHYDROGEN_GBIOGASMETHANATION(G) OR IHYDROGEN_GBIOGASUPGRADING(G)))=
IOF0001*GKFX(Y,IA,G)
;

** ENDOGENOUS BIOMETHANE CAPACITY ACCUMULATED INVESTMENTS
G_CAP_YCRAF(Y,C,IR,IA,G,FFF,'BIOMETHANE',TECH_TYPE,'ENDOGENOUS','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) AND (IHYDROGEN_GH2TOBIOMETH(G) OR IHYDROGEN_GBIOGASMETHANATION(G) OR IHYDROGEN_GBIOGASUPGRADING(G)))=
IOF0001*VGKNACCUMNET.L(Y,IA,G)
;

** ENDOGENOUS HYDROGEN CAPACITY ACCUMULATED DECOMMISSIONING
$ifi %DECOM%==yes G_CAP_YCRAF(Y,C,IR,IA,G,FFF,'BIOMETHANE',TECH_TYPE,'DECOMMISSIONING','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,G) AND (IHYDROGEN_GH2TOBIOMETH(G) OR IHYDROGEN_GBIOGASMETHANATION(G) OR IHYDROGEN_GBIOGASUPGRADING(G)))=
$ifi %DECOM%==yes -IOF0001*VDECOM_EXO_ACCUM.L(Y,IA,G)
;

* ------------- END OF BIOMETHANE CAPACITY --------------

$label NO_HYDROGEN_CAPACITY

* ---------------- END OF ENERGY CAPACITY -----------------

* -------------------- ENERGY STORAGE ---------------------

* ------------- ELECTRICITY STORAGE --------------

** EXOGENOUS ELECTRICITY STORAGE

G_STO_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'EXOGENOUS','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND (IGESTO(IGE) OR IGESTOS(IGE) OR IGHYRS(IGE)) )=
IOF0001*GKFX(Y,IA,IGE)*(1$(NOT IGHYRS(IGE)) + HYRSMAXVOL_G(IA,IGE)$IGHYRS(IGE))
;

** ENDOGENOUS ELECTRIC STORAGE ACCUMULATED INVESTMENTS
G_STO_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'ENDOGENOUS','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND (IGESTO(IGE) OR IGESTOS(IGE) OR IGHYRS(IGE)) )=
IOF0001*VGKNACCUMNET.L(Y,IA,IGE)*(1$(NOT IGHYRS(IGE)) + HYRSMAXVOL_G(IA,IGE)$IGHYRS(IGE))
;

** ENDOGENOUS ELECTRICITY STORAGE ACCUMULATED DECOMMISSIONING
$ifi %DECOM%==yes G_STO_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'DECOMMISSIONING','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,IGE) AND (IGESTO(IGE) OR IGESTOS(IGE) OR IGHYRS(IGE)) )=
$ifi %DECOM%==yes -IOF0001*VDECOM_EXO_ACCUM.L(Y,IA,IGE)*(1$(NOT IGHYRS(IGE)) + HYRSMAXVOL_G(IA,IGE)$IGHYRS(IGE))
;

* ------------- END OF ELECTRICITY CAPACITY --------------

* ------------- HEAT STORAGE --------------

** EXOGENOUS HEAT STORAGE

G_STO_YCRAF(Y,C,IR,IA,IGH,FFF,'HEAT',TECH_TYPE,'EXOGENOUS','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND (IGHSTO(IGH) OR IGHSTOS(IGH)))=
IOF0001*GKFX(Y,IA,IGH)
;

** ENDOGENOUS HEAT STORAGE ACCUMULATED INVESTMENTS
G_STO_YCRAF(Y,C,IR,IA,IGH,FFF,'HEAT',TECH_TYPE,'ENDOGENOUS','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND (IGHSTO(IGH) OR IGHSTOS(IGH)))=
IOF0001*VGKNACCUMNET.L(Y,IA,IGH)
;

** ENDOGENOUS HEAT STORAGE ACCUMULATED DECOMMISSIONING
$ifi %DECOM%==yes G_STO_YCRAF(Y,C,IR,IA,IGH,FFF,'HEAT',TECH_TYPE,'DECOMMISSIONING','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,IGH) AND (IGHSTO(IGH) OR IGHSTOS(IGH)))=
$ifi %DECOM%==yes -IOF0001*VDECOM_EXO_ACCUM.L(Y,IA,IGH)
;

* ------------- END OF HEAT STORAGE --------------

$ifi not %HYDROGEN%==yes  $goto NO_HYDROGEN_STORAGE
* ------------- HYDROGEN STORAGE --------------

** EXOGENOUS HYDROGEN STORAGE

G_STO_YCRAF(Y,C,IR,IA,IHYDROGEN_GH2STO,FFF,'HYDROGEN',TECH_TYPE,'EXOGENOUS','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IHYDROGEN_GH2STO) AND IGF(IHYDROGEN_GH2STO,FFF) AND GTECH_TYPE(IHYDROGEN_GH2STO,TECH_TYPE) )=
IOF0001*GKFX(Y,IA,IHYDROGEN_GH2STO)
;

** ENDOGENOUS HYDROGEN STORAGE ACCUMULATED INVESTMENTS
G_STO_YCRAF(Y,C,IR,IA,IHYDROGEN_GH2STO,FFF,'HYDROGEN',TECH_TYPE,'ENDOGENOUS','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IHYDROGEN_GH2STO) AND IGF(IHYDROGEN_GH2STO,FFF) AND GTECH_TYPE(IHYDROGEN_GH2STO,TECH_TYPE))=
IOF0001*VGKNACCUMNET.L(Y,IA,IHYDROGEN_GH2STO)
;

** ENDOGENOUS HYDROGEN STORAGE ACCUMULATED DECOMMISSIONING
$ifi %DECOM%==yes G_STO_YCRAF(Y,C,IR,IA,IHYDROGEN_GH2STO,FFF,'HYDROGEN',TECH_TYPE,'DECOMMISSIONING','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IHYDROGEN_GH2STO) AND IGF(IHYDROGEN_GH2STO,FFF) AND GTECH_TYPE(IHYDROGEN_GH2STO,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,IHYDROGEN_GH2STO))=
$ifi %DECOM%==yes -IOF0001*VDECOM_EXO_ACCUM.L(Y,IA,IHYDROGEN_GH2STO)
;
* ------------- END OF HYDROGEN STORAGE --------------
$label NO_HYDROGEN_STORAGE

* ---------------- END OF ENERGY STORAGE -----------------




* -------------------- ENERGY PRODUCTION ---------------------

** ELECTRICITY PRODUCTION HOURLY
PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY',TECH_TYPE,'MWh')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE))=
       VGE_T.L(Y,IA,IGE,S,T);

** ELECTRICITY PRODUCTION ANNUALLY
PRO_YCRAGF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'TWh')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE))=
       SUM((S,T),IHOURSINST(S,T)*VGE_T.L(Y,IA,IGE,S,T)*IHOURFRAC)/IOF1000000;

** HEAT PRODUCTION HOURLY
PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'HEAT',TECH_TYPE,'MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE))=
       VGH_T.L(Y,IA,IGH,S,T);

** HEAT PRODUCTION ANNUALY
PRO_YCRAGF(Y,C,IR,IA,IGH,FFF,'HEAT',TECH_TYPE,'TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE))=
       SUM((S,T),IHOURSINST(S,T)*VGH_T.L(Y,IA,IGH,S,T)*IHOURFRAC)/IOF1000000;

$ifi not %HYDROGEN%==yes  $goto NO_HYDROGEN_PRODUCTION
** HYDROGEN PRODUCTION HOURLY
PRO_YCRAGFST(Y,C,IR,IA,G,FFF,S,T,'HYDROGEN',TECH_TYPE,'MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) AND IHYDROGEN(G) )=
       +(VGE_T.L(Y,IA,G,S,T)*GDATA(G,'GDFE'))$IHYDROGEN_GETOH2(G)
       +(VGE_T.L(Y,IA,G,S,T)*GDATA(G,'GDFE'))$IHYDROGEN_GETOHH2(G)
       +(VGE_T.L(Y,IA,G,S,T)*GDATA(G,'GDFE'))$IHYDROGEN_GEHTOH2(G)
       +(VHYDROGEN_GH2_T.L(Y,IA,G,S,T))$(IHYDROGEN_GCH4TOH2(G) OR IHYDROGEN_GH2STO(G))
;

** HYDROGEN PRODUCTION ANNUALY
PRO_YCRAGF(Y,C,IR,IA,G,FFF,'HYDROGEN',TECH_TYPE,'TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) AND IHYDROGEN(G))=
       SUM((S,T),IHOURSINST(S,T)*PRO_YCRAGFST(Y,C,IR,IA,G,FFF,S,T,'HYDROGEN',TECH_TYPE,'MWh')*IHOURFRAC)/IOF1000000
;

** BIOMETHANE PRODUCTION HOURLY
PRO_YCRAGFST(Y,C,IR,IA,G,FFF,S,T,'BIOMETHANE',TECH_TYPE,'MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) AND (IHYDROGEN_GH2TOBIOMETH(G) OR IHYDROGEN_GBIOGASMETHANATION(G) OR IHYDROGEN_GBIOGASUPGRADING(G)))=
       VGBIOMETH_T.L(Y,IA,G,S,T)$IHYDROGEN_GH2TOBIOMETH(G)
       + VGBIOGASMETHANATION_T.L(Y,IA,G,S,T)$IHYDROGEN_GBIOGASMETHANATION(G)
      +VGBIOGASUPGRADING_T.L(Y,IA,G,S,T)$IHYDROGEN_GBIOGASUPGRADING(G)
;

** BIOMETHANE PRODUCTION ANNUALY
PRO_YCRAGF(Y,C,IR,IA,G,FFF,'BIOMETHANE',TECH_TYPE,'TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) AND (IHYDROGEN_GH2TOBIOMETH(G) OR IHYDROGEN_GBIOGASMETHANATION(G) OR IHYDROGEN_GBIOGASUPGRADING(G)))=
       SUM((S,T),IHOURSINST(S,T)*PRO_YCRAGFST(Y,C,IR,IA,G,FFF,S,T,'BIOMETHANE',TECH_TYPE,'MWh')*IHOURFRAC)/IOF1000000
;
$label NO_HYDROGEN_PRODUCTION

* ---------------- END OF ENERGY PRODUCTION -----------------


* -------------------- FUEL CONSUMPTION ---------------------

** HOURLY FUEL CONSUMPTION
F_CONS_YCRAST(Y,C,IR,IA,G,FFF,S,T,TECH_TYPE,'MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE))=
       VGF_T.L(Y,IA,G,S,T);

** ANNUAL FUEL CONSUMPTION
F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE))=
       SUM((S,T),IHOURSINST(S,T)*VGF_T.L(Y,IA,G,S,T)*IHOURFRAC)/IOF1000000;

* OPTIFLOW FUEL CONSUMPTION
$ifi not %OPTIFLOW%==yes $goto NO_OPTIFLOW_FUEL_USE
$ifi not %baloptfuelbridge%==yes $goto NO_OPTIFLOW_FUEL_USE
** HOURLY
F_CONS_YCRAST(Y,C,IR,IA,'EBAVERE_ST_WOODCHI_BP',FFF,S,T,'OPTIFLOW','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA))=
      SUM((PROCSOURCE,FLOW)$(IFPROC(FFF,PROCSOURCE) AND IFFLOW(FFF,FLOW)),VFLOWSOURCE.L(Y,IA,PROCSOURCE,FLOW,S,T))/IOF3P6;
** ANNUAL
F_CONS_YCRA(Y,C,IR,IA,'EBAVERE_ST_WOODCHI_BP',FFF,'OPTIFLOW','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA))=
      (SUM((S,T,PROCSOURCE,FLOW)$(IFPROC(FFF,PROCSOURCE) AND IFFLOW(FFF,FLOW)),IHOURSINST(S,T)*VFLOWSOURCE.L(Y,IA,PROCSOURCE,FLOW,S,T)*IHOURFRAC)/IOF3P6)/IOF1000000;
$label NO_OPTIFLOW_FUEL_USE
;





* ---------------- END OF FUEL CONSUMPTION -----------------

* ---------------- ENERGY CURTAILMENT -----------------------
*HOURLY CURTAILMENT
CURT_YCRAGFST(Y,C,IR,IA,IGHYRR,FFF,S,T,'ELECTRICITY','HYDRO-RUN-OF-RIVER','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGHYRR) AND IGF(IGHYRR,FFF) AND GTECH_TYPE(IGHYRR,'HYDRO-RUN-OF-RIVER') AND IWTRRRSUM(IA) > 0)=
(WTRRRFLH(IA) * (
           GKFX(Y,IA,IGHYRR)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGHYRR)
        +  VGKNACCUMNET.L(Y,IA,IGHYRR)
                          ) * WTRRRVAR_T(IA,S,T) * (1$(NOT IGKRATE(IA,IGHYRR,S,T)) + IGKRATE(IA,IGHYRR,S,T))) / IWTRRRSUM(IA)
-VGE_T.L(Y,IA,IGHYRR,S,T);

CURT_YCRAGFST(Y,C,IR,IA,IGWND,FFF,S,T,'ELECTRICITY','WIND-ON','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGWND) AND IGF(IGWND,FFF) AND GTECH_TYPE(IGWND,'WIND-ON') AND IWND_SUMST(IA) > 0)=
$ifi %WNDFLH_DOL%==AAA         (WNDFLH(IA) * (
$ifi %WNDFLH_DOL%==AAA_GGG     (WNDFLH(IA,IGWND) * (
            GKFX(Y,IA,IGWND)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGWND)
         + VGKNACCUMNET.L(Y,IA,IGWND)
                 )  * WND_VAR_T(IA,S,T) * (1$(NOT IGKRATE(IA,IGWND,S,T)) + IGKRATE(IA,IGWND,S,T))) / IWND_SUMST(IA)
-VGE_T.L(Y,IA,IGWND,S,T);

CURT_YCRAGFST(Y,C,IR,IA,IGWND,FFF,S,T,'ELECTRICITY','WIND-OFF','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGWND) AND IGF(IGWND,FFF) AND GTECH_TYPE(IGWND,'WIND-OFF') AND IWND_SUMST(IA) > 0)=
$ifi %WNDFLH_DOL%==AAA         (WNDFLH(IA) * (
$ifi %WNDFLH_DOL%==AAA_GGG     (WNDFLH(IA,IGWND) * (
            GKFX(Y,IA,IGWND)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGWND)
         + VGKNACCUMNET.L(Y,IA,IGWND)
                 )  * WND_VAR_T(IA,S,T) * (1$(NOT IGKRATE(IA,IGWND,S,T)) + IGKRATE(IA,IGWND,S,T))) / IWND_SUMST(IA)
-VGE_T.L(Y,IA,IGWND,S,T);

CURT_YCRAGFST(Y,C,IR,IA,IGSOLE,FFF,S,T,'ELECTRICITY','SOLAR-PV','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGSOLE) AND IGF(IGSOLE,FFF) AND GTECH_TYPE(IGSOLE,'SOLAR-PV') AND ISOLESUMST(IA) > 0)=
$ifi %SOLEFLH_DOL%==AAA         (SOLEFLH(IA) * (
$ifi %SOLEFLH_DOL%==AAA_GGG     (SOLEFLH(IA,IGSOLE) * (
            GKFX(Y,IA,IGSOLE)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGSOLE)
         + VGKNACCUMNET.L(Y,IA,IGSOLE)
                 )  * SOLE_VAR_T(IA,S,T) * (1$(NOT IGKRATE(IA,IGSOLE,S,T)) + IGKRATE(IA,IGSOLE,S,T))) / ISOLESUMST(IA)
-VGE_T.L(Y,IA,IGSOLE,S,T);

CURT_YCRAGFST(Y,C,IR,IA,IGSOLH,FFF,S,T,'HEAT','SOLAR-HEATING','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGSOLH) AND IGF(IGSOLH,FFF) AND GTECH_TYPE(IGSOLH,'SOLAR-HEATING') AND ISOLHSUMST(IA) > 0)=
$ifi %SOLHFLH_DOL%==AAA         (SOLHFLH(IA) * (
$ifi %SOLHFLH_DOL%==AAA_GGG     (SOLHFLH(IA,IGSOLH) * (
            GKFX(Y,IA,IGSOLH)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGSOLH)
         + VGKNACCUMNET.L(Y,IA,IGSOLH)
                 )  * SOLH_VAR_T(IA,S,T) * (1$(NOT IGKRATE(IA,IGSOLH,S,T)) + IGKRATE(IA,IGSOLH,S,T))) / ISOLHSUMST(IA)
-VGH_T.L(Y,IA,IGSOLH,S,T);


*ANNUAL CURTAILMENT
CURT_YCRAGF(Y,C,IR,IA,IGHYRR,FFF,'ELECTRICITY','HYDRO-RUN-OF-RIVER','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGHYRR) AND IGF(IGHYRR,FFF) AND GTECH_TYPE(IGHYRR,'HYDRO-RUN-OF-RIVER'))=
SUM((S,T),IHOURSINST(S,T)*CURT_YCRAGFST(Y,C,IR,IA,IGHYRR,FFF,S,T,'ELECTRICITY','HYDRO-RUN-OF-RIVER','MWh')*IHOURFRAC)/IOF1000000
;
CURT_YCRAGF(Y,C,IR,IA,IGWND,FFF,'ELECTRICITY','WIND-ON','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGWND) AND IGF(IGWND,FFF) AND GTECH_TYPE(IGWND,'WIND-ON'))=
SUM((S,T),IHOURSINST(S,T)*CURT_YCRAGFST(Y,C,IR,IA,IGWND,FFF,S,T,'ELECTRICITY','WIND-ON','MWh')*IHOURFRAC)/IOF1000000
;
CURT_YCRAGF(Y,C,IR,IA,IGWND,FFF,'ELECTRICITY','WIND-OFF','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGWND) AND IGF(IGWND,FFF) AND GTECH_TYPE(IGWND,'WIND-OFF'))=
SUM((S,T),IHOURSINST(S,T)*CURT_YCRAGFST(Y,C,IR,IA,IGWND,FFF,S,T,'ELECTRICITY','WIND-OFF','MWh')*IHOURFRAC)/IOF1000000
;
CURT_YCRAGF(Y,C,IR,IA,IGSOLE,FFF,'ELECTRICITY','SOLAR-PV','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGSOLE) AND IGF(IGSOLE,FFF) AND GTECH_TYPE(IGSOLE,'SOLAR-PV'))=
SUM((S,T),IHOURSINST(S,T)*CURT_YCRAGFST(Y,C,IR,IA,IGSOLE,FFF,S,T,'ELECTRICITY','SOLAR-PV','MWh')*IHOURFRAC)/IOF1000000
;
CURT_YCRAGF(Y,C,IR,IA,IGSOLH,FFF,'HEAT','SOLAR-HEATING','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGSOLH) AND IGF(IGSOLH,FFF) AND GTECH_TYPE(IGSOLH,'SOLAR-HEATING'))=
SUM((S,T),IHOURSINST(S,T)*CURT_YCRAGFST(Y,C,IR,IA,IGSOLH,FFF,S,T,'HEAT','SOLAR-HEATING','MWh')*IHOURFRAC)/IOF1000000
;

*Curtailment of hydro reservoirs should be analyzed by aggregating the technology set
CURT_YCRAGF(Y,C,IR,IA,IGHYRS,FFF,'ELECTRICITY','HYDRO-RESERVOIRS','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGHYRS) AND IGF(IGHYRS,FFF) AND GTECH_TYPE(IGHYRS,'HYDRO-RESERVOIRS'))=
SUM(S,
        IHYINF_S(IA,S) *
        (
        GKFX(Y,IA,IGHYRS)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGHYRS)
       +VGKNACCUMNET.L(Y,IA,IGHYRS)
         )
         -SUM(T,IHYRST(S,T)*VGE_T.L(Y,IA,IGHYRS,S,T))
    )/IOF1000000*IHOURFRAC
;


* ---------------- END OF ENERGY CURTAILMENT -----------------------

* ------------------------- ENERGY DEMAND ----------------------

* ------------- ELECTRICITY DEMAND --------------

*HOURLY
** EXOGENOUS DEMAND

$ifi %TRANSPORT%==yes EL_DEMAND_YCRST(Y,C,IR,S,T,'EXOGENOUS','MWh')$(CCCRRR(C,IR) AND SUM(DEUSER$(NOT IDEUSER_TRANS(DEUSER)),IDE_SUMST(IR,DEUSER)))=
$ifi %TRANSPORT%==yes SUM((DEUSER)$(IDE_SUMST(IR,DEUSER) AND (NOT IDEUSER_TRANS(DEUSER))), DE(Y,IR,DEUSER) * DE_VAR_T(IR,DEUSER,S,T) / IDE_SUMST(IR,DEUSER))

$ifi not %TRANSPORT%==yes EL_DEMAND_YCRST(Y,C,IR,S,T,'EXOGENOUS','MWh')$(CCCRRR(C,IR) AND SUM(DEUSER,IDE_SUMST(IR,DEUSER)))=
$ifi not %TRANSPORT%==yes SUM((DEUSER)$(IDE_SUMST(IR,DEUSER)), DE(Y,IR,DEUSER) * DE_VAR_T(IR,DEUSER,S,T) / IDE_SUMST(IR,DEUSER))


$ifi not %DEMANDRESPONSE%==yes  $goto NO_DEMANDRESPONSE
* Downward regulation for DR shed technologies:
-       SUM(IADR(Y,IA,DR_TECH_SHED)$(RRRAAA(IR,IA) AND CCCRRR(C,IR)), VDR_DOWN.L(Y,IA,DR_TECH_SHED,S,T))
* Balance between upward and downward regulation for DR shift technologies:
*TODO - efficiency
-       SUM(IADR(Y,IA,DR_TECH_SHIFT)$(RRRAAA(IR,IA) AND CCCRRR(C,IR)), VDR_DOWN.L(Y,IA,DR_TECH_SHIFT,S,T)) + SUM(IADR(Y,IA,DR_TECH_SHIFT)$(RRRAAA(IR,IA) AND CCCRRR(C,IR)), VDR_UP.L(Y,IA,DR_TECH_SHIFT,S,T))
$label NO_DEMANDRESPONSE
;

$ifi %TRANSPORT%==yes EL_DEMAND_YCRST(Y,C,IR,S,T,'EXO_TRANS','MWh')$(CCCRRR(C,IR) AND SUM(IDEUSER_TRANS,IDE_SUMST(IR,IDEUSER_TRANS)))=
$ifi %TRANSPORT%==yes SUM((IDEUSER_TRANS)$IDE_SUMST(IR,IDEUSER_TRANS), DE(Y,IR,IDEUSER_TRANS) * DE_VAR_T(IR,IDEUSER_TRANS,S,T) / IDE_SUMST(IR,IDEUSER_TRANS))
;

** ENDOGENOUS DEMAND

EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDOGENOUS_ELECT2HEAT','MWh')$CCCRRR(C,IR)=
* P2H CONSUMPTION
         SUM(IA$RRRAAA(IR,IA), SUM(IGETOH$IAGK_HASORPOT(Y,IA,IGETOH), VGE_T.L(Y,IA,IGETOH,S,T)))
;

EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_INTRASTO','MWh')$CCCRRR(C,IR)=
* INTRASEASONAL ELECTRICITY STORAGE
$ifi not %stointers%==all         +SUM(IA$RRRAAA(IR,IA),SUM(IGESTO$IAGK_HASORPOT(Y,IA,IGESTO),VESTOLOADT.L(Y,IA,IGESTO,S,T)))
$ifi     %stointers%==all         +SUM(IA$RRRAAA(IR,IA),SUM(IGESTO$IAGK_HASORPOT(Y,IA,IGESTO),VESTOLOADTS.L(Y,IA,IGESTO,S,T)))
;

EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_INTERSTO','MWh')$CCCRRR(C,IR)=
* INTERSEASONAL ELECTRICITY STORAGE
         +SUM(IA$RRRAAA(IR,IA),SUM(IGESTOS$IAGK_HASORPOT(Y,IA,IGESTOS),VESTOLOADTS.L(Y,IA,IGESTOS,S,T)))
;

$ifi not %EV%==yes $goto NO_EV
EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_EV','MWh')$CCCRRR(C,IR)=
                                + VEV_VG2V_BEV.L(Y,IR,S,T)
                                + VEV_VG2V_PHEV.L(Y,IR,S,T)
$ifi %V2G%==yes                 - VEV_VV2G_BEV.L(Y,IR,S,T)
$ifi %V2G%==yes                 - VEV_VV2G_PHEV.L(Y,IR,S,T)
$ifi %STEPCHARGSCHEME%==yes     - VEV_VV2G_BEV.L(Y,IR,S,T)$(cs('v2g',IY411))
$ifi %STEPCHARGSCHEME%==yes     - VEV_VV2G_PHEV.L(Y,IR,S,T)$(cs('v2g',IY411))
;
$label NO_EV

$ifi %TRANSPORT%==yes EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_OTHERTRANS','MWh')$CCCRRR(C,IR)=
$ifi %TRANSPORT%==yes $ifi %TRANSPORT_SYNTHFUEL_COMMODITY%==ELECTRICITY +VTRANSDEMAND_T.L(Y,IR,S,T)
$ifi %TRANSPORT%==yes +0;

$ifi %CCS%==yes EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_CCS','MWh')$CCCRRR(C,IR)=
$ifi %CCS%==yes SUM((IA,G)$(IAGK_HASORPOT(Y,IA,G) AND RRRAAA(IR,IA) AND CCS_G(G)),VGF_T.L(Y,IA,G,S,T) * IM_CO2(G)*IOF0001 * IOF3P6 * CCS_CO2CAPTEFF_G(G)*  CCS_DECO2COMP_G(G));

$ifi not %HYDROGEN%==yes $goto NO_HYDROGEN_DEMAND
EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_H2','MWh')$CCCRRR(C,IR)=
+SUM(IAGK_HASORPOT(Y,IA,IHYDROGEN_GETOH2)$(RRRAAA(IR,IA)), VGE_T.L(Y,IA,IHYDROGEN_GETOH2,S,T))
+SUM(IAGK_HASORPOT(Y,IA,IHYDROGEN_GETOHH2)$(RRRAAA(IR,IA)), VGE_T.L(Y,IA,IHYDROGEN_GETOHH2,S,T))
+SUM(IAGK_HASORPOT(Y,IA,IHYDROGEN_GEHTOH2)$(RRRAAA(IR,IA)), VGE_T.L(Y,IA,IHYDROGEN_GEHTOH2,S,T))
;

EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_BIOMETHANE','MWh')$CCCRRR(C,IR)=
*Adding electricity consumption from BIOMETHANE-DAC generation
+ SUM(IAGK_HASORPOT(Y,IA,IHYDROGEN_GH2TOBIOMETH)$(RRRAAA(IR,IA)),VGBIOMETH_T.L(Y,IA,IHYDROGEN_GH2TOBIOMETH,S,T)*DAC_DE(IHYDROGEN_GH2TOBIOMETH))
*Adding electricity consumption from biogas upgrading generation
+ SUM(IAGK_HASORPOT(Y,IA,IHYDROGEN_GBIOGASUPGRADING)$(RRRAAA(IR,IA)),VGBIOGASUPGRADING_T.L(Y,IA,IHYDROGEN_GBIOGASUPGRADING,S,T)*BIOGASUPGRADING_DE(IHYDROGEN_GBIOGASUPGRADING))
*Adding electricity consumption from biogas methanation generation
+ SUM(IAGK_HASORPOT(Y,IA,IHYDROGEN_GBIOGASMETHANATION)$(RRRAAA(IR,IA)),VGBIOGASMETHANATION_T.L(Y,IA,IHYDROGEN_GBIOGASMETHANATION,S,T)*METHANATION_DE(IHYDROGEN_GBIOGASMETHANATION))
;
$label NO_HYDROGEN_DEMAND

$ifi not %OPTIFLOW%==yes $goto NO_OPTIFLOW_DEMAND_EL
EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_OPTIFLOW','MWh')$CCCRRR(C,IR)=
   +SUM(IA$(RRRAAA(IR,IA)),VFLOWBUFFER.L(Y,IA,'ElecBuffer','ELECFLOW',S,T))
;
$label NO_OPTIFLOW_DEMAND_EL

**LOSSES
EL_DEMAND_YCRST(Y,C,IR,S,T,'DIST_LOSSES','MWh')$CCCRRR(C,IR)=
* EXOGENOUS DEMAND
EL_DEMAND_YCRST(Y,C,IR,S,T,'EXOGENOUS','MWh')*DISLOSS_E(IR)/(1-DISLOSS_E(IR))
* P2H CONSUMPTION
+SUM(IA$RRRAAA(IR,IA), SUM(IGETOH$IAGK_HASORPOT(Y,IA,IGETOH), VGE_T.L(Y,IA,IGETOH,S,T)*DISLOSS_E_AG(IA,IGETOH)/(1-DISLOSS_E_AG(IA,IGETOH))))
* INTRASEASONAL ELECTRICITY STORAGE
$ifi not %stointers%==all +SUM(IA$RRRAAA(IR,IA),SUM(IGESTO$IAGK_HASORPOT(Y,IA,IGESTO),VESTOLOADT.L(Y,IA,IGESTO,S,T)*DISLOSS_E_AG(IA,IGESTO)/(1-DISLOSS_E_AG(IA,IGESTO))))
$ifi     %stointers%==all +SUM(IA$RRRAAA(IR,IA),SUM(IGESTO$IAGK_HASORPOT(Y,IA,IGESTO),VESTOLOADTS.L(Y,IA,IGESTO,S,T)*DISLOSS_E_AG(IA,IGESTO)/(1-DISLOSS_E_AG(IA,IGESTO))))
* INTERSEASONAL ELECTRICITY STORAGE
+SUM(IA$RRRAAA(IR,IA),SUM(IGESTOS$IAGK_HASORPOT(Y,IA,IGESTOS),VESTOLOADT.L(Y,IA,IGESTOS,S,T)*DISLOSS_E_AG(IA,IGESTOS)/(1-DISLOSS_E_AG(IA,IGESTOS))))
* EVs
$ifi not %EV%==yes $goto NO_EV2
                               +(
                                + VEV_VG2V_BEV.L(Y,IR,S,T)
                                + VEV_VG2V_PHEV.L(Y,IR,S,T)
                                )*EV_DISTLOSSLOAD(IR)/(1-EV_DISTLOSSLOAD(IR))
                                +(
$ifi %V2G%==yes                 - VEV_VV2G_BEV.L(Y,IR,S,T)
$ifi %V2G%==yes                 - VEV_VV2G_PHEV.L(Y,IR,S,T)
$ifi %STEPCHARGSCHEME%==yes     - VEV_VV2G_BEV.L(Y,IR,S,T)$(cs('v2g',IY411))
$ifi %STEPCHARGSCHEME%==yes     - VEV_VV2G_PHEV.L(Y,IR,S,T)$(cs('v2g',IY411))
                                +0)*EV_DISTLOSSUNLOAD(IR)/(1-EV_DISTLOSSUNLOAD(IR))
$label NO_EV2
*CCS
$ifi %CCS%==yes +SUM((IA,G)$(IAGK_HASORPOT(Y,IA,G) AND RRRAAA(IR,IA) AND CCS_G(G)),VGF_T.L(Y,IA,G,S,T) * IM_CO2(G)*IOF0001 * IOF3P6 * CCS_CO2CAPTEFF_G(G)* CCS_DECO2COMP_G(G)*DISLOSS_E_AG(IA,G)/(1-DISLOSS_E_AG(IA,G)))

$ifi not %HYDROGEN%==yes $goto NO_HYDROGEN_DEMAND2
+SUM(IAGK_HASORPOT(Y,IA,IHYDROGEN_GETOH2)$(RRRAAA(IR,IA)), VGE_T.L(Y,IA,IHYDROGEN_GETOH2,S,T)*DISLOSS_E_AG(IA,IHYDROGEN_GETOH2)/(1-DISLOSS_E_AG(IA,IHYDROGEN_GETOH2)))
+SUM(IAGK_HASORPOT(Y,IA,IHYDROGEN_GETOHH2)$(RRRAAA(IR,IA)), VGE_T.L(Y,IA,IHYDROGEN_GETOHH2,S,T)*DISLOSS_E_AG(IA,IHYDROGEN_GETOHH2)/(1-DISLOSS_E_AG(IA,IHYDROGEN_GETOHH2)))
+SUM(IAGK_HASORPOT(Y,IA,IHYDROGEN_GEHTOH2)$(RRRAAA(IR,IA)), VGE_T.L(Y,IA,IHYDROGEN_GEHTOH2,S,T)*DISLOSS_E_AG(IA,IHYDROGEN_GEHTOH2)/(1-DISLOSS_E_AG(IA,IHYDROGEN_GEHTOH2)))
+SUM(IAGK_HASORPOT(Y,IA,IHYDROGEN_GH2TOBIOMETH)$(RRRAAA(IR,IA)),VGBIOMETH_T.L(Y,IA,IHYDROGEN_GH2TOBIOMETH,S,T)*DAC_DE(IHYDROGEN_GH2TOBIOMETH)*DISLOSS_E_AG(IA,IHYDROGEN_GH2TOBIOMETH)/(1-DISLOSS_E_AG(IA,IHYDROGEN_GH2TOBIOMETH)))
+SUM(IAGK_HASORPOT(Y,IA,IHYDROGEN_GBIOGASUPGRADING)$(RRRAAA(IR,IA)),VGBIOGASUPGRADING_T.L(Y,IA,IHYDROGEN_GBIOGASUPGRADING,S,T)*BIOGASUPGRADING_DE(IHYDROGEN_GBIOGASUPGRADING)*DISLOSS_E_AG(IA,IHYDROGEN_GBIOGASUPGRADING)/(1-DISLOSS_E_AG(IA,IHYDROGEN_GBIOGASUPGRADING)))
+SUM(IAGK_HASORPOT(Y,IA,IHYDROGEN_GBIOGASMETHANATION)$(RRRAAA(IR,IA)),VGBIOGASMETHANATION_T.L(Y,IA,IHYDROGEN_GBIOGASMETHANATION,S,T)*METHANATION_DE(IHYDROGEN_GBIOGASMETHANATION)*DISLOSS_E_AG(IA,IHYDROGEN_GBIOGASMETHANATION)/(1-DISLOSS_E_AG(IA,IHYDROGEN_GBIOGASMETHANATION)))
$label NO_HYDROGEN_DEMAND2

*PRODUCTION SIDE
+SUM((IGE,IA,FFF,TECH_TYPE)$((NOT IGETOH(IGE)) AND RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY',TECH_TYPE,'MWh')*DISLOSS_E_AG(IA,IGE))
;

EL_DEMAND_YCRST(Y,C,IR,S,T,'TRANS_LOSSES','MWh')$CCCRRR(C,IR)=
*TRANSMISSION LOSSES
+SUM(IRE$IXK_HASORPOT(Y,IRE,IR),VX_T.L(Y,IRE,IR,S,T)*XLOSS(IRE,IR));


*ANNUAL DEMAND
** EXOGENOUS DEMAND

$ifi %TRANSPORT%==yes EL_DEMAND_YCR(Y,C,IR,'EXOGENOUS','TWh')$(CCCRRR(C,IR) AND SUM(DEUSER$(NOT IDEUSER_TRANS(DEUSER)),IDE_SUMST(IR,DEUSER)))=
$ifi %TRANSPORT%==yes SUM((DEUSER,S,T)$(IDE_SUMST(IR,DEUSER) AND (NOT IDEUSER_TRANS(DEUSER))), IHOURSINST(S,T)*IHOURFRAC*DE(Y,IR,DEUSER) * DE_VAR_T(IR,DEUSER,S,T) / IDE_SUMST(IR,DEUSER))/IOF1000000;

$ifi not %TRANSPORT%==yes EL_DEMAND_YCR(Y,C,IR,'EXOGENOUS','TWh')$(CCCRRR(C,IR) AND SUM(DEUSER,IDE_SUMST(IR,DEUSER)))=
$ifi not %TRANSPORT%==yes SUM((DEUSER,S,T)$(IDE_SUMST(IR,DEUSER)), IHOURSINST(S,T)*IHOURFRAC*DE(Y,IR,DEUSER) * DE_VAR_T(IR,DEUSER,S,T) / IDE_SUMST(IR,DEUSER))/IOF1000000;

$ifi %TRANSPORT%==yes EL_DEMAND_YCR(Y,C,IR,'EXO_TRANS','TWh')$(CCCRRR(C,IR) AND SUM(IDEUSER_TRANS,IDE_SUMST(IR,IDEUSER_TRANS)))=
$ifi %TRANSPORT%==yes SUM((IDEUSER_TRANS,S,T)$IDE_SUMST(IR,IDEUSER_TRANS), IHOURSINST(S,T)*IHOURFRAC*DE(Y,IR,IDEUSER_TRANS) * DE_VAR_T(IR,IDEUSER_TRANS,S,T) / IDE_SUMST(IR,IDEUSER_TRANS))/IOF1000000;

** ENDOGENOUS DEMAND

EL_DEMAND_YCR(Y,C,IR,'ENDOGENOUS_ELECT2HEAT','TWh')$CCCRRR(C,IR)=
* HEAT PUMPS CONSUMPTION
         SUM((S,T,IA)$RRRAAA(IR,IA), SUM(IGETOH$IAGK_HASORPOT(Y,IA,IGETOH), IHOURSINST(S,T)*IHOURFRAC*VGE_T.L(Y,IA,IGETOH,S,T)))/IOF1000000
;

EL_DEMAND_YCR(Y,C,IR,'ENDO_INTRASTO','TWh')$CCCRRR(C,IR)=
* INTRASEASONAL ELECTRICITY STORAGE LOSS
$ifi not %stointers%==all   +SUM((S,T,IA)$RRRAAA(IR,IA),SUM(IGESTO$IAGK_HASORPOT(Y,IA,IGESTO),IHOURSINST(S,T)*IHOURFRAC*(VESTOLOADT.L(Y,IA,IGESTO,S,T)-VGE_T.L(Y,IA,IGESTO,S,T))))/IOF1000000
$ifi     %stointers%==all   +SUM((S,T,IA)$RRRAAA(IR,IA),SUM(IGESTO$IAGK_HASORPOT(Y,IA,IGESTO),IHOURSINST(S,T)*IHOURFRAC*(VESTOLOADTS.L(Y,IA,IGESTO,S,T)-VGE_T.L(Y,IA,IGESTO,S,T))))/IOF1000000
;

EL_DEMAND_YCR(Y,C,IR,'ENDO_INTERSTO','TWh')$CCCRRR(C,IR)=
* INTERSEASONAL ELECTRICITY STORAGE LOSS
         +SUM((S,T,IA)$RRRAAA(IR,IA),SUM(IGESTOS$IAGK_HASORPOT(Y,IA,IGESTOS),IHOURSINST(S,T)*IHOURFRAC*(VESTOLOADTS.L(Y,IA,IGESTOS,S,T)-VGE_T.L(Y,IA,IGESTOS,S,T))))/IOF1000000
;

EL_DEMAND_YCR(Y,C,IR,'ENDO_EV','TWh')$CCCRRR(C,IR)=
         SUM((S,T),IHOURSINST(S,T)*EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_EV','MWh')*IHOURFRAC)/IOF1000000
;

EL_DEMAND_YCR(Y,C,IR,'ENDO_OTHERTRANS','TWh')$CCCRRR(C,IR)=
          SUM((S,T),IHOURSINST(S,T)*EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_OTHERTRANS','MWh')*IHOURFRAC)/IOF1000000
;

EL_DEMAND_YCR(Y,C,IR,'DIST_LOSSES','TWh')$CCCRRR(C,IR)=
           SUM((S,T),IHOURSINST(S,T)*EL_DEMAND_YCRST(Y,C,IR,S,T,'DIST_LOSSES','MWh')*IHOURFRAC)/IOF1000000
;

EL_DEMAND_YCR(Y,C,IR,'TRANS_LOSSES','TWh')$CCCRRR(C,IR)=
           SUM((S,T),IHOURSINST(S,T)*EL_DEMAND_YCRST(Y,C,IR,S,T,'TRANS_LOSSES','MWh')*IHOURFRAC)/IOF1000000
;

EL_DEMAND_YCR(Y,C,IR,'ENDO_CCS','TWh')$CCCRRR(C,IR)=
           SUM((S,T),IHOURSINST(S,T)*EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_CCS','MWh')*IHOURFRAC)/IOF1000000
;

EL_DEMAND_YCR(Y,C,IR,'ENDO_H2','TWh')$CCCRRR(C,IR)=
           SUM((S,T),IHOURSINST(S,T)*EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_H2','MWh')*IHOURFRAC)/IOF1000000
;

EL_DEMAND_YCR(Y,C,IR,'ENDO_OPTIFLOW','TWh')$CCCRRR(C,IR)=
           SUM((S,T),IHOURSINST(S,T)*EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_OPTIFLOW','MWh')*IHOURFRAC)/IOF1000000
;

EL_DEMAND_YCR(Y,C,IR,'ENDO_BIOMETHANE','TWh')$CCCRRR(C,IR)=
           SUM((S,T),IHOURSINST(S,T)*EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_BIOMETHANE','MWh')*IHOURFRAC)/IOF1000000
;

* ----------- END OF ELECTRICITY DEMAND --------------

* ------------- HEAT DEMAND --------------
*HOURLY DEMAND
** EXOGENOUS DEMAND

H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'EXOGENOUS','MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND SUM(DHUSER,IDH_SUMST(IA,DHUSER)))=
SUM((DHUSER)$IDH_SUMST(IA,DHUSER), DH(Y,IA,DHUSER) * DH_VAR_T(IA,DHUSER,S,T) / IDH_SUMST(IA,DHUSER))

$ifi not %INDIVUSERS%==yes         $goto NO_INDIVUSERSGROUP
$ifi not %GROUP_DEMAND_SHIFT%==yes $goto NO_INDIVUSERSGROUP
+ (
*Removing default demand
     -SUM((DHUSER)$IDH_SUMST(IA,DHUSER), DH(Y,IA,DHUSER) * DH_VAR_T(IA,DHUSER,S,T) / IDH_SUMST(IA,DHUSER))
*Introducing new allocated demand
    +SUM((INDIVUSERS_GROUP,DHUSER)$INDIVUSERS_GROUP_A(IA,DHUSER,INDIVUSERS_GROUP),
             VINDIVUSERS_GROUPSHARE.L(Y,IR,DHUSER,INDIVUSERS_GROUP)* DH_VAR_T(IA,DHUSER,S,T)/ IDH_SUMST(IA,DHUSER)
        )*SUM((INDIVUSERS_DEMANDTYPE,DHUSER)$INDIVUSERS_DEMANDTYPE_A(IA,DHUSER,INDIVUSERS_DEMANDTYPE),
              SUM(IAI$(RRRAAA(IR,IAI) AND INDIVUSERS_DEMANDTYPE_A(IAI,DHUSER,INDIVUSERS_DEMANDTYPE)), DH(Y,IAI,DHUSER))
             )
  )$(INDIVUSERS_AAA(IA)
     AND SUM((INDIVUSERS_GROUP,DHUSER),INDIVUSERS_GROUP_A(IA,DHUSER,INDIVUSERS_GROUP))
     AND SUM((INDIVUSERS_DEMANDTYPE,DHUSER),INDIVUSERS_DEMANDTYPE_A(IA,DHUSER,INDIVUSERS_DEMANDTYPE)))
$label NO_INDIVUSERSGROUP
;

** ENDOGENOUS DEMAND

H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'ENDO_INTRASTO','MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=
* INTRASEASONAL HEAT STORAGE
$ifi not %stointers%==all        SUM(IGHSTO$IAGK_HASORPOT(Y,IA,IGHSTO),VHSTOLOADT.L(Y,IA,IGHSTO,S,T))
$ifi     %stointers%==all        SUM(IGHSTO$IAGK_HASORPOT(Y,IA,IGHSTO),VHSTOLOADTS.L(Y,IA,IGHSTO,S,T))

;

H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'ENDO_INTERSTO','MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=
* INTERSEASONAL HEAT STORAGE
        + SUM(IGHSTOS$IAGK_HASORPOT(Y,IA,IGHSTOS),VHSTOLOADTS.L(Y,IA,IGHSTOS,S,T))
;

**LOSSES
H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'DIST_LOSSES','MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=
* EXOGENOUS DEMAND
H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'EXOGENOUS','MWh')*DISLOSS_H(IA)/(1-DISLOSS_H(IA))
;

$ifi %HEATTRANS%==yes H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'TRANS_LOSSES','MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=
$ifi %HEATTRANS%==yes *TRANSMISSION LOSSES
$ifi %HEATTRANS%==yes +SUM(IAE$IXHK_HASORPOT(Y,IAE,IA),VXH_T.L(Y,IAE,IA,S,T)*XHLOSS(IAE,IA));
$ifi %HEATTRANS%==yes ;

$ifi not %HYDROGEN%==yes $goto NO_HYDROGEN_DEMAND3
H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'ENDO_H2','MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=
+SUM(IAGK_HASORPOT(Y,IA,IHYDROGEN_GEHTOH2)$(RRRAAA(IR,IA)), VGH_T.L(Y,IA,IHYDROGEN_GEHTOH2,S,T))
;

$ifi not %OPTIFLOW%==yes $goto NO_OPTIFLOW_DEMAND_H
H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'ENDO_OPTIFLOW','MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=
*   +SUM(IA$(RRRAAA(IR,IA)),VFLOWBUFFER.L(Y,IA,'HeatBuffer','HEATFLOW',S,T))
   +VFLOWBUFFER.L(Y,IA,'HeatBuffer','HEATFLOW',S,T)
;
$label NO_OPTIFLOW_DEMAND_H

H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'ENDO_BIOMETHANE','MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=
+ SUM(IAGK_HASORPOT(Y,IA,IHYDROGEN_GH2TOBIOMETH)$(RRRAAA(IR,IA)),VGBIOMETH_T.L(Y,IA,IHYDROGEN_GH2TOBIOMETH,S,T)*DAC_DH(IHYDROGEN_GH2TOBIOMETH))
;
$label NO_HYDROGEN_DEMAND3


*ANNUAL DEMAND
** EXOGENOUS DEMAND

H_DEMAND_YCRA(Y,C,IR,IA,'EXOGENOUS','TWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND SUM(DHUSER,IDH_SUMST(IA,DHUSER)))=
SUM((S,T),IHOURSINST(S,T)*H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'EXOGENOUS','MWh')*IHOURFRAC)/IOF1000000;
;

** ENDOGENOUS DEMAND
H_DEMAND_YCRA(Y,C,IR,IA,'ENDO_INTRASTO','TWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=
* INTRASEASONAL HEAT STORAGE LOSS
$ifi not %stointers%==all        SUM((S,T,IGHSTO)$IAGK_HASORPOT(Y,IA,IGHSTO),IHOURSINST(S,T)*IHOURFRAC*(VHSTOLOADT.L(Y,IA,IGHSTO,S,T)-VGH_T.L(Y,IA,IGHSTO,S,T)))/IOF1000000
$ifi     %stointers%==all        SUM((S,T,IGHSTO)$IAGK_HASORPOT(Y,IA,IGHSTO),IHOURSINST(S,T)*IHOURFRAC*(VHSTOLOADTS.L(Y,IA,IGHSTO,S,T)-VGH_T.L(Y,IA,IGHSTO,S,T)))/IOF1000000

;

H_DEMAND_YCRA(Y,C,IR,IA,'ENDO_INTERSTO','TWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=

* INTERSEASONAL HEAT STORAGE LOSS
        + SUM((S,T,IGHSTOS)$IAGK_HASORPOT(Y,IA,IGHSTOS),IHOURSINST(S,T)*IHOURFRAC*(VHSTOLOADTS.L(Y,IA,IGHSTOS,S,T)-VGH_T.L(Y,IA,IGHSTOS,S,T)))/IOF1000000
;

H_DEMAND_YCRA(Y,C,IR,IA,'DIST_LOSSES','TWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=
           SUM((S,T),IHOURSINST(S,T)*IHOURFRAC*H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'DIST_LOSSES','MWh'))/IOF1000000
;

H_DEMAND_YCRA(Y,C,IR,IA,'TRANS_LOSSES','TWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=
           SUM((S,T),IHOURSINST(S,T)*IHOURFRAC*H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'TRANS_LOSSES','MWh'))/IOF1000000
;

H_DEMAND_YCRA(Y,C,IR,IA,'ENDO_H2','TWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=
           SUM((S,T),IHOURSINST(S,T)*IHOURFRAC*H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'ENDO_H2','MWh'))/IOF1000000
;

H_DEMAND_YCRA(Y,C,IR,IA,'ENDO_OPTIFLOW','TWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=
           SUM((S,T),IHOURSINST(S,T)*H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'ENDO_OPTIFLOW','MWh')*IHOURFRAC)/IOF1000000
;

H_DEMAND_YCRA(Y,C,IR,IA,'ENDO_BIOMETHANE','TWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=
           SUM((S,T),IHOURSINST(S,T)*IHOURFRAC*H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'ENDO_BIOMETHANE','MWh'))/IOF1000000
;
* ----------- END OF HEAT DEMAND --------------

$ifi not %HYDROGEN%==yes $goto NO_HYDROGEN_DEMAND30
* ------------- H2 DEMAND --------------

*HOURLY
** EXOGENOUS DEMAND

H2_DEMAND_YCRST(Y,C,IR,S,T,'EXOGENOUS','MWh')$(CCCRRR(C,IR))=
   IHYDROGEN_DH2_REGION_T_Y(Y,IR,S,T)+
   SUM(IA$RRRAAA(IR,IA),IHYDROGEN_DH2_AREA_T_Y(Y,IA,S,T))
;

$ifi not %flexible_h2_space%==yes $goto no_flexible_h2_space
H2_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_REGIONAL','MWh')$(CCCRRR(C,IR))=
   VGE_T_FLEXDH2.L(Y,IR,S,T)
;
$label no_flexible_h2_space

H2_DEMAND_YCRST(Y,C,IR,S,T,'DIST_LOSSES','MWh')$CCCRRR(C,IR)=
 IHYDROGEN_DH2_REGION_T_Y(Y,IR,S,T)*DISLOSS_H2(IR)/(1-DISLOSS_H2(IR))+
 SUM(IA$RRRAAA(IR,IA),IHYDROGEN_DH2_AREA_T_Y(Y,IA,S,T)*DISLOSS_H2(IA)/(1-DISLOSS_H2(IA)))
$ifi %flexible_h2_space%==yes + VGE_T_FLEXDH2.L(Y,IR,S,T)*DISLOSS_H2(IR)/(1-DISLOSS_H2(IR))
;

** ENDOGENOUS DEMAND

H2_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_INTERSTO','MWh')$CCCRRR(C,IR)=
* INTERSEASONAL H2 STORAGE
         +SUM(IA$RRRAAA(IR,IA),SUM(IHYDROGEN_GH2STO$IAGK_HASORPOT(Y,IA,IHYDROGEN_GH2STO),VHYDROGEN_STOLOADT.L(Y,IA,IHYDROGEN_GH2STO,S,T)))
;

$ifi %TRANSPORT%==yes H2_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_OTHERTRANS','MWh')$CCCRRR(C,IR)=
$ifi %TRANSPORT%==yes $ifi %TRANSPORT_SYNTHFUEL_COMMODITY%==HYDROGEN +VTRANSDEMAND_T.L(Y,IR,S,T)
$ifi %TRANSPORT%==yes +0;


H2_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_FUELCELL','MWh')$CCCRRR(C,IR)=
*Adding H2 consumption from FUELCELLS generation
+ SUM(IAGK_HASORPOT(Y,IA,G)$(RRRAAA(IR,IA) AND GTECH_TYPE(G,'FUELCELL')),VGF_T.L(Y,IA,G,S,T))
;

H2_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_BIOMETHANE','MWh')$CCCRRR(C,IR)=
*Adding H2 consumption from BIOMETHANE-DAC generation
+ SUM(IAGK_HASORPOT(Y,IA,IHYDROGEN_GH2TOBIOMETH)$(RRRAAA(IR,IA)),VGBIOMETH_T.L(Y,IA,IHYDROGEN_GH2TOBIOMETH,S,T)/GDATA(IHYDROGEN_GH2TOBIOMETH,'GDFE'))
;

$ifi not %OPTIFLOW%==yes $goto NO_OPTIFLOW_DEMAND_H2
H2_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_OPTIFLOW','MWh')$CCCRRR(C,IR)=
   +SUM(IA$(RRRAAA(IR,IA)),VFLOWSOURCE.L(Y,IA,'HydrogenSource_MWh','HYDROGENFLOW_MWh',S,T))
;
$label NO_OPTIFLOW_DEMAND_H2


**LOSSES

H2_DEMAND_YCRST(Y,C,IR,S,T,'TRANS_LOSSES','MWh')$CCCRRR(C,IR)=
*TRANSMISSION LOSSES
+SUM(IRE$IXH2K_HASORPOT(Y,IRE,IR),VXH2_T.L(Y,IRE,IR,S,T)*XH2LOSS(IRE,IR));


*ANNUAL DEMAND
** EXOGENOUS DEMAND

H2_DEMAND_YCR(Y,C,IR,'EXOGENOUS','TWh')$(CCCRRR(C,IR))=
SUM((S,T), IHOURSINST(S,T)*IHOURFRAC*H2_DEMAND_YCRST(Y,C,IR,S,T,'EXOGENOUS','MWh'))/IOF1000000;

** ENDOGENOUS DEMAND

H2_DEMAND_YCR(Y,C,IR,'ENDO_INTERSTO','TWh')$CCCRRR(C,IR)=
* INTERSEASONAL H2 STORAGE LOSS
         +SUM((S,T,IA)$RRRAAA(IR,IA),SUM(IHYDROGEN_GH2STO$IAGK_HASORPOT(Y,IA,IHYDROGEN_GH2STO),IHOURSINST(S,T)*IHOURFRAC*(VHYDROGEN_STOLOADT.L(Y,IA,IHYDROGEN_GH2STO,S,T)-VHYDROGEN_GH2_T.L(Y,IA,IHYDROGEN_GH2STO,S,T))))/IOF1000000
;

H2_DEMAND_YCR(Y,C,IR,'ENDO_FUELCELL','TWh')$CCCRRR(C,IR)=
          SUM((S,T),IHOURSINST(S,T)*H2_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_FUELCELL','MWh')*IHOURFRAC)/IOF1000000
;

H2_DEMAND_YCR(Y,C,IR,'ENDO_OTHERTRANS','TWh')$CCCRRR(C,IR)=
          SUM((S,T),IHOURSINST(S,T)*H2_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_OTHERTRANS','MWh')*IHOURFRAC)/IOF1000000
;

H2_DEMAND_YCR(Y,C,IR,'ENDO_OPTIFLOW','TWh')$CCCRRR(C,IR)=
           SUM((S,T),IHOURSINST(S,T)*H2_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_OPTIFLOW','MWh')*IHOURFRAC)/IOF1000000
;

H2_DEMAND_YCR(Y,C,IR,'TRANS_LOSSES','TWh')$CCCRRR(C,IR)=
           SUM((S,T),IHOURSINST(S,T)*H2_DEMAND_YCRST(Y,C,IR,S,T,'TRANS_LOSSES','MWh')*IHOURFRAC)/IOF1000000
;

H2_DEMAND_YCR(Y,C,IR,'DIST_LOSSES','TWh')$CCCRRR(C,IR)=
           SUM((S,T),IHOURSINST(S,T)*H2_DEMAND_YCRST(Y,C,IR,S,T,'DIST_LOSSES','MWh')*IHOURFRAC)/IOF1000000
;

H2_DEMAND_YCR(Y,C,IR,'ENDO_BIOMETHANE','TWh')$CCCRRR(C,IR)=
           SUM((S,T),IHOURSINST(S,T)*H2_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_BIOMETHANE','MWh')*IHOURFRAC)/IOF1000000
;

$ifi not %flexible_h2_space%==yes $goto no_flexible_h2_space2
H2_DEMAND_YCR(Y,C,IR,'ENDO_REGIONAL','TWh')$CCCRRR(C,IR)=
           SUM((S,T),IHOURSINST(S,T)*H2_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_REGIONAL','MWh')*IHOURFRAC)/IOF1000000
;
$label no_flexible_h2_space2

* ----------- END OF H2 DEMAND --------------
$label NO_HYDROGEN_DEMAND30

* -------------------- END OF ENERGY DEMAND -----------------------


* -------------------- TRANSMISSION CAPACITY -----------------------

** EXOGENOUS ELECTRICITY CAPACITY

X_CAP_YCR(Y,C,IR,IRI,'EXOGENOUS','GW')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=
IOF0001*XKFX(Y,IR,IRI)
;


** ENDOGENOUS ELECTRIC CAPACITY

$ifi not %TransInvest%==yes $goto Transmission_investments_end
X_CAP_YCR(Y,C,IR,IRI,'ENDOGENOUS','GW')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=
IOF0001*VXKNACCUMNET.L(Y,IR,IRI)
;
$label  Transmission_investments_end


* -------------------- END OF TRANSMISSION CAPACITY -----------------------

* -------------------- TRANSMISSION FLOW -----------------------

** HOURLY TRANSMISSION FLOW

X_FLOW_YCRST(Y,C,IR,IRI,S,T,'MWh')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=
VX_T.L(Y,IR,IRI,S,T);
;


** ANNUAL TRANSMISSION FLOW
X_FLOW_YCR(Y,C,IR,IRI,'TWh')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=
SUM((S,T),IHOURSINST(S,T)*IHOURFRAC*VX_T.L(Y,IR,IRI,S,T))/IOF1000000;
;



* -------------------- END OF TRANSMISSION FLOW -----------------------

$ifi NOT %HEATTRANS%==yes $goto no_heattrans
* -------------------- HEAT TRANSMISSION CAPACITY -----------------------

** EXOGENOUS HEAT TRANSMISSION CAPACITY

XH_CAP_YCA(Y,C,IA,IAI,'EXOGENOUS','GW')$(ICA(C,IA) AND (XHKFX(Y,IA,IAI) OR XHKFX_DH(Y,IA,IAI)))=
IOF0001*(XHKFX(Y,IA,IAI) +  XHKFX_DH(Y,IA,IAI))
;

** ENDOGENOUS HEAT TRANSMISSION CAPACITY

$ifi not %HeatTransInvest%==yes $goto Heat_Transmission_investments_end
XH_CAP_YCA(Y,C,IA,IAI,'ENDOGENOUS','GW')$(ICA(C,IA) AND IXHK_HASORPOT(Y,IA,IAI))=
IOF0001*VXHKNACCUMNET.L(Y,IA,IAI)
;
$label  Heat_Transmission_investments_end


* -------------------- END OF HEAT TRANSMISSION CAPACITY -----------------------

* -------------------- HEAT TRANSMISSION FLOW -----------------------

** HOURLY HEAT TRANSMISSION FLOW

XH_FLOW_YCAST(Y,C,IA,IAI,S,T,'MWh')$(ICA(C,IA) AND IXHK_HASORPOT(Y,IA,IAI))=
VXH_T.L(Y,IA,IAI,S,T);
;

** ANNUAL HEAT TRANSMISSION FLOW
XH_FLOW_YCA(Y,C,IA,IAI,'TWh')$(ICA(C,IA) AND IXHK_HASORPOT(Y,IA,IAI))=
SUM((S,T),IHOURSINST(S,T)*IHOURFRAC*VXH_T.L(Y,IA,IAI,S,T))/IOF1000000;
;

* -------------------- END OF HEAT TRANSMISSION FLOW -----------------------
$label no_heattrans


$ifi NOT %hydrogen%==yes $goto no_hydrogentrans
* -------------------- H2 TRANSMISSION CAPACITY -----------------------

** EXOGENOUS H2 TRANSMISSION CAPACITY

XH2_CAP_YCR(Y,C,IR,IRI,'EXOGENOUS','GW')$(CCCRRR(C,IR) AND XH2KFX(Y,IR,IRI))=
IOF0001*XH2KFX(Y,IR,IRI)
;

** ENDOGENOUS H2 TRANSMISSION CAPACITY

$ifi not %H2TransInvest%==yes $goto H2_Transmission_investments_end
XH2_CAP_YCR(Y,C,IR,IRI,'ENDOGENOUS','GW')$(CCCRRR(C,IR) AND IXH2K_HASORPOT(Y,IR,IRI))=
IOF0001*(VXH2KNACCUMNET.L(Y,IR,IRI)$SUM(IYALIAS2$(YVALUE(IYALIAS2) LE YVALUE(Y)),IXH2KN(IYALIAS2,IR,IRI))
          +VXH2KNACCUMNET_NGTOH2.L(Y,IR,IRI)$SUM(IYALIAS2$(YVALUE(IYALIAS2) LE YVALUE(Y)),IXH2KN_NGTOH2(IYALIAS2,IR,IRI)))
;
$label  H2_Transmission_investments_end


* -------------------- END OF HEAT TRANSMISSION CAPACITY -----------------------

* -------------------- H2 TRANSMISSION FLOW -----------------------

** HOURLY H2 TRANSMISSION FLOW

XH2_FLOW_YCRST(Y,C,IR,IRI,S,T,'MWh')$(CCCRRR(C,IR) AND IXH2K_HASORPOT(Y,IR,IRI))=
VXH2_T.L(Y,IR,IRI,S,T);
;

** ANNUAL H2 TRANSMISSION FLOW
XH2_FLOW_YCR(Y,C,IR,IRI,'TWh')$(CCCRRR(C,IR) AND IXH2K_HASORPOT(Y,IR,IRI))=
SUM((S,T),IHOURSINST(S,T)*IHOURFRAC*VXH2_T.L(Y,IR,IRI,S,T))/IOF1000000;
;

* -------------------- END OF H2 TRANSMISSION FLOW -----------------------
$label no_hydrogentrans

* ---------------------- ECONOMIC OUTPUT ----------------------

* ------------- COMMODITY PRICES --------------

** ELECTRICITY PRICES

*HOURLY ELECTRICITY PRICE

EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')$CCCRRR(C,IR)=OMONEY*QEEQ.M(Y,IR,S,T)
$ifi %RollingSeasons%==yes         /IHOURSINTROLLINGSEASON(S,T)
$ifi not %RollingSeasons%==yes     /IHOURSINST(S,T)
/IDISCOUNTFACTOR(Y)/IWEIGHTY(Y);

*AVERAGE ELECTRICITY PRICE

EL_PRICE_YCR(Y,C,IR,'AVERAGE','Money_per_MWh')$CCCRRR(C,IR)=
SUM((S,T),EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')/(IWEIGHSUMS*IWEIGHSUMT/(WEIGHT_S(S)*WEIGHT_T(T))));

$ontext
*EXCLUDED SO FAR
*AVERAGE ELECTRICITY PRICE WEIGTHED BY CONSUMPTION

EL_PRICE_YCR(Y,C,IR,'AVERAGE_WEIGHTED_BY_CONSUMPTION','Money_per_MWh')$CCCRRR(C,IR)=
               SUM((VARIABLE_CATEGORY,S,T),
                         EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')*EL_DEMAND_YCRST(Y,C,IR,S,T,VARIABLE_CATEGORY,'MWh')
                         )
               /SUM((VARIABLE_CATEGORY,S,T),EL_DEMAND_YCRST(Y,C,IR,S,T,VARIABLE_CATEGORY,'MWh'))
;

*AVERAGE ELECTRICITY PRICE WEIGTHED BY PRODUCTION

EL_PRICE_YCR(Y,C,IR,'AVERAGE_WEIGHTED_BY_PRODUCTION','Money_per_MWh')$CCCRRR(C,IR)=
               SUM((IGE,IA,FFF,S,T,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,IGE)),
                         EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')*PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY',TECH_TYPE,'MWh')
                         )
               /SUM((IGE,IA,FFF,S,T,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY',TECH_TYPE,'MWh'))
;
$offtext

** END OF ELECTRICITY PRICES

** heatING PRICES

*HOURLY heatING PRICE

$ifi %BalancingMarket%==yes $ifi %NOHEATMARKET%==yes $goto NO_HEATMARKET
H_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND (SUM(DHUSER, IDH_SUMST(IA,DHUSER))
$ifi  %HEATTRANS%==yes OR SUM(IAE,IXHK_HASORPOT(Y,IAE,IA)) OR SUM(IAI,IXHK_HASORPOT(Y,IA,IAI))
))=OMONEY*QHEQ.M(Y,IA,S,T)
$ifi %RollingSeasons%==yes         /IHOURSINTROLLINGSEASON(S,T)
$ifi not %RollingSeasons%==yes     /IHOURSINST(S,T)
/IDISCOUNTFACTOR(Y)/IWEIGHTY(Y);
$label NO_HEATMARKET
$ifi %BalancingMarket%==yes $ifi %NOHEATMARKET%==yes  H_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=0;



*AVERAGE heatING PRICE

H_PRICE_YCRA(Y,C,IR,IA,'AVERAGE','Money_per_MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND (SUM(DHUSER, IDH_SUMST(IA,DHUSER))
$ifi  %HEATTRANS%==yes OR SUM(IAE,IXHK_HASORPOT(Y,IAE,IA)) OR SUM(IAI,IXHK_HASORPOT(Y,IA,IAI))
))=
SUM((S,T),H_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')/(IWEIGHSUMS*IWEIGHSUMT/(WEIGHT_S(S)*WEIGHT_T(T))));

$ontext
*EXCLUDED SO FAR
*AVERAGE heatING PRICE WEIGTHED BY CONSUMPTION

H_PRICE_YCRA(Y,C,IR,IA,'AVERAGE_WEIGHTED_BY_CONSUMPTION','Money_per_MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND SUM(DHUSER, IDH_SUMST(IA,DHUSER)))=
               SUM((VARIABLE_CATEGORY,S,T),
                         H_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')*H_DEMAND_YCRAST(Y,C,IR,IA,S,T,VARIABLE_CATEGORY,'MWh')
                         )
               /SUM((VARIABLE_CATEGORY,S,T),H_DEMAND_YCRAST(Y,C,IR,IA,S,T,VARIABLE_CATEGORY,'MWh'))
;

*AVERAGE heatING PRICE WEIGTHED BY PRODUCTION

H_PRICE_YCRA(Y,C,IR,IA,'AVERAGE_WEIGHTED_BY_PRODUCTION','Money_per_MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND SUM(DHUSER, IDH_SUMST(IA,DHUSER)))=
               SUM((IGH,FFF,S,T,TECH_TYPE)$IAGK_HASORPOT(Y,IA,IGH),
                         H_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')*PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'HEAT',TECH_TYPE,'MWh')
                         )
               /SUM((IGH,FFF,S,T,TECH_TYPE)$IAGK_HASORPOT(Y,IA,IGH),PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'HEAT',TECH_TYPE,'MWh'))
;
$offtext

** END OF heatING PRICES

$ifi NOT %HYDROGEN%==yes $goto NO_HYDROGEN_PRICE

*HOURLY HYDROGEN PRICE
H2_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')$CCCRRR(C,IR)=OMONEY*QHYDROGEN_EQ.M(Y,IR,S,T)
$ifi %RollingSeasons%==yes         /IHOURSINTROLLINGSEASON(S,T)
$ifi not %RollingSeasons%==yes     /IHOURSINST(S,T)
/IDISCOUNTFACTOR(Y)/IWEIGHTY(Y);

*AVERAGE Hydrogen PRICE

H2_PRICE_YCR(Y,C,IR,'AVERAGE','Money_per_MWh')$CCCRRR(C,IR)=
SUM((S,T),H2_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')/(IWEIGHSUMS*IWEIGHSUMT/(WEIGHT_S(S)*WEIGHT_T(T))));


*HOURLY BIOMETHANE PRICE
BIOMETH_PRICE_YST(Y,S,T,'Money_per_MWh')=OMONEY*QBIOMETHANE_EQ.M(Y,S,T)
$ifi %RollingSeasons%==yes         /IHOURSINTROLLINGSEASON(S,T)
$ifi not %RollingSeasons%==yes     /IHOURSINST(S,T)
/IDISCOUNTFACTOR(Y)/IWEIGHTY(Y);

$label NO_HYDROGEN_PRICE

* ------------- END OF COMMODITY PRICES --------------


* ------------- COSTS --------------

* -- GENERATION COSTS -----

** GENERATION INVESTMENTS

ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_CAPITAL_COSTS','Mmoney')$(CCCRRR(C,IR) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and RRRAAA(IR,IA))=
         OMONEY*(
               SUM(IYALIAS2$((ORD(IYALIAS2) LE ORD(Y)) AND IAGKNY(IYALIAS2,IA,G) AND (NOT IGKN_ES(G))),IYHASANNUITYG(IYALIAS2,Y,G)*VGKN.L(IYALIAS2,IA,G)*GINVCOST(IA,G)*ANNUITYCG(C,G))
$ifi %OFFSHOREGRID%==yes  + SUM(IYALIAS2$((ORD(IYALIAS2) LE ORD(Y)) AND IAGKNY(IYALIAS2,IA,G) AND IGKN_ES(G)),IYHASANNUITYG(IYALIAS2,Y,G)*SUM(XES,VGLAMBDA.L(IYALIAS2,IA,G,XES)*GINVCOST_ES(G,XES))*ANNUITYCG(C,G))
)
;

** GENEARTION FIXED COSTS

ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_FIXED_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and IAGK_HASORPOT(Y,IA,G))=
         OMONEY/IOF1000*GOMFCOST(IA,G)*(
         GKFX(Y,IA,G)
         +VGKNACCUMNET.L(Y,IA,G)
$ifi %DECOM%==yes         -VDECOM_EXO_ACCUM.L(Y,IA,G)$IGDECOMEXOPOT(Y,IA,G)
         )
;

** GENERATION O&M COSTS

ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_OPERATIONAL_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and IAGK_HASORPOT(Y,IA,G))=
   OMONEY/IOF1000000*
     (
      (IHOURFRAC *GOMVCOSTO(IA,G)*(
      SUM((S,T), IHOURSINST(S,T) * VGE_T.L(Y,IA,G,S,T))$IGKE(G)
$ifi %HYDROGEN%==yes    + SUM((S,T), IHOURSINST(S,T)*VHYDROGEN_GH2_T.L(Y,IA,G,S,T))$(IHYDROGEN_GH2STO(G) OR IHYDROGEN_GCH4TOH2(G))
$ifi %HYDROGEN%==yes    + SUM((S,T), IHOURSINST(S,T)*VGBIOMETH_T.L(Y,IA,G,S,T))$IHYDROGEN_GH2TOBIOMETH(G)
$ifi %HYDROGEN%==yes    + SUM((S,T), IHOURSINST(S,T)*VGBIOGASMETHANATION_T.L(Y,IA,G,S,T))$IHYDROGEN_GBIOGASMETHANATION(G)
$ifi %HYDROGEN%==yes    + SUM((S,T), IHOURSINST(S,T)*VGBIOGASUPGRADING_T.L(Y,IA,G,S,T))$IHYDROGEN_GBIOGASUPGRADING(G)
      ))
   +  (IHOURFRAC *GOMVCOSTO(IA,G) * GDATA(G,'GDCV') *
      (
   +   SUM((S,T), IHOURSINST(S,T) * VGH_T.L(Y,IA,G,S,T))))$IGKH(G)

   +  (IHOURFRAC *GOMVCOSTIN(IA,G)*
      (
   +   SUM((S,T), IHOURSINST(S,T) * VGF_T.L(Y,IA,G,S,T))))
     ) ;

** GENERATION FUEL COSTS

ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_FUEL_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and IAGK_HASORPOT(Y,IA,G))=
   OMONEY/IOF1000000*(
                 IHOURFRAC *FUELPRICE(Y,IA,FFF)*IOF3P6
$ifi not %STEPWISEPRICE%==yes $goto No_STEPWISEPRICE
                 +(QSTEPWISEPRICE_GFTOSTEP.M(Y,C,FFF)/(IDISCOUNTFACTOR(Y)*IWEIGHTY(Y)))$ISTEPWISEPRICE_F(Y,C,FFF)
$label No_STEPWISEPRICE
                 )*
   (
                 SUM((S,T), IHOURSINST(S,T) * VGF_T.L(Y,IA,G,S,T) )$IAGK_HASORPOT(Y,IA,G)
    )$(IGNOTETOH(G) and (not IGESTO(G)) and (not IGHSTO(G)) and (not IGESTOS(G)) and (not IGHSTOS(G)))


* Electricity storages:
$ifi not %stointers%==all   +OMONEY/IOF1000000*SUM((S,T), EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')*VESTOLOADT.L(Y,IA,G,S,T))$IGESTO(G)
$ifi     %stointers%==all   +OMONEY/IOF1000000*SUM((S,T), EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')*VESTOLOADTS.L(Y,IA,G,S,T))$IGESTO(G)
    +OMONEY/IOF1000000*SUM((S,T), EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')*VESTOLOADTS.L(Y,IA,G,S,T))$IGESTOS(G)
* Heat storages:
$ifi not %stointers%==all     +OMONEY/IOF1000000*SUM((S,T), H_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')*VHSTOLOADT.L(Y,IA,G,S,T))$IGHSTO(G)
$ifi     %stointers%==all     +OMONEY/IOF1000000*SUM((S,T), H_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')*VHSTOLOADTS.L(Y,IA,G,S,T))$IGHSTO(G)
    +OMONEY/IOF1000000*SUM((S,T), H_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')*VHSTOLOADTS.L(Y,IA,G,S,T))$IGHSTOS(G)
* Electricity to heat:
    +OMONEY/IOF1000000*SUM((S,T), EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')*VGE_T.L(Y,IA,G,S,T))$(IAGK_HASORPOT(Y,IA,G) and IGETOH(G))

$ifi not %HYDROGEN%==yes $goto NO_HYDROGEN_PRICE2
     +OMONEY/IOF1000000*SUM((S,T), EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')*VGE_T.L(Y,IA,G,S,T))$(IHYDROGEN_GETOH2(G) OR IHYDROGEN_GEHTOH2(G) OR IHYDROGEN_GETOHH2(G))
     +OMONEY/IOF1000000*SUM((S,T), H_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')*VGH_T.L(Y,IA,G,S,T))$IHYDROGEN_GEHTOH2(G)
     +OMONEY/IOF1000000*SUM((S,T), H2_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')*VHYDROGEN_STOLOADT.L(Y,IA,G,S,T))$IHYDROGEN_GH2STO(G)
     +OMONEY/IOF1000000*SUM((S,T), H2_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')*VGF_T.L(Y,IA,G,S,T))$IHYDROGEN_GH2FUEL(G)
     +OMONEY/IOF1000000*SUM((S,T), H2_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')*VGF_T.L(Y,IA,G,S,T)
                                   + H_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')*VGBIOMETH_T.L(Y,IA,G,S,T)*DAC_DH(G)
                                   + EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')*VGBIOMETH_T.L(Y,IA,G,S,T)*DAC_DE(G)
                        )$IHYDROGEN_GH2TOBIOMETH(G)
     +OMONEY/IOF1000000*SUM((S,T),  EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')*VGBIOGASUPGRADING_T.L(Y,IA,G,S,T)*BIOGASUPGRADING_DE(G)
                        )$IHYDROGEN_GBIOGASUPGRADING(G)
     +OMONEY/IOF1000000*SUM((S,T),      H2_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')*VGBIOGASMETHANATION_T.L(Y,IA,G,S,T)*METHANATION_DH2(G)
                                   + EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')*VGBIOGASMETHANATION_T.L(Y,IA,G,S,T)*METHANATION_DE(G)
                        )$IHYDROGEN_GBIOGASMETHANATION(G)
     +OMONEY/IOF1000000*SUM((S,T), BIOMETH_PRICE_YST(Y,S,T,'Money_per_MWh')*VGF_T.L(Y,IA,G,S,T))$IGBIOMETHANE(G)
$label NO_HYDROGEN_PRICE2
;

** CO2 EMISSIONS COST
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_CO2_TAX','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and IAGK_HASORPOT(Y,IA,G))=
         SUM(GROUP$(GROUP_AAA(GROUP,IA)),OMONEY/IOF1000000*IHOURFRAC *IM_CO2(G)*IOF0001 * IOF3P6 *EMI_POL(Y,C,GROUP,"TAX_CO2") * SUM((S,T), IHOURSINST(S,T)*(VGF_T.L(Y,IA,G,S,T)
$ifi %CCS%==yes    -(VGF_T.L(Y,IA,G,S,T)*CCS_CO2CAPTEFF_G(G))$CCS_G(G)
)))
;

** CO2 TRANSPORT COST
$ifi %CCS%==yes ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_CO2_TRANSPORT','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and IAGK_HASORPOT(Y,IA,G) AND CCS_G(G))=
$ifi %CCS%==yes          OMONEY/IOF1000000*IHOURFRAC *IM_CO2(G)*IOF0001 * IOF3P6 * CCS_TRANSPORTCOST * SUM((S,T), IHOURSINST(S,T)*(VGF_T.L(Y,IA,G,S,T)));


** OTHER EMISSIONS COST
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_OTHER_EMI_TAX','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and IAGK_HASORPOT(Y,IA,G))=
         SUM(GROUP$(GROUP_AAA(GROUP,IA)),OMONEY/IOF1000000*
         ( SUM((S,T), IHOURSINST(S,T) * (IHOURFRAC *IM_SO2(G)*IOF0001) * IOF3P6 * VGF_T.L(Y,IA,G,S,T))*EMI_POL(Y,C,GROUP,"TAX_SO2")
          + SUM((S,T), IHOURSINST(S,T) * (IHOURFRAC *GDATA(G,'GDNOX')*IOF0000001) * IOF3P6 * VGF_T.L(Y,IA,G,S,T))* EMI_POL(Y,C,GROUP,"TAX_NOX")
          ))
;


** UNIT COMMITMENT COST
$ifi not %UnitComm%==yes $goto No_UC
ECO_G_YCRAG(Y,C,IR,IA,IGUC,FFF,TECH_TYPE,'COSTS','GENERATION_UC_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(IGUC,FFF) AND GTECH_TYPE(IGUC,TECH_TYPE) and IAGK_HASORPOT(Y,IA,IGUC))=
OMONEY/IOF1000000*SUM((S,T), IHOURSINST(S,T)*(
                                                 VUCU.L(Y,IA,IGUC,S,T)*GDATA(IGUC,'GDUCUCOST')*GDATA(IGUC,'GDUCUNITSIZE')/(1$((NOT IGESTO(IGUC)) AND (NOT IGESTOS(IGUC)) AND (NOT IGHSTO(IGUC)) AND (NOT IGHSTOS(IGUC))) + GDATA(IGUC,'GDSTOHUNLD')$(IGESTO(IGUC) OR IGESTOS(IGUC) OR IGHSTO(IGUC) OR IGHSTOS(IGUC)))/CHRONOHOUR(S,T)*WEIGHT_T(T)  !! startup cost
                                                 +VUCD.L(Y,IA,IGUC,S,T)*GDATA(IGUC,'GDUCDCOST')*GDATA(IGUC,'GDUCUNITSIZE')/(1$((NOT IGESTO(IGUC)) AND (NOT IGESTOS(IGUC)) AND (NOT IGHSTO(IGUC)) AND (NOT IGHSTOS(IGUC))) + GDATA(IGUC,'GDSTOHUNLD')$(IGESTO(IGUC) OR IGESTOS(IGUC) OR IGHSTO(IGUC) OR IGHSTOS(IGUC)))/CHRONOHOUR(S,T)*WEIGHT_T(T)  !! shutdown cost
                                                 +VUCON.L(Y,IA,IGUC,S,T)*IHOURFRAC*GDATA(IGUC,'GDUCCOST0')*GDATA(IGUC,'GDUCUNITSIZE')/(1$((NOT IGESTO(IGUC)) AND (NOT IGESTOS(IGUC)) AND (NOT IGHSTO(IGUC)) AND (NOT IGHSTOS(IGUC))) + GDATA(IGUC,'GDSTOHUNLD')$(IGESTO(IGUC) OR IGESTOS(IGUC) OR IGHSTO(IGUC) OR IGHSTOS(IGUC))) !! fixed hourly cost
                                                 +VUCU_STOLOAD.L(Y,IA,IGUC,S,T)*GDATA(IGUC,'GDUCUCOST')*GDATA(IGUC,'GDUCUNITSIZE')/(1$((NOT IGESTO(IGUC)) AND (NOT IGESTOS(IGUC)) AND (NOT IGHSTO(IGUC)) AND (NOT IGHSTOS(IGUC))) + GDATA(IGUC,'GDSTOHLOAD')$(IGESTO(IGUC) OR IGESTOS(IGUC) OR IGHSTO(IGUC) OR IGHSTOS(IGUC)))/CHRONOHOUR(S,T)*WEIGHT_T(T)  !! startup cost
                                                 +VUCD_STOLOAD.L(Y,IA,IGUC,S,T)*GDATA(IGUC,'GDUCDCOST')*GDATA(IGUC,'GDUCUNITSIZE')/(1$((NOT IGESTO(IGUC)) AND (NOT IGESTOS(IGUC)) AND (NOT IGHSTO(IGUC)) AND (NOT IGHSTOS(IGUC))) + GDATA(IGUC,'GDSTOHLOAD')$(IGESTO(IGUC) OR IGESTOS(IGUC) OR IGHSTO(IGUC) OR IGHSTOS(IGUC)))/CHRONOHOUR(S,T)*WEIGHT_T(T)  !! shutdown cost
                                                 +VUCON_STOLOAD.L(Y,IA,IGUC,S,T)*IHOURFRAC*GDATA(IGUC,'GDUCCOST0')*GDATA(IGUC,'GDUCUNITSIZE')/(1$((NOT IGESTO(IGUC)) AND (NOT IGESTOS(IGUC)) AND (NOT IGHSTO(IGUC)) AND (NOT IGHSTOS(IGUC))) + GDATA(IGUC,'GDSTOHLOAD')$(IGESTO(IGUC) OR IGESTOS(IGUC) OR IGHSTO(IGUC) OR IGHSTOS(IGUC))) !! fixed hourly cost

$ifi %STEPWISE_RAMPING_UC%==ramp_const_cost      +SUM(RAMPING_RATE,(VRAMPING_DOWN.L(Y,IA,IGUC,RAMPING_RATE,S,T)*IRAMPING_G_COSTS_DOWN(IGUC,RAMPING_RATE)+VRAMPING_UP.L(Y,IA,IGUC,RAMPING_RATE,S,T)*IRAMPING_G_COSTS_UP(IGUC,RAMPING_RATE))/CHRONOHOUR(S,T)*WEIGHT_T(T))  !! ramping cost
))
;
$label NO_UC


*TAXES
$ifi NOT %TAXES%==yes  $goto No_TAXES
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_TAXES','Mmoney')$(CCCRRR(C,IR) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G))=
OMONEY/IOF1000000*(
* Tax(+)/subsidy(-) on fuel use for electricity generation
    + SUM((IS3,T),
         IHOURSINST(IS3,T) * IHOURFRAC *TAX_F_EL(Y,C,G)  * VGE_T.L(Y,IA,G,IS3,T)/ (GDATA(G,'GDFE') * (1$(NOT GEFFRATE(IA,G))+GEFFRATE(IA,G))))

* Tax(+)/subsidy(-) on fuel use for heat generation
    + SUM((IS3,T),
         IHOURSINST(IS3,T) * IHOURFRAC *TAX_F_HEAT(Y,C,G) * GDATA(G,'GDCV') * VGH_T.L(Y,IA,G,IS3,T)/ (GDATA(G,'GDFE') * (1$(NOT GEFFRATE(IA,G))+GEFFRATE(IA,G))))

* Tax(+)/subsidy(-) on fuel use
       + SUM((IS3,T),
              IHOURSINST(IS3,T) * IHOURFRAC *TAX_F(Y,C,G)  * VGF_T.L(Y,IA,G,IS3,T))

* Tax(+)/subsidy(-) on heat generation
    + SUM((IS3,T),
         IHOURSINST(IS3,T) * IHOURFRAC *TAX_G_HEAT(Y,C,G)  * VGH_T.L(Y,IA,G,IS3,T))

* Tax(+)/subsidy(-) on heat generation
    + SUM((IS3,T),
         IHOURSINST(IS3,T) * IHOURFRAC *TAX_G_EL(Y,C,G)  * VGE_T.L(Y,IA,G,IS3,T))
)
;
$label NO_TAXES


*GRID TARIFFS
$ifi NOT %GRIDTARIFFS%==yes  $goto No_GRIDTARIFFS
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_GRID_TARIFFS','Mmoney')$(CCCRRR(C,IR) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND (IGETOH(G)
$ifi %HYDROGEN%==yes OR IHYDROGEN_GEHTOH2(G) OR IHYDROGEN_GETOHH2(G) OR IHYDROGEN_GETOH2(G)
))=
OMONEY/IOF1000000*(

* Fixed component and subscribed power (per MW installed)
                 + IOF1000 *
                         IGR_PRICE_TECH(Y,IR,'GRDSUBS')  * (VGKNACCUMNET.L(Y,IA,G)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,G)$IGDECOMEXOPOT(Y,IA,G)
                          + GKFX(Y,IA,G)) /((GDATA(G,'GDFE') *(1$(NOT GEFFRATE(IA,G))+GEFFRATE(IA,G)))$IGETOH(G)  + 1$(NOT IGETOH(G)))

* Energy charge winter
                 + SUM((IS3,T)$IGR_TIME('GRDECW',IR,IS3,T), IHOURSINST(IS3,T) * IHOURFRAC *IGR_PRICE_TECH(Y,IR,'GRDECW')  * VGE_T.L(Y,IA,G,IS3,T))
* Energy charge summer
                 + SUM((IS3,T)$IGR_TIME('GRDECS',IR,IS3,T), IHOURSINST(IS3,T) * IHOURFRAC *IGR_PRICE_TECH(Y,IR,'GRDECS')  * VGE_T.L(Y,IA,G,IS3,T))
* Energy charge TOU step 1
                 + SUM((IS3,T)$IGR_TIME('GRDTOU1',IR,IS3,T), IHOURSINST(IS3,T) * IHOURFRAC *IGR_PRICE_TECH(Y,IR,'GRDTOU1')  * VGE_T.L(Y,IA,G,IS3,T))
* Energy charge TOU step 2
                 + SUM((IS3,T)$IGR_TIME('GRDTOU2',IR,IS3,T),  IHOURSINST(IS3,T) * IHOURFRAC *IGR_PRICE_TECH(Y,IR,'GRDTOU2')  * VGE_T.L(Y,IA,G,IS3,T))
* Energy charge TOU step 3
                 + SUM((IS3,T)$IGR_TIME('GRDTOU3',IR,IS3,T), IHOURSINST(IS3,T) * IHOURFRAC *IGR_PRICE_TECH(Y,IR,'GRDTOU3')  * VGE_T.L(Y,IA,G,IS3,T))
* Energy charge TOU step 4
                 + SUM((IS3,T)$IGR_TIME('GRDTOU4',IR,IS3,T),  IHOURSINST(IS3,T) * IHOURFRAC *IGR_PRICE_TECH(Y,IR,'GRDTOU4')  * VGE_T.L(Y,IA,G,IS3,T))
)
;
$label NO_GRIDTARIFFS

* -- TRANSMISSION -----
*ELECTRICITY
** TRANSMISSION INVESTMENTS
ECO_X_YCR(Y,C,IR,IRI,'COSTS','TRANSMISSION_CAPITAL_COSTS','Mmoney')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=
          OMONEY*IOF0000001*(
         SUM((IYALIAS2)$((IXKN(IYALIAS2,IRI,IR) OR IXKN(IYALIAS2,IR,IRI)) AND (ORD(IYALIAS2) LE ORD(Y)) AND (NOT (IXKN_ES(IR,IRI) OR IXKN_ES(IRI,IR)))),
                                    IOF05*ANNUITYCX(C)*IYHASANNUITYX(IYALIAS2,Y)*VXKN.L(IYALIAS2,IR,IRI)*XINVCOST(IYALIAS2,IR,IRI))
$ifi %OFFSHOREGRID%==yes          +SUM((IYALIAS2)$((IXKN(IYALIAS2,IRI,IR) OR IXKN(IYALIAS2,IR,IRI)) AND (ORD(IYALIAS2) LE ORD(Y)) AND (IXKN_ES(IR,IRI) OR IXKN_ES(IRI,IR))),
$ifi %OFFSHOREGRID%==yes           IOF05*ANNUITYCX(C)*IYHASANNUITYX(IYALIAS2,Y)*SUM(XES,VXLAMBDA.L(IYALIAS2,IR,IRI,XES)*XINVCOST_ES(IYALIAS2,IR,IRI,XES)))
)
;
** TRANSMISSION FLOW COSTS
ECO_X_YCR(Y,C,IR,IRI,'COSTS','TRANSMISSION_OPERATIONAL_COSTS','Mmoney')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=
OMONEY*IOF0000001* SUM((S,T), IHOURSINST(S,T) * (VX_T.L(Y,IR,IRI,S,T) * IHOURFRAC *XCOST(IR,IRI)));

** TRANSMISSION TRADE COST
ECO_X_YCR(Y,C,IR,IRI,'COSTS','TRANSMISSION_TRADE_COSTS','Mmoney')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=
OMONEY*IOF0000001*SUM((S,T), IHOURSINST(S,T) * (VX_T.L(Y,IRI,IR,S,T) * EL_PRICE_YCRST(Y,C,IRI,S,T,'Money_per_MWh')));

** TRANSMISSION TRADE INCOME
ECO_X_YCR(Y,C,IR,IRI,'INCOME','TRANSMISSION_TRADE_INCOME','Mmoney')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=
OMONEY*IOF0000001*SUM((S,T), IHOURSINST(S,T) * (VX_T.L(Y,IR,IRI,S,T) * EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')));

*HEAT
$ifi not %HEATTRANS%==yes $goto No_HEATTRANS_costs
** TRANSMISSION INVESTMENTS
ECO_XH_YCRA(Y,C,IR,IA,IAI,'COSTS','HEAT_TRANSMISSION_CAPITAL_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IXHK_HASORPOT(Y,IA,IAI))=
          OMONEY*IOF0000001*SUM((IYALIAS2)$((IXHKN(IYALIAS2,IAI,IA) OR IXHKN(IYALIAS2,IA,IAI)) AND ORD(IYALIAS2) LE ORD(Y)) ,
                                    IOF05*ANNUITYCXH(C)*IYHASANNUITYXH(IYALIAS2,Y)*VXHKN.L(IYALIAS2,IA,IAI)*XHINVCOST(IYALIAS2,IA,IAI)
                                );
** TRANSMISSION FLOW
ECO_XH_YCRA(Y,C,IR,IA,IAI,'COSTS','HEAT_TRANSMISSION_OPERATIONAL_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IXHK_HASORPOT(Y,IA,IAI))=
OMONEY*IOF0000001*SUM((S,T), IHOURSINST(S,T) * (VXH_T.L(Y,IA,IAI,S,T) * IHOURFRAC *XHCOST(IA,IAI)));

** TRANSMISSION TRADE COST
ECO_XH_YCRA(Y,C,IR,IA,IAI,'COSTS','TRANSMISSION_TRADE_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IXHK_HASORPOT(Y,IA,IAI))=
OMONEY*IOF0000001*SUM((S,T), IHOURSINST(S,T) * (VXH_T.L(Y,IAI,IA,S,T) * H_PRICE_YCRAST(Y,C,IR,IAI,S,T,'Money_per_MWh')));

** TRANSMISSION TRADE INCOME
ECO_XH_YCRA(Y,C,IR,IA,IAI,'INCOME','TRANSMISSION_TRADE_INCOME','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IXHK_HASORPOT(Y,IA,IAI))=
OMONEY*IOF0000001*SUM((S,T), IHOURSINST(S,T) * (VXH_T.L(Y,IA,IAI,S,T) * H_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')));


$label No_HEATTRANS_costs

*Hydrogen
$ifi not %HYDROGEN%==yes $goto No_H2TRANS_costs

** TRANSMISSION INVESTMENTS
ECO_XH2_YCR(Y,C,IR,IRI,'COSTS','H2_TRANSMISSION_CAPITAL_COSTS','Mmoney')$(CCCRRR(C,IR) AND IXH2K_HASORPOT(Y,IR,IRI))=
          OMONEY*IOF0000001*(SUM((IYALIAS2)$((IXH2KN(IYALIAS2,IRI,IR) OR IXH2KN(IYALIAS2,IR,IRI)) AND (NOT (IXKN_ES(IR,IRI) OR IXKN_ES(IRI,IR)))  AND ORD(IYALIAS2) LE ORD(Y)) ,
                                    IOF05*ANNUITYCXH2(C)*IYHASANNUITYXH2(IYALIAS2,Y)*VXH2KN.L(IYALIAS2,IR,IRI)*XH2INVCOST(IYALIAS2,IR,IRI))
                             +  SUM((IYALIAS2)$((IXH2KN_NGTOH2(IYALIAS2,IRI,IR) OR IXH2KN_NGTOH2(IYALIAS2,IR,IRI)) AND ORD(IYALIAS2) LE ORD(Y)) ,
                                    IOF05*ANNUITYCXH2(C)*IYHASANNUITYXH2(IYALIAS2,Y)*VXH2KN_NGTOH2.L(IYALIAS2,IR,IRI)*XH2INVCOST_NGTOH2(IYALIAS2,IR,IRI))
$ifi %ES_H2TRANS%==yes                             +SUM((IYALIAS2)$((IXH2KN(IYALIAS2,IRI,IR) OR IXH2KN(IYALIAS2,IR,IRI)) AND (ORD(IYALIAS2) LE ORD(Y)) AND (IXH2KN_ES(IR,IRI) OR IXH2KN_ES(IRI,IR))),
$ifi %ES_H2TRANS%==yes      IOF05*ANNUITYCXH2(C)*IYHASANNUITYXH2(IYALIAS2,Y)*SUM(XES,VXH2LAMBDA.L(IYALIAS2,IR,IRI,XES)*XH2INVCOST_ES(IYALIAS2,IR,IRI,XES)))
$ifi %ES_H2TRANS%==yes                           )
                            )
;

** TRANSMISSION FLOW
ECO_XH2_YCR(Y,C,IR,IRI,'COSTS','H2_TRANSMISSION_OPERATIONAL_COSTS','Mmoney')$(CCCRRR(C,IR) AND IXH2K_HASORPOT(Y,IR,IRI))=
OMONEY*IOF0000001*SUM((S,T), IHOURSINST(S,T) * (VXH2_T.L(Y,IR,IRI,S,T) * IHOURFRAC *XH2COST(IR,IRI)));

** TRANSMISSION TRADE COST
ECO_XH2_YCR(Y,C,IR,IRI,'COSTS','TRANSMISSION_TRADE_COSTS','Mmoney')$(CCCRRR(C,IR) AND IXH2K_HASORPOT(Y,IR,IRI))=
OMONEY*IOF0000001*SUM((S,T), IHOURSINST(S,T) * (VXH2_T.L(Y,IR,IRI,S,T) * H2_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')));

** TRANSMISSION TRADE INCOME
ECO_XH2_YCR(Y,C,IR,IRI,'INCOME','TRANSMISSION_TRADE_INCOME','Mmoney')$(CCCRRR(C,IR) AND IXH2K_HASORPOT(Y,IR,IRI))=
OMONEY*IOF0000001*SUM((S,T), IHOURSINST(S,T) * (VXH2_T.L(Y,IR,IRI,S,T) * H2_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')));


$label No_H2TRANS_costs

* ------------- END OF COSTS --------------

* ------------- OBJECTIVE FUNCTION --------------

OBJ_YCR(Y,C,IR,'GENERATION_FUEL_COSTS','Mmoney')$CCCRRR(C,IR)=

*GENERATION COSTS
SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND NOT((IGESTOALL(G) OR IGHSTOALL(G) OR IGETOH(G)
$ifi %STEPWISEPRICE%==yes    OR ISTEPWISEPRICE_F(Y,C,FFF)
$ifi %HYDROGEN%==yes   OR IHYDROGEN_GETOH2(G) OR IHYDROGEN_GEHTOH2(G) OR IHYDROGEN_GETOHH2(G) OR IHYDROGEN_GH2STO(G) OR IHYDROGEN_GH2TOE(G) OR IHYDROGEN_GH2TOEH(G) OR IHYDROGEN_GH2TOBIOMETH(G) OR IGBIOMETHANE(G)
))),
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_FUEL_COSTS','Mmoney')
)

$ifi not %STEPWISEPRICE%==yes $goto No_STEPWISEPRICE2
   +SUM(STEPWISEPRICE_FFF$(ISTEPWISEPRICE_F(Y,C,STEPWISEPRICE_FFF) AND SUM(STEPWISEPRICE_L,VSTEPWISEPRICE_VGF_T_STEP.L(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF))),
       OMONEY/IOF1000000*(
         (0+ SUM((S,T,IA,G)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G) AND IGF(G,STEPWISEPRICE_FFF)), IHOURSINST(S,T) * VGF_T.L(Y,IA,G,S,T)*IOF3P6)*IHOURFRAC)*
          SUM(STEPWISEPRICE_L,STEPWISEPRICE_FUELPRICE_STEP(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF)*VSTEPWISEPRICE_VGF_T_STEP.L(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF))
          /SUM(STEPWISEPRICE_L,VSTEPWISEPRICE_VGF_T_STEP.L(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF))
       )
    )
$label No_STEPWISEPRICE2

;

OBJ_YCR(Y,C,IR,'GENERATION_OPPORTUNITY_COSTS_STORAGE','Mmoney')$CCCRRR(C,IR)=
$ifi %import_results%==yes $ifi %ADDESTOVOLTS%==price +OMONEY*IOF0000001*SUM((S,T,IA,IGESTOS)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,IGESTOS)),IHOURSINST(S,T)*ESTOVOLTSVAL(Y,IA,IGESTOS,S,T)*IHOURFRAC*(VGE_T.L(Y,IA,IGESTOS,S,T)/GDATA(IGESTOS,'GDFE')-VESTOLOADTS.L(Y,IA,IGESTOS,S,T)))
$ifi %import_results%==yes $ifi %ADDHSTOVOLTS%==price +OMONEY*IOF0000001*SUM((S,T,IA,IGHSTOS)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,IGHSTOS)),IHOURSINST(S,T)*HSTOVOLTSVAL(Y,IA,IGHSTOS,S,T)*IHOURFRAC*(VGH_T.L(Y,IA,IGHSTOS,S,T)/GDATA(IGHSTOS,'GDFE')-VHSTOLOADTS.L(Y,IA,IGHSTOS,S,T)))
$ifi %import_results%==yes $ifi %ADDESTOVOLT%==price +OMONEY*IOF0000001*SUM((S,T,IA,IGESTO)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,IGESTO)),IHOURSINST(S,T)*ESTOVOLTVAL(Y,IA,IGESTO,S,T)*IHOURFRAC*(VGE_T.L(Y,IA,IGESTO,S,T)/GDATA(IGESTO,'GDFE')-VESTOLOADT.L(Y,IA,IGESTO,S,T)))
$ifi %import_results%==yes $ifi %ADDHSTOVOLT%==price +OMONEY*IOF0000001*SUM((S,T,IA,IGHSTO)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,IGHSTO)),IHOURSINST(S,T)*HSTOVOLTVAL(Y,IA,IGHSTO,S,T)*IHOURFRAC*(VGH_T.L(Y,IA,IGHSTO,S,T)/GDATA(IGHSTO,'GDFE')-VHSTOLOADT.L(Y,IA,IGHSTO,S,T)))
$ifi %import_results%==yes $ifi %HYDROGEN%==yes $ifi %ADDH2STOVOLTS%==price +OMONEY*IOF0000001*SUM((S,T,IA,IHYDROGEN_GH2STO)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,IHYDROGEN_GH2STO)),IHOURSINST(S,T)*H2STOVOLTSVAL(Y,IA,IHYDROGEN_GH2STO,S,T)*IHOURFRAC*(VHYDROGEN_GH2_T.L(Y,IA,IHYDROGEN_GH2STO,S,T)/GDATA(IHYDROGEN_GH2STO,'GDFE')-VHYDROGEN_STOLOADT.L(Y,IA,IHYDROGEN_GH2STO,S,T)))
$ifi %import_results%==yes $ifi %HYDROGEN%==yes $ifi %ADDBIOMETHSTOVOLTS%==price +OMONEY*IOF0000001*SUM((S,T)$(SUM((IA,IGBIOMETHANE),IAGK_HASORPOT(Y,IA,IGBIOMETHANE)) AND SUM((IA,IHYDROGEN_GH2TOBIOMETH),IAGK_HASORPOT(Y,IA,IHYDROGEN_GH2TOBIOMETH))),IHOURSINST(S,T)*BIOMETHSTOVOLTSVAL(Y,S,T)*IHOURFRAC*(VBIOMETH_STOUNLOADT.L(Y,S,T)-VBIOMETH_STOLOADT.L(Y,S,T))  )
+0;

OBJ_YCR(Y,C,IR,'GENERATION_REDISPATCH_COMPENSATION_COST','Mmoney')$CCCRRR(C,IR)=
$ifi %import_results%==yes $ifi %ADDENERGYDISPATCH%==yes  $ifi %BalancingMarket%==yes $ifi %BALANCINGRUNPURPOSE%==TSOREDISPATCH +OMONEY*IOF0000001*SUM((S,T,IAGK_HASORPOT(Y,IA,IGE))$(NOT (IGETOH(IGE)) AND RRRAAA(IR,IA) AND GE_T(Y,IA,IGE,S,T)),IHOURFRAC*ELECTRICITY_PRICE(Y,IR,S,T)*IHOURSINST(S,T)*(1-DISLOSS_E_AG(IA,IGE))*VGE_T_DOWN.L(Y,IA,IGE,S,T))
$ifi %import_results%==yes $ifi %ADDENERGYDISPATCH%==yes  $ifi %BalancingMarket%==yes $ifi %BALANCINGRUNPURPOSE%==TSOREDISPATCH +OMONEY*IOF0000001*SUM((S,T,IAGK_HASORPOT(Y,IA,IGH))$(RRRAAA(IR,IA) AND GH_T(Y,IA,IGH,S,T)),IHOURFRAC*HEAT_PRICE(Y,IA,S,T)*IHOURSINST(S,T)*VGH_T_DOWN.L(Y,IA,IGH,S,T))
$ifi %import_results%==yes $ifi %ADDENERGYDISPATCH%==yes  $ifi %BalancingMarket%==yes $ifi %BALANCINGRUNPURPOSE%==TSOREDISPATCH $ifi %HYDROGEN%==yes +OMONEY*IOF0000001*SUM((S,T,IAGK_HASORPOT(Y,IA,IHYDROGEN))$(RRRAAA(IR,IA) AND GH2_T(Y,IA,IHYDROGEN,S,T)),IHOURFRAC*HYDROGEN_PRICE(Y,IR,S,T)*IHOURSINST(S,T)*VGH2_T_DOWN.L(Y,IA,IHYDROGEN,S,T))
$ifi %import_results%==yes $ifi %ADDENERGYDISPATCH%==yes  $ifi %BalancingMarket%==yes $ifi %BALANCINGRUNPURPOSE%==TSOREDISPATCH $ifi %HYDROGEN%==yes +OMONEY*IOF0000001*SUM((S,T,IAGK_HASORPOT(Y,IA,IHYDROGEN_GH2TOBIOMETH))$(RRRAAA(IR,IA) AND GBIOMETHANE_T(Y,IA,IHYDROGEN_GH2TOBIOMETH,S,T)),IHOURFRAC*BIOMETHANE_PRICE(Y,S,T)*IHOURSINST(S,T)*VGBIOMETHANE_T_DOWN.L(Y,IA,IHYDROGEN_GH2TOBIOMETH,S,T))
$ifi %import_results%==yes $ifi %ADDENERGYDISPATCH%==yes  $ifi %BalancingMarket%==yes $ifi %BALANCINGRUNPURPOSE%==TSOREDISPATCH $ifi %HYDROGEN%==yes +OMONEY*IOF0000001*SUM((S,T,IAGK_HASORPOT(Y,IA,IHYDROGEN_GBIOGASUPGRADING))$(RRRAAA(IR,IA) AND GBIOGASUPGRADING_T(Y,IA,IHYDROGEN_GBIOGASUPGRADING,S,T)),IHOURFRAC*BIOMETHANE_PRICE(Y,S,T)*IHOURSINST(S,T)*VGBIOGASUPGRADING_T_DOWN.L(Y,IA,IHYDROGEN_GBIOGASUPGRADING,S,T))
$ifi %import_results%==yes $ifi %ADDENERGYDISPATCH%==yes  $ifi %BalancingMarket%==yes $ifi %BALANCINGRUNPURPOSE%==TSOREDISPATCH $ifi %HYDROGEN%==yes +OMONEY*IOF0000001*SUM((S,T,IAGK_HASORPOT(Y,IA,IHYDROGEN_GBIOGASMETHANATION))$(RRRAAA(IR,IA) AND GBIOGASMETHANATION_T(Y,IA,IHYDROGEN_GBIOGASMETHANATION,S,T)),IHOURFRAC*BIOMETHANE_PRICE(Y,S,T)*IHOURSINST(S,T)*VGBIOGASMETHANATION_T_DOWN.L(Y,IA,IHYDROGEN_GBIOGASMETHANATION,S,T))
+0;


OBJ_YCR(Y,C,IR,'HYDRO_PROFILE','Mmoney')$CCCRRR(C,IR)=
OMONEY*IOF0000001*SUM((IA,IGHYRS)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,IGHYRS)), SUM((S,T), HYPPROFILS(IA,S)*IHOURFRAC* IHOURSINST(S,T)
     * VGE_T.L(Y,IA,IGHYRS,S,T)))
;

OBJ_YCR(Y,C,IR,'GENERATION_CAPITAL_COSTS','Mmoney')$CCCRRR(C,IR)=
SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G)),
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_CAPITAL_COSTS','Mmoney')
)
;

OBJ_YCR(Y,C,IR,'GENERATION_FIXED_COSTS','Mmoney')$CCCRRR(C,IR)=
SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G)),
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_FIXED_COSTS','Mmoney')
)
;

OBJ_YCR(Y,C,IR,'GENERATION_OPERATIONAL_COSTS','Mmoney')$CCCRRR(C,IR)=
SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G)),
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_OPERATIONAL_COSTS','Mmoney')
)
;

OBJ_YCR(Y,C,IR,'GENERATION_CO2_TAX','Mmoney')$CCCRRR(C,IR)=
SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G)),
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_CO2_TAX','Mmoney')
)
;

$ifi %CCS%==yes  OBJ_YCR(Y,C,IR,'GENERATION_CO2_TRANSPORT','Mmoney')$CCCRRR(C,IR)=
$ifi %CCS%==yes  SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G) AND CCS_G(G)),ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_CO2_TRANSPORT','Mmoney'));

OBJ_YCR(Y,C,IR,'GENERATION_OTHER_EMI_TAX','Mmoney')$CCCRRR(C,IR)=
SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G)),
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_OTHER_EMI_TAX','Mmoney')
)
;

OBJ_YCR(Y,C,IR,'GENERATION_UC_COSTS','Mmoney')$CCCRRR(C,IR)=
SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G)),
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_UC_COSTS','Mmoney')
)
;

OBJ_YCR(Y,C,IR,'TRANSMISSION_CAPITAL_COSTS','Mmoney')$CCCRRR(C,IR)=
SUM(IRI$IXK_HASORPOT(Y,IR,IRI),ECO_X_YCR(Y,C,IR,IRI,'COSTS','TRANSMISSION_CAPITAL_COSTS','Mmoney'))
;

OBJ_YCR(Y,C,IR,'TRANSMISSION_OPERATIONAL_COSTS','Mmoney')$CCCRRR(C,IR)=
SUM(IRI$IXK_HASORPOT(Y,IR,IRI), ECO_X_YCR(Y,C,IR,IRI,'COSTS','TRANSMISSION_OPERATIONAL_COSTS','Mmoney'))
;

$ifi not %HEATTRANS%==yes $goto No_HEATTRANS_costs_2
OBJ_YCR(Y,C,IR,'HEAT_TRANSMISSION_CAPITAL_COSTS','Mmoney')$CCCRRR(C,IR)=
SUM((IA,IAI)$(RRRAAA(IR,IA) AND IXHK_HASORPOT(Y,IA,IAI)),ECO_XH_YCRA(Y,C,IR,IA,IAI,'COSTS','HEAT_TRANSMISSION_CAPITAL_COSTS','Mmoney'))
;

OBJ_YCR(Y,C,IR,'HEAT_TRANSMISSION_OPERATIONAL_COSTS','Mmoney')$CCCRRR(C,IR)=
SUM((IA,IAI)$(RRRAAA(IR,IA) AND IXHK_HASORPOT(Y,IA,IAI)),ECO_XH_YCRA(Y,C,IR,IA,IAI,'COSTS','HEAT_TRANSMISSION_OPERATIONAL_COSTS','Mmoney'))
;
$label  No_HEATTRANS_costs_2

$ifi not %HYDROGEN%==yes $goto No_H2_costs_2
OBJ_YCR(Y,C,IR,'H2_TRANSMISSION_CAPITAL_COSTS','Mmoney')$CCCRRR(C,IR)=
SUM(IRI$IXH2K_HASORPOT(Y,IR,IRI),ECO_XH2_YCR(Y,C,IR,IRI,'COSTS','H2_TRANSMISSION_CAPITAL_COSTS','Mmoney'))
;

OBJ_YCR(Y,C,IR,'H2_TRANSMISSION_OPERATIONAL_COSTS','Mmoney')$CCCRRR(C,IR)=
SUM(IRI$IXH2K_HASORPOT(Y,IR,IRI), ECO_XH2_YCR(Y,C,IR,IRI,'COSTS','H2_TRANSMISSION_OPERATIONAL_COSTS','Mmoney'))
;
$label  No_H2_costs_2


$ifi NOT %TAXES%==yes  $goto No_TAXES2
OBJ_YCR(Y,C,IR,'TAXES','Mmoney')$CCCRRR(C,IR)=
SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G)), ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_TAXES','Mmoney'))
+OMONEY/IOF1000000*(

* Tax on electricity consumption
    + SUM(DEUSER, TAX_DE(C,DEUSER)*IHOURFRAC * SUM((IS3,T), IHOURSINST(IS3,T) * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0))  )

$ifi %DFLEXQUANT%==yes           + SUM((DEUSER), *IHOURFRACTAX_DE(C,DEUSER) *(
$ifi %DFLEXQUANT%==yes             SUM(DEF_U1,VDEF_T.L(Y,IR,DEUSER,IS3,T,DEF_U1) ) - SUM(DEF_D1,VDEF_T.L(Y,IR,DEUSER,IS3,T,DEF_D1) )
$ifi %DFLEXQUANT%==yes           + SUM(DEF_U2,VDEF_T.L(Y,IR,DEUSER,IS3,T,DEF_U2) ) - SUM(DEF_D2,VDEF_T.L(Y,IR,DEUSER,IS3,T,DEF_D2) )
$ifi %DFLEXQUANT%==yes           + SUM(DEF_U3,VDEF_T.L(Y,IR,DEUSER,IS3,T,DEF_U3) ) - SUM(DEF_D3,VDEF_T.L(Y,IR,DEUSER,IS3,T,DEF_D3) ))


+SUM(IA$RRRAAA(IR,IA),
* Tax on heat consumption
    + SUM(DHUSER, TAX_DH(C,DHUSER)*IHOURFRAC * DH(Y,IA,DHUSER))

$ifi %DFLEXQUANT%==yes      + SUM((DHUSER), TAX_DH(C,DHUSER)*IHOURFRAC * (
$ifi %DFLEXQUANT%==yes          SUM(DHF_U1,VDHF_T.L(Y,IA,DHUSER,IS3,T,DHF_U1) ) - SUM(DHF_D1,VDHF_T.L(Y,IA,DHUSER,IS3,T,DHF_D1) )
$ifi %DFLEXQUANT%==yes        + SUM(DHF_U2,VDHF_T.L(Y,IA,DHUSER,IS3,T,DHF_U2) ) - SUM(DHF_D2,VDHF_T.L(Y,IA,DHUSER,IS3,T,DHF_D2) )
$ifi %DFLEXQUANT%==yes        + SUM(DHF_U3,VDHF_T.L(Y,IA,DHUSER,IS3,T,DHF_U3) ) - SUM(DHF_D3,VDHF_T.L(Y,IA,DHUSER,IS3,T,DHF_D3) ) )
)
)
;
$label NO_TAXES2


*GRID TARIFFS2
$ifi NOT %GRIDTARIFFS%==yes  $goto No_GRIDTARIFFS2
OBJ_YCR(Y,C,IR,'GRID_TARIFFS','Mmoney')$CCCRRR(C,IR)=
SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G) AND (IGETOH(G)
$ifi %HYDROGEN%==yes OR IHYDROGEN_GEHTOH2(G) OR IHYDROGEN_GETOHH2(G) OR IHYDROGEN_GETOH2(G)
)), ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_GRID_TARIFFS','Mmoney'))

+OMONEY/IOF1000000*(

* Demand charge winter
                 + IOF1000 * SUM((MMM)$(SUM((IS3,T)$IGR_TIME('GRDDCW',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), SUM(IA$(SUM(IGETOH,IAGK_HASORPOT(Y,IA,IGETOH)) AND RRRAAA(IR,IA)), VGETOH_CAP1.L(Y,IA,MMM)) *  IGR_PRICE_TECH(Y,IR,'GRDDCW'))/(IGR_MONTHSINSIM/card(MMM))    !! Demand charge winter
* Demand charge summer
                 + IOF1000 * SUM((MMM)$(SUM((IS3,T)$IGR_TIME('GRDDCS',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), SUM(IA$(SUM(IGETOH,IAGK_HASORPOT(Y,IA,IGETOH)) AND RRRAAA(IR,IA)), VGETOH_CAP1.L(Y,IA,MMM)) *  IGR_PRICE_TECH(Y,IR,'GRDDCS'))/(IGR_MONTHSINSIM/card(MMM))    !! Demand charge summer
* Demand charge 2 (e.g for spring and fall months)
                 + IOF1000 * SUM((MMM)$(SUM((IS3,T)$IGR_TIME('GRDDC2',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), SUM(IA$(SUM(IGETOH,IAGK_HASORPOT(Y,IA,IGETOH)) AND RRRAAA(IR,IA)), VGETOH_CAP1.L(Y,IA,MMM)) *  IGR_PRICE_TECH(Y,IR,'GRDDC2'))/(IGR_MONTHSINSIM/card(MMM))    !! Demand charge 2

* Demand charge TOU
                 + IOF1000 * SUM((MMM),   SUM(IA$(SUM(IGETOH,IAGK_HASORPOT(Y,IA,IGETOH)) AND RRRAAA(IR,IA)), VGETOH_CAP2.L(Y,IA,MMM)) *  IGR_PRICE_TECH(Y,IR,'GRDDCTOU'))/(IGR_MONTHSINSIM/card(MMM))
* Demand charge yearly
                 + IOF1000 *    SUM(IA$(SUM(IGETOH,IAGK_HASORPOT(Y,IA,IGETOH)) AND RRRAAA(IR,IA)), VGETOH_CAPY.L(Y,IA)) *  IGR_PRICE_TECH(Y,IR,'GRDDCY')

* Tariffs on user groups
                 + SUM((IS3,T,DEUSER)$IGR_TIME('GRDECW',IR,IS3,T),  IHOURSINST(IS3,T)*IHOURFRAC * IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDECW')  * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0) )
                 + SUM((IS3,T,DEUSER)$IGR_TIME('GRDECS',IR,IS3,T),  IHOURSINST(IS3,T)*IHOURFRAC * IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDECS')  * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0) )
                 + SUM((IS3,T,DEUSER)$IGR_TIME('GRDTOU1',IR,IS3,T),  IHOURSINST(IS3,T)*IHOURFRAC * IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDTOU1') * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0) )
                 + SUM((IS3,T,DEUSER)$IGR_TIME('GRDTOU2',IR,IS3,T),  IHOURSINST(IS3,T)*IHOURFRAC * IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDTOU2') * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0) )
                 + SUM((IS3,T,DEUSER)$IGR_TIME('GRDTOU3',IR,IS3,T),  IHOURSINST(IS3,T)*IHOURFRAC * IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDTOU3') * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0) )
                 + SUM((IS3,T,DEUSER)$IGR_TIME('GRDTOU4',IR,IS3,T),  IHOURSINST(IS3,T)*IHOURFRAC * IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDTOU4') * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0) )

                 + IOF1000 * SUM((DEUSER,MMM)$(SUM((IS3,T)$IGR_TIME('GRDDCW',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), VDEUSER_CAP1.L(Y,IR,DEUSER,MMM) *  IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDDCW'))/(IGR_MONTHSINSIM/CARD(MMM))
                 + IOF1000 * SUM((DEUSER,MMM)$(SUM((IS3,T)$IGR_TIME('GRDDCS',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), VDEUSER_CAP1.L(Y,IR,DEUSER,MMM) *  IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDDCS'))/(IGR_MONTHSINSIM/CARD(MMM))
                 + IOF1000 * SUM((DEUSER,MMM)$(SUM((IS3,T)$IGR_TIME('GRDDC2',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), VDEUSER_CAP1.L(Y,IR,DEUSER,MMM) *  IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDDC2'))/(IGR_MONTHSINSIM/CARD(MMM))
                 + IOF1000 * SUM((DEUSER,MMM),   VDEUSER_CAP2.L(Y,IR,DEUSER,MMM) *  IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDDCTOU'))/(IGR_MONTHSINSIM/card(MMM))
                 + IOF1000 * SUM((DEUSER),  VDEUSER_CAPY.L(Y,IR,DEUSER) *  IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDDCY'))

)

;
$label NO_GRIDTARIFFS2

*ADD OTHER COSTS(?)


* ------------- END OF OBJECTIVE FUNCTION --------------


* ------------- INCOME --------------

** ELECTRICITY SALES
ECO_G_YCRAG(Y,C,IR,IA,IGE,FFF,TECH_TYPE,'INCOME','ELECTRICITY_SALE','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) and IGNOTETOH(IGE))=
 OMONEY*IOF0000001*
   SUM((S,T), IHOURSINST(S,T)*VGE_T.L(Y,IA,IGE,S,T)*EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh'))
;

** HEAT SALES
ECO_G_YCRAG(Y,C,IR,IA,IGH,FFF,TECH_TYPE,'INCOME','HEAT_SALE','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE))=
 OMONEY*IOF0000001*
   SUM((S,T), IHOURSINST(S,T)*VGH_T.L(Y,IA,IGH,S,T)*H_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh'))
;

$ifi not %BalancingMarket%==yes $goto NO_TSOREDISPATCH1
$ifi not %BALANCINGRUNPURPOSE%==TSOREDISPATCH $goto NO_TSOREDISPATCH1
ECO_G_YCRAG(Y,C,IR,IA,IGE,FFF,TECH_TYPE,'INCOME','ELECTRICITY_REDISPATCH_COMPENSATION','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) and IGNOTETOH(IGE))=
 OMONEY*IOF0000001*(
   SUM((S,T), IHOURFRAC*IHOURSINST(S,T)*(1-DISLOSS_E_AG(IA,IGE))*VGE_T_DOWN.L(Y,IA,IGE,S,T)*ELECTRICITY_PRICE(Y,IR,S,T)))
;

ECO_G_YCRAG(Y,C,IR,IA,IGH,FFF,TECH_TYPE,'INCOME','HEAT_REDISPATCH_COMPENSATION','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE))=
 OMONEY*IOF0000001*
   SUM((S,T), IHOURFRAC*IHOURSINST(S,T)*VGH_T_DOWN.L(Y,IA,IGH,S,T)*HEAT_PRICE(Y,IA,S,T))
;
$label NO_TSOREDISPATCH1

$ifi not %HYDROGEN%==yes $goto NO_HYDROGEN_INCOME
** HYDROGEN SALES
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'INCOME','H2_SALE','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) AND IHYDROGEN(G) AND NOT(IHYDROGEN_GH2TOBIOMETH(G)))=
 OMONEY*IOF0000001*
   SUM((S,T), IHOURSINST(S,T)*PRO_YCRAGFST(Y,C,IR,IA,G,FFF,S,T,'HYDROGEN',TECH_TYPE,'MWh')*H2_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh'))
;

** BIOMETHANE SALES
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'INCOME','BIOMETHANE_SALE','Mmoney')$((IHYDROGEN_GH2TOBIOMETH(G) OR IHYDROGEN_GBIOGASUPGRADING(G) OR IHYDROGEN_GBIOGASMETHANATION(G)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE))=
 OMONEY*IOF0000001*(
        +SUM((S,T), IHOURSINST(S,T)*VGBIOMETH_T.L(Y,IA,G,S,T)*BIOMETH_PRICE_YST(Y,S,T,'Money_per_MWh'))$IHYDROGEN_GH2TOBIOMETH(G)
        +SUM((S,T), IHOURSINST(S,T)*VGBIOGASMETHANATION_T.L(Y,IA,G,S,T)*BIOMETH_PRICE_YST(Y,S,T,'Money_per_MWh'))$IHYDROGEN_GBIOGASMETHANATION(G)
        +SUM((S,T), IHOURSINST(S,T)*VGBIOGASUPGRADING_T.L(Y,IA,G,S,T)*BIOMETH_PRICE_YST(Y,S,T,'Money_per_MWh'))$IHYDROGEN_GBIOGASUPGRADING(G)
)
;

$ifi not %BalancingMarket%==yes $goto NO_TSOREDISPATCH2
$ifi not %BALANCINGRUNPURPOSE%==TSOREDISPATCH $goto NO_TSOREDISPATCH2
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'INCOME','HYDROGEN_REDISPATCH_COMPENSATION','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) AND IHYDROGEN(G))=
 OMONEY*IOF0000001*
   SUM((S,T), IHOURFRAC*IHOURSINST(S,T)*VGH2_T_DOWN.L(Y,IA,G,S,T)*HYDROGEN_PRICE(Y,IR,S,T))
;

ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'INCOME','BIOMETHANE_REDISPATCH_COMPENSATION','Mmoney')$((IHYDROGEN_GH2TOBIOMETH(G) OR IHYDROGEN_GBIOGASUPGRADING(G) OR IHYDROGEN_GBIOGASMETHANATION(G)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE))=
 OMONEY*IOF0000001*(
        +SUM((S,T), IHOURFRAC*IHOURSINST(S,T)*VGBIOMETHANE_T_DOWN.L(Y,IA,G,S,T)*BIOMETHANE_PRICE(Y,S,T))$IHYDROGEN_GH2TOBIOMETH(G)
        +SUM((S,T), IHOURFRAC*IHOURSINST(S,T)*VGBIOGASMETHANATION_T_DOWN.L(Y,IA,G,S,T)*BIOMETHANE_PRICE(Y,S,T))$IHYDROGEN_GBIOGASMETHANATION(G)
        +SUM((S,T), IHOURFRAC*IHOURSINST(S,T)*VGBIOGASUPGRADING_T_DOWN.L(Y,IA,G,S,T)*BIOMETHANE_PRICE(Y,S,T))$IHYDROGEN_GBIOGASUPGRADING(G)
)
;
$label NO_TSOREDISPATCH2

$label NO_HYDROGEN_INCOME

* ------------- END OF INCOME --------------

* ------------- PROFIT --------------
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'PROFIT','TOTAL_PROFIT','Mmoney')$IAGK_HASORPOT(Y,IA,G)=
SUM(SUBCATEGORY, ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'INCOME',SUBCATEGORY,'Mmoney')-ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS',SUBCATEGORY,'Mmoney')
)
;

ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'PROFIT','ENERGY_SPECIFIC_PROFIT','Money_per_MWh')$(IAGK_HASORPOT(Y,IA,G) AND SUM(COMMODITY,PRO_YCRAGF(Y,C,IR,IA,G,FFF,COMMODITY,TECH_TYPE,'TWh')) > 0  ) =
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'PROFIT','TOTAL_PROFIT','Mmoney')/SUM(COMMODITY,PRO_YCRAGF(Y,C,IR,IA,G,FFF,COMMODITY,TECH_TYPE,'TWh'))
;
* ------------- END OF PROFIT --------------

* --------- END OF ECONOMIC OUTPUT -----------

* ---------------------- ENVIRONMENT ----------------------

* ------------- CO2 EMISSIONS -------------

EMI_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'kton')$(F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh'))=
(F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh')
$ifi %CCS%==yes   -(F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh')*CCS_CO2CAPTEFF_G(G))$CCS_G(G)
)*IM_CO2(G)*IOF3P6
;

* ------------- END OF CO2 EMISSIONS -------------

* ------------- CO2 CAPTURE -------------
$ifi not %CCS%==yes $goto NO_CCS
CC_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'kton')$(F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh') AND CCS_G(G))=
   (F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh')*CCS_CO2CAPTEFF_G(G))*(IM_CO2(G) + IM_CO2RE(G))*IOF3P6
;

$label NO_CCS
* ------------- END OF CO2 CAPTURE -------------

* ---------------------- END OF ENVIRONMENT ----------------------

*---------------ELECTRICITY BALANCE------------
*GENERATION
EL_BALANCE_YCRST(Y,C,IR,'CONDENSING',S,T,'MWh')$CCCRRR(C,IR)=SUM((IA,IGE,FFF)$(RRRAAA(IR,IA) AND IGF(IGE,FFF) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY','CONDENSING','MWh'));
EL_BALANCE_YCRST(Y,C,IR,'CHP-BACK-PRESSURE',S,T,'MWh')$CCCRRR(C,IR)=SUM((IA,IGE,FFF)$(RRRAAA(IR,IA) AND IGF(IGE,FFF) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY','CHP-BACK-PRESSURE','MWh'));
EL_BALANCE_YCRST(Y,C,IR,'CHP-EXTRACTION',S,T,'MWh')$CCCRRR(C,IR)=SUM((IA,IGE,FFF)$(RRRAAA(IR,IA) AND IGF(IGE,FFF) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY','CHP-EXTRACTION','MWh'));
EL_BALANCE_YCRST(Y,C,IR,'INTER-STO',S,T,'MWh')$CCCRRR(C,IR)=SUM((IA,IGE,FFF)$(RRRAAA(IR,IA) AND IGF(IGE,FFF) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY','INTERSEASONAL-ELECT-STORAGE','MWh'));
EL_BALANCE_YCRST(Y,C,IR,'INTRA-STO',S,T,'MWh')$CCCRRR(C,IR)=SUM((IA,IGE,FFF)$(RRRAAA(IR,IA) AND IGF(IGE,FFF) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY','INTRASEASONAL-ELECT-STORAGE','MWh'));
EL_BALANCE_YCRST(Y,C,IR,'HYDRO-RESERVOIRS',S,T,'MWh')$CCCRRR(C,IR)=SUM((IA,IGE,FFF)$(RRRAAA(IR,IA) AND IGF(IGE,FFF) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY','HYDRO-RESERVOIRS','MWh'));
EL_BALANCE_YCRST(Y,C,IR,'HYDRO-RUN-OF-RIVER',S,T,'MWh')$CCCRRR(C,IR)=SUM((IA,IGE,FFF)$(RRRAAA(IR,IA) AND IGF(IGE,FFF) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY','HYDRO-RUN-OF-RIVER','MWh'));
EL_BALANCE_YCRST(Y,C,IR,'WIND-ON',S,T,'MWh')$CCCRRR(C,IR)=SUM((IA,IGE,FFF)$(RRRAAA(IR,IA) AND IGF(IGE,FFF) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY','WIND-ON','MWh'));
EL_BALANCE_YCRST(Y,C,IR,'WIND-OFF',S,T,'MWh')$CCCRRR(C,IR)=SUM((IA,IGE,FFF)$(RRRAAA(IR,IA) AND IGF(IGE,FFF) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY','WIND-OFF','MWh'));
EL_BALANCE_YCRST(Y,C,IR,'SOLAR-PV',S,T,'MWh')$CCCRRR(C,IR)=SUM((IA,IGE,FFF)$(RRRAAA(IR,IA) AND IGF(IGE,FFF) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY','SOLAR-PV','MWh'));
EL_BALANCE_YCRST(Y,C,IR,'HYDRO-WAVE ',S,T,'MWh')$CCCRRR(C,IR)=SUM((IA,IGE,FFF)$(RRRAAA(IR,IA) AND IGF(IGE,FFF) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY','HYDRO-WAVE','MWh'));
$ifi %HYDROGEN%==yes EL_BALANCE_YCRST(Y,C,IR,'FUELCELL',S,T,'MWh')$CCCRRR(C,IR)=SUM((IA,IGE,FFF)$(RRRAAA(IR,IA) AND IGF(IGE,FFF) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY','FUELCELL','MWh'));

*DEMAND
EL_BALANCE_YCRST(Y,C,IR,'DEMAND_EXO',S,T,'MWh')$CCCRRR(C,IR)=EL_DEMAND_YCRST(Y,C,IR,S,T,'EXOGENOUS','MWh');
EL_BALANCE_YCRST(Y,C,IR,'DEMAND_EXOTRANS',S,T,'MWh')$CCCRRR(C,IR)=EL_DEMAND_YCRST(Y,C,IR,S,T,'EXO_TRANS','MWh');
EL_BALANCE_YCRST(Y,C,IR,'DEMAND_P2H',S,T,'MWh')$CCCRRR(C,IR)=EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDOGENOUS_ELECT2HEAT','MWh');
EL_BALANCE_YCRST(Y,C,IR,'DEMAND_INTRASTO',S,T,'MWh')$CCCRRR(C,IR)=EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_INTRASTO','MWh');
EL_BALANCE_YCRST(Y,C,IR,'DEMAND_INTERSTO',S,T,'MWh')$CCCRRR(C,IR)=EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_INTERSTO','MWh');
EL_BALANCE_YCRST(Y,C,IR,'DEMAND_EV',S,T,'MWh')$CCCRRR(C,IR)=EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_EV','MWh');
EL_BALANCE_YCRST(Y,C,IR,'DEMAND_OTHERTRANS',S,T,'MWh')$CCCRRR(C,IR)=EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_OTHERTRANS','MWh');
EL_BALANCE_YCRST(Y,C,IR,'DEMAND_DISTLOSSES',S,T,'MWh')$CCCRRR(C,IR)=EL_DEMAND_YCRST(Y,C,IR,S,T,'DIST_LOSSES','MWh');
EL_BALANCE_YCRST(Y,C,IR,'DEMAND_CCS',S,T,'MWh')$CCCRRR(C,IR)=EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_CCS','MWh');
$ifi %HYDROGEN%==yes EL_BALANCE_YCRST(Y,C,IR,'DEMAND_P2G',S,T,'MWh')$CCCRRR(C,IR)=EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_H2','MWh')+EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_BIOMETHANE','MWh');


*TRANSMISSION
EL_BALANCE_YCRST(Y,C,IR,'NETEXPORT',S,T,'MWh')$CCCRRR(C,IR)=SUM(IRE$IXK_HASORPOT(Y,IR,IRE),VX_T.L(Y,IR,IRE,S,T))-SUM(IRI$IXK_HASORPOT(Y,IRI,IR),VX_T.L(Y,IRI,IR,S,T)*(1-XLOSS(IRI,IR)));

*CURTAILMENT
EL_BALANCE_YCRST(Y,C,IR,'CURTAILMENT',S,T,'MWh')$CCCRRR(C,IR)=
SUM((IGE,TECH_TYPE,IA,FFF)$(RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE)),
CURT_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY',TECH_TYPE,'MWh')
);

*PRICE
EL_BALANCE_YCRST(Y,C,IR,'PRICE',S,T,'Money_per_MWh')$CCCRRR(C,IR)=EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh');
*---------------END ELECTRICITY BALANCE------------


*---------------HEAT BALANCE------------
*GENERATION
H_BALANCE_YCRAST(Y,C,IR,IA,'P2H',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=SUM((IGH,FFF)$(IGF(IGH,FFF) AND IAGK_HASORPOT(Y,IA,IGH)),PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'HEAT','ELECT-TO-HEAT','MWh'));
H_BALANCE_YCRAST(Y,C,IR,IA,'CHP-BACK-PRESSURE',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=SUM((IGH,FFF)$(IGF(IGH,FFF) AND IAGK_HASORPOT(Y,IA,IGH)),PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'HEAT','CHP-BACK-PRESSURE','MWh'));
H_BALANCE_YCRAST(Y,C,IR,IA,'CHP-EXTRACTION',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=SUM((IGH,FFF)$(IGF(IGH,FFF) AND IAGK_HASORPOT(Y,IA,IGH)),PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'HEAT','CHP-EXTRACTION','MWh'));
H_BALANCE_YCRAST(Y,C,IR,IA,'INTER-STO',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=SUM((IGH,FFF)$(IGF(IGH,FFF) AND IAGK_HASORPOT(Y,IA,IGH)),PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'HEAT','INTERSEASONAL-HEAT-STORAGE','MWh'));
H_BALANCE_YCRAST(Y,C,IR,IA,'INTRA-STO',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=SUM((IGH,FFF)$(IGF(IGH,FFF) AND IAGK_HASORPOT(Y,IA,IGH)),PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'HEAT','INTRASEASONAL-HEAT-STORAGE','MWh'));
H_BALANCE_YCRAST(Y,C,IR,IA,'BOILERS',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=SUM((IGH,FFF)$(IGF(IGH,FFF) AND IAGK_HASORPOT(Y,IA,IGH)),PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'HEAT','BOILERS','MWh'));
H_BALANCE_YCRAST(Y,C,IR,IA,'SOLAR-HEATING',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=SUM((IGH,FFF)$(IGF(IGH,FFF) AND IAGK_HASORPOT(Y,IA,IGH)),PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'HEAT','SOLAR-HEATING','MWh'));
$ifi %HYDROGEN%==yes H_BALANCE_YCRAST(Y,C,IR,IA,'FUELCELL',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=SUM((IGH,FFF)$(IGF(IGH,FFF) AND IAGK_HASORPOT(Y,IA,IGH)),PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'HEAT','FUELCELL','MWh'));


*DEMAND
H_BALANCE_YCRAST(Y,C,IR,IA,'DEMAND_EXO',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'EXOGENOUS','MWh');
H_BALANCE_YCRAST(Y,C,IR,IA,'DEMAND_INTRASTO',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'ENDO_INTRASTO','MWh');
H_BALANCE_YCRAST(Y,C,IR,IA,'DEMAND_INTERSTO',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'ENDO_INTERSTO','MWh');
H_BALANCE_YCRAST(Y,C,IR,IA,'DEMAND_DISTLOSSES',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'DIST_LOSSES','MWh');
$ifi %HYDROGEN%==yes H_BALANCE_YCRAST(Y,C,IR,IA,'DEMAND_P2G',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'ENDO_H2','MWh')+ H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'ENDO_BIOMETHANE','MWh');

*TRANSMISSION
$ifi %HEATTRANS%==yes  H_BALANCE_YCRAST(Y,C,IR,IA,'NETEXPORT',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=SUM(IAE$IXHK_HASORPOT(Y,IA,IAE),VXH_T.L(Y,IA,IAE,S,T))-SUM(IAI$IXHK_HASORPOT(Y,IAI,IA),VXH_T.L(Y,IAI,IA,S,T)*(1-XHLOSS(IAI,IA)));

*CURTAILMENT
H_BALANCE_YCRAST(Y,C,IR,IA,'CURTAILMENT',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=
SUM((IGH,TECH_TYPE,FFF)$(IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE)),
CURT_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'HEAT',TECH_TYPE,'MWh')
);

*PRICE
H_BALANCE_YCRAST(Y,C,IR,IA,'PRICE',S,T,'Money_per_MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=H_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh');
*---------------END HEAT BALANCE------------

* IF OPTIFLOW EXISTS, ALSO CALCULATE OPTIFLOW OUTPUT
$ifi %OPTIFLOW%==yes $if     EXIST '../../base/output/Optiflow_OutputFile.inc' $INCLUDE         '../../base/output/Optiflow_OutputFile.inc';

execute_unload "MainResults.gdx" G_CAP_YCRAF,PRO_YCRAGF,F_CONS_YCRA,EMI_YCRAG,X_CAP_YCR,ECO_G_YCRAG,ECO_X_YCR,OBJ_YCR,EL_PRICE_YCR,EL_PRICE_YCRST,PRO_YCRAGFST,EL_DEMAND_YCRST,EL_DEMAND_YCR,
H_DEMAND_YCRAST,H_DEMAND_YCRA,H_PRICE_YCRAST,H_PRICE_YCRA,G_STO_YCRAF,X_FLOW_YCRST,X_FLOW_YCR,F_CONS_YCRAST,CC_YCRAG,
EL_BALANCE_YCRST,CURT_YCRAGFST,CURT_YCRAGF,H_BALANCE_YCRAST

$ifi %HEATTRANS%==yes ,XH_CAP_YCA,XH_FLOW_YCAST,XH_FLOW_YCA,ECO_XH_YCRA
$ifi %HYDROGEN%==yes  ,H2_PRICE_YCRST,H2_PRICE_YCR,BIOMETH_PRICE_YST,XH2_CAP_YCR, XH2_FLOW_YCRST,XH2_FLOW_YCR,ECO_XH2_YCR,H2_DEMAND_YCRST,H2_DEMAND_YCR
$ifi %OPTIFLOW%==yes  ,VFLOW_Opti_C,VFLOWTRANS_Opti_A,VFLOWTRANS_Opti_C,ECO_PROC_YCRAP,VPROCKAPNEW,VPROCKAPACCUMNET
;


*Establishing modified sets and parameters to original for further calculations

$ifi not %stointers%==all $goto NO_STOINTERS2
*Modification of STORAGE
IGESTOS(IGESTO) = YES; IGHSTOS(IGHSTO) = YES;
IGESTO(IGESTO) = NO;   IGHSTO(IGHSTO) = NO;
$label NO_STOINTERS2

$ifi not %RollingSeasons%==yes $goto NO_ROLLINGSEASONS
*Modification of IHOURSINST
IHOURSINST(SSS,TTT)=0;
LOOP(IS3LOOPSET$(ORD(IS3LOOPSET) LE (CARD(S)-ROLLINGSEASONSNUMBER+1)),
         IS3(S)=NO;
         IS3(S)$((ORD(S) LE (ORD(IS3LOOPSET)+ROLLINGSEASONSNUMBER-1)) AND (ORD(S) GE ORD(IS3LOOPSET)) )=YES;
         IWEIGHSUMS=SUM(IS3, WEIGHT_S(IS3));
         IHOURSINST(IS3,T)=IOF8760*WEIGHT_S(IS3)*WEIGHT_T(T)/(IWEIGHSUMS*SUM(ITALIAS, WEIGHT_T(ITALIAS)))/IHOURFRAC;
);

$label NO_ROLLINGSEASONS

*Deleting files to release memory:
G_CAP_YCRAF(Y,C,RRR,AAA,G,FFF,COMMODITY,TECH_TYPE,VARIABLE_CATEGORY,UNITS)=0;
G_STO_YCRAF(Y,C,RRR,AAA,G,FFF,COMMODITY,TECH_TYPE,VARIABLE_CATEGORY,UNITS)=0;
PRO_YCRAGFST(Y,C,RRR,AAA,G,FFF,SSS,TTT,COMMODITY,TECH_TYPE,UNITS)=0;
PRO_YCRAGF(Y,C,RRR,AAA,G,FFF,COMMODITY,TECH_TYPE,UNITS)=0;
F_CONS_YCRAST(Y,C,RRR,AAA,G,FFF,SSS,TTT,TECH_TYPE,UNITS)=0;
F_CONS_YCRA(Y,C,RRR,AAA,G,FFF,TECH_TYPE,UNITS)=0;
$ifi %CCS%==yes CC_YCRAG(Y,C,RRR,AAA,G,FFF,TECH_TYPE,UNITS)=0;
X_CAP_YCR(Y,C,IRRRE,IRRRI,VARIABLE_CATEGORY,UNITS)=0;
X_FLOW_YCRST(Y,C,IRRRE,IRRRI,SSS,TTT,UNITS)=0;
X_FLOW_YCR(Y,C,IRRRE,IRRRI,UNITS)=0;
ECO_G_YCRAG(Y,C,RRR,AAA,G,FFF,TECH_TYPE,CATEGORY,SUBCATEGORY,UNITS)=0;
ECO_X_YCR(Y,C,RRR,IRRRI,CATEGORY,SUBCATEGORY,UNITS)=0;
OBJ_YCR(Y,C,RRR,SUBCATEGORY,UNITS)=0;
EL_PRICE_YCR(Y,C,RRR,PRICE_CATEGORY,UNITS)=0;
EL_PRICE_YCRST(Y,C,RRR,SSS,TTT,UNITS)=0;
H_PRICE_YCRA(Y,C,RRR,AAA,PRICE_CATEGORY,UNITS)=0;
H_PRICE_YCRAST(Y,C,RRR,AAA,SSS,TTT,UNITS)=0;
EL_DEMAND_YCRST(Y,C,RRR,SSS,TTT,VARIABLE_CATEGORY,UNITS)=0;
EL_DEMAND_YCR(Y,C,RRR,VARIABLE_CATEGORY,UNITS)=0;
H_DEMAND_YCRAST(Y,C,RRR,AAA,SSS,TTT,VARIABLE_CATEGORY,UNITS)=0;
H_DEMAND_YCRA(Y,C,RRR,AAA,VARIABLE_CATEGORY,UNITS)=0;
EMI_YCRAG(Y,C,RRR,AAA,G,FFF,TECH_TYPE,UNITS)=0;
CURT_YCRAGFST(Y,C,RRR,AAA,G,FFF,SSS,TTT,COMMODITY,TECH_TYPE,UNITS)=0;
CURT_YCRAGF(Y,C,RRR,AAA,G,FFF,COMMODITY,TECH_TYPE,UNITS)=0;
EL_BALANCE_YCRST(Y,C,RRR,EL_BAL_TYPE,SSS,TTT,UNITS)=0;
H_BALANCE_YCRAST(Y,C,RRR,AAA,H_BAL_TYPE,SSS,TTT,UNITS)=0;
$ifi %HYDROGEN%==yes H2_PRICE_YCRST(Y,C,RRR,SSS,TTT,UNITS)=0;
$ifi %HYDROGEN%==yes H2_PRICE_YCR(Y,C,RRR,PRICE_CATEGORY,UNITS)=0;
$ifi %HYDROGEN%==yes BIOMETH_PRICE_YST(Y,SSS,TTT,UNITS)=0;
$ifi %HEATTRANS%==yes XH_CAP_YCA(Y,C,IAAAE,IAAAI,VARIABLE_CATEGORY,UNITS)=0;
$ifi %HEATTRANS%==yes XH_FLOW_YCAST(Y,C,IAAAE,IAAAI,SSS,TTT,UNITS)=0;
$ifi %HEATTRANS%==yes XH_FLOW_YCA(Y,C,IAAAE,IAAAI,UNITS)=0;
$ifi %HEATTRANS%==yes ECO_XH_YCRA(Y,C,RRR,AAA,IAAAI,CATEGORY,SUBCATEGORY,UNITS)=0;
$ifi %HYDROGEN%==yes  XH2_CAP_YCR(Y,C,IRRRE,IRRRI,VARIABLE_CATEGORY,UNITS)=0;
$ifi %HYDROGEN%==yes  XH2_FLOW_YCRST(Y,C,IRRRE,IRRRI,SSS,TTT,UNITS)=0;
$ifi %HYDROGEN%==yes  XH2_FLOW_YCR(Y,C,IRRRE,IRRRI,UNITS)=0;
$ifi %HYDROGEN%==yes  ECO_XH2_YCR(Y,C,RRR,IRRRI,CATEGORY,SUBCATEGORY,UNITS)=0;
$ifi %HYDROGEN%==yes  H2_DEMAND_YCRST(Y,C,RRR,SSS,TTT,VARIABLE_CATEGORY,UNITS)=0;
$ifi %HYDROGEN%==yes  H2_DEMAND_YCR(Y,C,RRR,VARIABLE_CATEGORY,UNITS)=0;
