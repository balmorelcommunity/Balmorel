*Create output file

SET CATEGORY /COSTS,INCOME,REVENUE/;
SET SUBCATEGORY /ELECTRICITY_SALE,HEAT_SALE,GENERATION_CAPITAL_COSTS,GENERATION_FIXED_COSTS,GENERATION_OPERATIONAL_COSTS,GENERATION_FUEL_COSTS,GENERATION_TAXES,GENERATION_GRID_TARIFFS,TRANSMISSION_CAPITAL_COSTS,TRANSMISSION_OPERATIONAL_COSTS,TRANSMISSION_TRADE_INCOME,TRANSMISSION_TRADE_COSTS,HEAT_TRANSMISSION_CAPITAL_COSTS,HEAT_TRANSMISSION_OPERATIONAL_COSTS,GENERATION_CO2_TAX,GENERATION_UC_COSTS,GENERATION_OTHER_EMI_TAX,HYDRO_PROFILE,TAXES,GRID_TARIFFS,TOTAL_REVENUE,ENERGY_SPECIFIC_REVENUE/;
SET COMMODITY /ELECTRICITY,HEAT/;
SET TECH_TYPE /CONDENSING,CHP-BACK-PRESSURE,CHP-EXTRACTION,BOILERS,ELECT-TO-HEAT,INTERSEASONAL-HEAT-STORAGE,INTERSEASONAL-ELECT-STORAGE,INTRASEASONAL-HEAT-STORAGE,INTRASEASONAL-ELECT-STORAGE,HYDRO-RESERVOIRS,HYDRO-RUN-OF-RIVER,WIND-ON,WIND-OFF,SOLAR-PV,SOLAR-HEATING,HYDRO-WAVE,HEAT-PUMP,EL-BOILER/;
SET GTECH_TYPE(GGG,TECH_TYPE);
SET PRICE_CATEGORY /AVERAGE,AVERAGE_WEIGHTED_BY_CONSUMPTION,AVERAGE_WEIGHTED_BY_PRODUCTION/;
SET VARIABLE_CATEGORY /EXOGENOUS,ENDOGENOUS,DECOMMISSIONING,ENDOGENOUS_ELECT2HEAT,ENDO_INTRASTO,ENDO_INTERSTO,ENDO_EV,ENDO_HEATPUMP,ENDO_ELBOILER/;
SET EL_BAL_TYPE /CURTAILMENT,NETEXPORT,PRICE,DEMAND_EXO,DEMAND_LOSS,DEMAND_INTERSTO,DEMAND_INTRASTO,DEMAND_P2H,DEMAND_EV,CONDENSING,CHP-BACK-PRESSURE,CHP-EXTRACTION,ELECT-TO-HEAT,INTER-STO,INTRA-STO,HYDRO-RESERVOIRS,HYDRO-RUN-OF-RIVER,WIND-ON,WIND-OFF,SOLAR-PV,HYDRO-WAVE/;
SET H_BAL_TYPE /CURTAILMENT,NETEXPORT,PRICE,DEMAND_EXO,DEMAND_LOSS,DEMAND_INTERSTO,DEMAND_INTRASTO,BOILERS,CHP-BACK-PRESSURE,CHP-EXTRACTION,INTER-STO,INTRA-STO,SOLAR-HEATING,P2H/;
SET UNITS /GW,TWh,MWh,Money_per_MWh,Mmoney,kton,GWh/;


   GTECH_TYPE(G,'CONDENSING')$IGCND(G)                                                    = YES;
   GTECH_TYPE(G,'CHP-BACK-PRESSURE')$IGBPR(G)                                             = YES;
   GTECH_TYPE(G,'CHP-EXTRACTION')$IGEXT(G)                                                = YES;
   GTECH_TYPE(G,'BOILERS')$IGHOB(G)                                                       = YES;
   GTECH_TYPE(G,'ELECT-TO-HEAT')$IGETOH(G)                                                = YES;
   GTECH_TYPE(G,'INTRASEASONAL-HEAT-STORAGE')$IGHSTO(G)                                   = YES;
   GTECH_TYPE(G,'INTERSEASONAL-HEAT-STORAGE')$IGHSTOS(G)                                  = YES;
   GTECH_TYPE(G,'INTRASEASONAL-ELECT-STORAGE')$IGESTO(G)                                  = YES;
   GTECH_TYPE(G,'INTERSEASONAL-ELECT-STORAGE')$IGESTOS(G)                                 = YES;
   GTECH_TYPE(G,'HYDRO-RESERVOIRS')$IGHYRS(G)                                             = YES;
   GTECH_TYPE(G,'HYDRO-RUN-OF-RIVER')$IGHYRR(G)                                           = YES;
   GTECH_TYPE(G,'WIND-ON')$(IGWND(G) AND GDATA(G,'GDTECHGROUP')  EQ WINDTURBINE_ONSHORE)  = YES;
   GTECH_TYPE(G,'WIND-OFF')$(IGWND(G) AND GDATA(G,'GDTECHGROUP') EQ WINDTURBINE_OFFSHORE) = YES;
   GTECH_TYPE(G,'SOLAR-PV')$IGSOLE(G)                                                     = YES;
   GTECH_TYPE(G,'SOLAR-HEATING')$IGSOLH(G)                                                = YES;
   GTECH_TYPE(G,'HYDRO-WAVE')$IGWAVE(G)                                                   = YES;


PARAMETER G_CAP_YCRAF(Y,C,RRR,AAA,G,FFF,COMMODITY,TECH_TYPE,VARIABLE_CATEGORY,UNITS) "Generation capacity for each year, country, region, area, technology, fuel, commodity, technology type and variable type (GW)";
PARAMETER G_STO_YCRAF(Y,C,RRR,AAA,G,FFF,COMMODITY,TECH_TYPE,VARIABLE_CATEGORY,UNITS) "Generation storage for each year, country, region, area, technology, fuel, commodity, technology type and variable type (GWh)";
PARAMETER PRO_YCRAGFST(Y,C,RRR,AAA,G,FFF,SSS,TTT,COMMODITY,TECH_TYPE,UNITS) "Energy Production for each year, country, region, area, technology, fuel, season, hour, commodity and technology type (MWh)" ;
PARAMETER PRO_YCRAGF(Y,C,RRR,AAA,G,FFF,COMMODITY,TECH_TYPE,UNITS) "Annual Energy Production for each year, country, region, area, technology, fuel, commodity and technology type (TWh)";
PARAMETER F_CONS_YCRAST(Y,C,RRR,AAA,G,FFF,SSS,TTT,TECH_TYPE,UNITS) "Fuel consumption for each year, country, region, area, technology, fuel, season, hour and technology type (MWh)";
PARAMETER F_CONS_YCRA(Y,C,RRR,AAA,G,FFF,TECH_TYPE,UNITS) "Fuel consumption for each year, country, region, area, technology, fuel and technology type (TWh)";
PARAMETER X_CAP_YCR(Y,C,IRRRE,IRRRI,VARIABLE_CATEGORY,UNITS) "Transmission capacity for each year, country, from region to region (GW)";
PARAMETER X_FLOW_YCRST(Y,C,IRRRE,IRRRI,SSS,TTT,UNITS) "Transmission flow for each year, country, from region to region, for each season and hour (MWh)";
PARAMETER X_FLOW_YCR(Y,C,IRRRE,IRRRI,UNITS) "Transmission flow for each year, country, from region to region (TWh)";
PARAMETER ECO_G_YCRAG(Y,C,RRR,AAA,G,FFF,TECH_TYPE,CATEGORY,SUBCATEGORY,UNITS) "Generation Economic Output for each year, country, region, fuel, technology type, area and technology (Mmoney)";
PARAMETER ECO_X_YCR(Y,C,RRR,CATEGORY,SUBCATEGORY,UNITS) "Electric Transmission Economic Output for each year, country and region (Mmoney)";
PARAMETER ECO_XH_YCRA(Y,C,RRR,AAA,CATEGORY,SUBCATEGORY,UNITS) "Heat Transmission Economic Output for each year, country and region and area (Mmoney)";
PARAMETER OBJ_YCR(Y,C,RRR,SUBCATEGORY,UNITS) "Objective function (Mmoney)";
PARAMETER EL_PRICE_YCR(Y,C,RRR,PRICE_CATEGORY,UNITS) "Average Electricity Prices for each region (money/MWh)";
PARAMETER EL_PRICE_YCRST(Y,C,RRR,SSS,TTT,UNITS) "Hourly Electricity Prices for each region (money/MWh)";
PARAMETER H_PRICE_YCRA(Y,C,RRR,AAA,PRICE_CATEGORY,UNITS) "Average heating Prices for each area(money/MWh)";
PARAMETER H_PRICE_YCRAST(Y,C,RRR,AAA,SSS,TTT,UNITS) "Hourly heating Prices for each area (money(MWh)";
PARAMETER EL_DEMAND_YCRST(Y,C,RRR,SSS,TTT,VARIABLE_CATEGORY,UNITS) "Aggregated Hourly Electricity Demand (MWh)";
PARAMETER EL_DEMAND_YCR(Y,C,RRR,VARIABLE_CATEGORY,UNITS) "Aggregated annual Electricity Demand (TWh)";
PARAMETER H_DEMAND_YCRAST(Y,C,RRR,AAA,SSS,TTT,VARIABLE_CATEGORY,UNITS) "Aggregated Hourly heat Demand (MWh)";
PARAMETER H_DEMAND_YCRA(Y,C,RRR,AAA,VARIABLE_CATEGORY,UNITS) "Aggregated Annual heat Demand (MWh)";
PARAMETER EMI_YCRAG(Y,C,RRR,AAA,G,FFF,TECH_TYPE,UNITS) "Annual CO2 emissions(ktons)";
PARAMETER CURT_YCRAGFST(Y,C,RRR,AAA,G,FFF,SSS,TTT,COMMODITY,TECH_TYPE,UNITS) "Hourly energy curtailment per country, region, area, technology, fuel, hour and technology type (MWh)";
PARAMETER CURT_YCRAGF(Y,C,RRR,AAA,G,FFF,COMMODITY,TECH_TYPE,UNITS) "Hourly energy curtailment per country, region, area, technology, fuel, hour and technology type (MWh)";
PARAMETER EL_BALANCE_YCRST(Y,C,RRR,EL_BAL_TYPE,SSS,TTT,UNITS);
PARAMETER H_BALANCE_YCRAST(Y,C,RRR,AAA,H_BAL_TYPE,SSS,TTT,UNITS);

**Additional Addon output
*HEATRANS
$ifi %HEATTRANS%==yes PARAMETER XH_CAP_YCA(Y,C,IAAAE,IAAAI,VARIABLE_CATEGORY,UNITS) "Heat Transmission capacity for each year, country, from area to area (GW)";
$ifi %HEATTRANS%==yes PARAMETER XH_FLOW_YCAST(Y,C,IAAAE,IAAAI,SSS,TTT,UNITS) "Heat Transmission flow for each year, country, from areas to area, for each season and hour (MWh)";
$ifi %HEATTRANS%==yes PARAMETER XH_FLOW_YCA(Y,C,IAAAE,IAAAI,UNITS) "Heat Transmission flow for each year, country, from area to area (TWh)";


*Modification of IHOURSINST
$ifi %RollingSeasons%==yes PARAMETER IHOURSINTROLLINGSEASON(S,T) "Length of time segment (hours) used in rolling season mode";
$ifi %RollingSeasons%==yes IWEIGHSUMS=SUM(SSS$(ORD(SSS) LE ROLLINGSEASONSNUMBER), WEIGHT_S(SSS));
$ifi %RollingSeasons%==yes IHOURSINTROLLINGSEASON(S,T)=IOF8760*WEIGHT_S(S)*WEIGHT_T(T)/(IWEIGHSUMS*SUM(ITALIAS, WEIGHT_T(ITALIAS)));


IWEIGHSUMS = SUM(S, WEIGHT_S(S));
IWEIGHSUMT = SUM(T, WEIGHT_T(T));
IHOURSINST(SSS,TTT)=0;
IHOURSINST(S,T)=IOF8760*WEIGHT_S(S)*WEIGHT_T(T)/(IWEIGHSUMS*IWEIGHSUMT);



* -------------------- ENERGY CAPACITY ---------------------

* ------------- ELECTRICITY CAPACITY --------------

** EXOGENOUS ELECTRICITY CAPACITY

G_CAP_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'EXOGENOUS','GW')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE))=
IOF0001*GKFX(Y,IA,IGE)/(1$((NOT IGESTO(IGE)) AND (NOT IGESTOS(IGE)))+GDATA(IGE,'GDSTOHUNLD')$((IGESTO(IGE)) OR IGESTOS(IGE)))
;


** ENDOGENOUS ELECTRIC CAPACITY ACCUMULATED INVESTMENTS
G_CAP_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'ENDOGENOUS','GW')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE))=
IOF0001*VGKNACCUMNET.L(Y,IA,IGE)/(1$((NOT IGESTO(IGE)) AND (NOT IGESTOS(IGE)))+GDATA(IGE,'GDSTOHUNLD')$((IGESTO(IGE)) OR IGESTOS(IGE)))
;

** ENDOGENOUS ELECTRICITY CAPACITY ACCUMULATED DECOMMISSIONING
$ifi %DECOM%==yes G_CAP_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'DECOMMISSIONING','GW')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,IGE))=
$ifi %DECOM%==yes -IOF0001*VDECOM_EXO_ACCUM.L(Y,IA,IGE)/(1$((NOT IGESTO(IGE)) AND (NOT IGESTOS(IGE)))+GDATA(IGE,'GDSTOHUNLD')$((IGESTO(IGE)) OR IGESTOS(IGE)))
;


* ------------- END OF ELECTRICITY CAPACITY --------------

* ------------- HEAT CAPACITY --------------

** EXOGENOUS HEAT CAPACITY

G_CAP_YCRAF(Y,C,IR,IA,IGH,FFF,'HEAT',TECH_TYPE,'EXOGENOUS','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE))=
IOF0001*GKFX(Y,IA,IGH)/((1$((NOT IGHSTO(IGH)) AND (NOT IGHSTOS(IGH)))+GDATA(IGH,'GDSTOHUNLD')$((IGHSTO(IGH)) OR IGHSTOS(IGH)))*(GDATA(IGH,'GDCB')$IGBPR(IGH)+ (GDATA(IGH,'GDCB')+GDATA(IGH,'GDCV'))$IGEXT(IGH)+1$(NOT (IGBPR(IGH) OR IGEXT(IGH)))))
;


** ENDOGENOUS HEAT CAPACITY ACCUMULATED INVESTMENTS
G_CAP_YCRAF(Y,C,IR,IA,IGH,FFF,'HEAT',TECH_TYPE,'ENDOGENOUS','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE))=
IOF0001*VGKNACCUMNET.L(Y,IA,IGH)/((1$((NOT IGHSTO(IGH)) AND (NOT IGHSTOS(IGH)))+GDATA(IGH,'GDSTOHUNLD')$((IGHSTO(IGH)) OR IGHSTOS(IGH)))*(GDATA(IGH,'GDCB')$IGBPR(IGH)+ (GDATA(IGH,'GDCB')+GDATA(IGH,'GDCV'))$IGEXT(IGH)+1$(NOT (IGBPR(IGH) OR IGEXT(IGH)))))
;

** ENDOGENOUS HEAT CAPACITY ACCUMULATED DECOMMISSIONING
$ifi %DECOM%==yes G_CAP_YCRAF(Y,C,IR,IA,IGH,FFF,'HEAT',TECH_TYPE,'DECOMMISSIONING','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,IGH))=
$ifi %DECOM%==yes -IOF0001*VDECOM_EXO_ACCUM.L(Y,IA,IGH)/((1$((NOT IGHSTO(IGH)) AND (NOT IGHSTOS(IGH)))+GDATA(IGH,'GDSTOHUNLD')$((IGHSTO(IGH)) OR IGHSTOS(IGH)))*(GDATA(IGH,'GDCB')$IGBPR(IGH)+ (GDATA(IGH,'GDCB')+GDATA(IGH,'GDCV'))$IGEXT(IGH)+1$(NOT (IGBPR(IGH) OR IGEXT(IGH)))))
;

* ------------- END OF HEAT CAPACITY --------------

* ---------------- END OF ENERGY CAPACITY -----------------

* -------------------- ENERGY STORAGE ---------------------

* ------------- ELECTRICITY STORAGE --------------

** EXOGENOUS ELECTRICITY STORAGE

G_STO_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'EXOGENOUS','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND (IGESTO(IGE) OR IGESTOS(IGE)) )=
IOF0001*GKFX(Y,IA,IGE)
;

** ENDOGENOUS ELECTRIC STORAGE ACCUMULATED INVESTMENTS
G_STO_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'ENDOGENOUS','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND (IGESTO(IGE) OR IGESTOS(IGE)) )=
IOF0001*VGKNACCUMNET.L(Y,IA,IGE)
;

** ENDOGENOUS ELECTRICITY STORAGE ACCUMULATED DECOMMISSIONING
$ifi %DECOM%==yes G_STO_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'DECOMMISSIONING','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,IGE) AND (IGESTO(IGE) OR IGESTOS(IGE)) )=
$ifi %DECOM%==yes -IOF0001*VDECOM_EXO_ACCUM.L(Y,IA,IGE)
;


* ------------- END OF ELECTRICITY CAPACITY --------------

* ------------- HEAT STORAGE --------------

** EXOGENOUS HEAT STORAGE

G_STO_YCRAF(Y,C,IR,IA,IGH,FFF,'HEAT',TECH_TYPE,'EXOGENOUS','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND (IGHSTO(IGH) OR IGHSTOS(IGH)))=
IOF0001*GKFX(Y,IA,IGH)
;

** ENDOGENOUS HEAT STORAGE ACCUMULATED INVESTMENTS
G_STO_YCRAF(Y,C,IR,IA,IGH,FFF,'HEAT',TECH_TYPE,'ENDOGENOUS','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND (IGHSTO(IGH) OR IGHSTOS(IGH)))=
IOF0001*VGKNACCUMNET.L(Y,IA,IGH)
;

** ENDOGENOUS HEAT STORAGE ACCUMULATED DECOMMISSIONING
$ifi %DECOM%==yes G_STO_YCRAF(Y,C,IR,IA,IGH,FFF,'HEAT',TECH_TYPE,'DECOMMISSIONING','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,IGH) AND (IGHSTO(IGH) OR IGHSTOS(IGH)))=
$ifi %DECOM%==yes -IOF0001*VDECOM_EXO_ACCUM.L(Y,IA,IGH)
;

* ------------- END OF HEAT STORAGE --------------

* ---------------- END OF ENERGY STORAGE -----------------




* -------------------- ENERGY PRODUCTION ---------------------

** ELECTRICITY PRODUCTION HOURLY
PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY',TECH_TYPE,'MWh')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE))=
       VGE_T.L(Y,IA,IGE,S,T);

** ELECTRICITY PRODUCTION ANNUALLY
PRO_YCRAGF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,'TWh')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE))=
       SUM((S,T),IHOURSINST(S,T)*VGE_T.L(Y,IA,IGE,S,T))/IOF1000000;

** heat PRODUCTION HOURLY
PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'HEAT',TECH_TYPE,'MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE))=
       VGH_T.L(Y,IA,IGH,S,T);

** heat PRODUCTION ANNUALY
PRO_YCRAGF(Y,C,IR,IA,IGH,FFF,'HEAT',TECH_TYPE,'TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE))=
       SUM((S,T),IHOURSINST(S,T)*VGH_T.L(Y,IA,IGH,S,T))/IOF1000000;

* ---------------- END OF ENERGY PRODUCTION -----------------


* -------------------- FUEL CONSUMPTION ---------------------

** HOURLY FUEL CONSUMPTION
F_CONS_YCRAST(Y,C,IR,IA,G,FFF,S,T,TECH_TYPE,'MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE))=
       VGF_T.L(Y,IA,G,S,T);

** ANNUAL FUEL CONSUMPTION
F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE))=
       SUM((S,T),IHOURSINST(S,T)*VGF_T.L(Y,IA,G,S,T))/IOF1000000;


* ---------------- END OF FUEL CONSUMPTION -----------------

* ---------------- ENERGY CURTAILMENT -----------------------
*HOURLY CURTAILMENT
CURT_YCRAGFST(Y,C,IR,IA,IGHYRR,FFF,S,T,'ELECTRICITY','HYDRO-RUN-OF-RIVER','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGHYRR) AND IGF(IGHYRR,FFF) AND GTECH_TYPE(IGHYRR,'HYDRO-RUN-OF-RIVER'))=
(WTRRRFLH(IA) * (
           GKFX(Y,IA,IGHYRR)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGHYRR)
        +  VGKNACCUMNET.L(Y,IA,IGHYRR)
                          ) * WTRRRVAR_T(IA,S,T) * (1$(NOT IGKRATE(IA,IGHYRR,S,T)) + IGKRATE(IA,IGHYRR,S,T))) / IWTRRRSUM(IA)
-VGE_T.L(Y,IA,IGHYRR,S,T);

CURT_YCRAGFST(Y,C,IR,IA,IGWND,FFF,S,T,'ELECTRICITY','WIND-ON','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGWND) AND IGF(IGWND,FFF) AND GTECH_TYPE(IGWND,'WIND-ON'))=
$ifi %WNDFLH_DOL%==AAA         (WNDFLH(IA) * (
$ifi %WNDFLH_DOL%==AAA_GGG     (WNDFLH(IA,IGWND) * (
            GKFX(Y,IA,IGWND)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGWND)
         + VGKNACCUMNET.L(Y,IA,IGWND)
                 )  * WND_VAR_T(IA,S,T) * (1$(NOT IGKRATE(IA,IGWND,S,T)) + IGKRATE(IA,IGWND,S,T))) / IWND_SUMST(IA)
-VGE_T.L(Y,IA,IGWND,S,T);

CURT_YCRAGFST(Y,C,IR,IA,IGWND,FFF,S,T,'ELECTRICITY','WIND-OFF','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGWND) AND IGF(IGWND,FFF) AND GTECH_TYPE(IGWND,'WIND-OFF'))=
$ifi %WNDFLH_DOL%==AAA         (WNDFLH(IA) * (
$ifi %WNDFLH_DOL%==AAA_GGG     (WNDFLH(IA,IGWND) * (
            GKFX(Y,IA,IGWND)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGWND)
         + VGKNACCUMNET.L(Y,IA,IGWND)
                 )  * WND_VAR_T(IA,S,T) * (1$(NOT IGKRATE(IA,IGWND,S,T)) + IGKRATE(IA,IGWND,S,T))) / IWND_SUMST(IA)
-VGE_T.L(Y,IA,IGWND,S,T);

CURT_YCRAGFST(Y,C,IR,IA,IGSOLE,FFF,S,T,'ELECTRICITY','SOLAR-PV','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGSOLE) AND IGF(IGSOLE,FFF) AND GTECH_TYPE(IGSOLE,'SOLAR-PV'))=
$ifi %SOLEFLH_DOL%==AAA         (SOLEFLH(IA) * (
$ifi %SOLEFLH_DOL%==AAA_GGG     (SOLEFLH(IA,IGSOLE) * (
            GKFX(Y,IA,IGSOLE)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGSOLE)
         + VGKNACCUMNET.L(Y,IA,IGSOLE)
                 )  * SOLE_VAR_T(IA,S,T) * (1$(NOT IGKRATE(IA,IGSOLE,S,T)) + IGKRATE(IA,IGSOLE,S,T))) / ISOLESUMST(IA)
-VGE_T.L(Y,IA,IGSOLE,S,T);

CURT_YCRAGFST(Y,C,IR,IA,IGSOLH,FFF,S,T,'HEAT','SOLAR-HEATING','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGSOLH) AND IGF(IGSOLH,FFF) AND GTECH_TYPE(IGSOLH,'SOLAR-HEATING'))=
$ifi %SOLHFLH_DOL%==AAA         (SOLHFLH(IA) * (
$ifi %SOLHFLH_DOL%==AAA_GGG     (SOLHFLH(IA,IGSOLH) * (
            GKFX(Y,IA,IGSOLH)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGSOLH)
         + VGKNACCUMNET.L(Y,IA,IGSOLH)
                 )  * SOLH_VAR_T(IA,S,T) * (1$(NOT IGKRATE(IA,IGSOLH,S,T)) + IGKRATE(IA,IGSOLH,S,T))) / ISOLHSUMST(IA)
-VGE_T.L(Y,IA,IGSOLH,S,T);


*ANNUAL CURTAILMENT
CURT_YCRAGF(Y,C,IR,IA,IGHYRR,FFF,'ELECTRICITY','HYDRO-RUN-OF-RIVER','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGHYRR) AND IGF(IGHYRR,FFF) AND GTECH_TYPE(IGHYRR,'HYDRO-RUN-OF-RIVER'))=
SUM((S,T),IHOURSINST(S,T)*CURT_YCRAGFST(Y,C,IR,IA,IGHYRR,FFF,S,T,'ELECTRICITY','HYDRO-RUN-OF-RIVER','MWh'))/IOF1000000
;
CURT_YCRAGF(Y,C,IR,IA,IGWND,FFF,'ELECTRICITY','WIND-ON','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGWND) AND IGF(IGWND,FFF) AND GTECH_TYPE(IGWND,'WIND-ON'))=
SUM((S,T),IHOURSINST(S,T)*CURT_YCRAGFST(Y,C,IR,IA,IGWND,FFF,S,T,'ELECTRICITY','WIND-ON','MWh'))/IOF1000000
;
CURT_YCRAGF(Y,C,IR,IA,IGWND,FFF,'ELECTRICITY','WIND-OFF','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGWND) AND IGF(IGWND,FFF) AND GTECH_TYPE(IGWND,'WIND-OFF'))=
SUM((S,T),IHOURSINST(S,T)*CURT_YCRAGFST(Y,C,IR,IA,IGWND,FFF,S,T,'ELECTRICITY','WIND-OFF','MWh'))/IOF1000000
;
CURT_YCRAGF(Y,C,IR,IA,IGSOLE,FFF,'ELECTRICITY','SOLAR-PV','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGSOLE) AND IGF(IGSOLE,FFF) AND GTECH_TYPE(IGSOLE,'SOLAR-PV'))=
SUM((S,T),IHOURSINST(S,T)*CURT_YCRAGFST(Y,C,IR,IA,IGSOLE,FFF,S,T,'ELECTRICITY','SOLAR-PV','MWh'))/IOF1000000
;
CURT_YCRAGF(Y,C,IR,IA,IGSOLH,FFF,'HEAT','SOLAR-HEATING','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGSOLH) AND IGF(IGSOLH,FFF) AND GTECH_TYPE(IGSOLH,'SOLAR-HEATING'))=
SUM((S,T),IHOURSINST(S,T)*CURT_YCRAGFST(Y,C,IR,IA,IGSOLH,FFF,S,T,'HEAT','SOLAR-HEATING','MWh'))/IOF1000000
;

*Curtailment of hydro reservoirs should be analyzed by aggregating the technology set
CURT_YCRAGF(Y,C,IR,IA,IGHYRS,FFF,'ELECTRICITY','HYDRO-RESERVOIRS','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGHYRS) AND IGF(IGHYRS,FFF) AND GTECH_TYPE(IGHYRS,'HYDRO-RESERVOIRS'))=
SUM(S,
        IHYINF_S(IA,S) *
        (
        GKFX(Y,IA,IGHYRS)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGHYRS)
       +VGKNACCUMNET.L(Y,IA,IGHYRS)
         )
         -SUM(T,IHYRST(S,T)*VGE_T.L(Y,IA,IGHYRS,S,T))
    )/IOF1000000
;


* ---------------- END OF ENERGY CURTAILMENT -----------------------

* ------------------------- ENERGY DEMAND ----------------------

* ------------- ELECTRICITY DEMAND --------------

*HOURLY
** EXOGENOUS DEMAND

EL_DEMAND_YCRST(Y,C,IR,S,T,'EXOGENOUS','MWh')$CCCRRR(C,IR)=
         SUM(DEUSER,IDE_T(Y,IR,DEUSER,S,T))/(1-DISLOSS_E(IR));

** ENDOGENOUS DEMAND

EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDOGENOUS_ELECT2HEAT','MWh')$CCCRRR(C,IR)=
* HEAT PUMPS CONSUMPTION
         SUM(IA$RRRAAA(IR,IA), SUM(IGETOH$IAGK_HASORPOT(Y,IA,IGETOH), VGE_T.L(Y,IA,IGETOH,S,T)))
;

EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_INTRASTO','MWh')$CCCRRR(C,IR)=
* INTRASEASONAL ELECTRICITY STORAGE
         +SUM(IA$RRRAAA(IR,IA),SUM(IGESTO$IAGK_HASORPOT(Y,IA,IGESTO),VESTOLOADT.L(Y,IA,IGESTO,S,T)))
;

EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_INTERSTO','MWh')$CCCRRR(C,IR)=
* INTERSEASONAL ELECTRICITY STORAGE
         +SUM(IA$RRRAAA(IR,IA),SUM(IGESTOS$IAGK_HASORPOT(Y,IA,IGESTOS),VESTOLOADTS.L(Y,IA,IGESTOS,S,T)))
;

$ifi %EV%==yes EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_EV','MWh')$CCCRRR(C,IR)=
$ifi %EV%==yes +VEV_VNETCHARGE_BEV.L(Y,IR,S,T) +VEV_VNETCHARGE_PHEV.L(Y,IR,S,T)
*-VEV_VNETDISCHARGE_BEV.L(Y,IR,S,T)
;

*ANNUAL DEMAND
** EXOGENOUS DEMAND

EL_DEMAND_YCR(Y,C,IR,'EXOGENOUS','TWh')$CCCRRR(C,IR)=
         SUM((S,T,DEUSER),IHOURSINST(S,T)*IDE_T(Y,IR,DEUSER,S,T))/(1-DISLOSS_E(IR))/IOF1000000;

** ENDOGENOUS DEMAND

EL_DEMAND_YCR(Y,C,IR,'ENDOGENOUS_ELECT2HEAT','TWh')$CCCRRR(C,IR)=
* HEAT PUMPS CONSUMPTION
         SUM((S,T,IA)$RRRAAA(IR,IA), SUM(IGETOH$IAGK_HASORPOT(Y,IA,IGETOH), IHOURSINST(S,T)*VGE_T.L(Y,IA,IGETOH,S,T)))/IOF1000000
;

EL_DEMAND_YCR(Y,C,IR,'ENDO_INTRASTO','TWh')$CCCRRR(C,IR)=
* INTRASEASONAL ELECTRICITY STORAGE LOSS
         +SUM((S,T,IA)$RRRAAA(IR,IA),SUM(IGESTO$IAGK_HASORPOT(Y,IA,IGESTO),IHOURSINST(S,T)*(VESTOLOADT.L(Y,IA,IGESTO,S,T)-VGE_T.L(Y,IA,IGESTO,S,T))))/IOF1000000
;

EL_DEMAND_YCR(Y,C,IR,'ENDO_INTERSTO','TWh')$CCCRRR(C,IR)=
* INTERSEASONAL ELECTRICITY STORAGE LOSS
         +SUM((S,T,IA)$RRRAAA(IR,IA),SUM(IGESTOS$IAGK_HASORPOT(Y,IA,IGESTOS),IHOURSINST(S,T)*(VESTOLOADTS.L(Y,IA,IGESTOS,S,T)-VGE_T.L(Y,IA,IGESTOS,S,T))))/IOF1000000
;

EL_DEMAND_YCR(Y,C,IR,'ENDO_EV','TWh')$CCCRRR(C,IR)=
         SUM((S,T),IHOURSINST(S,T)*EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_EV','MWh'))/IOF1000000;

* ----------- END OF ELECTRICITY DEMAND --------------

* ------------- HEAT DEMAND --------------
*HOURLY DEMAND
** EXOGENOUS DEMAND

H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'EXOGENOUS','MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=
         SUM(DHUSER,IDH_T(Y,IA,DHUSER,S,T))/(1-DISLOSS_H(IA));

** ENDOGENOUS DEMAND

H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'ENDO_INTRASTO','MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=

* INTRASEASONAL HEAT STORAGE
        SUM(IGHSTO$IAGK_HASORPOT(Y,IA,IGHSTO),VHSTOLOADT.L(Y,IA,IGHSTO,S,T))

;

H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'ENDO_INTERSTO','MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=

* INTERSEASONAL HEAT STORAGE
        + SUM(IGHSTOS$IAGK_HASORPOT(Y,IA,IGHSTOS),VHSTOLOADTS.L(Y,IA,IGHSTOS,S,T))
;

*ANNUAL DEMAND
** EXOGENOUS DEMAND

H_DEMAND_YCRA(Y,C,IR,IA,'EXOGENOUS','TWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=
         SUM((S,T,DHUSER),IHOURSINST(S,T)*IDH_T(Y,IA,DHUSER,S,T))/(1-DISLOSS_H(IA))/IOF1000000;

** ENDOGENOUS DEMAND

H_DEMAND_YCRA(Y,C,IR,IA,'ENDO_INTRASTO','TWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=

* INTRASEASONAL HEAT STORAGE LOSS
        SUM((S,T,IGHSTO)$IAGK_HASORPOT(Y,IA,IGHSTO),IHOURSINST(S,T)*(VHSTOLOADT.L(Y,IA,IGHSTO,S,T)-VGH_T.L(Y,IA,IGHSTO,S,T)))/IOF1000000

;

H_DEMAND_YCRA(Y,C,IR,IA,'ENDO_INTERSTO','TWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=

* INTERSEASONAL HEAT STORAGE LOSS
        + SUM((S,T,IGHSTOS)$IAGK_HASORPOT(Y,IA,IGHSTOS),IHOURSINST(S,T)*(VHSTOLOADTS.L(Y,IA,IGHSTOS,S,T)-VGH_T.L(Y,IA,IGHSTOS,S,T)))/IOF1000000
;

* ----------- END OF HEAT DEMAND --------------

* -------------------- END OF ENERGY DEMAND -----------------------


* -------------------- TRANSMISSION CAPACITY -----------------------

** EXOGENOUS ELECTRICITY CAPACITY

X_CAP_YCR(Y,C,IR,IRI,'EXOGENOUS','GW')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=
IOF0001*XKFX(Y,IR,IRI)
;


** ENDOGENOUS ELECTRIC CAPACITY

$ifi not %TransInvest%==yes $goto Transmission_investments_end
X_CAP_YCR(Y,C,IR,IRI,'ENDOGENOUS','GW')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=
IOF0001*VXKNACCUMNET.L(Y,IR,IRI)
;
$label  Transmission_investments_end


* -------------------- END OF TRANSMISSION CAPACITY -----------------------

* -------------------- TRANSMISSION FLOW -----------------------

** HOURLY TRANSMISSION FLOW

X_FLOW_YCRST(Y,C,IR,IRI,S,T,'MWh')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=
VX_T.L(Y,IR,IRI,S,T);
;


** ANNUAL TRANSMISSION FLOW
X_FLOW_YCR(Y,C,IR,IRI,'TWh')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=
SUM((S,T),IHOURSINST(S,T)*VX_T.L(Y,IR,IRI,S,T))/IOF1000000;
;



* -------------------- END OF TRANSMISSION FLOW -----------------------

$ifi NOT %HEATTRANS%==yes $goto no_heattrans
* -------------------- HEAT TRANSMISSION CAPACITY -----------------------

** EXOGENOUS HEAT TRANSMISSION CAPACITY

XH_CAP_YCA(Y,C,IA,IAI,'EXOGENOUS','GW')$(ICA(C,IA) AND IXHK_HASORPOT(Y,IA,IAI))=
IOF0001*XHKFX(Y,IA,IAI)
;


** ENDOGENOUS HEAT TRANSMISSION CAPACITY

$ifi not %HeatTransInvest%==yes $goto Heat_Transmission_investments_end
XH_CAP_YCA(Y,C,IA,IAI,'ENDOGENOUS','GW')$(ICA(C,IA) AND IXHK_HASORPOT(Y,IA,IAI))=
IOF0001*VXHKNACCUMNET.L(Y,IA,IAI)
;
$label  Heat_Transmission_investments_end


* -------------------- END OF HEAT TRANSMISSION CAPACITY -----------------------

* -------------------- HEAT TRANSMISSION FLOW -----------------------

** HOURLY HEAT TRANSMISSION FLOW

XH_FLOW_YCAST(Y,C,IA,IAI,S,T,'MWh')$(ICA(C,IA) AND IXHK_HASORPOT(Y,IA,IAI))=
VXH_T.L(Y,IA,IAI,S,T);
;

** ANNUAL HEAT TRANSMISSION FLOW
XH_FLOW_YCA(Y,C,IA,IAI,'TWh')$(ICA(C,IA) AND IXHK_HASORPOT(Y,IA,IAI))=
SUM((S,T),IHOURSINST(S,T)*VXH_T.L(Y,IA,IAI,S,T))/IOF1000000;
;

* -------------------- END OF HEAT TRANSMISSION FLOW -----------------------
$label no_heattrans

* ---------------------- ECONOMIC OUTPUT ----------------------

* ------------- COMMODITY PRICES --------------

** ELECTRICITY PRICES

*HOURLY ELECTRICITY PRICE

EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')$CCCRRR(C,IR)=OMONEY*QEEQ.M(Y,IR,S,T)
$ifi %RollingSeasons%==yes         /IHOURSINTROLLINGSEASON(S,T)
$ifi not %RollingSeasons%==yes     /IHOURSINST(S,T)
/IDISCOUNTFACTOR(Y)/IWEIGHTY(Y);

*AVERAGE ELECTRICITY PRICE

EL_PRICE_YCR(Y,C,IR,'AVERAGE','Money_per_MWh')$CCCRRR(C,IR)=
SUM((S,T),EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')/(IWEIGHSUMS*IWEIGHSUMT/(WEIGHT_S(S)*WEIGHT_T(T))));

$ontext
*EXCLUDED SO FAR
*AVERAGE ELECTRICITY PRICE WEIGTHED BY CONSUMPTION

EL_PRICE_YCR(Y,C,IR,'AVERAGE_WEIGHTED_BY_CONSUMPTION','Money_per_MWh')$CCCRRR(C,IR)=
               SUM((VARIABLE_CATEGORY,S,T),
                         EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')*EL_DEMAND_YCRST(Y,C,IR,S,T,VARIABLE_CATEGORY,'MWh')
                         )
               /SUM((VARIABLE_CATEGORY,S,T),EL_DEMAND_YCRST(Y,C,IR,S,T,VARIABLE_CATEGORY,'MWh'))
;

*AVERAGE ELECTRICITY PRICE WEIGTHED BY PRODUCTION

EL_PRICE_YCR(Y,C,IR,'AVERAGE_WEIGHTED_BY_PRODUCTION','Money_per_MWh')$CCCRRR(C,IR)=
               SUM((IGE,IA,FFF,S,T,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,IGE)),
                         EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')*PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY',TECH_TYPE,'MWh')
                         )
               /SUM((IGE,IA,FFF,S,T,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY',TECH_TYPE,'MWh'))
;
$offtext

** END OF ELECTRICITY PRICES

** heatING PRICES

*HOURLY heatING PRICE

H_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND SUM(DHUSER, IDH_SUMST(IA,DHUSER)))=OMONEY*QHEQ.M(Y,IA,S,T)
$ifi %RollingSeasons%==yes         /IHOURSINTROLLINGSEASON(S,T)
$ifi not %RollingSeasons%==yes     /IHOURSINST(S,T)
/IDISCOUNTFACTOR(Y)/IWEIGHTY(Y);

*AVERAGE heatING PRICE

H_PRICE_YCRA(Y,C,IR,IA,'AVERAGE','Money_per_MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND SUM(DHUSER, IDH_SUMST(IA,DHUSER)))=
SUM((S,T),H_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')/(IWEIGHSUMS*IWEIGHSUMT/(WEIGHT_S(S)*WEIGHT_T(T))));

$ontext
*EXCLUDED SO FAR
*AVERAGE heatING PRICE WEIGTHED BY CONSUMPTION

H_PRICE_YCRA(Y,C,IR,IA,'AVERAGE_WEIGHTED_BY_CONSUMPTION','Money_per_MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND SUM(DHUSER, IDH_SUMST(IA,DHUSER)))=
               SUM((VARIABLE_CATEGORY,S,T),
                         H_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')*H_DEMAND_YCRAST(Y,C,IR,IA,S,T,VARIABLE_CATEGORY,'MWh')
                         )
               /SUM((VARIABLE_CATEGORY,S,T),H_DEMAND_YCRAST(Y,C,IR,IA,S,T,VARIABLE_CATEGORY,'MWh'))
;

*AVERAGE heatING PRICE WEIGTHED BY PRODUCTION

H_PRICE_YCRA(Y,C,IR,IA,'AVERAGE_WEIGHTED_BY_PRODUCTION','Money_per_MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND SUM(DHUSER, IDH_SUMST(IA,DHUSER)))=
               SUM((IGH,FFF,S,T,TECH_TYPE)$IAGK_HASORPOT(Y,IA,IGH),
                         H_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')*PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'HEAT',TECH_TYPE,'MWh')
                         )
               /SUM((IGH,FFF,S,T,TECH_TYPE)$IAGK_HASORPOT(Y,IA,IGH),PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'HEAT',TECH_TYPE,'MWh'))
;
$offtext

** END OF heatING PRICES


* ------------- END OF COMMODITY PRICES --------------


* ------------- COSTS --------------

* -- GENERATION COSTS -----

** GENERATION INVESTMENTS

ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_CAPITAL_COSTS','Mmoney')$(CCCRRR(C,IR) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and RRRAAA(IR,IA))=
         OMONEY*SUM(IYALIAS2$((ORD(IYALIAS2) LE ORD(Y)) AND IAGKNY(IYALIAS2,IA,G) AND (NOT IGKN_ES(G))),IYHASANNUITYG(IYALIAS2,Y,G)*VGKN.L(IYALIAS2,IA,G)*GINVCOST(IA,G)*ANNUITYCG(C,G));

** GENEARTION FIXED COSTS

ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_FIXED_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and IAGK_HASORPOT(Y,IA,G))=
         OMONEY/IOF1000*GOMFCOST(IA,G)*(
         GKFX(Y,IA,G)
         +VGKNACCUMNET.L(Y,IA,G)
$ifi %DECOM%==yes         -VDECOM_EXO_ACCUM.L(Y,IA,G)$IGDECOMEXOPOT(Y,IA,G)
         )
;

** GENERATION O&M COSTS

ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_OPERATIONAL_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and IAGK_HASORPOT(Y,IA,G))=
   OMONEY/IOF1000000*
     ((GOMVCOSTO(IA,G)*
      (
   +   SUM((S,T), IHOURSINST(S,T) * VGE_T.L(Y,IA,G,S,T))$IAGK_HASORPOT(Y,IA,G)))$IGKE(G)

   +  (GOMVCOSTO(IA,G) * GDATA(G,'GDCV') *
      (
   +   SUM((S,T), IHOURSINST(S,T) * VGH_T.L(Y,IA,G,S,T))$IAGK_HASORPOT(Y,IA,G)))$IGKH(G)

   +  (GOMVCOSTIN(IA,G)*
      (
   +   SUM((S,T), IHOURSINST(S,T) * VGF_T.L(Y,IA,G,S,T))$IAGK_HASORPOT(Y,IA,G)))
     ) ;

** GENERATION FUEL COSTS

ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_FUEL_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and IAGK_HASORPOT(Y,IA,G))=
   OMONEY*IOF3P6/IOF1000000*(
                 FUELPRICE(Y,IA,FFF)
$ifi not %STEPWISEPRICE%==yes $goto No_STEPWISEPRICE
                 + (SUM((STEPWISEPRICE_L,STEPWISEPRICE_FFF(FFF))$ISTEPWISEPRICE_F(Y,C,STEPWISEPRICE_FFF),
                 STEPWISEPRICE_FUELPRICE_STEP(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF)*VSTEPWISEPRICE_VGF_T_STEP.L(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF))
                 /SUM((STEPWISEPRICE_L,STEPWISEPRICE_FFF(FFF)),VSTEPWISEPRICE_VGF_T_STEP.L(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF)))$(SUM(STEPWISEPRICE_FFF(FFF),ISTEPWISEPRICE_F(Y,C,STEPWISEPRICE_FFF)) AND SUM((STEPWISEPRICE_L,STEPWISEPRICE_FFF(FFF)),VSTEPWISEPRICE_VGF_T_STEP.L(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF)))
$label No_STEPWISEPRICE
                 )*
   (
                 SUM((S,T), IHOURSINST(S,T) * VGF_T.L(Y,IA,G,S,T) )$IAGK_HASORPOT(Y,IA,G)
    )$(IGNOTETOH(G) and (not IGESTO(G)) and (not IGHSTO(G)) and (not IGESTOS(G)) and (not IGHSTOS(G)))


* Electricity storages:
    +OMONEY/IOF1000000*SUM((S,T), EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')*VESTOLOADT.L(Y,IA,G,S,T))$IGESTO(G)
    +OMONEY/IOF1000000*SUM((S,T), EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')*VESTOLOADTS.L(Y,IA,G,S,T))$IGESTOS(G)
* Heat storages:
    +OMONEY/IOF1000000*SUM((S,T), H_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')*VHSTOLOADT.L(Y,IA,G,S,T))$IGHSTO(G)
    +OMONEY/IOF1000000*SUM((S,T), H_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')*VHSTOLOADTS.L(Y,IA,G,S,T))$IGHSTOS(G)
* Electricity to heat:
    +OMONEY/IOF1000000*SUM((S,T), EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')*VGE_T.L(Y,IA,G,S,T))$(IAGK_HASORPOT(Y,IA,G) and IGETOH(G))
;

** CO2 EMISSIONS COST
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_CO2_TAX','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and IAGK_HASORPOT(Y,IA,G))=
         OMONEY/IOF1000000*IM_CO2(G)*IOF0001 * IOF3P6 *M_POL(Y,"TAX_CO2",C) * SUM((S,T), IHOURSINST(S,T)*VGF_T.L(Y,IA,G,S,T))$IAGK_HASORPOT(Y,IA,G);

** OTHER EMISSIONS COST
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_OTHER_EMI_TAX','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and IAGK_HASORPOT(Y,IA,G))=
         OMONEY/IOF1000000*
         ( SUM((S,T), IHOURSINST(S,T) * (IM_SO2(G)*IOF0001) * IOF3P6 * VGF_T.L(Y,IA,G,S,T))*M_POL(Y,"TAX_SO2",C)
          + SUM((S,T), IHOURSINST(S,T) * (GDATA(G,'GDNOX')*IOF0000001) * IOF3P6 * VGF_T.L(Y,IA,G,S,T))* M_POL(Y,"TAX_NOX",C)
          )
;


** UNIT COMMITMENT COST
$ifi not %UnitComm%==yes $goto No_UC
ECO_G_YCRAG(Y,C,IR,IA,IGUC,FFF,TECH_TYPE,'COSTS','GENERATION_UC_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(IGUC,FFF) AND GTECH_TYPE(IGUC,TECH_TYPE) and IAGK_HASORPOT(Y,IA,IGUC))=
OMONEY/IOF1000000*SUM((S,T), IHOURSINST(S,T)*(
                                                 VUCU.L(Y,IA,IGUC,S,T)*GDATA(IGUC,'GDUCUCOST')*GDATA(IGUC,'GDUCUNITSIZE')  !! startup cost
                                                 +VUCD.L(Y,IA,IGUC,S,T)*GDATA(IGUC,'GDUCDCOST')*GDATA(IGUC,'GDUCUNITSIZE')  !! shutdown cost
                                                 +VUCON.L(Y,IA,IGUC,S,T)*GDATA(IGUC,'GDUCCOST0')*GDATA(IGUC,'GDUCUNITSIZE') !! fixed hourly cost
$ifi %STEPWISE_RAMPING_UC%==ramp_const_cost      +SUM(RAMPING_RATE,VRAMPING_DOWN.L(Y,IA,IGUC,RAMPING_RATE,S,T)*IRAMPING_G_COSTS_DOWN(IGUC,RAMPING_RATE)+VRAMPING_UP.L(Y,IA,IGUC,RAMPING_RATE,S,T)*IRAMPING_G_COSTS_UP(IGUC,RAMPING_RATE))  !! ramping cost
))
;
$label NO_UC


*TAXES
$ifi NOT %TAXES%==yes  $goto No_TAXES
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_TAXES','Mmoney')$(CCCRRR(C,IR) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G))=
OMONEY/IOF1000000*(
* Tax(+)/subsidy(-) on fuel use for electricity generation
    + SUM((IS3,T),
         IHOURSINST(IS3,T) * TAX_F_EL(Y,C,G)  * VGE_T.L(Y,IA,G,IS3,T)/ (GDATA(G,'GDFE') * (1$(NOT GEFFRATE(IA,G))+GEFFRATE(IA,G))))

* Tax(+)/subsidy(-) on fuel use for heat generation
    + SUM((IS3,T),
         IHOURSINST(IS3,T) * TAX_F_HEAT(Y,C,G) * GDATA(G,'GDCV') * VGH_T.L(Y,IA,G,IS3,T)/ (GDATA(G,'GDFE') * (1$(NOT GEFFRATE(IA,G))+GEFFRATE(IA,G))))

* Tax(+)/subsidy(-) on fuel use
       + SUM((IS3,T),
              IHOURSINST(IS3,T) * TAX_F(Y,C,G)  * VGF_T.L(Y,IA,G,IS3,T))

* Tax(+)/subsidy(-) on heat generation
    + SUM((IS3,T),
         IHOURSINST(IS3,T) * TAX_G_HEAT(Y,C,G)  * VGH_T.L(Y,IA,G,IS3,T))

* Tax(+)/subsidy(-) on heat generation
    + SUM((IS3,T),
         IHOURSINST(IS3,T) * TAX_G_EL(Y,C,G)  * VGE_T.L(Y,IA,G,IS3,T))
)
;
$label NO_TAXES


*GRID TARIFFS
$ifi NOT %GRIDTARIFFS%==yes  $goto No_GRIDTARIFFS
ECO_G_YCRAG(Y,C,IR,IA,IGETOH,FFF,TECH_TYPE,'COSTS','GENERATION_GRID_TARIFFS','Mmoney')$(CCCRRR(C,IR) AND IGF(IGETOH,FFF) AND GTECH_TYPE(IGETOH,TECH_TYPE) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGETOH))=
OMONEY/IOF1000000*(

* Fixed component and subscribed power (per MW installed)
                 + IOF1000 *
                         IGR_PRICE_TECH(Y,IR,'GRDSUBS')  * (VGKNACCUMNET.L(Y,IA,IGETOH)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGETOH)$IGDECOMEXOPOT(Y,IA,IGETOH)
                          + GKFX(Y,IA,IGETOH)) /(GDATA(IGETOH,'GDFE') *(1$(NOT GEFFRATE(IA,IGETOH))+GEFFRATE(IA,IGETOH)))

* Energy charge winter
                 + SUM((IS3,T)$IGR_TIME('GRDECW',IR,IS3,T), IHOURSINST(IS3,T) * IGR_PRICE_TECH(Y,IR,'GRDECW')  * VGE_T.L(Y,IA,IGETOH,IS3,T))
* Energy charge summer
                 + SUM((IS3,T)$IGR_TIME('GRDECS',IR,IS3,T), IHOURSINST(IS3,T) * IGR_PRICE_TECH(Y,IR,'GRDECS')  * VGE_T.L(Y,IA,IGETOH,IS3,T))
* Energy charge TOU step 1
                 + SUM((IS3,T)$IGR_TIME('GRDTOU1',IR,IS3,T), IHOURSINST(IS3,T) * IGR_PRICE_TECH(Y,IR,'GRDTOU1')  * VGE_T.L(Y,IA,IGETOH,IS3,T))
* Energy charge TOU step 2
                 + SUM((IS3,T)$IGR_TIME('GRDTOU2',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_TECH(Y,IR,'GRDTOU2')  * VGE_T.L(Y,IA,IGETOH,IS3,T))
* Energy charge TOU step 3
                 + SUM((IS3,T)$IGR_TIME('GRDTOU3',IR,IS3,T), IHOURSINST(IS3,T) * IGR_PRICE_TECH(Y,IR,'GRDTOU3')  * VGE_T.L(Y,IA,IGETOH,IS3,T))
* Energy charge TOU step 4
                 + SUM((IS3,T)$IGR_TIME('GRDTOU4',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_TECH(Y,IR,'GRDTOU4')  * VGE_T.L(Y,IA,IGETOH,IS3,T))
)
;
$label NO_GRIDTARIFFS

* -- TRANSMISSION -----
*ELECTRICITY
** TRANSMISSION INVESTMENTS
ECO_X_YCR(Y,C,IR,'COSTS','TRANSMISSION_CAPITAL_COSTS','Mmoney')$CCCRRR(C,IR)=
          OMONEY*IOF0000001*SUM((IRI,IYALIAS2)$((IXKN(IYALIAS2,IRI,IR) OR IXKN(IYALIAS2,IR,IRI)) AND (ORD(IYALIAS2) LE ORD(Y)) AND (NOT (IXKN_ES(IR,IRI) OR IXKN_ES(IRI,IR)))) ,
                                    IOF05*ANNUITYCX(C)*IYHASANNUITYX(IYALIAS2,Y)*VXKN.L(IYALIAS2,IR,IRI)*XINVCOST(IYALIAS2,IR,IRI)
                                );
** TRANSMISSION FLOW COSTS
ECO_X_YCR(Y,C,IR,'COSTS','TRANSMISSION_OPERATIONAL_COSTS','Mmoney')$CCCRRR(C,IR)=
OMONEY*IOF0000001*SUM(IRI$IXK_HASORPOT(Y,IR,IRI), SUM((S,T), IHOURSINST(S,T) * (VX_T.L(Y,IR,IRI,S,T) * XCOST(IR,IRI))));

** TRANSMISSION TRADE COST
ECO_X_YCR(Y,C,IR,'COSTS','TRANSMISSION_TRADE_COSTS','Mmoney')$CCCRRR(C,IR)=
OMONEY*IOF0000001*SUM(IRI$IXK_HASORPOT(Y,IRI,IR), SUM((S,T), IHOURSINST(S,T) * (VX_T.L(Y,IRI,IR,S,T) * EL_PRICE_YCRST(Y,C,IRI,S,T,'Money_per_MWh'))));

** TRANSMISSION TRADE INCOME
ECO_X_YCR(Y,C,IR,'INCOME','TRANSMISSION_TRADE_INCOME','Mmoney')$CCCRRR(C,IR)=
OMONEY*IOF0000001*SUM(IRI$IXK_HASORPOT(Y,IR,IRI), SUM((S,T), IHOURSINST(S,T) * (VX_T.L(Y,IR,IRI,S,T) * EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh'))));

*HEAT
$ifi not %HEATTRANS%==yes $goto No_HEATTRANS_costs
** TRANSMISSION INVESTMENTS
ECO_XH_YCRA(Y,C,IR,IA,'COSTS','HEAT_TRANSMISSION_CAPITAL_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA))=
          OMONEY*IOF0000001*SUM((IAI,IYALIAS2)$((IXHKN(IYALIAS2,IAI,IA) OR IXHKN(IYALIAS2,IA,IAI)) AND ORD(IYALIAS2) LE ORD(Y)) ,
                                    IOF05*ANNUITYCXH(C)*IYHASANNUITYXH(IYALIAS2,Y)*VXHKN.L(IYALIAS2,IA,IAI)*XHINVCOST(IYALIAS2,IA,IAI)
                                );
** TRANSMISSION FLOW
ECO_XH_YCRA(Y,C,IR,IA,'COSTS','HEAT_TRANSMISSION_OPERATIONAL_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA))=
OMONEY*IOF0000001*SUM(IAI$IXHK_HASORPOT(Y,IA,IAI), SUM((S,T), IHOURSINST(S,T) * (VXH_T.L(Y,IA,IAI,S,T) * XHCOST(IA,IAI))));


** TRANSMISSION TRADE COST
ECO_XH_YCRA(Y,C,IR,IA,'COSTS','TRANSMISSION_TRADE_COSTS','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA))=
OMONEY*IOF0000001*SUM(IAI$IXHK_HASORPOT(Y,IAI,IA), SUM((S,T), IHOURSINST(S,T) * (VXH_T.L(Y,IAI,IA,S,T) * H_PRICE_YCRAST(Y,C,IR,IAI,S,T,'Money_per_MWh'))));

** TRANSMISSION TRADE INCOME
ECO_XH_YCRA(Y,C,IR,IA,'INCOME','TRANSMISSION_TRADE_INCOME','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA))=
OMONEY*IOF0000001*SUM(IAI$IXHK_HASORPOT(Y,IA,IAI), SUM((S,T), IHOURSINST(S,T) * (VXH_T.L(Y,IA,IAI,S,T) * H_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh'))));


$label No_HEATTRANS_costs

* ------------- END OF COSTS --------------

* ------------- OBJECTIVE FUNCTION --------------

OBJ_YCR(Y,C,IR,'GENERATION_FUEL_COSTS','Mmoney')$CCCRRR(C,IR)=

*GENERATION COSTS
SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G) AND NOT((IGESTOALL(G) OR IGHSTOALL(G) OR IGETOH(G)))),
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_FUEL_COSTS','Mmoney')
)
;

OBJ_YCR(Y,C,IR,'HYDRO_PROFILE','Mmoney')$CCCRRR(C,IR)=
OMONEY*IOF0000001*SUM((IA,IGHYRS)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,IGHYRS)), SUM((S,T), HYPPROFILS(IA,S)* IHOURSINST(S,T)
     * VGE_T.L(Y,IA,IGHYRS,S,T)))
;

OBJ_YCR(Y,C,IR,'GENERATION_CAPITAL_COSTS','Mmoney')$CCCRRR(C,IR)=
SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G)),
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_CAPITAL_COSTS','Mmoney')
)
;

OBJ_YCR(Y,C,IR,'GENERATION_FIXED_COSTS','Mmoney')$CCCRRR(C,IR)=
SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G)),
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_FIXED_COSTS','Mmoney')
)
;

OBJ_YCR(Y,C,IR,'GENERATION_OPERATIONAL_COSTS','Mmoney')$CCCRRR(C,IR)=
SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G)),
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_OPERATIONAL_COSTS','Mmoney')
)
;

OBJ_YCR(Y,C,IR,'GENERATION_CO2_TAX','Mmoney')$CCCRRR(C,IR)=
SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G)),
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_CO2_TAX','Mmoney')
)
;

OBJ_YCR(Y,C,IR,'GENERATION_OTHER_EMI_TAX','Mmoney')$CCCRRR(C,IR)=
SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G)),
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_OTHER_EMI_TAX','Mmoney')
)
;

OBJ_YCR(Y,C,IR,'GENERATION_UC_COSTS','Mmoney')$CCCRRR(C,IR)=
SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G)),
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_UC_COSTS','Mmoney')
)
;

OBJ_YCR(Y,C,IR,'TRANSMISSION_CAPITAL_COSTS','Mmoney')$CCCRRR(C,IR)=
ECO_X_YCR(Y,C,IR,'COSTS','TRANSMISSION_CAPITAL_COSTS','Mmoney')
;

OBJ_YCR(Y,C,IR,'TRANSMISSION_OPERATIONAL_COSTS','Mmoney')$CCCRRR(C,IR)=
ECO_X_YCR(Y,C,IR,'COSTS','TRANSMISSION_OPERATIONAL_COSTS','Mmoney')
;

$ifi not %HEATTRANS%==yes $goto No_HEATTRANS_costs_2
OBJ_YCR(Y,C,IR,'HEAT_TRANSMISSION_CAPITAL_COSTS','Mmoney')$CCCRRR(C,IR)=
SUM(IA$RRRAAA(IR,IA),ECO_XH_YCRA(Y,C,IR,IA,'COSTS','HEAT_TRANSMISSION_CAPITAL_COSTS','Mmoney'))
;

OBJ_YCR(Y,C,IR,'HEAT_TRANSMISSION_OPERATIONAL_COSTS','Mmoney')$CCCRRR(C,IR)=
SUM(IA$RRRAAA(IR,IA),ECO_XH_YCRA(Y,C,IR,IA,'COSTS','HEAT_TRANSMISSION_OPERATIONAL_COSTS','Mmoney'))
;
$label  No_HEATTRANS_costs_2


$ifi NOT %TAXES%==yes  $goto No_TAXES2
OBJ_YCR(Y,C,IR,'TAXES','Mmoney')$CCCRRR(C,IR)=
SUM((IA,G,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,G)), ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS','GENERATION_TAXES','Mmoney'))
+OMONEY/IOF1000000*(

* Tax on electricity consumption
    + SUM(DEUSER, TAX_DE(C,DEUSER) * SUM((IS3,T), IHOURSINST(IS3,T) * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0))  )

$ifi %DFLEXQUANT%==yes           + SUM((DEUSER), TAX_DE(C,DEUSER) *(
$ifi %DFLEXQUANT%==yes             SUM(DEF_U1,VDEF_T.L(Y,IR,DEUSER,IS3,T,DEF_U1) ) - SUM(DEF_D1,VDEF_T.L(Y,IR,DEUSER,IS3,T,DEF_D1) )
$ifi %DFLEXQUANT%==yes           + SUM(DEF_U2,VDEF_T.L(Y,IR,DEUSER,IS3,T,DEF_U2) ) - SUM(DEF_D2,VDEF_T.L(Y,IR,DEUSER,IS3,T,DEF_D2) )
$ifi %DFLEXQUANT%==yes           + SUM(DEF_U3,VDEF_T.L(Y,IR,DEUSER,IS3,T,DEF_U3) ) - SUM(DEF_D3,VDEF_T.L(Y,IR,DEUSER,IS3,T,DEF_D3) ))


+SUM(IA$RRRAAA(IR,IA),
* Tax on heat consumption
    + SUM(DHUSER, TAX_DH(C,DHUSER) * DH(Y,IA,DHUSER))

$ifi %DFLEXQUANT%==yes      + SUM((DHUSER), TAX_DH(C,DHUSER) * (
$ifi %DFLEXQUANT%==yes          SUM(DHF_U1,VDHF_T.L(Y,IA,DHUSER,IS3,T,DHF_U1) ) - SUM(DHF_D1,VDHF_T.L(Y,IA,DHUSER,IS3,T,DHF_D1) )
$ifi %DFLEXQUANT%==yes        + SUM(DHF_U2,VDHF_T.L(Y,IA,DHUSER,IS3,T,DHF_U2) ) - SUM(DHF_D2,VDHF_T.L(Y,IA,DHUSER,IS3,T,DHF_D2) )
$ifi %DFLEXQUANT%==yes        + SUM(DHF_U3,VDHF_T.L(Y,IA,DHUSER,IS3,T,DHF_U3) ) - SUM(DHF_D3,VDHF_T.L(Y,IA,DHUSER,IS3,T,DHF_D3) ) )
)
)
;
$label NO_TAXES2


*GRID TARIFFS2
$ifi NOT %GRIDTARIFFS%==yes  $goto No_GRIDTARIFFS2
OBJ_YCR(Y,C,IR,'GRID_TARIFFS','Mmoney')$CCCRRR(C,IR)=
SUM((IA,IGETOH,FFF,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,IGETOH)), ECO_G_YCRAG(Y,C,IR,IA,IGETOH,FFF,TECH_TYPE,'COSTS','GENERATION_GRID_TARIFFS','Mmoney'))

+OMONEY/IOF1000000*(

* Demand charge winter
                 + IOF1000 * SUM((MMM)$(SUM((IS3,T)$IGR_TIME('GRDDCW',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), SUM(IA$(SUM(IGETOH,IAGK_HASORPOT(Y,IA,IGETOH)) AND RRRAAA(IR,IA)), VGETOH_CAP1.L(Y,IA,MMM)) *  IGR_PRICE_TECH(Y,IR,'GRDDCW'))/(IGR_MONTHSINSIM/card(MMM))    !! Demand charge winter
* Demand charge summer
                 + IOF1000 * SUM((MMM)$(SUM((IS3,T)$IGR_TIME('GRDDCS',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), SUM(IA$(SUM(IGETOH,IAGK_HASORPOT(Y,IA,IGETOH)) AND RRRAAA(IR,IA)), VGETOH_CAP1.L(Y,IA,MMM)) *  IGR_PRICE_TECH(Y,IR,'GRDDCS'))/(IGR_MONTHSINSIM/card(MMM))    !! Demand charge summer
* Demand charge 2 (e.g for spring and fall months)
                 + IOF1000 * SUM((MMM)$(SUM((IS3,T)$IGR_TIME('GRDDC2',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), SUM(IA$(SUM(IGETOH,IAGK_HASORPOT(Y,IA,IGETOH)) AND RRRAAA(IR,IA)), VGETOH_CAP1.L(Y,IA,MMM)) *  IGR_PRICE_TECH(Y,IR,'GRDDC2'))/(IGR_MONTHSINSIM/card(MMM))    !! Demand charge 2

* Demand charge TOU
                 + IOF1000 * SUM((MMM),   SUM(IA$(SUM(IGETOH,IAGK_HASORPOT(Y,IA,IGETOH)) AND RRRAAA(IR,IA)), VGETOH_CAP2.L(Y,IA,MMM)) *  IGR_PRICE_TECH(Y,IR,'GRDDCTOU'))/(IGR_MONTHSINSIM/card(MMM))
* Demand charge yearly
                 + IOF1000 *    SUM(IA$(SUM(IGETOH,IAGK_HASORPOT(Y,IA,IGETOH)) AND RRRAAA(IR,IA)), VGETOH_CAPY.L(Y,IA)) *  IGR_PRICE_TECH(Y,IR,'GRDDCY')

* Tariffs on user groups
                 + SUM((IS3,T,DEUSER)$IGR_TIME('GRDECW',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDECW')  * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0) )
                 + SUM((IS3,T,DEUSER)$IGR_TIME('GRDECS',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDECS')  * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0) )
                 + SUM((IS3,T,DEUSER)$IGR_TIME('GRDTOU1',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDTOU1') * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0) )
                 + SUM((IS3,T,DEUSER)$IGR_TIME('GRDTOU2',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDTOU2') * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0) )
                 + SUM((IS3,T,DEUSER)$IGR_TIME('GRDTOU3',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDTOU3') * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0) )
                 + SUM((IS3,T,DEUSER)$IGR_TIME('GRDTOU4',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDTOU4') * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0) )

                 + IOF1000 * SUM((DEUSER,MMM)$(SUM((IS3,T)$IGR_TIME('GRDDCW',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), VDEUSER_CAP1.L(Y,IR,DEUSER,MMM) *  IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDDCW'))/(IGR_MONTHSINSIM/CARD(MMM))
                 + IOF1000 * SUM((DEUSER,MMM)$(SUM((IS3,T)$IGR_TIME('GRDDCS',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), VDEUSER_CAP1.L(Y,IR,DEUSER,MMM) *  IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDDCS'))/(IGR_MONTHSINSIM/CARD(MMM))
                 + IOF1000 * SUM((DEUSER,MMM)$(SUM((IS3,T)$IGR_TIME('GRDDC2',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), VDEUSER_CAP1.L(Y,IR,DEUSER,MMM) *  IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDDC2'))/(IGR_MONTHSINSIM/CARD(MMM))
                 + IOF1000 * SUM((DEUSER,MMM),   VDEUSER_CAP2.L(Y,IR,DEUSER,MMM) *  IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDDCTOU'))/(IGR_MONTHSINSIM/card(MMM))
                 + IOF1000 * SUM((DEUSER),  VDEUSER_CAPY.L(Y,IR,DEUSER) *  IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDDCY'))

)


;
$label NO_GRIDTARIFFS2

*ADD OTHER COSTS(HYDROGEN,ETC)


* ------------- END OF OBJECTIVE FUNCTION --------------


* ------------- INCOME --------------

** ELECTRICITY SALES
ECO_G_YCRAG(Y,C,IR,IA,IGE,FFF,TECH_TYPE,'INCOME','ELECTRICITY_SALE','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) and IGNOTETOH(IGE))=
 OMONEY*IOF0000001*
   SUM((S,T), IHOURSINST(S,T)*VGE_T.L(Y,IA,IGE,S,T)*EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh'))
;

** HEAT SALES
ECO_G_YCRAG(Y,C,IR,IA,IGH,FFF,TECH_TYPE,'INCOME','HEAT_SALE','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE))=
 OMONEY*IOF0000001*
   SUM((S,T), IHOURSINST(S,T)*VGH_T.L(Y,IA,IGH,S,T)*H_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh'))
;

* ------------- END OF INCOME --------------

* ------------- REVENUE --------------
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'REVENUE','TOTAL_REVENUE','Mmoney')$IAGK_HASORPOT(Y,IA,G)=
SUM(SUBCATEGORY, ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'INCOME',SUBCATEGORY,'Mmoney')-ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'COSTS',SUBCATEGORY,'Mmoney')
)
;

ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'REVENUE','ENERGY_SPECIFIC_REVENUE','Money_per_MWh')$(IAGK_HASORPOT(Y,IA,G) AND SUM(COMMODITY,PRO_YCRAGF(Y,C,IR,IA,G,FFF,COMMODITY,TECH_TYPE,'TWh') > 0)) =
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'REVENUE','TOTAL_REVENUE','Mmoney')/SUM(COMMODITY,PRO_YCRAGF(Y,C,IR,IA,G,FFF,COMMODITY,TECH_TYPE,'TWh'))
;
* ------------- END OF REVENUE --------------

* --------- END OF ECONOMIC OUTPUT -----------

* ---------------------- ENVIRONMENT ----------------------

* ------------- CO2 EMISSIONS -------------

EMI_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,'kton')$(F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh'))=
F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh')*IM_CO2(G)*IOF3P6;

* ------------- END OF CO2 EMISSIONS -------------

* ---------------------- END OF ENVIRONMENT ----------------------

*---------------ELECTRICITY BALANCE------------
*GENERATION
EL_BALANCE_YCRST(Y,C,IR,'CONDENSING',S,T,'MWh')$CCCRRR(C,IR)=SUM((IA,IGE,FFF)$(RRRAAA(IR,IA) AND IGF(IGE,FFF) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY','CONDENSING','MWh'));
EL_BALANCE_YCRST(Y,C,IR,'CHP-BACK-PRESSURE',S,T,'MWh')$CCCRRR(C,IR)=SUM((IA,IGE,FFF)$(RRRAAA(IR,IA) AND IGF(IGE,FFF) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY','CHP-BACK-PRESSURE','MWh'));
EL_BALANCE_YCRST(Y,C,IR,'CHP-EXTRACTION',S,T,'MWh')$CCCRRR(C,IR)=SUM((IA,IGE,FFF)$(RRRAAA(IR,IA) AND IGF(IGE,FFF) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY','CHP-EXTRACTION','MWh'));
EL_BALANCE_YCRST(Y,C,IR,'INTER-STO',S,T,'MWh')$CCCRRR(C,IR)=SUM((IA,IGE,FFF)$(RRRAAA(IR,IA) AND IGF(IGE,FFF) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY','INTERSEASONAL-ELECT-STORAGE','MWh'));
EL_BALANCE_YCRST(Y,C,IR,'INTRA-STO',S,T,'MWh')$CCCRRR(C,IR)=SUM((IA,IGE,FFF)$(RRRAAA(IR,IA) AND IGF(IGE,FFF) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY','INTRASEASONAL-ELECT-STORAGE','MWh'));
EL_BALANCE_YCRST(Y,C,IR,'HYDRO-RESERVOIRS',S,T,'MWh')$CCCRRR(C,IR)=SUM((IA,IGE,FFF)$(RRRAAA(IR,IA) AND IGF(IGE,FFF) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY','HYDRO-RESERVOIRS','MWh'));
EL_BALANCE_YCRST(Y,C,IR,'HYDRO-RUN-OF-RIVER',S,T,'MWh')$CCCRRR(C,IR)=SUM((IA,IGE,FFF)$(RRRAAA(IR,IA) AND IGF(IGE,FFF) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY','HYDRO-RUN-OF-RIVER','MWh'));
EL_BALANCE_YCRST(Y,C,IR,'WIND-ON',S,T,'MWh')$CCCRRR(C,IR)=SUM((IA,IGE,FFF)$(RRRAAA(IR,IA) AND IGF(IGE,FFF) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY','WIND-ON','MWh'));
EL_BALANCE_YCRST(Y,C,IR,'WIND-OFF',S,T,'MWh')$CCCRRR(C,IR)=SUM((IA,IGE,FFF)$(RRRAAA(IR,IA) AND IGF(IGE,FFF) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY','WIND-OFF','MWh'));
EL_BALANCE_YCRST(Y,C,IR,'SOLAR-PV',S,T,'MWh')$CCCRRR(C,IR)=SUM((IA,IGE,FFF)$(RRRAAA(IR,IA) AND IGF(IGE,FFF) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY','SOLAR-PV','MWh'));
EL_BALANCE_YCRST(Y,C,IR,'HYDRO-WAVE ',S,T,'MWh')$CCCRRR(C,IR)=SUM((IA,IGE,FFF)$(RRRAAA(IR,IA) AND IGF(IGE,FFF) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY','HYDRO-WAVE ','MWh'));

*DEMAND
EL_BALANCE_YCRST(Y,C,IR,'DEMAND_EXO',S,T,'MWh')$CCCRRR(C,IR)=EL_DEMAND_YCRST(Y,C,IR,S,T,'EXOGENOUS','MWh');
EL_BALANCE_YCRST(Y,C,IR,'DEMAND_P2H',S,T,'MWh')$CCCRRR(C,IR)=EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDOGENOUS_ELECT2HEAT','MWh');
EL_BALANCE_YCRST(Y,C,IR,'DEMAND_INTRASTO',S,T,'MWh')$CCCRRR(C,IR)=EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_INTRASTO','MWh');
EL_BALANCE_YCRST(Y,C,IR,'DEMAND_INTERSTO',S,T,'MWh')$CCCRRR(C,IR)=EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_INTERSTO','MWh');
EL_BALANCE_YCRST(Y,C,IR,'DEMAND_EV',S,T,'MWh')$CCCRRR(C,IR)=EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDO_EV','MWh');

*TRANSMISSION
EL_BALANCE_YCRST(Y,C,IR,'NETEXPORT',S,T,'MWh')$CCCRRR(C,IR)=SUM(IRE$IXK_HASORPOT(Y,IR,IRE),VX_T.L(Y,IR,IRE,S,T))-SUM(IRI$IXK_HASORPOT(Y,IRI,IR),VX_T.L(Y,IRI,IR,S,T)*(1-XLOSS(IRI,IR)));

*CURTAILMENT
EL_BALANCE_YCRST(Y,C,IR,'CURTAILMENT',S,T,'MWh')$CCCRRR(C,IR)=
SUM((IGE,TECH_TYPE,IA,FFF)$(RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE)),
CURT_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY',TECH_TYPE,'MWh')
);

*PRICE
EL_BALANCE_YCRST(Y,C,IR,'PRICE',S,T,'Money_per_MWh')$CCCRRR(C,IR)=EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh');
*---------------END ELECTRICITY BALANCE------------


*---------------HEAT BALANCE------------
*GENERATION
H_BALANCE_YCRAST(Y,C,IR,IA,'P2H',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=SUM((IGH,FFF)$(IGF(IGH,FFF) AND IAGK_HASORPOT(Y,IA,IGH)),PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'HEAT','ELECT-TO-HEAT','MWh'));
H_BALANCE_YCRAST(Y,C,IR,IA,'CHP-BACK-PRESSURE',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=SUM((IGH,FFF)$(IGF(IGH,FFF) AND IAGK_HASORPOT(Y,IA,IGH)),PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'HEAT','CHP-BACK-PRESSURE','MWh'));
H_BALANCE_YCRAST(Y,C,IR,IA,'CHP-EXTRACTION',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=SUM((IGH,FFF)$(IGF(IGH,FFF) AND IAGK_HASORPOT(Y,IA,IGH)),PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'HEAT','CHP-EXTRACTION','MWh'));
H_BALANCE_YCRAST(Y,C,IR,IA,'INTER-STO',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=SUM((IGH,FFF)$(IGF(IGH,FFF) AND IAGK_HASORPOT(Y,IA,IGH)),PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'HEAT','INTERSEASONAL-HEAT-STORAGE','MWh'));
H_BALANCE_YCRAST(Y,C,IR,IA,'INTRA-STO',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=SUM((IGH,FFF)$(IGF(IGH,FFF) AND IAGK_HASORPOT(Y,IA,IGH)),PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'HEAT','INTRASEASONAL-HEAT-STORAGE','MWh'));
H_BALANCE_YCRAST(Y,C,IR,IA,'BOILERS',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=SUM((IGH,FFF)$(IGF(IGH,FFF) AND IAGK_HASORPOT(Y,IA,IGH)),PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'HEAT','BOILERS','MWh'));
H_BALANCE_YCRAST(Y,C,IR,IA,'SOLAR-HEATING',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=SUM((IGH,FFF)$(IGF(IGH,FFF) AND IAGK_HASORPOT(Y,IA,IGH)),PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'HEAT','SOLAR-HEATING','MWh'));

*DEMAND
H_BALANCE_YCRAST(Y,C,IR,IA,'DEMAND_EXO',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'EXOGENOUS','MWh');
H_BALANCE_YCRAST(Y,C,IR,IA,'DEMAND_INTRASTO',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'ENDO_INTRASTO','MWh');
H_BALANCE_YCRAST(Y,C,IR,IA,'DEMAND_INTERSTO',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'ENDO_INTERSTO','MWh');

*TRANSMISSION
$ifi %HEATTRANS%==yes  H_BALANCE_YCRAST(Y,C,IR,IA,'NETEXPORT',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=SUM(IAE$IXHK_HASORPOT(Y,IA,IAE),VXH_T.L(Y,IA,IAE,S,T))-SUM(IAI$IXHK_HASORPOT(Y,IAI,IA),VXH_T.L(Y,IAI,IA,S,T)*(1-XHLOSS(IAI,IA)));

*CURTAILMENT
H_BALANCE_YCRAST(Y,C,IR,IA,'CURTAILMENT',S,T,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=
SUM((IGH,TECH_TYPE,FFF)$(IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE)),
CURT_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'HEAT',TECH_TYPE,'MWh')
);

*PRICE
H_BALANCE_YCRAST(Y,C,IR,IA,'PRICE',S,T,'Money_per_MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA))=H_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh');
*---------------END HEAT BALANCE------------



execute_unload "MainResults.gdx" ECO_G_YCRAG,ECO_X_YCR,OBJ_YCR,EL_PRICE_YCR,EL_PRICE_YCRST,PRO_YCRAGFST,EL_DEMAND_YCRST,EL_DEMAND_YCR,
H_DEMAND_YCRAST,H_DEMAND_YCRA,PRO_YCRAGF,H_PRICE_YCRAST,H_PRICE_YCRA,G_CAP_YCRAF,G_STO_YCRAF,X_CAP_YCR,X_FLOW_YCRST,X_FLOW_YCR,F_CONS_YCRAST,F_CONS_YCRA,EMI_YCRAG,
EL_BALANCE_YCRST,CURT_YCRAGFST,CURT_YCRAGF,H_BALANCE_YCRAST,ECO_XH_YCRA

$ifi %HEATTRANS%==yes ,XH_CAP_YCA,XH_FLOW_YCAST,XH_FLOW_YCA

;























** ----------------------- AMALIAS OUTPUT (FOR INSPIRATION) --------------------------

$ontext
* MODELING OF THE ELECTRICITY GENERATION

SET EDemand_type /classic, transport, "individual heating", "process heating", DH, H2/;


SET TECHTYPE  'Type of technologies for calculation of marginals' %semislash%
$if     EXIST '../data/TECHTYPE.inc' $INCLUDE      '../data/TECHTYPE.inc';
$if not EXIST '../data/TECHTYPE.inc' $INCLUDE '../../base/data/TECHTYPE.inc';
%semislash%;

SET GTECHTYPE(GGG,TECHTYPE) 'Type of technology aggrupated'%semislash%
$if     EXIST '../data/GTECHTYPE.inc' $INCLUDE      '../data/GTECHTYPE.inc';
$if not EXIST '../data/GTECHTYPE.inc' $INCLUDE '../../base/data/GTECHTYPE.inc';
%semislash%;

SET ELECTRADE /Import, Export, "Net Export"/;


SET RRROFFSHORE(RRR);
RRROFFSHORE(IR)$SUM(IA,RRRAAA(IR,IA)$AAAOFFSHORE(IA))=YES;
RRROFFSHORE("DK_E")=NO;
RRROFFSHORE("DK_W")=NO;
RRROFFSHORE("DK_KF")=NO;
RRROFFSHORE("DE_KF")=NO;

SET CAPACITIES /New, Existing, TOTAL/;






Parameter EPrice_RST(RRR,SSS,TTT)  'Electricity price per hour at a regional level - cost of producing one unit more of electricity for own consumption or for export (€ 2015/MWh)';
EPrice_RST(IR,S,T)=QEEQ.M(IR,S,T)*MONEYCONVERT/IHOURSINST(S,T);

Parameter HPrice_AST(AAA,SSS,TTT)'Heat price per hour at an area level - cost of producing one unit more of heat(€ 2015/MWh)';
HPrice_AST(IA,S,T)=QHEQ.M(IA,S,T)*MONEYCONVERT/IHOURSINST(S,T);

$if     EXIST '../addons/_hooks/OutputCalculation.inc' $INCLUDE      '../addons/_hooks/OutputCalculation.inc';
$if not EXIST '../addons/_hooks/OutputCalculation.inc' $INCLUDE '../../base/addons/_hooks/OutputCalculation.inc';


* MODELING OF THE ELECTRICITY DEMAND

Parameter EDemand_RST(EDemand_type,RRR,SSS,TTT) 'Electricity Demand (MW)';

EDemand_RST("classic",IR,S,T)$(NOT (RETRANS(IR) OR RINDUS(IR)))=IDE_T_Y(IR,S,T);

EDemand_RST("transport",IR,S,T)=SUM(RETRANS$IXKINI_Y(IR,RETRANS),IDE_T_Y(RETRANS,S,T));

EDemand_RST("individual heating",IR,S,T)=SUM(IAGK_Y(IA,IGE)$((RRRAAA(IR,IA) AND (AINDHEAT(IA))) AND IGETOH(IGE)), VGE_T.L(IA,IGE,S,T))
$ifi %bb2%==yes                         +SUM(IAGKN(IA,IGE)$((RRRAAA(IR,IA) AND (AINDHEAT(IA))) AND IGETOH(IGE)), VGEN_T.L(IA,IGE,S,T));

EDemand_RST("process heating",IR,S,T)=SUM(RINDUS$IXKINI_Y(IR,RINDUS),IDE_T_Y(RINDUS,S,T));

EDemand_RST("DH",IR,S,T)=SUM(IAGK_Y(IA,IGE)$((RRRAAA(IR,IA) AND (NOT AINDHEAT(IA))) AND IGETOH(IGE)), VGE_T.L(IA,IGE,S,T))
$ifi %bb2%==yes         +SUM(IAGKN(IA,IGE)$((RRRAAA(IR,IA) AND (NOT AINDHEAT(IA))) AND IGETOH(IGE)), VGEN_T.L(IA,IGE,S,T));

$ifi %H2%==yes  EDemand_RST("H2",IR,S,T)=SUM(IAGK_Y(IA,IGETOH2)$(RRRAAA(IR,IA)), VGE_T.L(IA,IGETOH2,S,T))
$ifi %bb2%==yes                         +SUM(IAGKN(IA,IGETOH2)$(RRRAAA(IR,IA)), VGEN_T.L(IA,IGETOH2,S,T));


Parameter Edemand_CST(EDemand_type,CCC,SSS,TTT) 'Electricity Demand (MW)';
Edemand_CST(EDemand_type,C,S,T)=SUM(IR$CCCRRR(C,IR),EDemand_RST(EDemand_type,IR,S,T));


Parameter Edemand_R(EDemand_type,RRR) 'Electricity Demand (PJ/year)';
Edemand_R(EDemand_type,IR)=SUM((S,T),EDemand_RST(EDemand_type,IR,S,T)*IHOURSINST(S,T))*IOF0000001*IOF3P6;


Parameter Edemand_C(EDemand_type,CCC) 'Electricity Demand (PJ/year)';
Edemand_C(EDemand_type,C)=SUM((S,T),EDemand_CST(EDemand_type,C,S,T)*IHOURSINST(S,T))*IOF0000001*IOF3P6;

Parameter Hdemand_AST(AAA,SSS,TTT) 'District Heating Demand (MW)';
Hdemand_AST(IA,S,T)$(NOT AINDHEAT(IA))=IDH_T_Y(IA,S,T);


*Weighted average electricity price by consumption quantity (€ 2015/MWh)

Parameter EPrice_R(RRR) 'Weighted average electricity price by consumption quantity per year at a regional level (€ 2015/MWh)';
EPrice_R(IR)$(SUM((EDemand_type,S,T),EDemand_RST(EDemand_type,IR,S,T)*IHOURSINST(S,T)))=SUM((EDemand_type,S,T),EDemand_RST(EDemand_type,IR,S,T)*IHOURSINST(S,T)*EPrice_RST(IR,S,T))/SUM((EDemand_type,S,T),EDemand_RST(EDemand_type,IR,S,T)*IHOURSINST(S,T));

Parameter EPrice_CST(CCC,SSS,TTT) 'Weighted average electricity price by consumption quantity at a country level(€ 2015/MWh)';
EPrice_CST(C,S,T)=SUM((EDemand_type,IR)$CCCRRR(C,IR),EDemand_RST(EDemand_type,IR,S,T)*EPrice_RST(IR,S,T))/SUM((EDemand_type,IR)$CCCRRR(C,IR),EDemand_RST(EDemand_type,IR,S,T));

Parameter EPrice_C(CCC) 'Weighted average electricity price by consumption quantity per year at a country level (€ 2015/MWh)';
EPrice_C(C)=SUM((EDemand_type,S,T),EDemand_CST(EDemand_type,C,S,T)*IHOURSINST(S,T)*EPrice_CST(C,S,T))/SUM((EDemand_type,S,T),EDemand_CST(EDemand_type,C,S,T)*IHOURSINST(S,T));

*Weighted average heat price by consumption quantity (€ 2012/MWh)
Parameter HPrice_AS(AAA,SSS) 'Weighted average heat price by consumption quantity per week at an area level (€ 2015/MWh)';
HPrice_AS(IA,S)$(SUM(T,Hdemand_AST(IA,S,T)))=SUM(T,HPrice_AST(IA,S,T)*Hdemand_AST(IA,S,T)*IHOURSINST(S,T))/SUM(T,Hdemand_AST(IA,S,T)*IHOURSINST(S,T));

Parameter HPrice_A(AAA) 'Weighted average heat price by consumption quantity per year at an area level (€ 2015/MWh)';
HPrice_A(IA)$(SUM((S,T),Hdemand_AST(IA,S,T)))=SUM((S,T),HPrice_AST(IA,S,T)*Hdemand_AST(IA,S,T)*IHOURSINST(S,T))/SUM((S,T),Hdemand_AST(IA,S,T)*IHOURSINST(S,T));

Parameter HPrice_C(CCC) 'Weighted average heat price by consumption quantity per year at an country level (€ 2015/MWh)';
HPrice_C(CCC)$(SUM((S,T,IA)$ICA(CCC,IA),Hdemand_AST(IA,S,T)))=SUM((S,T,IA)$ICA(CCC,IA),HPrice_AST(IA,S,T)*Hdemand_AST(IA,S,T)*IHOURSINST(S,T))/SUM((S,T,IA)$ICA(CCC,IA),Hdemand_AST(IA,S,T)*IHOURSINST(S,T));



Parameter EProduction_RST(RRR,TECHTYPE,SSS,TTT) 'Electricity production at a regional level (MW)';
EProduction_RST(IR,TECHTYPE,S,T)= SUM(IAGK_Y(IA,IGE)$((RRRAAA(IR,IA) AND (NOT AAAOFFSHORE(IA))) AND IGNOTETOH(IGE) AND GTECHTYPE(IGE,TECHTYPE)), VGE_T.L(IA,IGE,S,T))
$ifi %bb2%==yes                  +SUM(IAGKN(IA,IGE)$((RRRAAA(IR,IA) AND (NOT AAAOFFSHORE(IA))) AND IGNOTETOH(IGE) AND GTECHTYPE(IGE,TECHTYPE)), VGEN_T.L(IA,IGE,S,T))
                                 +SUM(RRROFFSHORE$IXKINI_Y(RRROFFSHORE,IR),(SUM(IAGK_Y(IA,IGE)$((RRRAAA(RRROFFSHORE,IA) AND (AAAOFFSHORE(IA))) AND IGNOTETOH(IGE) AND GTECHTYPE(IGE,TECHTYPE)), VGE_T.L(IA,IGE,S,T)))*(1-XLOSS(RRROFFSHORE,IR)))
$ifi %bb2%==yes                  +SUM(RRROFFSHORE$IXKINI_Y(RRROFFSHORE,IR),(SUM(IAGKN(IA,IGE)$((RRRAAA(RRROFFSHORE,IA) AND (AAAOFFSHORE(IA))) AND IGNOTETOH(IGE) AND GTECHTYPE(IGE,TECHTYPE)), VGEN_T.L(IA,IGE,S,T)))*(1-XLOSS(RRROFFSHORE,IR)))

$if     EXIST '../addons/_hooks/EProduction_RST.inc' $INCLUDE      '../addons/_hooks/EProduction_RST.inc';
$if not EXIST '../addons/_hooks/EProduction_RST.inc' $INCLUDE '../../base/addons/_hooks/EProduction_RST.inc';

;

Parameter ETrade_RST(RRR,ELECTRADE,SSS,TTT) 'Electricity trade at a regional level (MW)';

*electricity import, do not consider import from offshore regions
ETrade_RST(IR,"Import",S,T)$((NOT RETRANS(IR)) AND (NOT RINDUS(IR)))=SUM(IRE$((IXKINI_Y(IRE,IR) OR IXKN(IRE,IR) OR IXKN(IR,IRE)) AND (NOT RRROFFSHORE(IRE))), VX_T.L(IRE,IR,S,T)*(1-XLOSS(IRE,IR)));

*electricity export, do not consider export towards EV region or industrial region
ETrade_RST(IR,"Export",S,T)$(NOT RRROFFSHORE(IR))= SUM(IRI$((IXKINI_Y(IR,IRI) OR IXKN(IR,IRI) OR IXKN(IRI,IR)) AND ((NOT RETRANS(IRI)) AND (NOT RINDUS(IRI)))),VX_T.L(IR,IRI,S,T));

ETrade_RST(IR,"Net Export",S,T)=ETrade_RST(IR,"Export",S,T)-ETrade_RST(IR,"Import",S,T);

Parameter Eproduction_CST(CCC,TECHTYPE,SSS,TTT) 'Electricity production at a country level (MW)';
Eproduction_CST(C,TECHTYPE,S,T)=SUM(IR$CCCRRR(C,IR),EProduction_RST(IR,TECHTYPE,S,T));

Parameter ETrade_CST(CCC,ELECTRADE,SSS,TTT) 'Electricity trade at a country level (MW)';
ETrade_CST(C,"Import",S,T)=SUM((IR,IRE)$(CCCRRR(C,IR) AND (NOT CCCRRR(C,IRE))AND (IXKINI_Y(IRE,IR) OR IXKN(IR,IRE) OR IXKN(IRE,IR)) AND (IXKINI_Y(IRE,IR) NE INF)), VX_T.L(IRE,IR,S,T)*(1-XLOSS(IRE,IR)));
ETrade_CST(C,"Export",S,T)=SUM((IR,IRI)$(CCCRRR(C,IR) AND (NOT CCCRRR(C,IRI))AND (IXKINI_Y(IR,IRI) OR IXKN(IRI,IR) OR IXKN(IR,IRI)) AND (IXKINI_Y(IR,IRI) NE INF)), VX_T.L(IR,IRI,S,T));
ETrade_CST(C,"Net Export",S,T)=ETrade_CST(C,"Export",S,T)-ETrade_CST(C,"Import",S,T);


*Energy production and trade at a yearly level in (PJ/year) units:
Parameter Eproduction_R(RRR,TECHTYPE) 'Electricity production at a regional level (PJ/year)';
EProduction_R(IR,TECHTYPE)=(SUM((S,T),EProduction_RST(IR,TECHTYPE,S,T)*IHOURSINST(S,T)))*IOF0000001*IOF3P6;

Parameter Eproduction_C(CCC,TECHTYPE) 'Electricity production at a country level (PJ/year)';
EProduction_C(C,TECHTYPE)= (SUM((S,T),EProduction_CST(C,TECHTYPE,S,T)*IHOURSINST(S,T)))*IOF0000001*IOF3P6;

Parameter ETrade_R(RRR,ELECTRADE) 'Electricity trade at a regional level (PJ/year)';
ETrade_R(IR,ELECTRADE)=(SUM((S,T),ETrade_RST(IR,ELECTRADE,S,T)*IHOURSINST(S,T)))*IOF0000001*IOF3P6;

Parameter ETrade_C(CCC,ELECTRADE) 'Electricity trade at a regional level (PJ/year)';
ETrade_C(C,ELECTRADE)=(SUM((S,T),ETrade_CST(C,ELECTRADE,S,T)*IHOURSINST(S,T)))*IOF0000001*IOF3P6;

EProduction_RST(IR,"Net Trade",S,T)=-ETrade_RST(IR,"Net Export",S,T);
Eproduction_CST(C,"Net Trade",S,T)=-ETrade_CST(C,"Net Export",S,T);


*HEAT GENERATION

Parameter Hproduction_AST(AAA,TECHTYPE,SSS,TTT) 'Heat production at an area level (MW)';

Hproduction_AST(IA,TECHTYPE,S,T)$(NOT AINDHEAT(IA))=
                                  SUM(IGBPR$(IAGK_Y(IA,IGBPR) AND GTECHTYPE(IGBPR,TECHTYPE)),VGH_T.L(IA,IGBPR,S,T))
$ifi %bb2%==yes                 + SUM(IGBPR$(IAGKN(IA,IGBPR) AND GTECHTYPE(IGBPR,TECHTYPE)),VGHN_T.L(IA,IGBPR,S,T))
                                + SUM(IGEXT$(IAGK_Y(IA,IGEXT) AND GTECHTYPE(IGEXT,TECHTYPE)),VGH_T.L(IA,IGEXT,S,T))
$ifi %bb2%==yes                 + SUM(IGEXT$(IAGKN(IA,IGEXT) AND GTECHTYPE(IGEXT,TECHTYPE)),VGHN_T.L(IA,IGEXT,S,T))
                                + SUM(IGHH$(IAGK_Y(IA,IGHH) AND GTECHTYPE(IGHH,TECHTYPE)),VGH_T.L(IA,IGHH,S,T))
$ifi %bb2%==yes                 + SUM(IGHH$(IAGKN(IA,IGHH) AND GTECHTYPE(IGHH,TECHTYPE)),VGHN_T.L(IA,IGHH,S,T))
                                + SUM(IGETOH$(IAGK_Y(IA,IGETOH) AND GTECHTYPE(IGETOH,TECHTYPE)),VGH_T.L(IA,IGETOH,S,T))
$ifi %bb2%==yes                 + SUM(IGETOH$(IAGKN(IA,IGETOH) AND GTECHTYPE(IGETOH,TECHTYPE)),VGHN_T.L(IA,IGETOH,S,T))
$ifi %H2%==yes                  +SUM(IGETOH2$(IAGK_Y(IA,IGETOH2) AND GTECHTYPE(IGETOH2,TECHTYPE)), VGH_T.L(IA,IGETOH2,S,T))
$ifi %H2%==yes                  +SUM(IGETOH2$(IAGKN(IA,IGETOH2) AND GTECHTYPE(IGETOH2,TECHTYPE)), VGHN_T.L(IA,IGETOH2,S,T))

$if     EXIST '../addons/_hooks/Hproduction_AST.inc' $INCLUDE      '../addons/_hooks/Hproduction_AST.inc';
$if not EXIST '../addons/_hooks/Hproduction_AST.inc' $INCLUDE '../../base/addons/_hooks/Hproduction_AST.inc';
;

Hproduction_AST(IA,"Heat to Storage",S,T)=VHSTOLOADT.L(IA,S,T)$SUM(IGHSTO$(IAGK_Y(IA,IGHSTO) or IAGKN(IA,IGHSTO)),1);
Hproduction_AST(IA,"Heat cool down",S,T)=VCOOLDOWN.L(IA,S,T);

Parameter Hproduction_AS(AAA,TECHTYPE,SSS) 'Heat production at an area level per week (MWh)';
Hproduction_AS(IA,TECHTYPE,S)=SUM(T,Hproduction_AST(IA,TECHTYPE,S,T)*WEIGHT_T(T)*SUM(TTT,WEIGHT_T(TTT))/IWEIGHSUMT);

Parameter Hproduction_A(AAA,TECHTYPE) 'Heat production at an area level per year (PJ/year)';
Hproduction_A(IA,TECHTYPE)=(SUM((S,T),Hproduction_AST(IA,TECHTYPE,S,T)*IHOURSINST(S,T)))*IOF0000001*IOF3P6;

Parameter Hproduction_CST(CCC,TECHTYPE,SSS,TTT) 'Heat production at a country level (MW)';
Hproduction_CST(C,TECHTYPE,S,T)=SUM(IA$ICA(C,IA),Hproduction_AST(IA,TECHTYPE,S,T));

Parameter Hproduction_CS(CCC,TECHTYPE,SSS) 'Heat production at a country level per week (MWh)';
Hproduction_CS(C,TECHTYPE,S)=SUM(IA$ICA(C,IA),Hproduction_AS(IA,TECHTYPE,S));

Parameter Hproduction_C(CCC,TECHTYPE) 'Heat production at a country level per year (PJ/year)';
Hproduction_C(C,TECHTYPE)=SUM(IA$ICA(C,IA),Hproduction_A(IA,TECHTYPE));

Parameter FuelConsumption_C(CCC,TECHTYPE)  'Fuel Consumption at a country level per type of technology (PJ/year)';
FuelConsumption_C(C,TECHTYPE)=SUM((IA,G,S,T)$(ICA(C,IA) AND GTECHTYPE(G,TECHTYPE)),IHOURSINST(S,T)*(VGF_T.L(IA,G,S,T)+VGFN_T.L(IA,G,S,T)))*IOF0000001*IOF3P6
$if     EXIST '../addons/_hooks/FuelConsumption_C.inc' $INCLUDE      '../addons/_hooks/FuelConsumption_C.inc';
$if not EXIST '../addons/_hooks/FuelConsumption_C.inc' $INCLUDE '../../base/addons/_hooks/FuelConsumption_C.inc';

;

Parameter FuelConsumption_TOTAL(TECHTYPE)  'Fuel Consumption in all the simulated countryes per type of technology (PJ/year)';
FuelConsumption_TOTAL(TECHTYPE)=SUM(C,FuelConsumption_C(C,TECHTYPE));

*_______________________________________________________________________________________________________________________________________________________________________________________________________________________________________

*New Installed Capacity per country for electricity and District Heating production

Parameter Elec_Capacity(CCC,CAPACITIES,TECHTYPE) 'Installed capacity for electricity production (MW)';

Elec_Capacity(C,'New',TECHTYPE)=SUM((IA,IGE)$(ICA(C,IA) AND IGNOTETOH(IGE) AND GTECHTYPE(IGE,TECHTYPE)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGE))
),VGKN.L(IA,IGE));

Elec_Capacity(C,'Existing',TECHTYPE)=SUM((IA,IGE)$(ICA(C,IA) AND IGNOTETOH(IGE) AND GTECHTYPE(IGE,TECHTYPE) AND IAGK_Y(IA,IGE)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGE))
),IGKFX_Y(IA,IGE));

Elec_Capacity(C,'TOTAL',TECHTYPE)=Elec_Capacity(C,'New',TECHTYPE)+Elec_Capacity(C,'Existing',TECHTYPE);



Parameter Heat_Capacity(CCC,CAPACITIES,TECHTYPE) 'Installed capacity for heat production (MW)';

Heat_Capacity(C,'New',TECHTYPE)=SUM(IA$(ICA(C,IA) AND (NOT AINDHEAT(IA))),
SUM(IGBPR$(GTECHTYPE(IGBPR,TECHTYPE)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGBPR))
),VGKN.L(IA,IGBPR)/GDATA(IGBPR,'GDCB'))
+SUM(IGEXT$(GTECHTYPE(IGEXT,TECHTYPE)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGEXT))
),VGKN.L(IA,IGEXT)/GDATA(IGEXT,'GDCB'))
+SUM(IGHHNOSTO$(GTECHTYPE(IGHHNOSTO,TECHTYPE)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGHHNOSTO))
),VGKN.L(IA,IGHHNOSTO ))
+SUM(IGETOH$(GTECHTYPE(IGETOH,TECHTYPE)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGETOH))
),VGKN.L(IA,IGETOH)));

Heat_Capacity(C,'Existing',TECHTYPE)=SUM(IA$(ICA(C,IA) AND (NOT AINDHEAT(IA))),
SUM(IGBPR$(GTECHTYPE(IGBPR,TECHTYPE) AND IAGK_Y(IA,IGBPR)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGBPR))
),IGKFX_Y(IA,IGBPR)/GDATA(IGBPR,'GDCB'))
+SUM(IGEXT$(GTECHTYPE(IGEXT,TECHTYPE) AND IAGK_Y(IA,IGEXT)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGEXT))
),IGKFX_Y(IA,IGEXT)/GDATA(IGEXT,'GDCB'))
+SUM(IGHHNOSTO$(GTECHTYPE(IGHHNOSTO,TECHTYPE) AND IAGK_Y(IA,IGHHNOSTO)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGHHNOSTO))
),IGKFX_Y(IA,IGHHNOSTO ))
+SUM(IGETOH$(GTECHTYPE(IGETOH,TECHTYPE) AND IAGK_Y(IA,IGETOH)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGETOH))
),IGKFX_Y(IA,IGETOH)));

Heat_Capacity(C,'TOTAL',TECHTYPE)=Heat_Capacity(C,'New',TECHTYPE)+Heat_Capacity(C,'Existing',TECHTYPE);




*_______________________________________________________________________________________________________________________________________________________________________________________________________________________________________

execute_unload "MainResults.gdx" EPrice_RST, HPrice_AST, EDemand_RST, EDemand_R, Edemand_CST, Edemand_C, Hdemand_AST, EPrice_R, EPrice_CST, EPrice_C, HPrice_AS, HPrice_A, HPrice_C, EProduction_RST, ETrade_RST,
Eproduction_CST, ETrade_CST, EProduction_R, EProduction_C, ETrade_R, ETrade_C, Hproduction_AST, Hproduction_AS, Hproduction_A, Hproduction_CST, Hproduction_CS, Hproduction_C, FuelConsumption_C, FuelConsumption_TOTAL,
Elec_Capacity,Heat_Capacity,VOBJ,VGKN

$if     EXIST '../addons/_hooks/Outputgdx.inc' $INCLUDE      '../addons/_hooks/Outputgdx.inc';
$if not EXIST '../addons/_hooks/Outputgdx.inc' $INCLUDE '../../base/addons/_hooks/Outputgdx.inc';


;


*End of the output file
$offtext
