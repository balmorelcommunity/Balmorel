*Create output file for NMBU
* adaptions to the original by tilseb@dtu.dk; Aug. 2019
* modified from FutureGas version. Remove Industry

SET CATEGORY /COSTS,INCOME,REVENUE/;
SET SUBCATEGORY /ELECTRICITY_SALE,HEAT_SALE,GENERATION_CAPITAL_COSTS,GENERATION_FIXED_COSTS,GENERATION_OPERATIONAL_COSTS,GENERATION_FUEL_COSTS,GENERATION_HYDRO,TRANSMISSION_CAPITAL_COSTS,TRANSMISSION_OPERATIONAL_COSTS,TRANSMISSION_TRADE_INCOME,TRANSMISSION_TRADE_COSTS,GENERATION_CO2_TAX,GENERATION_UC_COSTS,GENERATION_OTHER_EMI_TAX,GENERATION_TAXES,GENERATION_SUBSIDIES,GENERATION_GRID_TARIFFS,GRID_TARIFFS,TAXES,SUBSIDIES,TOTAL_REVENUE,ENERGY_SPECIFIC_REVENUE/;
SET COMMODITY /ELECTRICITY,DISTRICT_HEAT,INDIVIDUAL_HEAT,SPACEHEAT,HOTWATER,PROCESS_HEAT/;
SET TECH_TYPE /CONDENSING,CHP-BACK-PRESSURE,CHP-EXTRACTION,BOILERS,ELECT-TO-HEAT,INTERSEASONAL-HEAT-STORAGE,INTERSEASONAL-ELECT-STORAGE,INTRASEASONAL-HEAT-STORAGE,INTRASEASONAL-ELECT-STORAGE,HYDRO-RESERVOIRS,HYDRO-RUN-OF-RIVER,WIND-ON,WIND-OFF,SOLAR-PV,SOLAR-HEATING,HYDRO-WAVE/;
SET TECH_CATEGORY /THERMAL_POWER,CHP,BOILER,STORAGE,P2X,WIND,SOLAR,HYDRO/;
SET GTECH_TYPE(GGG,TECH_TYPE);
SET TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY);
SET PRICE_CATEGORY /AVERAGE,AVERAGE_WEIGHTED_BY_CONSUMPTION,AVERAGE_WEIGHTED_BY_PRODUCTION/;
SET VARIABLE_CATEGORY /EXOGENOUS,ENDOGENOUS,DECOMMISSIONING/;
SET EL_BAL_TYPE /CURTAILMENT,NETEXPORT,PRICE,DEMAND_EXO,DEMAND_LOSS,DEMAND_INTERSTO,DEMAND_INTRASTO,DEMAND_P2H,DEMAND_EV,CONDENSING,CHP-BACK-PRESSURE,CHP-EXTRACTION,ELECT-TO-HEAT,INTER-STO,INTRA-STO,HYDRO-RESERVOIRS,HYDRO-RUN-OF-RIVER,WIND-ON,WIND-OFF,SOLAR-PV,HYDRO-WAVE/;
SET H_BAL_TYPE /CURTAILMENT,NETEXPORT,PRICE,DEMAND_EXO,DEMAND_LOSS,DEMAND_INTERSTO,DEMAND_INTRASTO,BOILERS,CHP-BACK-PRESSURE,CHP-EXTRACTION,INTER-STO,INTRA-STO,SOLAR-HEATING,P2H/;
SET UNITS /GW,TWh,MWh,Money_per_MWh,Mmoney,kton,GWh/;
SET EMISSION_TYPE /CO2,CH4/;
SET EUSER /OTHR,EV,P2X,HPandES_OTHER,P2H_IDVH,HP_IND/;
*"OTHR" EUSER represents the exogenous electricity demand of the following groups: RESE,PII and OTHER

* sets to allow for sector distinguishability
$ifi %INDHEAT%==yes SET INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE);
$ONMULTI
SET SECTOR /POWER_DH,INDIVIDUAL_HEAT,DISTRICT_HEAT,STORAGE_LOSS/;
$OFFMULTI
SET INDUSTRY_AREAS(AAA) / /;

   GTECH_TYPE(G,'CONDENSING')$IGCND(G)                                                    = YES;
   GTECH_TYPE(G,'CHP-BACK-PRESSURE')$IGBPR(G)                                             = YES;
   GTECH_TYPE(G,'CHP-EXTRACTION')$IGEXT(G)                                                = YES;
   GTECH_TYPE(G,'BOILERS')$IGHOB(G)                                                       = YES;
   GTECH_TYPE(G,'ELECT-TO-HEAT')$IGETOH(G)                                                = YES;
   GTECH_TYPE(G,'INTRASEASONAL-HEAT-STORAGE')$IGHSTO(G)                                   = YES;
   GTECH_TYPE(G,'INTERSEASONAL-HEAT-STORAGE')$IGHSTOS(G)                                  = YES;
   GTECH_TYPE(G,'INTRASEASONAL-ELECT-STORAGE')$IGESTO(G)                                  = YES;
   GTECH_TYPE(G,'INTERSEASONAL-ELECT-STORAGE')$IGESTOS(G)                                 = YES;
   GTECH_TYPE(G,'HYDRO-RESERVOIRS')$IGHYRS(G)                                             = YES;
   GTECH_TYPE(G,'HYDRO-RUN-OF-RIVER')$IGHYRR(G)                                           = YES;
   GTECH_TYPE(G,'WIND-ON')$(IGWND(G) AND GDATA(G,'GDTECHGROUP')  EQ WINDTURBINE_ONSHORE)  = YES;
   GTECH_TYPE(G,'WIND-OFF')$(IGWND(G) AND (GDATA(G,'GDTECHGROUP') EQ WINDTURBINE_OFFSHORE_NEARSHORE OR GDATA(G,'GDTECHGROUP') EQ WINDTURBINE_OFFSHORE_FAR)) = YES;
   GTECH_TYPE(G,'SOLAR-PV')$IGSOLE(G)                                                     = YES;
   GTECH_TYPE(G,'SOLAR-HEATING')$IGSOLH(G)                                                = YES;
   GTECH_TYPE(G,'HYDRO-WAVE')$IGWAVE(G)                                                   = YES;

   TECH_TYPE_CATEGORY('CONDENSING','THERMAL_POWER')  = YES;
   TECH_TYPE_CATEGORY('CHP-BACK-PRESSURE','CHP')     = YES;
   TECH_TYPE_CATEGORY('CHP-EXTRACTION','CHP')        = YES;
   TECH_TYPE_CATEGORY('BOILERS','BOILER')            = YES;
   TECH_TYPE_CATEGORY('ELECT-TO-HEAT','P2X')         = YES;
   TECH_TYPE_CATEGORY('INTRASEASONAL-HEAT-STORAGE','STORAGE')         = YES;
   TECH_TYPE_CATEGORY('INTERSEASONAL-HEAT-STORAGE','STORAGE')         = YES;
   TECH_TYPE_CATEGORY('INTRASEASONAL-ELECT-STORAGE','STORAGE')        = YES;
   TECH_TYPE_CATEGORY('INTERSEASONAL-ELECT-STORAGE','STORAGE')        = YES;
   TECH_TYPE_CATEGORY('HYDRO-RESERVOIRS','HYDRO')    = YES;
   TECH_TYPE_CATEGORY('HYDRO-RUN-OF-RIVER','HYDRO')  = YES;
   TECH_TYPE_CATEGORY('HYDRO-WAVE','HYDRO')          = YES;
   TECH_TYPE_CATEGORY('WIND-ON','WIND')              = YES;
   TECH_TYPE_CATEGORY('WIND-OFF','WIND')             = YES;
   TECH_TYPE_CATEGORY('SOLAR-PV','SOLAR')            = YES;
   TECH_TYPE_CATEGORY('SOLAR-HEATING','SOLAR')       = YES;

PARAMETER G_CAP_YCRAF(Y,C,RRR,*,*,FFF,COMMODITY,TECH_TYPE,*,VARIABLE_CATEGORY,UNITS) "Generation capacity for each year, country, region, area, technology, fuel, commodity, technology type, variable type (GW)";
PARAMETER G_STO_YCRAF(Y,C,RRR,*,*,FFF,COMMODITY,TECH_TYPE,*,VARIABLE_CATEGORY,UNITS) "Generation storage for each year, country, region, area, technology, fuel, commodity, technology type and variable type (GWh)";
PARAMETER PRO_YCRAGFST(Y,C,RRR,*,*,FFF,SSS,TTT,*,TECH_TYPE,*,UNITS) "Energy Production for each year, country, region, area, technology, fuel, season, hour, commodity, technology type (MWh)" ;
PARAMETER PRO_YCRAGF(Y,C,RRR,*,*,FFF,*,TECH_TYPE,*,UNITS) "Annual Energy Production for each year, country, region, area, technology, fuel, commodity, technology type (TWh)";
PARAMETER F_CONS_YCRAST(Y,C,RRR,*,*,FFF,SSS,TTT,TECH_TYPE,*,SECTOR,UNITS) "Fuel consumption for each year, country, region, area, technology, fuel, season, hour and technology type (MWh)";
PARAMETER F_CONS_YCRA(Y,C,RRR,*,*,FFF,TECH_TYPE,*,SECTOR,UNITS) "Fuel consumption for each year, country, region, area, technology, fuel and technology type (TWh)";
PARAMETER X_CAP_YCR(Y,C,IRRRE,IRRRI,VARIABLE_CATEGORY,UNITS) "Transmission capacity for each year, country, from region to region (GW)";
PARAMETER X_FLOW_YCRST(Y,C,IRRRE,IRRRI,SSS,TTT,UNITS) "Transmission flow for each year, country, from region to region, for each season and hour (MWh)";
PARAMETER X_FLOW_YCR(Y,C,IRRRE,IRRRI,UNITS) "Transmission flow for each year, country, from region to region (TWh)";
PARAMETER ECO_G_YCRAG(Y,C,RRR,*,*,FFF,TECH_TYPE,*,CATEGORY,SUBCATEGORY,SECTOR,UNITS) "Generation Economic Output for each year, country, region, area and technology (Mmoney)";
PARAMETER ECO_X_YCR(Y,C,RRR,CATEGORY,SUBCATEGORY,UNITS) "Electric Transmission Economic Output for each year, country and region (Mmoney)";
PARAMETER OBJ_YCR(Y,C,RRR,SUBCATEGORY,SECTOR,UNITS) "Objective function (Mmoney)";
PARAMETER EL_PRICE_YCR(Y,C,RRR,PRICE_CATEGORY,UNITS) "Average Electricity Prices for each region (money/MWh)";
PARAMETER EL_PRICE_YCRST(Y,C,RRR,SSS,TTT,UNITS) "Hourly Electricity Prices for each region (money/MWh)";
PARAMETER DH_PRICE_YCRA(Y,C,RRR,AAA,PRICE_CATEGORY,UNITS) "Average District Heating Prices for each area(money/MWh)";
PARAMETER DH_PRICE_YCRAST(Y,C,RRR,AAA,SSS,TTT,UNITS) "Hourly District Heating Prices for each area (money(MWh)";
*PARAMETER EL_DEMAND_YCRST(Y,C,RRR,SSS,TTT,VARIABLE_CATEGORY,UNITS,*) "Aggregated Hourly Electricity Demand (MWh)";
PARAMETER EL_DEMAND_YCRST(Y,C,RRR,SSS,TTT,VARIABLE_CATEGORY,SECTOR,UNITS) "Hourly Electricity Demand (MWh)";
PARAMETER EL_NET_DEMAND_YCRST(Y,C,RRR,SSS,TTT,DEUSER,UNITS) "Net electricity Demand (MWh)";
PARAMETER EL_DEMAND_YCR(Y,C,RRR,VARIABLE_CATEGORY,SECTOR,UNITS) "Yearly Electricity Demand (MWh)";
PARAMETER H_DEMAND_YCRAST(Y,C,RRR,*,*,SSS,TTT,VARIABLE_CATEGORY,SECTOR,UNITS) "Hourly heat Demand (MWh)";
PARAMETER H_DEMAND_YCRA(Y,C,RRR,*,*,VARIABLE_CATEGORY,SECTOR,UNITS) "Yearly heat Demand (MWh)";
PARAMETER EMI_YCRAG(Y,C,RRR,*,*,FFF,TECH_TYPE,*,EMISSION_TYPE,SECTOR,UNITS) "Annual CO2-EQUIVALENT emissions (ktons)";
PARAMETER CURT_YCRAGFST(Y,C,RRR,AAA,G,FFF,SSS,TTT,COMMODITY,TECH_TYPE,UNITS) "Hourly energy curtailment per country, region, area, technology, fuel, hour and technology type (MWh)";
PARAMETER CURT_YCRAGF(Y,C,RRR,AAA,G,FFF,COMMODITY,TECH_TYPE,UNITS) "Hourly energy curtailment per country, region, area, technology, fuel, hour and technology type (MWh)";
PARAMETER EL_BALANCE_YCRST(Y,C,RRR,EL_BAL_TYPE,SSS,TTT,UNITS);
PARAMETER H_BALANCE_YCRAST(Y,C,RRR,AAA,H_BAL_TYPE,SSS,TTT,UNITS);

$ifi not %INDHEAT%==yes $goto endindheat
PARAMETER IDVH_PRICE_YCRA(Y,C,RRR,INDHEAT_AAA,PRICE_CATEGORY,UNITS) "Average individual Heating Prices for each area(money/MWh)";
PARAMETER IDVH_PRICE_YCRAST(Y,C,RRR,INDHEAT_AAA,SSS,TTT,UNITS) "Hourly Individual Heating Prices for each area (money(MWh)";

* individual heating generation technology types
*   INDHEAT_GTECH_TYPE(INDHEAT_G,'CHP-BACK-PRESSURE')             = YES$(INDHEAT_GDATA(INDHEAT_G,'GDTYPE') EQ GBPR);
*   INDHEAT_GTECH_TYPE(INDHEAT_G,'CHP-EXTRACTION')                = YES$(INDHEAT_GDATA(INDHEAT_G,'GDTYPE') EQ GEXT);
   INDHEAT_GTECH_TYPE(INDHEAT_G,'BOILERS')$INDHEAT_IGHOB(INDHEAT_G)             = YES;
   INDHEAT_GTECH_TYPE(INDHEAT_G,'ELECT-TO-HEAT')$INDHEAT_IGETOH(INDHEAT_G)      = YES;
   INDHEAT_GTECH_TYPE(INDHEAT_G,'INTRASEASONAL-HEAT-STORAGE')$INDHEAT_IGHSTO(INDHEAT_G)      = YES;
   INDHEAT_GTECH_TYPE(INDHEAT_G,'SOLAR-HEATING')$INDHEAT_IGSOLH(INDHEAT_G)      = YES;

$label endindheat

*Modification of IHOURSINST
$ifi %RollingSeasons%==yes PARAMETER IHOURSINTROLLINGSEASON(S,T) "Length of time segment (hours) used in rolling season mode";
$ifi %RollingSeasons%==yes IWEIGHSUMS=SUM(SSS$(ORD(SSS) LE ROLLINGSEASONSNUMBER), WEIGHT_S(SSS));
$ifi %RollingSeasons%==yes IHOURSINTROLLINGSEASON(S,T)=IOF8760*WEIGHT_S(S)*WEIGHT_T(T)/(IWEIGHSUMS*SUM(ITALIAS, WEIGHT_T(ITALIAS)))/IHOURFRAC;


IWEIGHSUMS = SUM(S, WEIGHT_S(S));
IWEIGHSUMT = SUM(T, WEIGHT_T(T));
IHOURSINST(SSS,TTT)=0;
IHOURSINST(S,T)=IOF8760*WEIGHT_S(S)*WEIGHT_T(T)/(IWEIGHSUMS*IWEIGHSUMT);


* -------------------- ENERGY CAPACITY ---------------------

* ------------- ELECTRICITY CAPACITY --------------

** EXOGENOUS ELECTRICITY CAPACITY

G_CAP_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,TECH_CATEGORY,'EXOGENOUS','GW')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND NOT INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
         IOF0001*(GKFX(Y,IA,IGE)/(1$((NOT IGESTO(IGE)) AND (NOT IGESTOS(IGE)))+GDATA(IGE,'GDSTOHUNLD')$((IGESTO(IGE)) OR IGESTOS(IGE))));
** ENDOGENOUS ELECTRIC CAPACITY ACCUMULATED INVESTMENTS
G_CAP_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,TECH_CATEGORY,'ENDOGENOUS','GW')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND NOT INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
         IOF0001*(VGKNACCUMNET.L(Y,IA,IGE)/(1$((NOT IGESTO(IGE)) AND (NOT IGESTOS(IGE)))+GDATA(IGE,'GDSTOHUNLD')$((IGESTO(IGE)) OR IGESTOS(IGE))));
** ENDOGENOUS ELECTRICITY CAPACITY ACCUMULATED DECOMMISSIONING
$ifi %DECOM%==yes G_CAP_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,TECH_CATEGORY,'DECOMMISSIONING','GW')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,IGE) AND NOT INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
$ifi %DECOM%==yes -IOF0001*(VDECOM_EXO_ACCUM.L(Y,IA,IGE)/(1$((NOT IGESTO(IGE)) AND (NOT IGESTOS(IGE)))+GDATA(IGE,'GDSTOHUNLD')$((IGESTO(IGE)) OR IGESTOS(IGE))));

* ------------- END OF ELECTRICITY CAPACITY --------------

* ------------- HEAT CAPACITY --------------

** EXOGENOUS HEAT CAPACITY
*** DISTRICT HEAT
G_CAP_YCRAF(Y,C,IR,IA,IGH,FFF,'DISTRICT_HEAT',TECH_TYPE,TECH_CATEGORY,'EXOGENOUS','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND NOT INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
IOF0001*GKFX(Y,IA,IGH)/((1$((NOT IGHSTO(IGH)) AND (NOT IGHSTOS(IGH)))+GDATA(IGH,'GDSTOHUNLD')$((IGHSTO(IGH)) OR IGHSTOS(IGH)))*(GDATA(IGH,'GDCB')$IGBPR(IGH)+ (GDATA(IGH,'GDCB')+GDATA(IGH,'GDCV'))$IGEXT(IGH)+1$(NOT (IGBPR(IGH) OR IGEXT(IGH)))));

*** INDHEAT
$ifi %INDHEAT%==yes G_CAP_YCRAF(Y,C,IR,INDHEAT_IA,INDHEAT_G,FFF,'INDIVIDUAL_HEAT',TECH_TYPE,INDHEAT_GROUP,'EXOGENOUS','GW')$(CCCRRR(C,IR) and RRRAAA(IR,INDHEAT_IA) and (INDHEAT_GDATA(INDHEAT_G,'GDFUEL') EQ FDATA(FFF,'FDACRONYM')) AND INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE))=
$ifi %INDHEAT%==yes          IOF0001*INDHEAT_GKFX(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)/(1$(NOT INDHEAT_IGHSTO(INDHEAT_G))+ INDHEAT_GDATA(INDHEAT_G,'GDSTOHUNLD')$(INDHEAT_IGHSTO(INDHEAT_G)));

*** INDUSTRY
***G_CAP_YCRAF(Y,C,IR,IA,IGH,FFF,'PROCESS_HEAT',TECH_TYPE,TECH_CATEGORY'EXOGENOUS','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
***         IOF0001*(GKFX(Y,IA,IGH)/(1$((NOT IGHSTO(IGH)) AND (NOT IGHSTOS(IGH)))+GDATA(IGH,'GDSTOHUNLD')$((IGHSTO(IGH)) OR IGHSTOS(IGH))));


** ENDOGENOUS HEAT CAPACITY ACCUMULATED INVESTMENTS
*** DISTRICT HEAT
G_CAP_YCRAF(Y,C,IR,IA,IGH,FFF,'DISTRICT_HEAT',TECH_TYPE,TECH_CATEGORY,'ENDOGENOUS','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND NOT INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
IOF0001*VGKNACCUMNET.L(Y,IA,IGH)/((1$((NOT IGHSTO(IGH)) AND (NOT IGHSTOS(IGH)))+GDATA(IGH,'GDSTOHUNLD')$((IGHSTO(IGH)) OR IGHSTOS(IGH)))*(GDATA(IGH,'GDCB')$IGBPR(IGH)+ (GDATA(IGH,'GDCB')+GDATA(IGH,'GDCV'))$IGEXT(IGH)+1$(NOT (IGBPR(IGH) OR IGEXT(IGH)))));

*** INDHEAT
$ifi %INDHEAT%==yes G_CAP_YCRAF(Y,C,IR,INDHEAT_IA,INDHEAT_G,FFF,'INDIVIDUAL_HEAT',TECH_TYPE,INDHEAT_GROUP,'ENDOGENOUS','GW')$(CCCRRR(C,IR) and RRRAAA(IR,INDHEAT_IA) and (INDHEAT_GDATA(INDHEAT_G,'GDFUEL') EQ FDATA(FFF,'FDACRONYM')) AND INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE))=
$ifi %INDHEAT%==yes          IOF0001*INDHEAT_VGKNACCUMNET.L(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)/(1$(NOT INDHEAT_IGHSTO(INDHEAT_G))+ INDHEAT_GDATA(INDHEAT_G,'GDSTOHUNLD')$(INDHEAT_IGHSTO(INDHEAT_G)));

*** INDUSTRY
***G_CAP_YCRAF(Y,C,IR,IA,IGH,FFF,'PROCESS_HEAT',TECH_TYPE,TECH_CATEGORY,'ENDOGENOUS','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
***         IOF0001*(VGKNACCUMNET.L(Y,IA,IGH)/(1$((NOT IGESTO(IGH)) AND (NOT IGESTOS(IGH)))+GDATA(IGH,'GDSTOHUNLD')$((IGESTO(IGH)) OR IGESTOS(IGH))));

** ENDOGENOUS HEAT CAPACITY ACCUMULATED DECOMMISSIONING
$ifi %DECOM%==yes G_CAP_YCRAF(Y,C,IR,IA,IGH,FFF,'DISTRICT_HEAT',TECH_TYPE,TECH_CATEGORY,'DECOMMISSIONING','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,IGH) AND NOT INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
$ifi %DECOM%==yes -IOF0001*VDECOM_EXO_ACCUM.L(Y,IA,IGH)/((1$((NOT IGHSTO(IGH)) AND (NOT IGHSTOS(IGH)))+GDATA(IGH,'GDSTOHUNLD')$((IGHSTO(IGH)) OR IGHSTOS(IGH)))*(GDATA(IGH,'GDCB')$IGBPR(IGH)+ (GDATA(IGH,'GDCB')+GDATA(IGH,'GDCV'))$IGEXT(IGH)+1$(NOT (IGBPR(IGH) OR IGEXT(IGH)))));


*** INDUSTRY
***$ifi %DECOM%==yes G_CAP_YCRAF(Y,C,IR,IA,IGH,FFF,'PROCESS_HEAT',TECH_TYPE,TECH_CATEGORY,'DECOMMISSIONING','GW')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,IGH) AND INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
***$ifi %DECOM%==yes -IOF0001*(VDECOM_EXO_ACCUM.L(Y,IA,IGH)/(1$((NOT IGESTO(IGH)) AND (NOT IGESTOS(IGH)))+GDATA(IGH,'GDSTOHUNLD')$((IGESTO(IGH)) OR IGESTOS(IGH))));

* ------------- END OF HEAT CAPACITY --------------

* ---------------- END OF ENERGY CAPACITY -----------------

* -------------------- ENERGY STORAGE ---------------------

* ------------- ELECTRICITY STORAGE --------------

** EXOGENOUS ELECTRICITY STORAGE

G_STO_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,TECH_CATEGORY,'EXOGENOUS','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND (IGESTO(IGE) OR IGESTOS(IGE)) AND NOT INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
IOF0001*GKFX(Y,IA,IGE)
;

** ENDOGENOUS ELECTRIC STORAGE ACCUMULATED INVESTMENTS
G_STO_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,TECH_CATEGORY,'ENDOGENOUS','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND (IGESTO(IGE) OR IGESTOS(IGE)) AND NOT INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
IOF0001*VGKNACCUMNET.L(Y,IA,IGE)
;

** ENDOGENOUS ELECTRICITY STORAGE ACCUMULATED DECOMMISSIONING
$ifi %DECOM%==yes G_STO_YCRAF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,TECH_CATEGORY,'DECOMMISSIONING','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,IGE) AND (IGESTO(IGE) OR IGESTOS(IGE)) AND NOT INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
$ifi %DECOM%==yes -IOF0001*VDECOM_EXO_ACCUM.L(Y,IA,IGE)
;


* ------------- END OF ELECTRICITY CAPACITY --------------

* ------------- HEAT STORAGE --------------

** EXOGENOUS HEAT STORAGE

G_STO_YCRAF(Y,C,IR,IA,IGH,FFF,'DISTRICT_HEAT',TECH_TYPE,TECH_CATEGORY,'EXOGENOUS','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND (IGHSTO(IGH) OR IGHSTOS(IGH)) AND NOT INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
IOF0001*GKFX(Y,IA,IGH)
;
*** INDHEAT
$ifi %INDHEAT%==yes G_STO_YCRAF(Y,C,IR,INDHEAT_IA,INDHEAT_IGHSTO,FFF,'INDIVIDUAL_HEAT',TECH_TYPE,INDHEAT_GROUP,'EXOGENOUS','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,INDHEAT_IA) AND INDHEAT_IAGK_HASORPOT(Y,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_GROUP) AND INDHEAT_IGF(INDHEAT_IGHSTO,FFF) AND INDHEAT_GTECH_TYPE(INDHEAT_IGHSTO,TECH_TYPE))=
$ifi %INDHEAT%==yes          IOF0001*(INDHEAT_GKFX(Y,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_GROUP));

** ENDOGENOUS HEAT STORAGE ACCUMULATED INVESTMENTS
G_STO_YCRAF(Y,C,IR,IA,IGH,FFF,'DISTRICT_HEAT',TECH_TYPE,TECH_CATEGORY,'ENDOGENOUS','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND (IGHSTO(IGH) OR IGHSTOS(IGH)) AND NOT INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
IOF0001*VGKNACCUMNET.L(Y,IA,IGH)
;
*** INDHEAT
$ifi %INDHEAT%==yes G_STO_YCRAF(Y,C,IR,INDHEAT_IA,INDHEAT_IGHSTO,FFF,'INDIVIDUAL_HEAT',TECH_TYPE,INDHEAT_GROUP,'ENDOGENOUS','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,INDHEAT_IA) AND INDHEAT_IAGK_HASORPOT(Y,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_GROUP) AND INDHEAT_IGF(INDHEAT_IGHSTO,FFF) AND INDHEAT_GTECH_TYPE(INDHEAT_IGHSTO,TECH_TYPE))=
$ifi %INDHEAT%==yes          IOF0001*(INDHEAT_VGKNACCUMNET.L(Y,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_GROUP));

** ENDOGENOUS HEAT STORAGE ACCUMULATED DECOMMISSIONING
$ifi %DECOM%==yes G_STO_YCRAF(Y,C,IR,IA,IGH,FFF,'DISTRICT_HEAT',TECH_TYPE,TECH_CATEGORY,'DECOMMISSIONING','GWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND IGDECOMEXOPOT(Y,IA,IGH) AND (IGHSTO(IGH) OR IGHSTOS(IGH)) AND NOT INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
$ifi %DECOM%==yes -IOF0001*VDECOM_EXO_ACCUM.L(Y,IA,IGH)
;

* ------------- END OF HEAT STORAGE --------------

* ---------------- END OF ENERGY STORAGE -----------------

* ------------- END OF HEAT CAPACITY --------------

* ---------------- END OF ENERGY CAPACITY -----------------


* -------------------- ENERGY PRODUCTION ---------------------

** ELECTRICITY PRODUCTION HOURLY
PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY',TECH_TYPE,TECH_CATEGORY,'MWh')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND NOT INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
       VGE_T.L(Y,IA,IGE,S,T);

** ELECTRICITY PRODUCTION ANNUALLY
PRO_YCRAGF(Y,C,IR,IA,IGE,FFF,'ELECTRICITY',TECH_TYPE,TECH_CATEGORY,'TWh')$((not IGETOH(IGE)) AND CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) AND NOT INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
       SUM((S,T),IHOURSINST(S,T)*VGE_T.L(Y,IA,IGE,S,T))/IOF1000000;

* ------------------------ END OF ELECTRICITY PRODUCTION --------------

* ------------------------ HEAT PRODUCTION ---------------------
** HEAT PRODUCTION HOURLY
*** DISTRICT HEAT
PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'DISTRICT_HEAT',TECH_TYPE,TECH_CATEGORY,'MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND NOT INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY) )=
       VGH_T.L(Y,IA,IGH,S,T);

*** INDHEAT
$ifi %INDHEAT%==yes PRO_YCRAGFST(Y,C,IR,INDHEAT_IA,INDHEAT_G,FFF,S,T,INDHEAT_DHUSER,TECH_TYPE,INDHEAT_GROUP,'MWh')$(CCCRRR(C,IR) and RRRAAA(IR,INDHEAT_IA) and (INDHEAT_GDATA(INDHEAT_G,'GDFUEL') EQ FDATA(FFF,'FDACRONYM')) AND INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE))=
$ifi %INDHEAT%==yes       INDHEAT_VGH_T.L(Y,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_G,INDHEAT_GROUP,S,T);

*** PROCESS HEAT
***PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'PROCESS_HEAT',TECH_TYPE,TECH_CATEGORY,'MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
***       VGH_T.L(Y,IA,IGH,S,T);

** HEAT PRODUCTION ANNUAL
*** DISTRICT HEAT
PRO_YCRAGF(Y,C,IR,IA,IGH,FFF,'DISTRICT_HEAT',TECH_TYPE,TECH_CATEGORY,'TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND NOT INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
       SUM((S,T),IHOURSINST(S,T)*VGH_T.L(Y,IA,IGH,S,T))/IOF1000000;

*** INDHEAT
$ifi %INDHEAT%==yes PRO_YCRAGF(Y,C,IR,INDHEAT_IA,INDHEAT_G,FFF,INDHEAT_DHUSER,TECH_TYPE,INDHEAT_GROUP,'TWh')$(CCCRRR(C,IR) and RRRAAA(IR,INDHEAT_IA) and (INDHEAT_GDATA(INDHEAT_G,'GDFUEL') EQ FDATA(FFF,'FDACRONYM')) AND INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE))=
$ifi %INDHEAT%==yes       SUM((S,T),IHOURSINST(S,T)*INDHEAT_VGH_T.L(Y,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_G,INDHEAT_GROUP,S,T))/IOF1000000;

*** PROCESS HEAT
***PRO_YCRAGF(Y,C,IR,IA,IGH,FFF,'PROCESS_HEAT',TECH_TYPE,TECH_CATEGORY,'TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
***       SUM((S,T),IHOURSINST(S,T)*VGH_T.L(Y,IA,IGH,S,T))/IOF1000000;


* ------------------------ END OF HEAT PRODUCTION --------------

* ---------------- END OF ENERGY PRODUCTION -----------------


* -------------------- FUEL CONSUMPTION ---------------------
$ifi not %VGF%==Y $goto endvgfy

** ANNUAL FUEL CONSUMPTION
F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,TECH_CATEGORY,'POWER_DH','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) AND NOT INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
       VGF_T.L(Y,IA,G)/IOF1000000;

*** INDHEAT
$ifi %INDHEAT%==yes F_CONS_YCRA(Y,C,IR,INDHEAT_IA,INDHEAT_G,FFF,TECH_TYPE,INDHEAT_GROUP,'INDIVIDUAL_HEAT','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,INDHEAT_IA) and (INDHEAT_GDATA(INDHEAT_G,'GDFUEL') EQ FDATA(FFF,'FDACRONYM')) AND INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE))=
$ifi %INDHEAT%==yes        INDHEAT_VGF_T.L(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)/IOF1000000;

*** INDUSTRY SECTOR
***F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'INDUSTRY','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) AND INDUSTRY_AREAS(IA))=
***       VGF_T.L(Y,IA,G)/IOF1000000;
***F_CONS_YCRA(Y,C,IR,IA,'GNR_EH_HEAT',FFF,TECH_TYPE,TECH_CATEGORY,'INDUSTRY','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,'GNR_EH_HEAT') AND IGF('GNR_EH_HEAT',FFF) AND GTECH_TYPE('GNR_EH_HEAT',TECH_TYPE) AND INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
***       0;
$label endvgfy

$ifi not %VGF%==ST $goto endvgfst
** HOURLY FUEL CONSUMPTION

*** OTHER SECTOR (INCL. INDHEAT)
F_CONS_YCRAST(Y,C,IR,IA,G,FFF,S,T,TECH_TYPE,TECH_CATEGORY,'POWER_DH','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) AND NOT INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
       VGF_T.L(Y,IA,G,S,T);
F_CONS_YCRAST(Y,C,IR,IA,'GNR_EH_HEAT',FFF,S,T,TECH_TYPE,TECH_CATEGORY,'POWER_DH','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,'GNR_EH_HEAT') AND IGF('GNR_EH_HEAT',FFF) AND GTECH_TYPE('GNR_EH_HEAT',TECH_TYPE) AND INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
       VGF_T.L(Y,IA,'GNR_EH_HEAT',S,T);

*** INDHEAT
F_CONS_YCRAST(Y,C,IR,INDHEAT_IA,INDHEAT_G,FFF,S,T,TECH_TYPE,INDHEAT_GROUP,'INDIVIDUAL_HEAT','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,INDHEAT_IA) and (INDHEAT_GDATA(INDHEAT_G,'GDFUEL') EQ FDATA(FFF,'FDACRONYM')) AND INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE))=
       INDHEAT_VGF_T.L(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP,S,T);

*** INDUSTRY SECTOR
***F_CONS_YCRAST(Y,C,IR,IA,G,FFF,S,T,TECH_TYPE,'INDUSTRY','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) AND INDUSTRY_AREAS(IA))=
***       VGF_T.L(Y,IA,G,S,T);
***F_CONS_YCRAST(Y,C,IR,IA,'GNR_EH_HEAT',FFF,S,T,TECH_TYPE,'INDUSTRY','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,'GNR_EH_HEAT') AND IGF('GNR_EH_HEAT',FFF) AND GTECH_TYPE('GNR_EH_HEAT',TECH_TYPE) AND INDUSTRY_AREAS(IA))=
***       0;


** ANNUAL FUEL CONSUMPTION
*** OTHER SECTOR (INCL. INDHEAT)
F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,TECH_CATEGORY,'POWER_DH','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) AND NOT INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
       SUM((S,T),IHOURSINST(S,T)*VGF_T.L(Y,IA,G,S,T))/IOF1000000;
F_CONS_YCRA(Y,C,IR,IA,'GNR_EH_HEAT',FFF,TECH_TYPE,TECH_CATEGORY,'POWER_DH','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,'GNR_EH_HEAT') AND IGF('GNR_EH_HEAT',FFF) AND GTECH_TYPE('GNR_EH_HEAT',TECH_TYPE) AND INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
       SUM((S,T),IHOURSINST(S,T)*VGF_T.L(Y,IA,'GNR_EH_HEAT',S,T))/IOF1000000;

*** INDHEAT
$ifi %INDHEAT%==yes  F_CONS_YCRA(Y,C,IR,INDHEAT_IA,INDHEAT_G,FFF,TECH_TYPE,INDHEAT_GROUP,'INDIVIDUAL_HEAT','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,INDHEAT_IA) and (INDHEAT_GDATA(INDHEAT_G,'GDFUEL') EQ FDATA(FFF,'FDACRONYM')) AND INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE))=
$ifi %INDHEAT%==yes        SUM((S,T),IHOURSINST(S,T)*INDHEAT_VGF_T.L(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP,S,T))/IOF1000000;

*** INDUSTRY SECTOR
***F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,TECH_CATEGORY,'INDUSTRY','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) AND INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
***       SUM((S,T),IHOURSINST(S,T)*VGF_T.L(Y,IA,G,S,T))/IOF1000000;
***F_CONS_YCRA(Y,C,IR,IA,'GNR_EH_HEAT',FFF,TECH_TYPE,TECH_CATEGORY,'INDUSTRY','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,'GNR_EH_HEAT') AND IGF('GNR_EH_HEAT',FFF) AND GTECH_TYPE('GNR_EH_HEAT',TECH_TYPE) AND INDUSTRY_AREAS(IA) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
***       0;
$label endvgfst

**** FUEL CONSUMPTION EQUATIONS FROM STANDARD OUTPUT FILE
$ONTEXT
** HOURLY FUEL CONSUMPTION
F_CONS_YCRAST(Y,C,IR,IA,G,FFF,S,T,TECH_TYPE,'MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE))=
       (VGE_T.L(Y,IA,G,S,T)*IGFE_FACTOR(Y,IA,G) + VGH_T.L(Y,IA,G,S,T)*IGFH_FACTOR(Y,IA,G));

** ANNUAL FUEL CONSUMPTION
F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,'TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE))=
       SUM((S,T),IHOURSINST(S,T)*(VGE_T.L(Y,IA,G,S,T)*IGFE_FACTOR(Y,IA,G) + VGH_T.L(Y,IA,G,S,T)*IGFH_FACTOR(Y,IA,G)))/IOF1000000;
$OFFTEXT


* ---------------- END OF FUEL CONSUMPTION -----------------


* ---------------- ENERGY CURTAILMENT -----------------------
*HOURLY CURTAILMENT
CURT_YCRAGFST(Y,C,IR,IA,IGHYRR,FFF,S,T,'ELECTRICITY','HYDRO-RUN-OF-RIVER','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGHYRR) AND IGF(IGHYRR,FFF) AND GTECH_TYPE(IGHYRR,'HYDRO-RUN-OF-RIVER') AND IWTRRRSUM(IA))=
(WTRRRFLH(IA) * (
           GKFX(Y,IA,IGHYRR)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGHYRR)
        +  VGKNACCUMNET.L(Y,IA,IGHYRR)
                          ) * WTRRRVAR_T(IA,S,T) * (1$(NOT IGKRATE(IA,IGHYRR,S,T)) + IGKRATE(IA,IGHYRR,S,T))) / IWTRRRSUM(IA)
-VGE_T.L(Y,IA,IGHYRR,S,T);

CURT_YCRAGFST(Y,C,IR,IA,IGWND,FFF,S,T,'ELECTRICITY','WIND-ON','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGWND) AND IGF(IGWND,FFF) AND GTECH_TYPE(IGWND,'WIND-ON') AND IWND_SUMST(IA))=
$ifi %WNDFLH_DOL%==AAA         (WNDFLH(IA) * (
$ifi %WNDFLH_DOL%==AAA_GGG     (WNDFLH(IA,IGWND) * (
            GKFX(Y,IA,IGWND)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGWND)
         + VGKNACCUMNET.L(Y,IA,IGWND)
                 )  * WND_VAR_T(IA,S,T) * (1$(NOT IGKRATE(IA,IGWND,S,T)) + IGKRATE(IA,IGWND,S,T))) / IWND_SUMST(IA)
-VGE_T.L(Y,IA,IGWND,S,T);

CURT_YCRAGFST(Y,C,IR,IA,IGWND,FFF,S,T,'ELECTRICITY','WIND-OFF','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGWND) AND IGF(IGWND,FFF) AND GTECH_TYPE(IGWND,'WIND-OFF') AND IWND_SUMST(IA))=
$ifi %WNDFLH_DOL%==AAA         (WNDFLH(IA) * (
$ifi %WNDFLH_DOL%==AAA_GGG     (WNDFLH(IA,IGWND) * (
            GKFX(Y,IA,IGWND)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGWND)
         + VGKNACCUMNET.L(Y,IA,IGWND)
                 )  * WND_VAR_T(IA,S,T) * (1$(NOT IGKRATE(IA,IGWND,S,T)) + IGKRATE(IA,IGWND,S,T))) / IWND_SUMST(IA)
-VGE_T.L(Y,IA,IGWND,S,T);

CURT_YCRAGFST(Y,C,IR,IA,IGSOLE,FFF,S,T,'ELECTRICITY','SOLAR-PV','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGSOLE) AND IGF(IGSOLE,FFF) AND GTECH_TYPE(IGSOLE,'SOLAR-PV') AND ISOLESUMST(IA))=
$ifi %SOLEFLH_DOL%==AAA         (SOLEFLH(IA) * (
$ifi %SOLEFLH_DOL%==AAA_GGG     (SOLEFLH(IA,IGSOLE) * (
            GKFX(Y,IA,IGSOLE)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGSOLE)
         + VGKNACCUMNET.L(Y,IA,IGSOLE)
                 )  * SOLE_VAR_T(IA,S,T) * (1$(NOT IGKRATE(IA,IGSOLE,S,T)) + IGKRATE(IA,IGSOLE,S,T))) / ISOLESUMST(IA)
-VGE_T.L(Y,IA,IGSOLE,S,T);

CURT_YCRAGFST(Y,C,IR,IA,IGSOLH,FFF,S,T,'DISTRICT_HEAT','SOLAR-HEATING','MWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGSOLH) AND IGF(IGSOLH,FFF) AND GTECH_TYPE(IGSOLH,'SOLAR-HEATING') AND ISOLHSUMST(IA))=
$ifi %SOLHFLH_DOL%==AAA         (SOLHFLH(IA) * (
$ifi %SOLHFLH_DOL%==AAA_GGG     (SOLHFLH(IA,IGSOLH) * (
            GKFX(Y,IA,IGSOLH)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGSOLH)
         + VGKNACCUMNET.L(Y,IA,IGSOLH)
                 )  * SOLH_VAR_T(IA,S,T) * (1$(NOT IGKRATE(IA,IGSOLH,S,T)) + IGKRATE(IA,IGSOLH,S,T))) / ISOLHSUMST(IA)
-VGE_T.L(Y,IA,IGSOLH,S,T);


*ANNUAL CURTAILMENT
CURT_YCRAGF(Y,C,IR,IA,IGHYRR,FFF,'ELECTRICITY','HYDRO-RUN-OF-RIVER','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGHYRR) AND IGF(IGHYRR,FFF) AND GTECH_TYPE(IGHYRR,'HYDRO-RUN-OF-RIVER'))=
SUM((S,T),IHOURSINST(S,T)*CURT_YCRAGFST(Y,C,IR,IA,IGHYRR,FFF,S,T,'ELECTRICITY','HYDRO-RUN-OF-RIVER','MWh'))/IOF1000000
;
CURT_YCRAGF(Y,C,IR,IA,IGWND,FFF,'ELECTRICITY','WIND-ON','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGWND) AND IGF(IGWND,FFF) AND GTECH_TYPE(IGWND,'WIND-ON'))=
SUM((S,T),IHOURSINST(S,T)*CURT_YCRAGFST(Y,C,IR,IA,IGWND,FFF,S,T,'ELECTRICITY','WIND-ON','MWh'))/IOF1000000
;
CURT_YCRAGF(Y,C,IR,IA,IGWND,FFF,'ELECTRICITY','WIND-OFF','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGWND) AND IGF(IGWND,FFF) AND GTECH_TYPE(IGWND,'WIND-OFF'))=
SUM((S,T),IHOURSINST(S,T)*CURT_YCRAGFST(Y,C,IR,IA,IGWND,FFF,S,T,'ELECTRICITY','WIND-OFF','MWh'))/IOF1000000
;
CURT_YCRAGF(Y,C,IR,IA,IGSOLE,FFF,'ELECTRICITY','SOLAR-PV','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGSOLE) AND IGF(IGSOLE,FFF) AND GTECH_TYPE(IGSOLE,'SOLAR-PV'))=
SUM((S,T),IHOURSINST(S,T)*CURT_YCRAGFST(Y,C,IR,IA,IGSOLE,FFF,S,T,'ELECTRICITY','SOLAR-PV','MWh'))/IOF1000000
;
CURT_YCRAGF(Y,C,IR,IA,IGSOLH,FFF,'DISTRICT_HEAT','SOLAR-HEATING','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGSOLH) AND IGF(IGSOLH,FFF) AND GTECH_TYPE(IGSOLH,'SOLAR-HEATING'))=
SUM((S,T),IHOURSINST(S,T)*CURT_YCRAGFST(Y,C,IR,IA,IGSOLH,FFF,S,T,'DISTRICT_HEAT','SOLAR-HEATING','MWh'))/IOF1000000
;

*Curtailment of hydro reservoirs should be analyzed by aggregating the technology set
CURT_YCRAGF(Y,C,IR,IA,IGHYRS,FFF,'ELECTRICITY','HYDRO-RESERVOIRS','TWh')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGHYRS) AND IGF(IGHYRS,FFF) AND GTECH_TYPE(IGHYRS,'HYDRO-RESERVOIRS'))=
SUM(S,
        IHYINF_S(IA,S) *
        (
        GKFX(Y,IA,IGHYRS)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGHYRS)
       +VGKNACCUMNET.L(Y,IA,IGHYRS)
         )
         -SUM(T,IHYRST(S,T)*VGE_T.L(Y,IA,IGHYRS,S,T))
    )/IOF1000000
;


* ---------------- END OF ENERGY CURTAILMENT -----------------------

* ------------------------- ENERGY DEMAND ----------------------

* ------------- ELECTRICITY DEMAND --------------

*HOURLY
** EXOGENOUS DEMAND

EL_DEMAND_YCRST(Y,C,IR,S,T,'EXOGENOUS',SECTOR,'MWh')$CCCRRR(C,IR)=
         SUM(DEUSER,DEUSERINSECTOR(DEUSER,SECTOR)*IDE_T(Y,IR,DEUSER,S,T))/(1-DISLOSS_E(IR));
*EL_DEMAND_YCRST(Y,C,IR,S,T,'EXOGENOUS','EV','MWh')$CCCRRR(C,IR) =         SUM(DEUSER,IDE_T(Y,IR,'EV',S,T))/(1-DISLOSS_E(IR)) ;
*EL_DEMAND_YCRST(Y,C,IR,S,T,'EXOGENOUS','P2X','MWh')$CCCRRR(C,IR) =         SUM(DEUSER,IDE_T(Y,IR,'PTX',S,T))/(1-DISLOSS_E(IR)) ;

EL_NET_DEMAND_YCRST(Y,C,IR,S,T,DEUSER,'MWh')$CCCRRR(C,IR)=
VDENET_T.L(Y,IR,DEUSER,S,T);

** ENDOGENOUS DEMAND

EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDOGENOUS','DISTRICT_HEAT','MWh')$CCCRRR(C,IR)=
* HEAT PUMPS CONSUMPTION
           SUM(IA$(RRRAAA(IR,IA) AND NOT INDUSTRY_AREAS(IA)), SUM(IGETOH$IAGK_HASORPOT(Y,IA,IGETOH), VGE_T.L(Y,IA,IGETOH,S,T)))
;

EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDOGENOUS','POWER_DH','MWh')$CCCRRR(C,IR)=
* INTRASEASONAL ELECTRICITY STORAGE
         +SUM(IA$(RRRAAA(IR,IA) AND NOT INDUSTRY_AREAS(IA)),SUM(IGESTO$IAGK_HASORPOT(Y,IA,IGESTO),VESTOLOADT.L(Y,IA,IGESTO,S,T)))
;

EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDOGENOUS','POWER_DH','MWh')$CCCRRR(C,IR)=
* INTERSEASONAL ELECTRICITY STORAGE
         +SUM(IA$(RRRAAA(IR,IA) AND NOT INDUSTRY_AREAS(IA)),SUM(IGESTOS$IAGK_HASORPOT(Y,IA,IGESTOS),VESTOLOADTS.L(Y,IA,IGESTOS,S,T)));
;

*** IND_HEAT SECTOR
$ifi %INDHEAT%==yes EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDOGENOUS','INDIVIDUAL_HEAT','MWh')$CCCRRR(C,IR)=
* HEAT PUMPS AND ELECTRIC HEATERS CONSUMPTION
$ifi %INDHEAT%==yes SUM(INDHEAT_IA$RRRAAA(IR,INDHEAT_IA), SUM((INDHEAT_DHUSER,INDHEAT_G,INDHEAT_GROUP)$(INDHEAT_IAGK_HASORPOT(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP) AND INDHEAT_IGETOH(INDHEAT_G) AND INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP) AND INDHEAT_GTODHUSER(INDHEAT_G,INDHEAT_DHUSER)),
$ifi %INDHEAT%==yes      INDHEAT_VGH_T.L(Y,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_G,INDHEAT_GROUP,S,T)/(INDHEAT_GDATA(INDHEAT_G,'GDFE')$(INDHEAT_IGETOH_NOTHP(INDHEAT_G))+INDHEAT_COP(INDHEAT_IA,INDHEAT_G,S,T)$(INDHEAT_IGHP(INDHEAT_G)))));


*** INDUSTRY SECTOR
***EL_DEMAND_YCRST(Y,C,IR,S,T,'ENDOGENOUS','HP_IND','MWh')$CCCRRR(C,IR)=
* HEAT PUMPS CONSUMPTION
***        SUM(IA$(RRRAAA(IR,IA) AND INDUSTRY_AREAS(IA)), SUM(IGETOH$IAGK_HASORPOT(Y,IA,IGETOH), VGE_T.L(Y,IA,IGETOH,S,T)));


*ANNUAL DEMAND
*summed to yearly values
** EXOGENOUS DEMAND

EL_DEMAND_YCR(Y,C,IR,'EXOGENOUS',SECTOR,'TWh')$CCCRRR(C,IR)=
         SUM((S,T,DEUSER),IHOURSINST(S,T)*IDE_T(Y,IR,DEUSER,S,T)*DEUSERINSECTOR(DEUSER,SECTOR))/(1-DISLOSS_E(IR)) /IOF1000000 ;
*EL_DEMAND_YCR(Y,C,IR,'EXOGENOUS','EV','TWh')$CCCRRR(C,IR) =         SUM((S,T,DEUSER),IHOURSINST(S,T)*IDE_T(Y,IR,'EV',S,T))/(1-DISLOSS_E(IR))/IOF1000000 ;
*EL_DEMAND_YCR(Y,C,IR,'EXOGENOUS','P2X','TWh')$CCCRRR(C,IR) =         SUM((S,T,DEUSER),IHOURSINST(S,T)*IDE_T(Y,IR,'PTX',S,T))/(1-DISLOSS_E(IR))/IOF1000000 ;

** ENDOGENOUS DEMAND

EL_DEMAND_YCR(Y,C,IR,'ENDOGENOUS','DISTRICT_HEAT','TWh')$CCCRRR(C,IR)=
* HEAT PUMPS CONSUMPTION
           SUM((S,T,IA)$(RRRAAA(IR,IA) AND NOT INDUSTRY_AREAS(IA)), SUM(IGETOH$IAGK_HASORPOT(Y,IA,IGETOH), IHOURSINST(S,T)*VGE_T.L(Y,IA,IGETOH,S,T)))/IOF1000000
;

EL_DEMAND_YCR(Y,C,IR,'ENDOGENOUS','STORAGE_LOSS','TWh')$CCCRRR(C,IR)=
* INTRASEASONAL ELECTRICITY STORAGE LOSS
         +SUM((S,T,IA)$(RRRAAA(IR,IA) AND NOT INDUSTRY_AREAS(IA)),SUM(IGESTO$IAGK_HASORPOT(Y,IA,IGESTO),IHOURSINST(S,T)*(VESTOLOADT.L(Y,IA,IGESTO,S,T)-VGE_T.L(Y,IA,IGESTO,S,T))))/IOF1000000
;

EL_DEMAND_YCR(Y,C,IR,'ENDOGENOUS','STORAGE_LOSS','TWh')$CCCRRR(C,IR)=
* INTERSEASONAL ELECTRICITY STORAGE LOSS
         +SUM((S,T,IA)$(RRRAAA(IR,IA) AND NOT INDUSTRY_AREAS(IA)),SUM(IGESTOS$IAGK_HASORPOT(Y,IA,IGESTOS),IHOURSINST(S,T)*(VESTOLOADTS.L(Y,IA,IGESTOS,S,T)-VGE_T.L(Y,IA,IGESTOS,S,T))))/IOF1000000
;

*** IND_HEAT SECTOR
$ifi %INDHEAT%==yes EL_DEMAND_YCR(Y,C,IR,'ENDOGENOUS','INDIVIDUAL_HEAT','TWh')$CCCRRR(C,IR)=
* HEAT PUMPS AND ELECTRIC HEATERS CONSUMPTION
$ifi %INDHEAT%==yes SUM(INDHEAT_IA$RRRAAA(IR,INDHEAT_IA), SUM((INDHEAT_DHUSER,INDHEAT_G,INDHEAT_GROUP,S,T)$(INDHEAT_IAGK_HASORPOT(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP) AND INDHEAT_IGETOH(INDHEAT_G) AND INDHEAT_GINGROUP(INDHEAT_G,INDHEAT_GROUP) AND INDHEAT_GTODHUSER(INDHEAT_G,INDHEAT_DHUSER)),
$ifi %INDHEAT%==yes      IHOURSINST(S,T)*INDHEAT_VGH_T.L(Y,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_G,INDHEAT_GROUP,S,T)/(INDHEAT_GDATA(INDHEAT_G,'GDFE')$(INDHEAT_IGETOH_NOTHP(INDHEAT_G))+INDHEAT_COP(INDHEAT_IA,INDHEAT_G,S,T)$(INDHEAT_IGHP(INDHEAT_G)))))/IOF1000000;


*** INDUSTRY SECTOR
***EL_DEMAND_YCR(Y,C,IR,'ENDOGENOUS','HP_IND','TWh')$CCCRRR(C,IR)=
* HEAT PUMPS CONSUMPTION
***        SUM((S,T),SUM(IA$(RRRAAA(IR,IA) AND INDUSTRY_AREAS(IA)), SUM(IGETOH$IAGK_HASORPOT(Y,IA,IGETOH), VGE_T.L(Y,IA,IGETOH,S,T))))/IOF1000000;


* ----------- END OF ELECTRICITY DEMAND --------------

* ------------- HEAT DEMAND --------------
*HOURLY DEMAND
** EXOGENOUS DEMAND

H_DEMAND_YCRAST(Y,C,IR,IA,DHUSER,S,T,'EXOGENOUS','DISTRICT_HEAT','MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND NOT INDUSTRY_AREAS(IA))=
         IDH_T(Y,IA,DHUSER,S,T)/(1-DISLOSS_H(IA));

*** INDHEAT SECTOR
$ifi %INDHEAT%==yes H_DEMAND_YCRAST(Y,C,IR,INDHEAT_IA,INDHEAT_DHUSER,S,T,'EXOGENOUS',SECTOR,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,INDHEAT_IA) AND INDHEAT_AAAINSECTOR(INDHEAT_IA,SECTOR))=
$ifi %INDHEAT%==yes           (INDHEAT_DH(Y,INDHEAT_IA,INDHEAT_DHUSER) * INDHEAT_DH_VAR_T(INDHEAT_IA,INDHEAT_DHUSER,S,T)) / (INDHEAT_IDH_SUMST(INDHEAT_IA,INDHEAT_DHUSER)$(INDHEAT_IDH_SUMST(INDHEAT_IA,INDHEAT_DHUSER) GT 0)+1$(INDHEAT_IDH_SUMST(INDHEAT_IA,INDHEAT_DHUSER) EQ 0)) ;

*** INDUSTRY SECTOR
***H_DEMAND_YCRAST(Y,C,IR,IA,S,T,'PROCESS_HEAT','EXOGENOUS','MWh','INDUSTRY')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND INDUSTRY_AREAS(IA))=
***         SUM(DHUSER,IDH_T(Y,IA,DHUSER,S,T))/(1-DISLOSS_H(IA));

** ENDOGENOUS DEMAND
H_DEMAND_YCRAST(Y,C,IR,IA,DHUSER,S,T,'ENDOGENOUS','DISTRICT_HEAT','MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND NOT INDUSTRY_AREAS(IA))=
* INTRASEASONAL HEAT STORAGE
        SUM(IGHSTO$IAGK_HASORPOT(Y,IA,IGHSTO),VHSTOLOADT.L(Y,IA,IGHSTO,S,T))
* INTERSEASONAL HEAT STORAGE
        + SUM(IGHSTOS$IAGK_HASORPOT(Y,IA,IGHSTOS),VHSTOLOADTS.L(Y,IA,IGHSTOS,S,T));

*** INDHEAT SECTOR
$ifi %INDHEAT%==yes H_DEMAND_YCRAST(Y,C,IR,INDHEAT_IA,INDHEAT_DHUSER,S,T,'ENDOGENOUS',SECTOR,'MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,INDHEAT_IA) AND INDHEAT_AAAINSECTOR(INDHEAT_IA,SECTOR))=
* INTRASEASONAL HEAT STORAGE
$ifi %INDHEAT%==yes         SUM((INDHEAT_IGHSTO,INDHEAT_GROUP)$INDHEAT_IAGK_HASORPOT(Y,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_GROUP),INDHEAT_VHSTOLOADT.L(Y,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_IGHSTO,INDHEAT_GROUP,S,T));

*summed yearly values
** EXOGENOUS DEMAND

H_DEMAND_YCRA(Y,C,IR,IA,DHUSER,'EXOGENOUS','DISTRICT_HEAT','TWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND NOT INDUSTRY_AREAS(IA))=
         SUM((S,T),IHOURSINST(S,T)*IDH_T(Y,IA,DHUSER,S,T))/(1-DISLOSS_H(IA))/IOF1000000;


*** INDHEAT SECTOR
$ifi %INDHEAT%==yes H_DEMAND_YCRA(Y,C,IR,INDHEAT_IA,INDHEAT_DHUSER,'EXOGENOUS',SECTOR,'TWh')$(CCCRRR(C,IR) AND RRRAAA(IR,INDHEAT_IA) AND INDHEAT_AAAINSECTOR(INDHEAT_IA,SECTOR))=
$ifi %INDHEAT%==yes         SUM((S,T),(IHOURSINST(S,T)*INDHEAT_DH(Y,INDHEAT_IA,INDHEAT_DHUSER) * INDHEAT_DH_VAR_T(INDHEAT_IA,INDHEAT_DHUSER,S,T)) / (INDHEAT_IDH_SUMST(INDHEAT_IA,INDHEAT_DHUSER)$(INDHEAT_IDH_SUMST(INDHEAT_IA,INDHEAT_DHUSER) GT 0)+1$(INDHEAT_IDH_SUMST(INDHEAT_IA,INDHEAT_DHUSER) EQ 0)))/IOF1000000 ;

*** INDUSTRY SECTOR
***H_DEMAND_YCRA(Y,C,IR,IA,'PROCESS_HEAT','EXOGENOUS','INDUSTRY','TWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND INDUSTRY_AREAS(IA))=
***         SUM((S,T,DHUSER),IHOURSINST(S,T)*IDH_T(Y,IA,DHUSER,S,T))/(1-DISLOSS_H(IA))/IOF1000000;

** ENDOGENOUS DEMAND
*** OTHER SECTOR
H_DEMAND_YCRA(Y,C,IR,IA,'DISTRICT_HEAT','ENDOGENOUS','STORAGE_LOSS','MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND NOT INDUSTRY_AREAS(IA))=
* INTRASEASONAL HEAT STORAGE LOSS
        SUM((S,T,IGHSTO)$IAGK_HASORPOT(Y,IA,IGHSTO),IHOURSINST(S,T)*(VHSTOLOADT.L(Y,IA,IGHSTO,S,T)-VGH_T.L(Y,IA,IGHSTO,S,T)))/IOF1000000;

H_DEMAND_YCRA(Y,C,IR,IA,'DISTRICT_HEAT','ENDOGENOUS','STORAGE_LOSS','MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND NOT INDUSTRY_AREAS(IA))=
* INTERSEASONAL HEAT STORAGE LOSS
        SUM((S,T,IGHSTOS)$IAGK_HASORPOT(Y,IA,IGHSTOS),IHOURSINST(S,T)*(VHSTOLOADTS.L(Y,IA,IGHSTOS,S,T)-VGH_T.L(Y,IA,IGHSTOS,S,T)))/IOF1000000;

*** INDHEAT SECTOR
$ifi %INDHEAT%==yes H_DEMAND_YCRA(Y,C,IR,INDHEAT_IA,'INDIVIDUAL_HEAT','ENDOGENOUS','STORAGE_LOSS','MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,INDHEAT_IA))=
* INTERSEASONAL HEAT STORAGE LOSS
$ifi %INDHEAT%==yes         SUM((INDHEAT_DHUSER,INDHEAT_IGHSTO,INDHEAT_GROUP,S,T)$INDHEAT_IAGK_HASORPOT(Y,INDHEAT_IA,INDHEAT_IGHSTO,INDHEAT_GROUP),IHOURSINST(S,T)*INDHEAT_VHSTOLOADT.L(Y,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_IGHSTO,INDHEAT_GROUP,S,T))/IOF1000000;
* ----------- END OF HEAT DEMAND --------------

* -------------------- END OF ENERGY DEMAND -----------------------


* -------------------- TRANSMISSION CAPACITY -----------------------

** EXOGENOUS ELECTRICITY CAPACITY

X_CAP_YCR(Y,C,IR,IRI,'EXOGENOUS','GW')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=
IOF0001*XKFX(Y,IR,IRI)
;


** ENDOGENOUS ELECTRIC CAPACITY

$ifi not %TransInvest%==yes $goto Transmission_investments_end
X_CAP_YCR(Y,C,IR,IRI,'ENDOGENOUS','GW')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=
IOF0001*VXKNACCUMNET.L(Y,IR,IRI)
;
$label  Transmission_investments_end


* -------------------- END OF TRANSMISSION CAPACITY -----------------------

* -------------------- TRANSMISSION FLOW -----------------------

** HOURLY TRANSMISSION FLOW

X_FLOW_YCRST(Y,C,IR,IRI,S,T,'MWh')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=
VX_T.L(Y,IR,IRI,S,T);
;


** ANNUAL TRANSMISSION FLOW
X_FLOW_YCR(Y,C,IR,IRI,'TWh')$(CCCRRR(C,IR) AND IXK_HASORPOT(Y,IR,IRI))=
SUM((S,T),IHOURSINST(S,T)*VX_T.L(Y,IR,IRI,S,T))/IOF1000000;
;



* -------------------- END OF TRANSMISSION FLOW -----------------------

$ifi NOT %HEATTRANS%==yes $goto no_heattrans
* -------------------- HEAT TRANSMISSION CAPACITY -----------------------

** EXOGENOUS HEAT TRANSMISSION CAPACITY

XH_CAP_YCA(Y,C,IA,IAI,'EXOGENOUS','GW')$(ICA(C,IA) AND IXHK_HASORPOT(Y,IA,IAI))=
IOF0001*XHKFX(Y,IA,IAI)
;


** ENDOGENOUS HEAT TRANSMISSION CAPACITY

$ifi not %HeatTransInvest%==yes $goto Heat_Transmission_investments_end
XH_CAP_YCA(Y,C,IA,IAI,'ENDOGENOUS','GW')$(ICA(C,IA) AND IXHK_HASORPOT(Y,IA,IAI))=
IOF0001*VXHKNACCUMNET.L(Y,IA,IAI)
;
$label  Heat_Transmission_investments_end


* -------------------- END OF HEAT TRANSMISSION CAPACITY -----------------------

* -------------------- HEAT TRANSMISSION FLOW -----------------------

** HOURLY HEAT TRANSMISSION FLOW

XH_FLOW_YCAST(Y,C,IA,IAI,S,T,'MWh')$(ICA(C,IA) AND IXHK_HASORPOT(Y,IA,IAI))=
VXH_T.L(Y,IA,IAI,S,T);
;

** ANNUAL HEAT TRANSMISSION FLOW
XH_FLOW_YCA(Y,C,IA,IAI,'TWh')$(ICA(C,IA) AND IXHK_HASORPOT(Y,IA,IAI))=
SUM((S,T),IHOURSINST(S,T)*VXH_T.L(Y,IA,IAI,S,T))/IOF1000000;
;

* -------------------- END OF HEAT TRANSMISSION FLOW -----------------------
$label no_heattrans

* ---------------------- ECONOMIC OUTPUT ----------------------

* ------------- COMMODITY PRICES --------------

** ELECTRICITY PRICES

*HOURLY ELECTRICITY PRICE

EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')$CCCRRR(C,IR)=OMONEY*QEEQ.M(Y,IR,S,T)
$ifi %RollingSeasons%==yes         /IHOURSINTROLLINGSEASON(S,T)
$ifi not %RollingSeasons%==yes     /IHOURSINST(S,T)
/IDISCOUNTFACTOR(Y)/IWEIGHTY(Y);

*AVERAGE ELECTRICITY PRICE

EL_PRICE_YCR(Y,C,IR,'AVERAGE','Money_per_MWh')$CCCRRR(C,IR)=
SUM((S,T),EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')/(IWEIGHSUMS*IWEIGHSUMT/(WEIGHT_S(S)*WEIGHT_T(T))));

$ontext
*EXCLUDED SO FAR
*AVERAGE ELECTRICITY PRICE WEIGTHED BY CONSUMPTION

EL_PRICE_YCR(Y,C,IR,'AVERAGE_WEIGHTED_BY_CONSUMPTION','Money_per_MWh')$CCCRRR(C,IR)=
               SUM((VARIABLE_CATEGORY,S,T),
                         EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')*EL_DEMAND_YCRST(Y,C,IR,S,T,VARIABLE_CATEGORY,'MWh')
                         )
               /SUM((VARIABLE_CATEGORY,S,T),EL_DEMAND_YCRST(Y,C,IR,S,T,VARIABLE_CATEGORY,'MWh'))
;

*AVERAGE ELECTRICITY PRICE WEIGTHED BY PRODUCTION

EL_PRICE_YCR(Y,C,IR,'AVERAGE_WEIGHTED_BY_PRODUCTION','Money_per_MWh')$CCCRRR(C,IR)=
               SUM((IGE,IA,FFF,S,T,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,IGE)),
                         EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')*PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY',TECH_TYPE,'MWh')
                         )
               /SUM((IGE,IA,FFF,S,T,TECH_TYPE)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(Y,IA,IGE)),PRO_YCRAGFST(Y,C,IR,IA,IGE,FFF,S,T,'ELECTRICITY',TECH_TYPE,'MWh'))
;
$offtext

** END OF ELECTRICITY PRICES

** DISTRICT HEATING PRICES

*HOURLY DISTRICT HEATING PRICE

DH_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND SUM(DHUSER, IDH_SUMST(IA,DHUSER)))=OMONEY*QHEQ.M(Y,IA,S,T)
$ifi %RollingSeasons%==yes         /IHOURSINTROLLINGSEASON(S,T)
$ifi not %RollingSeasons%==yes     /IHOURSINST(S,T)
/IDISCOUNTFACTOR(Y)/IWEIGHTY(Y);

*AVERAGE DISTRICT HEATING PRICE

DH_PRICE_YCRA(Y,C,IR,IA,'AVERAGE','Money_per_MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND SUM(DHUSER, IDH_SUMST(IA,DHUSER)))=
SUM((S,T),DH_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')/(IWEIGHSUMS*IWEIGHSUMT/(WEIGHT_S(S)*WEIGHT_T(T))));

$ontext
*EXCLUDED SO FAR
*AVERAGE DISTRICT HEATING PRICE WEIGTHED BY CONSUMPTION

DH_PRICE_YCRA(Y,C,IR,IA,'AVERAGE_WEIGHTED_BY_CONSUMPTION','Money_per_MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND SUM(DHUSER, IDH_SUMST(IA,DHUSER)))=
               SUM((VARIABLE_CATEGORY,S,T),
                         DH_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')*DH_DEMAND_YCRAST(Y,C,IR,IA,S,T,VARIABLE_CATEGORY,'MWh')
                         )
               /SUM((VARIABLE_CATEGORY,S,T),DH_DEMAND_YCRAST(Y,C,IR,IA,S,T,VARIABLE_CATEGORY,'MWh'))
;

*AVERAGE DISTRICT HEATING PRICE WEIGTHED BY PRODUCTION

DH_PRICE_YCRA(Y,C,IR,IA,'AVERAGE_WEIGHTED_BY_PRODUCTION','Money_per_MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,IA) AND SUM(DHUSER, IDH_SUMST(IA,DHUSER)))=
               SUM((IGH,FFF,S,T,TECH_TYPE)$IAGK_HASORPOT(Y,IA,IGH),
                         DH_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')*PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'DISTRICT_HEAT',TECH_TYPE,'MWh')
                         )
               /SUM((IGH,FFF,S,T,TECH_TYPE)$IAGK_HASORPOT(Y,IA,IGH),PRO_YCRAGFST(Y,C,IR,IA,IGH,FFF,S,T,'DISTRICT_HEAT',TECH_TYPE,'MWh'))
;
$offtext

** END OF DISTRICT HEATING PRICES

$ifi not %INDHEAT%==yes $goto noindividualheat
*HOURLY INDIVIDUAL HEATING PRICE
IDVH_PRICE_YCRAST(Y,C,IR,INDHEAT_IA,S,T,'Money_per_MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,INDHEAT_IA) AND SUM(INDHEAT_DHUSER, INDHEAT_IDH_SUMST(INDHEAT_IA,INDHEAT_DHUSER)))=OMONEY*SUM((INDHEAT_DHUSER,INDHEAT_GROUP),INDHEAT_QHEQDHUSER.M(Y,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_GROUP,S,T))
$ifi %RollingSeasons%==yes         /IHOURSINTROLLINGSEASON(S,T)
$ifi not %RollingSeasons%==yes     /IHOURSINST(S,T)
/IDISCOUNTFACTOR(Y)/IWEIGHTY(Y);

*AVERAGE INDIVIDUAL HEATING PRICE

IDVH_PRICE_YCRA(Y,C,IR,INDHEAT_IA,'AVERAGE','Money_per_MWh')$(CCCRRR(C,IR) AND RRRAAA(IR,INDHEAT_IA) AND SUM(INDHEAT_DHUSER, INDHEAT_IDH_SUMST(INDHEAT_IA,INDHEAT_DHUSER)))=
SUM((S,T),IDVH_PRICE_YCRAST(Y,C,IR,INDHEAT_IA,S,T,'Money_per_MWh')/(IWEIGHSUMS*IWEIGHSUMT/(WEIGHT_S(S)*WEIGHT_T(T))));


**END OF HOURLY INDIVIDUAL HEATING PRICE
$label noindividualheat
* ------------- END OF COMMODITY PRICES --------------


* ------------- COSTS --------------

* -- GENERATION COSTS -----

** GENERATION INVESTMENTS

ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,TECH_CATEGORY,'COSTS','GENERATION_CAPITAL_COSTS','POWER_DH','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
         OMONEY*
         (VGKN.L(Y,IA,G)*GINVCOST(IA,G)*ANNUITYCG(C,G)*SUM(IYALIAS2$(ORD(IYALIAS2) EQ ORD(Y)),IYHASANNUITYG(Y,IYALIAS2,G))
*investments in previous years
         + SUM(IYALIAS2$(ORD(IYALIAS2) LT ORD(Y)), VGKN.L(IYALIAS2,IA,G)*GINVCOST(IA,G)*ANNUITYCG(C,G)*IYHASANNUITYG(IYALIAS2,Y,G))
         );

$ifi %INDHEAT%==yes ECO_G_YCRAG(Y,C,IR,INDHEAT_IA,INDHEAT_G,FFF,TECH_TYPE,INDHEAT_GROUP,'COSTS','GENERATION_CAPITAL_COSTS','INDIVIDUAL_HEAT','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,INDHEAT_IA) AND INDHEAT_IGF(INDHEAT_G,FFF) AND INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE))=
$ifi %INDHEAT%==yes          OMONEY*(
$ifi %INDHEAT%==yes          INDHEAT_VGKN.L(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)*INDHEAT_GDATA(INDHEAT_G,'GDINVCOST0')*INDHEAT_ANNUITYCG(C,INDHEAT_G)*SUM(IYALIAS2$(ORD(IYALIAS2) EQ ORD(Y)),INDHEAT_IYHASANNUITYG(Y,IYALIAS2,INDHEAT_G))
$ifi %INDHEAT%==yes *investments in previous years
$ifi %INDHEAT%==yes          + SUM(IYALIAS2$(ORD(IYALIAS2) LT ORD(Y)), INDHEAT_VGKN.L(IYALIAS2,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)*INDHEAT_GDATA(INDHEAT_G,'GDINVCOST0')*INDHEAT_ANNUITYCG(C,INDHEAT_G)*INDHEAT_IYHASANNUITYG(IYALIAS2,Y,INDHEAT_G))
$ifi %INDHEAT%==yes          );

** GENEARTION FIXED COSTS

ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,TECH_CATEGORY,'COSTS','GENERATION_FIXED_COSTS','POWER_DH','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) AND IAGK_HASORPOT(Y,IA,G) AND GTECH_TYPE(G,TECH_TYPE) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
         OMONEY/IOF1000*GOMFCOST(IA,G)*(
         GKFX(Y,IA,G)
         +VGKNACCUMNET.L(Y,IA,G)
$ifi %DECOM%==yes         -VDECOM_EXO_ACCUM.L(Y,IA,G)$IGDECOMEXOPOT(Y,IA,G)
         );

$ifi %INDHEAT%==yes ECO_G_YCRAG(Y,C,IR,INDHEAT_IA,INDHEAT_G,FFF,TECH_TYPE,INDHEAT_GROUP,'COSTS','GENERATION_FIXED_COSTS','INDIVIDUAL_HEAT','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,INDHEAT_IA) AND INDHEAT_IGF(INDHEAT_G,FFF) AND INDHEAT_IAGK_HASORPOT(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP) AND INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE))=
$ifi %INDHEAT%==yes         OMONEY/IOF1000*INDHEAT_GDATA(INDHEAT_G,'GDOMFCOST0')*(
$ifi %INDHEAT%==yes         INDHEAT_GKFX(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)
$ifi %INDHEAT%==yes         +INDHEAT_VGKNACCUMNET.L(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)  );
** GENERATION O&M COSTS

ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,TECH_CATEGORY,'COSTS','GENERATION_OPERATIONAL_COSTS','POWER_DH','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) and IAGK_HASORPOT(Y,IA,G) AND GTECH_TYPE(G,TECH_TYPE) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
   OMONEY/IOF1000000*
     ((GOMVCOSTO(IA,G)*
      (
   +   SUM((S,T), IHOURSINST(S,T) * VGE_T.L(Y,IA,G,S,T))$IAGK_HASORPOT(Y,IA,G)))$IGKE(G)

   +  (GOMVCOSTO(IA,G) * GDATA(G,'GDCV') *
      (
   +   SUM((S,T), IHOURSINST(S,T) * VGH_T.L(Y,IA,G,S,T))$IAGK_HASORPOT(Y,IA,G)))$IGKH(G)

   +  (GOMVCOSTIN(IA,G)*
      (
$ifi %VGF%==ST   +   SUM((S,T), IHOURSINST(S,T) * VGF_T.L(Y,IA,G,S,T))$IAGK_HASORPOT(Y,IA,G)))
$ifi %VGF%==Y   +   VGF_T.L(Y,IA,G)$IAGK_HASORPOT(Y,IA,G)))
     ) ;

$ifi %INDHEAT%==yes ECO_G_YCRAG(Y,C,IR,INDHEAT_IA,INDHEAT_G,FFF,TECH_TYPE,INDHEAT_GROUP,'COSTS','GENERATION_OPERATIONAL_COSTS','INDIVIDUAL_HEAT','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,INDHEAT_IA) AND INDHEAT_IGF(INDHEAT_G,FFF) and INDHEAT_IAGK_HASORPOT(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP) AND INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE))=
$ifi %INDHEAT%==yes     OMONEY/IOF1000000*INDHEAT_GDATA(INDHEAT_G,'GDOMVCOST0')* SUM((INDHEAT_DHUSER,S,T), IHOURSINST(S,T)* INDHEAT_VGH_T.L(Y,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_G,INDHEAT_GROUP,S,T)
$ifi %INDHEAT%==yes $ifi not %COMBTECH%==yes $goto endcombtechOMVCOST
$ifi %INDHEAT%==yes        +SUM(INDHEAT_GALIAS$(INDHEAT_GGCOMB(INDHEAT_G,INDHEAT_GALIAS)),
$ifi %INDHEAT%==yes $ifi not %HYDROGEN1%==yes $goto endhydrogenQOBJ1OMVCOST
$ifi %INDHEAT%==yes         (1+INDHEAT_HYDROGEN_H2TOF(INDHEAT_GALIAS)$(INDHEAT_GDATA(INDHEAT_GALIAS,'GDFUEL') EQ THERMGAS_H2 OR INDHEAT_GDATA(INDHEAT_GALIAS,'GDFUEL') EQ BIOGAS_H2))*
$label endhydrogenQOBJ1OMVCOST
$ifi %INDHEAT%==yes          INDHEAT_VGH_T.L(Y,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_GALIAS,INDHEAT_GROUP,IS3,T) )
$label endcombtechOMVCOST
$ifi %INDHEAT%==yes         );

** GENERATION FUEL COSTS

ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,TECH_CATEGORY,'COSTS','GENERATION_FUEL_COSTS','POWER_DH','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) and IAGK_HASORPOT(Y,IA,G) AND GTECH_TYPE(G,TECH_TYPE) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
   OMONEY*IOF3P6/IOF1000000*(
                 FUELPRICE(Y,IA,FFF)
$ifi not %STEPWISEPRICE%==yes $goto No_STEPWISEPRICE
                 + (SUM((STEPWISEPRICE_L,STEPWISEPRICE_FFF(FFF))$ISTEPWISEPRICE_F(Y,C,STEPWISEPRICE_FFF),
                 STEPWISEPRICE_FUELPRICE_STEP(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF)*VSTEPWISEPRICE_VGF_T_STEP.L(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF))
                 /SUM((STEPWISEPRICE_L,STEPWISEPRICE_FFF(FFF)),VSTEPWISEPRICE_VGF_T_STEP.L(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF)))$(SUM(STEPWISEPRICE_FFF(FFF),ISTEPWISEPRICE_F(Y,C,STEPWISEPRICE_FFF)) AND SUM((STEPWISEPRICE_L,STEPWISEPRICE_FFF(FFF)),VSTEPWISEPRICE_VGF_T_STEP.L(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF)))
$label No_STEPWISEPRICE
                 )*
   (
$ifi %VGF%==ST                 SUM((S,T), IHOURSINST(S,T) * VGF_T.L(Y,IA,G,S,T) )$IAGK_HASORPOT(Y,IA,G)
$ifi %VGF%==Y                  VGF_T.L(Y,IA,G)$IAGK_HASORPOT(Y,IA,G)
    )$(IGNOTETOH(G) and (not IGESTO(G)) and (not IGHSTO(G)) and (not IGESTOS(G)) and (not IGHSTOS(G)))


* Electricity storages:
    +OMONEY/IOF1000000*SUM((S,T), EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')*VESTOLOADT.L(Y,IA,G,S,T))$IGESTO(G)
    +OMONEY/IOF1000000*SUM((S,T), EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')*VESTOLOADTS.L(Y,IA,G,S,T))$IGESTOS(G)
* Heat storages:
    +OMONEY/IOF1000000*SUM((S,T), DH_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')*VHSTOLOADT.L(Y,IA,G,S,T))$IGHSTO(G)
    +OMONEY/IOF1000000*SUM((S,T), DH_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh')*VHSTOLOADTS.L(Y,IA,G,S,T))$IGHSTOS(G)
* Electricity to heat:
    +OMONEY/IOF1000000*SUM((S,T), EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')*VGE_T.L(Y,IA,G,S,T))$(IAGK_HASORPOT(Y,IA,G) and IGETOH(G))
;

$ifi %INDHEAT%==yes ECO_G_YCRAG(Y,C,IR,INDHEAT_IA,INDHEAT_G,FFF,TECH_TYPE,INDHEAT_GROUP,'COSTS','GENERATION_FUEL_COSTS','INDIVIDUAL_HEAT','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,INDHEAT_IA) AND INDHEAT_IGF(INDHEAT_G,FFF) and INDHEAT_IAGK_HASORPOT(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP) AND INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE))=
$ifi %INDHEAT%==yes    OMONEY*IOF3P6/IOF1000000*(  FUELPRICE(Y,INDHEAT_IA,FFF)
$ifi %INDHEAT%==yes $ifi not %STEPWISEPRICE%==yes $goto No_STEPWISEPRICE_FCOST
$ifi %INDHEAT%==yes                  + (SUM((STEPWISEPRICE_L,STEPWISEPRICE_FFF(FFF))$ISTEPWISEPRICE_F(Y,C,STEPWISEPRICE_FFF),
$ifi %INDHEAT%==yes                  STEPWISEPRICE_FUELPRICE_STEP(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF)*VSTEPWISEPRICE_VGF_T_STEP.L(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF))
$ifi %INDHEAT%==yes                  /SUM((STEPWISEPRICE_L,STEPWISEPRICE_FFF(FFF)),VSTEPWISEPRICE_VGF_T_STEP.L(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF)))$(SUM(STEPWISEPRICE_FFF(FFF),ISTEPWISEPRICE_F(Y,C,STEPWISEPRICE_FFF)) AND SUM((STEPWISEPRICE_L,STEPWISEPRICE_FFF(FFF)),VSTEPWISEPRICE_VGF_T_STEP.L(Y,C,STEPWISEPRICE_L,STEPWISEPRICE_FFF)))
$label No_STEPWISEPRICE_FCOST
$ifi %INDHEAT%==yes                  )*
$ifi %INDHEAT%==yes    (
$ifi %INDHEAT%==yes $ifi %VGF%==ST                 SUM((S,T), IHOURSINST(S,T) * INDHEAT_VGF_T.L(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP,S,T) )$INDHEAT_IAGK_HASORPOT(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)
$ifi %INDHEAT%==yes $ifi %VGF%==Y                  INDHEAT_VGF_T.L(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)$INDHEAT_IAGK_HASORPOT(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)
$ifi %INDHEAT%==yes     )$((not INDHEAT_IGETOH(INDHEAT_G)) and (not INDHEAT_IGHSTO(INDHEAT_G)))
* Heat storages:
$ifi %INDHEAT%==yes     +OMONEY/IOF1000000*SUM((S,T,INDHEAT_DHUSER), IDVH_PRICE_YCRAST(Y,C,IR,INDHEAT_IA,S,T,'Money_per_MWh')*INDHEAT_VHSTOLOADT.L(Y,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_G,INDHEAT_GROUP,S,T))$INDHEAT_IGHSTO(INDHEAT_G)
* Electricity to heat:
$ifi %INDHEAT%==yes     +OMONEY/IOF1000000*SUM((S,T,INDHEAT_DHUSER),EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh')*(INDHEAT_VGH_T.L(Y,INDHEAT_IA,INDHEAT_DHUSER,INDHEAT_G,INDHEAT_GROUP,S,T)/(INDHEAT_GDATA(INDHEAT_G,'GDFE')$(INDHEAT_IGETOH_NOTHP(INDHEAT_G))+INDHEAT_COP(INDHEAT_IA,INDHEAT_G,S,T)$(INDHEAT_IGHP(INDHEAT_G))) ))$(INDHEAT_IAGK_HASORPOT(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP) and INDHEAT_IGETOH(INDHEAT_G));

** Hydro with storage seasonal price profile

ECO_G_YCRAG(Y,C,IR,IA,IGHYRS,FFF,TECH_TYPE,TECH_CATEGORY,'COSTS','GENERATION_HYDRO','POWER_DH','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(IGHYRS,FFF) and IAGK_HASORPOT(Y,IA,IGHYRS) AND GTECH_TYPE(IGHYRS,TECH_TYPE) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
   OMONEY/IOF1000000*
    SUM((S,T), HYPPROFILS(IA,S)* IHOURSINST(S,T)* VGE_T.L(Y,IA,IGHYRS,S,T))$IAGK_HASORPOT(Y,IA,IGHYRS);

** CO2 EMISSIONS COST
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,TECH_CATEGORY,'COSTS','GENERATION_CO2_TAX','POWER_DH','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) and IAGK_HASORPOT(Y,IA,G) AND GTECH_TYPE(G,TECH_TYPE) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
$ifi %VGF%==ST         OMONEY/IOF1000000*IM_CO2(G)*IOF0001 * IOF3P6 *M_POL(Y,"TAX_CO2",C) * SUM((S,T), IHOURSINST(S,T)*VGF_T.L(Y,IA,G,S,T))$IAGK_HASORPOT(Y,IA,G);
$ifi %VGF%==Y          OMONEY/IOF1000000*IM_CO2(G)*IOF0001 * IOF3P6 *M_POL(Y,"TAX_CO2",C) * VGF_T.L(Y,IA,G)$IAGK_HASORPOT(Y,IA,G);

$ifi %INDHEAT%==yes ECO_G_YCRAG(Y,C,IR,INDHEAT_IA,INDHEAT_G,FFF,TECH_TYPE,INDHEAT_GROUP,'COSTS','GENERATION_CO2_TAX','INDIVIDUAL_HEAT','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,INDHEAT_IA) AND INDHEAT_IGF(INDHEAT_G,FFF) and INDHEAT_IAGK_HASORPOT(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP) AND INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE))=
$ifi %INDHEAT%==yes $ifi %VGF%==ST         OMONEY/IOF1000000*FDATA(FFF,'FDCO2')*IOF0001 * IOF3P6 *M_POL(Y,"TAX_CO2",C) * SUM((S,T), IHOURSINST(S,T)*INDHEAT_VGF_T.L(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP,S,T))$INDHEAT_IAGK_HASORPOT(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP);
$ifi %INDHEAT%==yes $ifi %VGF%==Y          OMONEY/IOF1000000*FDATA(FFF,'FDCO2')*IOF0001 * IOF3P6 *M_POL(Y,"TAX_CO2",C) * INDHEAT_VGF_T.L(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)$INDHEAT_IAGK_HASORPOT(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP);

** OTHER EMISSIONS COST
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,TECH_CATEGORY,'COSTS','GENERATION_OTHER_EMI_TAX','POWER_DH','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(G,FFF) AND GTECH_TYPE(G,TECH_TYPE) and IAGK_HASORPOT(Y,IA,G) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
         OMONEY/IOF1000000*(
$ifi %VGF%==ST          SUM((S,T), IHOURSINST(S,T) * (FDATA(FFF,'FDSO2')*IOF0001) * IOF3P6 * VGF_T.L(Y,IA,G,S,T))*M_POL(Y,"TAX_SO2",C)
$ifi %VGF%==ST          + SUM((S,T), IHOURSINST(S,T) * (GDATA(G,'GDNOX')*IOF0000001) * IOF3P6 * VGF_T.L(Y,IA,G,S,T))* M_POL(Y,"TAX_NOX",C)
$ifi %VGF%==Y           (FDATA(FFF,'FDSO2')*IOF0001) * IOF3P6 * VGF_T.L(Y,IA,G)*M_POL(Y,"TAX_SO2",C)
$ifi %VGF%==Y          +  (GDATA(G,'GDNOX')*IOF0000001) * IOF3P6 * VGF_T.L(Y,IA,G)* M_POL(Y,"TAX_NOX",C)
          );

$ifi %INDHEAT%==yes ECO_G_YCRAG(Y,C,IR,INDHEAT_IA,INDHEAT_G,FFF,TECH_TYPE,INDHEAT_GROUP,'COSTS','GENERATION_OTHER_EMI_TAX','INDIVIDUAL_HEAT','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,INDHEAT_IA) AND INDHEAT_IGF(INDHEAT_G,FFF) AND INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE) and INDHEAT_IAGK_HASORPOT(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP))=
$ifi %INDHEAT%==yes          OMONEY/IOF1000000*(
$ifi %INDHEAT%==yes $ifi %VGF%==ST          SUM((S,T), IHOURSINST(S,T) * (FDATA(FFF,'FDSO2')*IOF0001) * IOF3P6 * INDHEAT_VGF_T.L(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP,S,T))*M_POL(Y,"TAX_SO2",C)
$ifi %INDHEAT%==yes $ifi %VGF%==ST          + SUM((S,T), IHOURSINST(S,T) * (INDHEAT_GDATA(INDHEAT_G,'GDNOX')*IOF0000001) * IOF3P6 * INDHEAT_VGF_T.L(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP,S,T))* M_POL(Y,"TAX_NOX",C)
$ifi %INDHEAT%==yes $ifi %VGF%==Y           (FDATA(FFF,'FDSO2')*IOF0001) * IOF3P6 * INDHEAT_VGF_T.L(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)*M_POL(Y,"TAX_SO2",C)
$ifi %INDHEAT%==yes $ifi %VGF%==Y          +  (INDHEAT_GDATA(INDHEAT_G,'GDNOX')*IOF0000001) * IOF3P6 * INDHEAT_VGF_T.L(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)* M_POL(Y,"TAX_NOX",C)
$ifi %INDHEAT%==yes           );

** UNIT COMMITMENT COST
$ifi not %UnitComm%==yes $goto No_UC
ECO_G_YCRAG(Y,C,IR,IA,IGUC,FFF,TECH_TYPE,TECH_CATEGORY,'COSTS','GENERATION_UC_COSTS','POWER_DH','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(IGUC,FFF) AND GTECH_TYPE(IGUC,TECH_TYPE) and IAGK_HASORPOT(Y,IA,IGUC) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
OMONEY/IOF1000000*SUM((S,T), IHOURSINST(S,T)*(
                                                 VUCU.L(Y,IA,IGUC,S,T)*GDATA(IGUC,'GDUCUCOST')*GDATA(IGUC,'GDUCUNITSIZE')/CHRONOHOUR(S,T)  !! startup cost
                                                 +VUCD.L(Y,IA,IGUC,S,T)*GDATA(IGUC,'GDUCDCOST')*GDATA(IGUC,'GDUCUNITSIZE')/CHRONOHOUR(S,T)  !! shutdown cost
                                                 +VUCON.L(Y,IA,IGUC,S,T)*GDATA(IGUC,'GDUCCOST0')*GDATA(IGUC,'GDUCUNITSIZE') !! fixed hourly cost
$ifi %STEPWISE_RAMPING_UC%==ramp_const_cost      +SUM(RAMPING_RATE,(VRAMPING_DOWN.L(Y,IA,IGUC,RAMPING_RATE,S,T)*IRAMPING_G_COSTS_DOWN(IGUC,RAMPING_RATE)+VRAMPING_UP.L(Y,IA,IGUC,RAMPING_RATE,S,T)*IRAMPING_G_COSTS_UP(IGUC,RAMPING_RATE))/CHRONOHOUR(S,T))  !! ramping cost
))
;
$label NO_UC


*TAXES
$ifi NOT %TAXES%==yes  $goto No_TAXES
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,TECH_CATEGORY,'COSTS','GENERATION_TAXES','POWER_DH','Mmoney')$(CCCRRR(C,IR) AND IGF(G,FFF) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND GTECH_TYPE(G,TECH_TYPE) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
OMONEY/IOF1000000*(
* Tax(+)/subsidy(-) on fuel use for electricity generation
    + SUM((IS3,T),
         IHOURSINST(IS3,T) * TAX_F_EL(Y,C,G)  * VGE_T.L(Y,IA,G,IS3,T)/ (GDATA(G,'GDFE') * (1$(NOT GEFFRATE(IA,G))+GEFFRATE(IA,G))))
* Tax(+)/subsidy(-) on fuel use for heat generation
    + SUM((IS3,T),
         IHOURSINST(IS3,T) * TAX_F_HEAT(Y,C,G) * GDATA(G,'GDCV') * VGH_T.L(Y,IA,G,IS3,T)/ (GDATA(G,'GDFE') * (1$(NOT GEFFRATE(IA,G))+GEFFRATE(IA,G))))
* Tax(+)/subsidy(-) on fuel use
$ifi %VGF%==ST     + SUM((IS3,T),IHOURSINST(IS3,T) * TAX_F(Y,C,G)  * VGF_T.L(Y,IA,G,IS3,T))
$ifi %VGF%==Y      + TAX_F(Y,C,G)  * VGF_T.L(Y,IA,G)
* Tax(+)/subsidy(-) on heat generation
    + SUM((IS3,T),
         IHOURSINST(IS3,T) * TAX_G_HEAT(Y,C,G)  * VGH_T.L(Y,IA,G,IS3,T))
* Tax(+)/subsidy(-) on electricity generation
    + SUM((IS3,T),
         IHOURSINST(IS3,T) * TAX_G_EL(Y,C,G)  * VGE_T.L(Y,IA,G,IS3,T))
)
;

$ifi %INDHEAT%==yes ECO_G_YCRAG(Y,C,IR,INDHEAT_IA,INDHEAT_G,FFF,TECH_TYPE,INDHEAT_GROUP,'COSTS','GENERATION_TAXES','INDIVIDUAL_HEAT','Mmoney')$(CCCRRR(C,IR) AND INDHEAT_IGF(INDHEAT_G,FFF) and RRRAAA(IR,INDHEAT_IA) and INDHEAT_IAGK_HASORPOT(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP) AND INDHEAT_GTECH_TYPE(INDHEAT_G,TECH_TYPE))=
* Tax(+)/subsidy(-) on fuel use
$ifi %INDHEAT%==yes                    OMONEY/IOF1000000*(
$ifi %INDHEAT%==yes $ifi %VGF%==ST     + SUM((IS3,T),IHOURSINST(IS3,T) * INDHEAT_TAX_F(Y,INDHEAT_IA,INDHEAT_G)  * INDHEAT_VGF_T.L(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP,IS3,T)));
$ifi %INDHEAT%==yes $ifi %VGF%==Y      + INDHEAT_TAX_F(Y,INDHEAT_IA,INDHEAT_G)  * INDHEAT_VGF_T.L(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP));

$label NO_TAXES

*GRID TARIFFS
$ifi NOT %GRIDTARIFFS%==yes  $goto No_GRIDTARIFFS
ECO_G_YCRAG(Y,C,IR,IA,IGETOH,FFF,TECH_TYPE,TECH_CATEGORY,'COSTS','GENERATION_GRID_TARIFFS','POWER_DH','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) AND IGF(IGETOH,FFF) and IAGK_HASORPOT(Y,IA,IGETOH) AND GTECH_TYPE(IGETOH,TECH_TYPE) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
OMONEY/IOF1000000*(
* Fixed component and subscribed power (per MW installed)
                 + IOF1000 *
                         IGR_PRICE_TECH(Y,IR,'GRDSUBS')  * (VGKNACCUMNET.L(Y,IA,IGETOH)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM.L(Y,IA,IGETOH)$IGDECOMEXOPOT(Y,IA,IGETOH)
                          + GKFX(Y,IA,IGETOH)) /(GDATA(IGETOH,'GDFE') *(1$(NOT GEFFRATE(IA,IGETOH))+GEFFRATE(IA,IGETOH)))
* Energy charge winter
                 + SUM((IS3,T)$IGR_TIME('GRDECW',IR,IS3,T), IHOURSINST(IS3,T) * IGR_PRICE_TECH(Y,IR,'GRDECW')  * VGE_T.L(Y,IA,IGETOH,IS3,T))
* Energy charge summer
                 + SUM((IS3,T)$IGR_TIME('GRDECS',IR,IS3,T), IHOURSINST(IS3,T) * IGR_PRICE_TECH(Y,IR,'GRDECS')  * VGE_T.L(Y,IA,IGETOH,IS3,T))
* Energy charge TOU step 1
                 + SUM((IS3,T)$IGR_TIME('GRDTOU1',IR,IS3,T), IHOURSINST(IS3,T) * IGR_PRICE_TECH(Y,IR,'GRDTOU1')  * VGE_T.L(Y,IA,IGETOH,IS3,T))
* Energy charge TOU step 2
                 + SUM((IS3,T)$IGR_TIME('GRDTOU2',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_TECH(Y,IR,'GRDTOU2')  * VGE_T.L(Y,IA,IGETOH,IS3,T))
* Energy charge TOU step 3
                 + SUM((IS3,T)$IGR_TIME('GRDTOU3',IR,IS3,T), IHOURSINST(IS3,T) * IGR_PRICE_TECH(Y,IR,'GRDTOU3')  * VGE_T.L(Y,IA,IGETOH,IS3,T))
* Energy charge TOU step 4
                 + SUM((IS3,T)$IGR_TIME('GRDTOU4',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_TECH(Y,IR,'GRDTOU4')  * VGE_T.L(Y,IA,IGETOH,IS3,T))
)
;
$label NO_GRIDTARIFFS

* -- TRANSMISSION COSTS -----
*ELECTRICITY
** TRANSMISSION INVESTMENTS COSTS
ECO_X_YCR(Y,C,IR,'COSTS','TRANSMISSION_CAPITAL_COSTS','Mmoney')$CCCRRR(C,IR)=
          OMONEY/IOF1000000*(
             SUM(IYALIAS2$(ORD(IYALIAS2) EQ ORD(Y)),IYHASANNUITYX(IYALIAS2,Y))*(
*EXPORTING REGION
                 SUM(IRI$IXK_HASORPOT(Y,IR,IRI), IOF05*VXKN.L(Y,IR,IRI)*XINVCOST(Y,IR,IRI)*IOF05*ANNUITYCX(C))
*IMPORTING REGION
                 + SUM(IRE$IXK_HASORPOT(Y,IRE,IR), IOF05*VXKN.L(Y,IRE,IR)*XINVCOST(Y,IRE,IR)*IOF05*ANNUITYCX(C))
             )
*investments in previous years
             + SUM(IYALIAS2$(ORD(IYALIAS2) EQ ORD(Y)),IYHASANNUITYX(IYALIAS2,Y)*(
*EXPORTING REGION
                 SUM(IRI$IXK_HASORPOT(IYALIAS2,IR,IRI),IOF05*VXKN.L(IYALIAS2,IR,IRI)*XINVCOST(IYALIAS2,IR,IRI)*IOF05*ANNUITYCX(C))
*IMPORTING REGION
                 + SUM(IRE$IXK_HASORPOT(IYALIAS2,IRE,IR), IOF05*VXKN.L(IYALIAS2,IRE,IR)*XINVCOST(IYALIAS2,IRE,IR)*IOF05*ANNUITYCX(C))
            ))
                                );

** TRANSMISSION FLOW COSTS

ECO_X_YCR(Y,C,IR,'COSTS','TRANSMISSION_OPERATIONAL_COSTS','Mmoney')$CCCRRR(C,IR)=
OMONEY*IOF0000001*SUM(IRI$IXK_HASORPOT(Y,IR,IRI), SUM((S,T), IHOURSINST(S,T) * (VX_T.L(Y,IR,IRI,S,T) * XCOST(IR,IRI))));

** TRANSMISSION TRADE COST
ECO_X_YCR(Y,C,IR,'COSTS','TRANSMISSION_TRADE_COSTS','Mmoney')$CCCRRR(C,IR)=
OMONEY*IOF0000001*SUM(IRI$IXK_HASORPOT(Y,IRI,IR), SUM((S,T), IHOURSINST(S,T) * (VX_T.L(Y,IRI,IR,S,T) * EL_PRICE_YCRST(Y,C,IRI,S,T,'Money_per_MWh'))));

** TRANSMISSION TRADE INCOME
ECO_X_YCR(Y,C,IR,'INCOME','TRANSMISSION_TRADE_INCOME','Mmoney')$CCCRRR(C,IR)=
OMONEY*IOF0000001*SUM(IRI$IXK_HASORPOT(Y,IR,IRI), SUM((S,T), IHOURSINST(S,T) * (VX_T.L(Y,IR,IRI,S,T) * EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh'))));


* ------------- END OF COSTS --------------

* ------------- INCOME --------------

$ifi not %SUBSIDIES%==yes $goto endsubsidies
** SUBSIDIES
ECO_G_YCRAG(Y,C,IR,IA,G,TECH_TYPE,TECH_CATEGORY,'INCOME','GENERATION_SUBSIDIES','Mmoney','POWER_DH')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,G) AND GTECH_TYPE(G,TECH_TYPE) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
OMONEY/IOF1000000*(
* Subsidy on fuel use for electricity generation
    - SUM((IS3,T), SUM(IAGK_HASORPOT(Y,IA,G)$(RRRAAA(IR,IA) AND IGE(G) AND SUBSIDIES_F_EL(Y,C,G)),
         IHOURSINST(IS3,T) * SUBSIDIES_F_EL(Y,C,G)  * VGE_T.l(Y,IA,G,IS3,T)/ (GDATA(G,'GDFE') * GEFFRATE(IA,G))))
* Subsidy on fuel use for heat generation
    - SUM((IS3,T), SUM(IAGK_HASORPOT(Y,IA,G)$(RRRAAA(IR,IA) AND IGH(G) AND SUBSIDIES_F_HEAT(Y,C,G)),
         IHOURSINST(IS3,T) * SUBSIDIES_F_HEAT(Y,C,G) * GDATA(G,'GDCV') * VGH_T.l(Y,IA,G,IS3,T)/ (GDATA(G,'GDFE') * GEFFRATE(IA,G))))
* Subsidy on fuel use
$ifi %VGF%==ST     - SUM((IS3,T), SUM(IAGK_HASORPOT(Y,IA,G)$(RRRAAA(IR,IA) AND SUBSIDIES_F(Y,C,G)), IHOURSINST(IS3,T) * SUBSIDIES_F(Y,C,G)  * VGF_T.l(Y,IA,G,IS3,T)))
$ifi %VGF%==Y      - SUM(IAGK_HASORPOT(Y,IA,G)$(RRRAAA(IR,IA) AND SUBSIDIES_F(Y,C,G)), SUBSIDIES_F(Y,C,G)  * VGF_T.l(Y,IA,G))
* Subsidy on heat generation
    - SUM((IS3,T), SUM(IAGK_HASORPOT(Y,IA,G)$(RRRAAA(IR,IA) AND SUBSIDIES_G_HEAT(Y,C,G)),
         IHOURSINST(IS3,T) * SUBSIDIES_G_HEAT(Y,C,G)  * VGH_T.l(Y,IA,G,IS3,T)))
* Subsidy on electricity generation
    - SUM((IS3,T), SUM(IAGK_HASORPOT(Y,IA,G)$(RRRAAA(IR,IA) AND SUBSIDIES_G_EL(Y,C,G)),
         IHOURSINST(IS3,T) * SUBSIDIES_G_EL(Y,C,G)  * VGE_T.l(Y,IA,G,IS3,T)))
* Subsidy on electricity consumption
    - SUM((DEUSER), SUBSIDIES_DE(C,DEUSER) * DE(Y,IR,DEUSER))
* Subsidy on heat consumption
    - SUM((DHUSER), SUBSIDIES_DH(C,DHUSER) * DH(Y,IA,DHUSER))
);
$label endsubsidies

* ------------- END OF INCOME --------------
* ------------- OBJECTIVE FUNCTION --------------

OBJ_YCR(Y,C,IR,SUBCATEGORY,'POWER_DH','Mmoney')$CCCRRR(C,IR)=

*GENERATION COSTS
SUM((IA,G,FFF,TECH_TYPE,TECH_CATEGORY)$(RRRAAA(IR,IA)AND IAGK_HASORPOT(Y,IA,G)),ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,TECH_CATEGORY,'COSTS',SUBCATEGORY,'POWER_DH','Mmoney'))
*TRANSMISSION COSTS
+ ECO_X_YCR(Y,C,IR,'COSTS',SUBCATEGORY,'Mmoney');

*INDIVIDUAL HEAT
$ifi %INDHEAT%==yes OBJ_YCR(Y,C,IR,SUBCATEGORY,'INDIVIDUAL_HEAT','Mmoney')$CCCRRR(C,IR)=
$ifi %INDHEAT%==yes SUM((INDHEAT_IA,INDHEAT_G,FFF,TECH_TYPE,INDHEAT_GROUP)$(RRRAAA(IR,INDHEAT_IA)AND INDHEAT_IAGK_HASORPOT(Y,INDHEAT_IA,INDHEAT_G,INDHEAT_GROUP)),ECO_G_YCRAG(Y,C,IR,INDHEAT_IA,INDHEAT_G,FFF,TECH_TYPE,INDHEAT_GROUP,'COSTS',SUBCATEGORY,'INDIVIDUAL_HEAT','Mmoney'));

$ifi NOT %TAXES%==yes  $goto No_TAXES2
OBJ_YCR(Y,C,IR,'TAXES','POWER_DH','Mmoney')$CCCRRR(C,IR)=
OMONEY/IOF1000000*(

* Tax on electricity consumption
    + SUM(DEUSER, TAX_DE(C,DEUSER) * SUM((IS3,T), IHOURSINST(IS3,T) * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0))  )

$ifi %DFLEXQUANT%==yes           + SUM((DEUSER), TAX_DE(C,DEUSER) *(
$ifi %DFLEXQUANT%==yes             SUM(DEF_U1,VDEF_T.L(Y,IR,DEUSER,IS3,T,DEF_U1) ) - SUM(DEF_D1,VDEF_T.L(Y,IR,DEUSER,IS3,T,DEF_D1) )
$ifi %DFLEXQUANT%==yes           + SUM(DEF_U2,VDEF_T.L(Y,IR,DEUSER,IS3,T,DEF_U2) ) - SUM(DEF_D2,VDEF_T.L(Y,IR,DEUSER,IS3,T,DEF_D2) )
$ifi %DFLEXQUANT%==yes           + SUM(DEF_U3,VDEF_T.L(Y,IR,DEUSER,IS3,T,DEF_U3) ) - SUM(DEF_D3,VDEF_T.L(Y,IR,DEUSER,IS3,T,DEF_D3) ))


+SUM(IA$RRRAAA(IR,IA),
* Tax on heat consumption
    + SUM(DHUSER, TAX_DH(C,DHUSER) * DH(Y,IA,DHUSER))

$ifi %DFLEXQUANT%==yes      + SUM((DHUSER), TAX_DH(C,DHUSER) * (
$ifi %DFLEXQUANT%==yes          SUM(DHF_U1,VDHF_T.L(Y,IA,DHUSER,IS3,T,DHF_U1) ) - SUM(DHF_D1,VDHF_T.L(Y,IA,DHUSER,IS3,T,DHF_D1) )
$ifi %DFLEXQUANT%==yes        + SUM(DHF_U2,VDHF_T.L(Y,IA,DHUSER,IS3,T,DHF_U2) ) - SUM(DHF_D2,VDHF_T.L(Y,IA,DHUSER,IS3,T,DHF_D2) )
$ifi %DFLEXQUANT%==yes        + SUM(DHF_U3,VDHF_T.L(Y,IA,DHUSER,IS3,T,DHF_U3) ) - SUM(DHF_D3,VDHF_T.L(Y,IA,DHUSER,IS3,T,DHF_D3) ) )
)
)
;
$label NO_TAXES2


*GRID TARIFFS2
$ifi NOT %GRIDTARIFFS%==yes  $goto No_GRIDTARIFFS2OBJ
OBJ_YCR(Y,C,IR,'GRID_TARIFFS','POWER_DH','Mmoney')$CCCRRR(C,IR)=
OMONEY/IOF1000000*(

* Demand charge winter
                 + IOF1000 * SUM((MMM)$(SUM((IS3,T)$IGR_TIME('GRDDCW',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), SUM(IA$(SUM(IGETOH,IAGK_HASORPOT(Y,IA,IGETOH)) AND RRRAAA(IR,IA)), VGETOH_CAP1.L(Y,IA,MMM)) *  IGR_PRICE_TECH(Y,IR,'GRDDCW'))/(IGR_MONTHSINSIM/card(MMM))    !! Demand charge winter
* Demand charge summer
                 + IOF1000 * SUM((MMM)$(SUM((IS3,T)$IGR_TIME('GRDDCS',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), SUM(IA$(SUM(IGETOH,IAGK_HASORPOT(Y,IA,IGETOH)) AND RRRAAA(IR,IA)), VGETOH_CAP1.L(Y,IA,MMM)) *  IGR_PRICE_TECH(Y,IR,'GRDDCS'))/(IGR_MONTHSINSIM/card(MMM))    !! Demand charge summer
* Demand charge 2 (e.g for spring and fall months)
                 + IOF1000 * SUM((MMM)$(SUM((IS3,T)$IGR_TIME('GRDDC2',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), SUM(IA$(SUM(IGETOH,IAGK_HASORPOT(Y,IA,IGETOH)) AND RRRAAA(IR,IA)), VGETOH_CAP1.L(Y,IA,MMM)) *  IGR_PRICE_TECH(Y,IR,'GRDDC2'))/(IGR_MONTHSINSIM/card(MMM))    !! Demand charge 2

* Demand charge TOU
                 + IOF1000 * SUM((MMM),   SUM(IA$(SUM(IGETOH,IAGK_HASORPOT(Y,IA,IGETOH)) AND RRRAAA(IR,IA)), VGETOH_CAP2.L(Y,IA,MMM)) *  IGR_PRICE_TECH(Y,IR,'GRDDCTOU'))/(IGR_MONTHSINSIM/card(MMM))
* Demand charge yearly
                 + IOF1000 *    SUM(IA$(SUM(IGETOH,IAGK_HASORPOT(Y,IA,IGETOH)) AND RRRAAA(IR,IA)), VGETOH_CAPY.L(Y,IA)) *  IGR_PRICE_TECH(Y,IR,'GRDDCY')

* Tariffs on user groups
                 + SUM((IS3,T,DEUSER)$IGR_TIME('GRDECW',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDECW')  * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0) )
                 + SUM((IS3,T,DEUSER)$IGR_TIME('GRDECS',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDECS')  * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0) )
                 + SUM((IS3,T,DEUSER)$IGR_TIME('GRDTOU1',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDTOU1') * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0) )
                 + SUM((IS3,T,DEUSER)$IGR_TIME('GRDTOU2',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDTOU2') * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0) )
                 + SUM((IS3,T,DEUSER)$IGR_TIME('GRDTOU3',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDTOU3') * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0) )
                 + SUM((IS3,T,DEUSER)$IGR_TIME('GRDTOU4',IR,IS3,T),  IHOURSINST(IS3,T) * IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDTOU4') * VDENET_T.L(Y,IR,DEUSER,IS3,T)$(IDE_SUMST(IR,DEUSER) GT 0) )

                 + IOF1000 * SUM((DEUSER,MMM)$(SUM((IS3,T)$IGR_TIME('GRDDCW',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), VDEUSER_CAP1.L(Y,IR,DEUSER,MMM) *  IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDDCW'))/(IGR_MONTHSINSIM/CARD(MMM))
                 + IOF1000 * SUM((DEUSER,MMM)$(SUM((IS3,T)$IGR_TIME('GRDDCS',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), VDEUSER_CAP1.L(Y,IR,DEUSER,MMM) *  IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDDCS'))/(IGR_MONTHSINSIM/CARD(MMM))
                 + IOF1000 * SUM((DEUSER,MMM)$(SUM((IS3,T)$IGR_TIME('GRDDC2',IR,IS3,T), ISSSTTTMMM(IS3,T,MMM))), VDEUSER_CAP1.L(Y,IR,DEUSER,MMM) *  IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDDC2'))/(IGR_MONTHSINSIM/CARD(MMM))
                 + IOF1000 * SUM((DEUSER,MMM),   VDEUSER_CAP2.L(Y,IR,DEUSER,MMM) *  IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDDCTOU'))/(IGR_MONTHSINSIM/card(MMM))
                 + IOF1000 * SUM((DEUSER),  VDEUSER_CAPY.L(Y,IR,DEUSER) *  IGR_PRICE_DEUSER(Y,IR,DEUSER,'GRDDCY'))

)
;
$label NO_GRIDTARIFFS2OBJ

*ADD OTHER COSTS(HYDROGEN,ETC)
* ------------- END OF OBJECTIVE FUNCTION --------------

* ------------- INCOME --------------

** ELECTRICITY SALES
ECO_G_YCRAG(Y,C,IR,IA,IGE,FFF,TECH_TYPE,TECH_CATEGORY,'INCOME','ELECTRICITY_SALE','POWER_DH','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGE) AND IGF(IGE,FFF) AND GTECH_TYPE(IGE,TECH_TYPE) and IGNOTETOH(IGE) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
 OMONEY*IOF0000001*
   SUM((S,T), IHOURSINST(S,T)*VGE_T.L(Y,IA,IGE,S,T)*EL_PRICE_YCRST(Y,C,IR,S,T,'Money_per_MWh'))
;

** HEAT SALES
ECO_G_YCRAG(Y,C,IR,IA,IGH,FFF,TECH_TYPE,TECH_CATEGORY,'INCOME','HEAT_SALE','POWER_DH','Mmoney')$(CCCRRR(C,IR) and RRRAAA(IR,IA) and IAGK_HASORPOT(Y,IA,IGH) AND IGF(IGH,FFF) AND GTECH_TYPE(IGH,TECH_TYPE) AND TECH_TYPE_CATEGORY(TECH_TYPE,TECH_CATEGORY))=
 OMONEY*IOF0000001*
   SUM((S,T), IHOURSINST(S,T)*VGH_T.L(Y,IA,IGH,S,T)*DH_PRICE_YCRAST(Y,C,IR,IA,S,T,'Money_per_MWh'))
;

* ------------- END OF INCOME --------------

* ------------- REVENUE --------------
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,TECH_CATEGORY,'REVENUE','TOTAL_REVENUE','POWER_DH','Mmoney')$IAGK_HASORPOT(Y,IA,G)=
SUM(SUBCATEGORY, ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,TECH_CATEGORY,'INCOME',SUBCATEGORY,'POWER_DH','Mmoney')-ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,TECH_CATEGORY,'COSTS',SUBCATEGORY,'POWER_DH','Mmoney')
)
;

ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,TECH_CATEGORY,'REVENUE','ENERGY_SPECIFIC_REVENUE','POWER_DH','Money_per_MWh')$(IAGK_HASORPOT(Y,IA,G) AND SUM(COMMODITY,PRO_YCRAGF(Y,C,IR,IA,G,FFF,COMMODITY,TECH_TYPE,TECH_CATEGORY,'TWh') > 0)) =
ECO_G_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,TECH_CATEGORY,'REVENUE','TOTAL_REVENUE','POWER_DH','Mmoney')/SUM(COMMODITY,PRO_YCRAGF(Y,C,IR,IA,G,FFF,COMMODITY,TECH_TYPE,TECH_CATEGORY,'TWh'))
;
* ------------- END OF REVENUE --------------

* --------- END OF ECONOMIC OUTPUT -----------

* ---------------------- ENVIRONMENT ----------------------

* ------------- CO2 EMISSIONS -------------

EMI_YCRAG(Y,C,IR,IA,G,FFF,TECH_TYPE,TECH_GROUP,'CO2',SECTOR,'kton')$(F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,TECH_GROUP,SECTOR,'TWh'))=
F_CONS_YCRA(Y,C,IR,IA,G,FFF,TECH_TYPE,TECH_GROUP,SECTOR,'TWh')*IM_CO2(G)*IOF3P6;

* ------------- END OF CO2 EMISSIONS -------------

* ---------------------- END OF ENVIRONMENT ----------------------




execute_unload "MainResults.gdx"      G_CAP_YCRAF,G_STO_YCRAF,PRO_YCRAGFST,PRO_YCRAGF,F_CONS_YCRA,EMI_YCRAG,
X_CAP_YCR,X_FLOW_YCRST,X_FLOW_YCR,
ECO_G_YCRAG,ECO_X_YCR,OBJ_YCR,
EL_PRICE_YCR,EL_PRICE_YCRST,EL_DEMAND_YCRST,EL_DEMAND_YCR,EL_NET_DEMAND_YCRST,
DH_PRICE_YCRA,DH_PRICE_YCRAST,H_DEMAND_YCRAST,H_DEMAND_YCRA,
CURT_YCRAGFST,CURT_YCRAGF

$ifi %INDHEAT%==yes  ,IDVH_PRICE_YCRA,IDVH_PRICE_YCRAST

;











** ----------------------- AMALIAS OUTPUT (FOR INSPIRATION) --------------------------

$ontext
* MODELING OF THE ELECTRICITY GENERATION

SET EDemand_type /classic, transport, "individual heating", "process heating", DH, H2/;


SET TECHTYPE  'Type of technologies for calculation of marginals' %semislash%
$if     EXIST '../data/TECHTYPE.inc' $INCLUDE      '../data/TECHTYPE.inc';
$if not EXIST '../data/TECHTYPE.inc' $INCLUDE '../../base/data/TECHTYPE.inc';
%semislash%;

SET GTECHTYPE(GGG,TECHTYPE) 'Type of technology aggrupated'%semislash%
$if     EXIST '../data/GTECHTYPE.inc' $INCLUDE      '../data/GTECHTYPE.inc';
$if not EXIST '../data/GTECHTYPE.inc' $INCLUDE '../../base/data/GTECHTYPE.inc';
%semislash%;

SET ELECTRADE /Import, Export, "Net Export"/;


SET RRROFFSHORE(RRR);
RRROFFSHORE(IR)$SUM(IA,RRRAAA(IR,IA)$AAAOFFSHORE(IA))=YES;
RRROFFSHORE("DK_E")=NO;
RRROFFSHORE("DK_W")=NO;
RRROFFSHORE("DK_KF")=NO;
RRROFFSHORE("DE_KF")=NO;

SET CAPACITIES /New, Existing, TOTAL/;






Parameter EPrice_RST(RRR,SSS,TTT)  'Electricity price per hour at a regional level - cost of producing one unit more of electricity for own consumption or for export ( 2015/MWh)';
EPrice_RST(IR,S,T)=QEEQ.M(IR,S,T)*MONEYCONVERT/IHOURSINST(S,T);

Parameter HPrice_AST(AAA,SSS,TTT)'Heat price per hour at an area level - cost of producing one unit more of heat( 2015/MWh)';
HPrice_AST(IA,S,T)=QHEQ.M(IA,S,T)*MONEYCONVERT/IHOURSINST(S,T);

$if     EXIST '../addons/_hooks/OutputCalculation.inc' $INCLUDE      '../addons/_hooks/OutputCalculation.inc';
$if not EXIST '../addons/_hooks/OutputCalculation.inc' $INCLUDE '../../base/addons/_hooks/OutputCalculation.inc';


* MODELING OF THE ELECTRICITY DEMAND

Parameter EDemand_RST(EDemand_type,RRR,SSS,TTT) 'Electricity Demand (MW)';

EDemand_RST("classic",IR,S,T)$(NOT (RETRANS(IR) OR RINDUS(IR)))=IDE_T_Y(IR,S,T);

EDemand_RST("transport",IR,S,T)=SUM(RETRANS$IXKINI_Y(IR,RETRANS),IDE_T_Y(RETRANS,S,T));

EDemand_RST("individual heating",IR,S,T)=SUM(IAGK_Y(IA,IGE)$((RRRAAA(IR,IA) AND (AINDHEAT(IA))) AND IGETOH(IGE)), VGE_T.L(IA,IGE,S,T))
$ifi %bb2%==yes                         +SUM(IAGKN(IA,IGE)$((RRRAAA(IR,IA) AND (AINDHEAT(IA))) AND IGETOH(IGE)), VGEN_T.L(IA,IGE,S,T));

EDemand_RST("process heating",IR,S,T)=SUM(RINDUS$IXKINI_Y(IR,RINDUS),IDE_T_Y(RINDUS,S,T));

EDemand_RST("DH",IR,S,T)=SUM(IAGK_Y(IA,IGE)$((RRRAAA(IR,IA) AND (NOT AINDHEAT(IA))) AND IGETOH(IGE)), VGE_T.L(IA,IGE,S,T))
$ifi %bb2%==yes         +SUM(IAGKN(IA,IGE)$((RRRAAA(IR,IA) AND (NOT AINDHEAT(IA))) AND IGETOH(IGE)), VGEN_T.L(IA,IGE,S,T));

$ifi %H2%==yes  EDemand_RST("H2",IR,S,T)=SUM(IAGK_Y(IA,IGETOH2)$(RRRAAA(IR,IA)), VGE_T.L(IA,IGETOH2,S,T))
$ifi %bb2%==yes                         +SUM(IAGKN(IA,IGETOH2)$(RRRAAA(IR,IA)), VGEN_T.L(IA,IGETOH2,S,T));


Parameter Edemand_CST(EDemand_type,CCC,SSS,TTT) 'Electricity Demand (MW)';
Edemand_CST(EDemand_type,C,S,T)=SUM(IR$CCCRRR(C,IR),EDemand_RST(EDemand_type,IR,S,T));


Parameter Edemand_R(EDemand_type,RRR) 'Electricity Demand (PJ/year)';
Edemand_R(EDemand_type,IR)=SUM((S,T),EDemand_RST(EDemand_type,IR,S,T)*IHOURSINST(S,T))*IOF0000001*IOF3P6;


Parameter Edemand_C(EDemand_type,CCC) 'Electricity Demand (PJ/year)';
Edemand_C(EDemand_type,C)=SUM((S,T),EDemand_CST(EDemand_type,C,S,T)*IHOURSINST(S,T))*IOF0000001*IOF3P6;

Parameter Hdemand_AST(AAA,SSS,TTT) 'District Heating Demand (MW)';
Hdemand_AST(IA,S,T)$(NOT AINDHEAT(IA))=IDH_T_Y(IA,S,T);


*Weighted average electricity price by consumption quantity ( 2015/MWh)

Parameter EPrice_R(RRR) 'Weighted average electricity price by consumption quantity per year at a regional level ( 2015/MWh)';
EPrice_R(IR)$(SUM((EDemand_type,S,T),EDemand_RST(EDemand_type,IR,S,T)*IHOURSINST(S,T)))=SUM((EDemand_type,S,T),EDemand_RST(EDemand_type,IR,S,T)*IHOURSINST(S,T)*EPrice_RST(IR,S,T))/SUM((EDemand_type,S,T),EDemand_RST(EDemand_type,IR,S,T)*IHOURSINST(S,T));

Parameter EPrice_CST(CCC,SSS,TTT) 'Weighted average electricity price by consumption quantity at a country level( 2015/MWh)';
EPrice_CST(C,S,T)=SUM((EDemand_type,IR)$CCCRRR(C,IR),EDemand_RST(EDemand_type,IR,S,T)*EPrice_RST(IR,S,T))/SUM((EDemand_type,IR)$CCCRRR(C,IR),EDemand_RST(EDemand_type,IR,S,T));

Parameter EPrice_C(CCC) 'Weighted average electricity price by consumption quantity per year at a country level ( 2015/MWh)';
EPrice_C(C)=SUM((EDemand_type,S,T),EDemand_CST(EDemand_type,C,S,T)*IHOURSINST(S,T)*EPrice_CST(C,S,T))/SUM((EDemand_type,S,T),EDemand_CST(EDemand_type,C,S,T)*IHOURSINST(S,T));

*Weighted average heat price by consumption quantity ( 2012/MWh)
Parameter HPrice_AS(AAA,SSS) 'Weighted average heat price by consumption quantity per week at an area level ( 2015/MWh)';
HPrice_AS(IA,S)$(SUM(T,Hdemand_AST(IA,S,T)))=SUM(T,HPrice_AST(IA,S,T)*Hdemand_AST(IA,S,T)*IHOURSINST(S,T))/SUM(T,Hdemand_AST(IA,S,T)*IHOURSINST(S,T));

Parameter HPrice_A(AAA) 'Weighted average heat price by consumption quantity per year at an area level ( 2015/MWh)';
HPrice_A(IA)$(SUM((S,T),Hdemand_AST(IA,S,T)))=SUM((S,T),HPrice_AST(IA,S,T)*Hdemand_AST(IA,S,T)*IHOURSINST(S,T))/SUM((S,T),Hdemand_AST(IA,S,T)*IHOURSINST(S,T));

Parameter HPrice_C(CCC) 'Weighted average heat price by consumption quantity per year at an country level ( 2015/MWh)';
HPrice_C(CCC)$(SUM((S,T,IA)$ICA(CCC,IA),Hdemand_AST(IA,S,T)))=SUM((S,T,IA)$ICA(CCC,IA),HPrice_AST(IA,S,T)*Hdemand_AST(IA,S,T)*IHOURSINST(S,T))/SUM((S,T,IA)$ICA(CCC,IA),Hdemand_AST(IA,S,T)*IHOURSINST(S,T));



Parameter EProduction_RST(RRR,TECHTYPE,SSS,TTT) 'Electricity production at a regional level (MW)';
EProduction_RST(IR,TECHTYPE,S,T)= SUM(IAGK_Y(IA,IGE)$((RRRAAA(IR,IA) AND (NOT AAAOFFSHORE(IA))) AND IGNOTETOH(IGE) AND GTECHTYPE(IGE,TECHTYPE)), VGE_T.L(IA,IGE,S,T))
$ifi %bb2%==yes                  +SUM(IAGKN(IA,IGE)$((RRRAAA(IR,IA) AND (NOT AAAOFFSHORE(IA))) AND IGNOTETOH(IGE) AND GTECHTYPE(IGE,TECHTYPE)), VGEN_T.L(IA,IGE,S,T))
                                 +SUM(RRROFFSHORE$IXKINI_Y(RRROFFSHORE,IR),(SUM(IAGK_Y(IA,IGE)$((RRRAAA(RRROFFSHORE,IA) AND (AAAOFFSHORE(IA))) AND IGNOTETOH(IGE) AND GTECHTYPE(IGE,TECHTYPE)), VGE_T.L(IA,IGE,S,T)))*(1-XLOSS(RRROFFSHORE,IR)))
$ifi %bb2%==yes                  +SUM(RRROFFSHORE$IXKINI_Y(RRROFFSHORE,IR),(SUM(IAGKN(IA,IGE)$((RRRAAA(RRROFFSHORE,IA) AND (AAAOFFSHORE(IA))) AND IGNOTETOH(IGE) AND GTECHTYPE(IGE,TECHTYPE)), VGEN_T.L(IA,IGE,S,T)))*(1-XLOSS(RRROFFSHORE,IR)))

$if     EXIST '../addons/_hooks/EProduction_RST.inc' $INCLUDE      '../addons/_hooks/EProduction_RST.inc';
$if not EXIST '../addons/_hooks/EProduction_RST.inc' $INCLUDE '../../base/addons/_hooks/EProduction_RST.inc';

;

Parameter ETrade_RST(RRR,ELECTRADE,SSS,TTT) 'Electricity trade at a regional level (MW)';

*electricity import, do not consider import from offshore regions
ETrade_RST(IR,"Import",S,T)$((NOT RETRANS(IR)) AND (NOT RINDUS(IR)))=SUM(IRE$((IXKINI_Y(IRE,IR) OR IXKN(IRE,IR) OR IXKN(IR,IRE)) AND (NOT RRROFFSHORE(IRE))), VX_T.L(IRE,IR,S,T)*(1-XLOSS(IRE,IR)));

*electricity export, do not consider export towards EV region or industrial region
ETrade_RST(IR,"Export",S,T)$(NOT RRROFFSHORE(IR))= SUM(IRI$((IXKINI_Y(IR,IRI) OR IXKN(IR,IRI) OR IXKN(IRI,IR)) AND ((NOT RETRANS(IRI)) AND (NOT RINDUS(IRI)))),VX_T.L(IR,IRI,S,T));

ETrade_RST(IR,"Net Export",S,T)=ETrade_RST(IR,"Export",S,T)-ETrade_RST(IR,"Import",S,T);

Parameter Eproduction_CST(CCC,TECHTYPE,SSS,TTT) 'Electricity production at a country level (MW)';
Eproduction_CST(C,TECHTYPE,S,T)=SUM(IR$CCCRRR(C,IR),EProduction_RST(IR,TECHTYPE,S,T));

Parameter ETrade_CST(CCC,ELECTRADE,SSS,TTT) 'Electricity trade at a country level (MW)';
ETrade_CST(C,"Import",S,T)=SUM((IR,IRE)$(CCCRRR(C,IR) AND (NOT CCCRRR(C,IRE))AND (IXKINI_Y(IRE,IR) OR IXKN(IR,IRE) OR IXKN(IRE,IR)) AND (IXKINI_Y(IRE,IR) NE INF)), VX_T.L(IRE,IR,S,T)*(1-XLOSS(IRE,IR)));
ETrade_CST(C,"Export",S,T)=SUM((IR,IRI)$(CCCRRR(C,IR) AND (NOT CCCRRR(C,IRI))AND (IXKINI_Y(IR,IRI) OR IXKN(IRI,IR) OR IXKN(IR,IRI)) AND (IXKINI_Y(IR,IRI) NE INF)), VX_T.L(IR,IRI,S,T));
ETrade_CST(C,"Net Export",S,T)=ETrade_CST(C,"Export",S,T)-ETrade_CST(C,"Import",S,T);


*Energy production and trade at a yearly level in (PJ/year) units:
Parameter Eproduction_R(RRR,TECHTYPE) 'Electricity production at a regional level (PJ/year)';
EProduction_R(IR,TECHTYPE)=(SUM((S,T),EProduction_RST(IR,TECHTYPE,S,T)*IHOURSINST(S,T)))*IOF0000001*IOF3P6;

Parameter Eproduction_C(CCC,TECHTYPE) 'Electricity production at a country level (PJ/year)';
EProduction_C(C,TECHTYPE)= (SUM((S,T),EProduction_CST(C,TECHTYPE,S,T)*IHOURSINST(S,T)))*IOF0000001*IOF3P6;

Parameter ETrade_R(RRR,ELECTRADE) 'Electricity trade at a regional level (PJ/year)';
ETrade_R(IR,ELECTRADE)=(SUM((S,T),ETrade_RST(IR,ELECTRADE,S,T)*IHOURSINST(S,T)))*IOF0000001*IOF3P6;

Parameter ETrade_C(CCC,ELECTRADE) 'Electricity trade at a regional level (PJ/year)';
ETrade_C(C,ELECTRADE)=(SUM((S,T),ETrade_CST(C,ELECTRADE,S,T)*IHOURSINST(S,T)))*IOF0000001*IOF3P6;

EProduction_RST(IR,"Net Trade",S,T)=-ETrade_RST(IR,"Net Export",S,T);
Eproduction_CST(C,"Net Trade",S,T)=-ETrade_CST(C,"Net Export",S,T);


*HEAT GENERATION

Parameter Hproduction_AST(AAA,TECHTYPE,SSS,TTT) 'Heat production at an area level (MW)';

Hproduction_AST(IA,TECHTYPE,S,T)$(NOT AINDHEAT(IA))=
                                  SUM(IGBPR$(IAGK_Y(IA,IGBPR) AND GTECHTYPE(IGBPR,TECHTYPE)),VGH_T.L(IA,IGBPR,S,T))
$ifi %bb2%==yes                 + SUM(IGBPR$(IAGKN(IA,IGBPR) AND GTECHTYPE(IGBPR,TECHTYPE)),VGHN_T.L(IA,IGBPR,S,T))
                                + SUM(IGEXT$(IAGK_Y(IA,IGEXT) AND GTECHTYPE(IGEXT,TECHTYPE)),VGH_T.L(IA,IGEXT,S,T))
$ifi %bb2%==yes                 + SUM(IGEXT$(IAGKN(IA,IGEXT) AND GTECHTYPE(IGEXT,TECHTYPE)),VGHN_T.L(IA,IGEXT,S,T))
                                + SUM(IGHH$(IAGK_Y(IA,IGHH) AND GTECHTYPE(IGHH,TECHTYPE)),VGH_T.L(IA,IGHH,S,T))
$ifi %bb2%==yes                 + SUM(IGHH$(IAGKN(IA,IGHH) AND GTECHTYPE(IGHH,TECHTYPE)),VGHN_T.L(IA,IGHH,S,T))
                                + SUM(IGETOH$(IAGK_Y(IA,IGETOH) AND GTECHTYPE(IGETOH,TECHTYPE)),VGH_T.L(IA,IGETOH,S,T))
$ifi %bb2%==yes                 + SUM(IGETOH$(IAGKN(IA,IGETOH) AND GTECHTYPE(IGETOH,TECHTYPE)),VGHN_T.L(IA,IGETOH,S,T))
$ifi %H2%==yes                  +SUM(IGETOH2$(IAGK_Y(IA,IGETOH2) AND GTECHTYPE(IGETOH2,TECHTYPE)), VGH_T.L(IA,IGETOH2,S,T))
$ifi %H2%==yes                  +SUM(IGETOH2$(IAGKN(IA,IGETOH2) AND GTECHTYPE(IGETOH2,TECHTYPE)), VGHN_T.L(IA,IGETOH2,S,T))

$if     EXIST '../addons/_hooks/Hproduction_AST.inc' $INCLUDE      '../addons/_hooks/Hproduction_AST.inc';
$if not EXIST '../addons/_hooks/Hproduction_AST.inc' $INCLUDE '../../base/addons/_hooks/Hproduction_AST.inc';
;

Hproduction_AST(IA,"Heat to Storage",S,T)=VHSTOLOADT.L(IA,S,T)$SUM(IGHSTO$(IAGK_Y(IA,IGHSTO) or IAGKN(IA,IGHSTO)),1);
Hproduction_AST(IA,"Heat cool down",S,T)=VCOOLDOWN.L(IA,S,T);

Parameter Hproduction_AS(AAA,TECHTYPE,SSS) 'Heat production at an area level per week (MWh)';
Hproduction_AS(IA,TECHTYPE,S)=SUM(T,Hproduction_AST(IA,TECHTYPE,S,T)*WEIGHT_T(T)*SUM(TTT,WEIGHT_T(TTT))/IWEIGHSUMT);

Parameter Hproduction_A(AAA,TECHTYPE) 'Heat production at an area level per year (PJ/year)';
Hproduction_A(IA,TECHTYPE)=(SUM((S,T),Hproduction_AST(IA,TECHTYPE,S,T)*IHOURSINST(S,T)))*IOF0000001*IOF3P6;

Parameter Hproduction_CST(CCC,TECHTYPE,SSS,TTT) 'Heat production at a country level (MW)';
Hproduction_CST(C,TECHTYPE,S,T)=SUM(IA$ICA(C,IA),Hproduction_AST(IA,TECHTYPE,S,T));

Parameter Hproduction_CS(CCC,TECHTYPE,SSS) 'Heat production at a country level per week (MWh)';
Hproduction_CS(C,TECHTYPE,S)=SUM(IA$ICA(C,IA),Hproduction_AS(IA,TECHTYPE,S));

Parameter Hproduction_C(CCC,TECHTYPE) 'Heat production at a country level per year (PJ/year)';
Hproduction_C(C,TECHTYPE)=SUM(IA$ICA(C,IA),Hproduction_A(IA,TECHTYPE));

Parameter FuelConsumption_C(CCC,TECHTYPE)  'Fuel Consumption at a country level per type of technology (PJ/year)';
FuelConsumption_C(C,TECHTYPE)=SUM((IA,G,S,T)$(ICA(C,IA) AND GTECHTYPE(G,TECHTYPE)),IHOURSINST(S,T)*(VGF_T.L(IA,G,S,T)+VGFN_T.L(IA,G,S,T)))*IOF0000001*IOF3P6
$if     EXIST '../addons/_hooks/FuelConsumption_C.inc' $INCLUDE      '../addons/_hooks/FuelConsumption_C.inc';
$if not EXIST '../addons/_hooks/FuelConsumption_C.inc' $INCLUDE '../../base/addons/_hooks/FuelConsumption_C.inc';

;

Parameter FuelConsumption_TOTAL(TECHTYPE)  'Fuel Consumption in all the simulated countryes per type of technology (PJ/year)';
FuelConsumption_TOTAL(TECHTYPE)=SUM(C,FuelConsumption_C(C,TECHTYPE));

*_______________________________________________________________________________________________________________________________________________________________________________________________________________________________________

*New Installed Capacity per country for electricity and District Heating production

Parameter Elec_Capacity(CCC,CAPACITIES,TECHTYPE) 'Installed capacity for electricity production (MW)';

Elec_Capacity(C,'New',TECHTYPE)=SUM((IA,IGE)$(ICA(C,IA) AND IGNOTETOH(IGE) AND GTECHTYPE(IGE,TECHTYPE)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGE))
),VGKN.L(IA,IGE));

Elec_Capacity(C,'Existing',TECHTYPE)=SUM((IA,IGE)$(ICA(C,IA) AND IGNOTETOH(IGE) AND GTECHTYPE(IGE,TECHTYPE) AND IAGK_Y(IA,IGE)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGE))
),IGKFX_Y(IA,IGE));

Elec_Capacity(C,'TOTAL',TECHTYPE)=Elec_Capacity(C,'New',TECHTYPE)+Elec_Capacity(C,'Existing',TECHTYPE);



Parameter Heat_Capacity(CCC,CAPACITIES,TECHTYPE) 'Installed capacity for heat production (MW)';

Heat_Capacity(C,'New',TECHTYPE)=SUM(IA$(ICA(C,IA) AND (NOT AINDHEAT(IA))),
SUM(IGBPR$(GTECHTYPE(IGBPR,TECHTYPE)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGBPR))
),VGKN.L(IA,IGBPR)/GDATA(IGBPR,'GDCB'))
+SUM(IGEXT$(GTECHTYPE(IGEXT,TECHTYPE)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGEXT))
),VGKN.L(IA,IGEXT)/GDATA(IGEXT,'GDCB'))
+SUM(IGHHNOSTO$(GTECHTYPE(IGHHNOSTO,TECHTYPE)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGHHNOSTO))
),VGKN.L(IA,IGHHNOSTO ))
+SUM(IGETOH$(GTECHTYPE(IGETOH,TECHTYPE)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGETOH))
),VGKN.L(IA,IGETOH)));

Heat_Capacity(C,'Existing',TECHTYPE)=SUM(IA$(ICA(C,IA) AND (NOT AINDHEAT(IA))),
SUM(IGBPR$(GTECHTYPE(IGBPR,TECHTYPE) AND IAGK_Y(IA,IGBPR)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGBPR))
),IGKFX_Y(IA,IGBPR)/GDATA(IGBPR,'GDCB'))
+SUM(IGEXT$(GTECHTYPE(IGEXT,TECHTYPE) AND IAGK_Y(IA,IGEXT)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGEXT))
),IGKFX_Y(IA,IGEXT)/GDATA(IGEXT,'GDCB'))
+SUM(IGHHNOSTO$(GTECHTYPE(IGHHNOSTO,TECHTYPE) AND IAGK_Y(IA,IGHHNOSTO)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGHHNOSTO))
),IGKFX_Y(IA,IGHHNOSTO ))
+SUM(IGETOH$(GTECHTYPE(IGETOH,TECHTYPE) AND IAGK_Y(IA,IGETOH)
$ifi %COMBTECH%==yes  AND (NOT IGCOMB2(IGETOH))
),IGKFX_Y(IA,IGETOH)));

Heat_Capacity(C,'TOTAL',TECHTYPE)=Heat_Capacity(C,'New',TECHTYPE)+Heat_Capacity(C,'Existing',TECHTYPE);




*_______________________________________________________________________________________________________________________________________________________________________________________________________________________________________

execute_unload "MainResults.gdx" EPrice_RST, HPrice_AST, EDemand_RST, EDemand_R, Edemand_CST, Edemand_C, Hdemand_AST, EPrice_R, EPrice_CST, EPrice_C, HPrice_AS, HPrice_A, HPrice_C, EProduction_RST, ETrade_RST,
Eproduction_CST, ETrade_CST, EProduction_R, EProduction_C, ETrade_R, ETrade_C, Hproduction_AST, Hproduction_AS, Hproduction_A, Hproduction_CST, Hproduction_CS, Hproduction_C, FuelConsumption_C, FuelConsumption_TOTAL,
Elec_Capacity,Heat_Capacity,VOBJ,VGKN

$if     EXIST '../addons/_hooks/Outputgdx.inc' $INCLUDE      '../addons/_hooks/Outputgdx.inc';
$if not EXIST '../addons/_hooks/Outputgdx.inc' $INCLUDE '../../base/addons/_hooks/Outputgdx.inc';


;


*End of the output file
$offtext
