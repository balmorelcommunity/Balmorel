EQUATIONS
QGKNACCUMNET_KPOTSTEP(Y,AAA,G)   'relationship between VGKNACCUMNET, GKFX and VGKNACCUMNET_KPOTSTEP'
QGKNACCUMNET_KPOTSTEP_PREV(Y,AAA,G,KPOTSTEP) 'Investments in previous years are kept in same step'
QKPOTSTEP_MAXC(Y,CCC,TECH_GROUP,KPOTSTEP)           'upper limit to installable capacity in each KPOTSTEP per country'
QKPOTSTEP_MAXR(Y,RRR,TECH_GROUP,KPOTSTEP)           'upper limit to installable capacity in each KPOTSTEP per region'
QKPOTSTEP_MAXA(Y,AAA,TECH_GROUP,KPOTSTEP)           'upper limit to installable capacity in each KPOTSTEP per area'
QKPOTSTEP_WINDFLH(Y,AAA,G,S,T)                   '  '
QKPOTSTEP_SOLEFLH(Y,AAA,G,S,T)                   '  ';
;



*** Define new variable for accumulated capacity over the kpotstep set
QGKNACCUMNET_KPOTSTEP(IY411,IA,G)$(IKPOTSTEP(IA,G) AND IAGK_HASORPOT(IY411,IA,G))..
         SUM(KPOTSTEP, VGKNACCUMNET_KPOTSTEP(IY411,IA,G,KPOTSTEP))
         =E=
         VGKNACCUMNET(IY411,IA,G)
;


QGKNACCUMNET_KPOTSTEP_PREV(IY411,IA,IGKFIND,KPOTSTEP)$(IKPOTSTEP(IA,IGKFIND) AND SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGKFIND)))..
* VGKNACCUMNET at end of this year (available at beginning of next year):
   VGKNACCUMNET_KPOTSTEP(IY411,IA,IGKFIND,KPOTSTEP)
     =E=
* VGKVACCUMNET available at end of previous (i.e., start of this) year PLUS investment during this year (missing: MINUS decommissioning due to lifetime expirence):
   SUM(IY402$IY411PREVY(IY411,IY402), VGKNACCUMNET_KPOTSTEP(IY402,IA,IGKFIND,KPOTSTEP)$SUM(Y$(YVALUE(Y) LT YVALUE(IY411)),IAGKNY(Y,IA,IGKFIND)))$(NOT IYFIRST(IY411))
 + VGKN_KPOTSTEP(IY411,IA,IGKFIND,KPOTSTEP)$IAGKNY(IY411,IA,IGKFIND)

*Decomissioning of endogenous capacities
- SUM(Y$(YVALUE(Y)< YVALUE(IY411)),VGKN_KPOTSTEP(Y,IA,IGKFIND,KPOTSTEP)*DECOM_G(Y,IY411,IGKFIND))
 ;



** Maximum installable potential per country
QKPOTSTEP_MAXC(IY411,C,TECH_GROUP,KPOTSTEP)$TECHGROUPKPOT_STEP(C,TECH_GROUP,KPOTSTEP)..
         SUM(IAGK_HASORPOT(IY411,IA,G)$((GDATA(G,'GDTECHGROUP') EQ TECH_GROUP_VALUE(TECH_GROUP)) AND ICA(C,IA) AND IKPOTSTEP(IA,G)),
                 IGKFX_KPOTSTEP(IY411,IA,G,KPOTSTEP)    )
         + SUM(IAGK_HASORPOT(IY411,IA,G)$(SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,G)) AND (GDATA(G,'GDTECHGROUP') EQ TECH_GROUP_VALUE(TECH_GROUP)) AND ICA(C,IA) AND IKPOTSTEP(IA,G)),
                 VGKNACCUMNET_KPOTSTEP(IY411,IA,G,KPOTSTEP)     )
         =L=
         TECHGROUPKPOT(C,TECH_GROUP) * TECHGROUPKPOT_STEP(C,TECH_GROUP,KPOTSTEP);

** Maximum installable potential per region
QKPOTSTEP_MAXR(IY411,IR,TECH_GROUP,KPOTSTEP)$TECHGROUPKPOT_STEP(IR,TECH_GROUP,KPOTSTEP)..
         SUM(IAGK_HASORPOT(IY411,IA,G)$((GDATA(G,'GDTECHGROUP') EQ TECH_GROUP_VALUE(TECH_GROUP)) AND RRRAAA(IR,IA) AND IKPOTSTEP(IA,G)),
                 IGKFX_KPOTSTEP(IY411,IA,G,KPOTSTEP)    )
         + SUM(IAGK_HASORPOT(IY411,IA,G)$(SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,G)) AND (GDATA(G,'GDTECHGROUP') EQ TECH_GROUP_VALUE(TECH_GROUP)) AND RRRAAA(IR,IA) AND IKPOTSTEP(IA,G)),
                 VGKNACCUMNET_KPOTSTEP(IY411,IA,G,KPOTSTEP)     )
         =L=
         TECHGROUPKPOT(IR,TECH_GROUP) * TECHGROUPKPOT_STEP(IR,TECH_GROUP,KPOTSTEP);

** Maximum installable potential per area
QKPOTSTEP_MAXA(IY411,IA,TECH_GROUP,KPOTSTEP)$TECHGROUPKPOT_STEP(IA,TECH_GROUP,KPOTSTEP)..
         SUM(IAGK_HASORPOT(IY411,IA,G)$(GDATA(G,'GDTECHGROUP') EQ TECH_GROUP_VALUE(TECH_GROUP) AND IKPOTSTEP(IA,G)),
                 IGKFX_KPOTSTEP(IY411,IA,G,KPOTSTEP)    )
         + SUM(IAGK_HASORPOT(IY411,IA,G)$(SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,G)) AND (GDATA(G,'GDTECHGROUP') EQ TECH_GROUP_VALUE(TECH_GROUP)) AND IKPOTSTEP(IA,G)),
                 VGKNACCUMNET_KPOTSTEP(IY411,IA,G,KPOTSTEP)     )
         =L=
         TECHGROUPKPOT(IA,TECH_GROUP) * TECHGROUPKPOT_STEP(IA,TECH_GROUP,KPOTSTEP);



** Maximum generation of wind power
QKPOTSTEP_WINDFLH(IY411,IA,IGWND,IS3,T)$(IKPOTSTEP(IA,IGWND) AND IAGK_HASORPOT(IY411,IA,IGWND) AND SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGWND)) AND IWND_SUMST(IA))..
                               VGE_T(IY411,IA,IGWND,IS3,T)

$ifi     %VRESHUTDOWN%==yes  =L=
$ifi not %VRESHUTDOWN%==yes  =E=

$ifi %WNDFLH_DOL%==AAA         (WNDFLH(IA) *
$ifi %WNDFLH_DOL%==AAA_GGG     (WNDFLH(IA,IGWND) *
                               SUM(KPOTSTEP,
                                         (VGKNACCUMNET_KPOTSTEP(IY411,IA,IGWND,KPOTSTEP) + IGKFX_KPOTSTEP(IY411,IA,IGWND,KPOTSTEP))
                                         *IKPOTSTEP_FLHA(IA,IGWND,KPOTSTEP)     )
                               * WND_VAR_T(IA,IS3,T) * IGKRATE(IA,IGWND,IS3,T)) / IWND_SUMST(IA)
;

** Maximum generation of solar power
QKPOTSTEP_SOLEFLH(IY411,IA,IGSOLE,IS3,T)$(IKPOTSTEP(IA,IGSOLE) AND IAGK_HASORPOT(IY411,IA,IGSOLE) AND SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGSOLE)) AND ISOLESUMST(IA))..
                              VGE_T(IY411,IA,IGSOLE,IS3,T)

$ifi     %VRESHUTDOWN%==yes  =L=
$ifi not %VRESHUTDOWN%==yes  =E=

$ifi %SOLEFLH_DOL%==AAA       (SOLEFLH(IA) *
$ifi %SOLEFLH_DOL%==AAA_GGG   (SOLEFLH(IA,IGSOLE) *
                              SUM(KPOTSTEP,
                                         (VGKNACCUMNET_KPOTSTEP(IY411,IA,IGSOLE,KPOTSTEP) + IGKFX_KPOTSTEP(IY411,IA,IGSOLE,KPOTSTEP))
                                         *IKPOTSTEP_FLHA(IA,IGSOLE,KPOTSTEP)     )
                              * SOLE_VAR_T(IA,IS3,T) * IGKRATE(IA,IGSOLE,IS3,T)) / ISOLESUMST(IA)
;