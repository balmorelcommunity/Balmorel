* Decommissioning introduced by Juan Gea Bermúdez 2018.

$ifi not %LIFETIME_DECOM_ENDO_G%==yes $ifi not %ECONOMIC_DECOM_ENDO_G%==yes  $goto DECOMMISSIONING_ENDOGENOUS_GENERATION

EQUATION  QDECOM_ENDO_ACCUM(Y,AAA,G)     'Equation that calculates the accumulated endogenous decommissioning of endogenous technologies';

QDECOM_ENDO_ACCUM(IY411,IA,IGKFIND)$(SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGKFIND)) AND IGDECOM(IGKFIND))..
        VDECOM_ENDO_ACCUM(IY411,IA,IGKFIND)
=E=
        VDECOM_ENDO(IY411,IA,IGKFIND)
$ifi  %REVERT_DECOM_ENDO_G%==yes          -VDECOM_ENDO_BACK(IY411,IA,IGKFIND)
        +SUM(IY402$IY411PREVY(IY411,IY402), VDECOM_ENDO_ACCUM(IY402,IA,IGKFIND)$SUM(Y$(YVALUE(Y) LT YVALUE(IY411)),IAGKNY(Y,IA,IGKFIND)))$(NOT IYFIRST(IY411))
;


$ifi not %LIFETIME_DECOM_ENDO_G%==yes $goto LIFETIME_DECOMMISSIONING_ENDOGENOUS_GENERATION
EQUATION  QDECOM_ENDO_ACCUM_REST(Y,AAA,G)   'Equation establishing the restriction on the minimum decommissionining of endogenous technologies';

QDECOM_ENDO_ACCUM_REST(IY411,IA,IGKFIND)$(SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGKFIND)) AND IGDECOM(IGKFIND))..
$ifi not %ECONOMIC_DECOM_ENDO_G%==yes       VDECOM_ENDO(IY411,IA,IGKFIND)
$ifi     %ECONOMIC_DECOM_ENDO_G%==yes       VDECOM_ENDO_ACCUM(IY411,IA,IGKFIND)
$ifi not %ECONOMIC_DECOM_ENDO_G%==yes       =E=
$ifi     %ECONOMIC_DECOM_ENDO_G%==yes       =G=
$ifi not %ECONOMIC_DECOM_ENDO_G%==yes       SUM(Y$(YVALUE(Y)< YVALUE(IY411)),VGKN(Y,IA,IGKFIND)*IGDECOMY(Y,IY411,IGKFIND))
$ifi     %ECONOMIC_DECOM_ENDO_G%==yes       SUM(Y$(YVALUE(Y)< YVALUE(IY411)),SUM(IY402$(YVALUE(IY402) LE YVALUE(IY411)),VGKN(Y,IA,IGKFIND)*IGDECOMY(Y,IY402,IGKFIND)))
;

$label  LIFETIME_DECOMMISSIONING_ENDOGENOUS_GENERATION

$label  DECOMMISSIONING_ENDOGENOUS_GENERATION


$ifi not %LIFETIME_DECOM_EXO_G%==yes $ifi not %ECONOMIC_DECOM_EXO_G%==yes  $goto NO_DECOMMISSIONING_EXOGENOUS_GENERATION

EQUATION  QDECOM_EXO_ACCUM(Y,AAA,G)         'Equation that calculates the accumulated endogenous decommissioning of exogenous technologies';
EQUATION  QDECOM_EXO_LIM(Y,AAA,G)           'Equation limiting the yearly decommission of exogenous technologies';



QDECOM_EXO_ACCUM(IY411,IA,G)$IGDECOMEXOPOT(IY411,IA,G)..
        VDECOM_EXO_ACCUM(IY411,IA,G)
=E=
        VDECOM_EXO(IY411,IA,G)
$ifi  %REVERT_DECOM_EXO_G%==yes          -VDECOM_EXO_BACK(IY411,IA,G)
        +SUM(IY402$IY411PREVY(IY411,IY402), VDECOM_EXO_ACCUM(IY402,IA,G))$(NOT IYFIRST(IY411))
;

QDECOM_EXO_LIM(IY411,IA,G)$IGDECOMEXOPOT(IY411,IA,G)..
        VDECOM_EXO_ACCUM(IY411,IA,G)
=L=
        GKFX(IY411,IA,G)
;

$ifi not %LIFETIME_DECOM_EXO_G%==yes $ifi %ECONOMIC_DECOM_EXO_G%==yes  $goto NO_DECOMMISSIONING_EXOGENOUS_GENERATION
EQUATION  QDECOM_EXO_ACCUM_REST(Y,AAA,G)    'Equation establishing the restriction on the minimum decommissionining of exogenous technologies';

QDECOM_EXO_ACCUM_REST(IY411,IA,G)$IGDECOMEXOPOT(IY411,IA,G)..
$ifi %LIFETIME_DECOM_EXO_G%==yes $ifi not %ECONOMIC_DECOM_EXO_G%==yes      VDECOM_EXO(IY411,IA,G)
$ifi %LIFETIME_DECOM_EXO_G%==yes $ifi  %ECONOMIC_DECOM_EXO_G%==yes         VDECOM_EXO_ACCUM(IY411,IA,G)
$ifi %LIFETIME_DECOM_EXO_G%==yes $ifi not %ECONOMIC_DECOM_EXO_G%==yes      =E=
$ifi %LIFETIME_DECOM_EXO_G%==yes $ifi  %ECONOMIC_DECOM_EXO_G%==yes         =G=
$ifi %LIFETIME_DECOM_EXO_G%==yes $ifi not %ECONOMIC_DECOM_EXO_G%==yes      IDECOM_EXO(IY411,IA,G)
$ifi %LIFETIME_DECOM_EXO_G%==yes $ifi  %ECONOMIC_DECOM_EXO_G%==yes         SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IDECOM_EXO(Y,IA,G))
;

$label  NO_DECOMMISSIONING_EXOGENOUS_GENERATION


