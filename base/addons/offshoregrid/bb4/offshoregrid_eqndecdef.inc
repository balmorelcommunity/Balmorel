*Offshore grid add-on. Implemented by Juan Gea Bermudez 2019

*Equations for modelling economy of scale
EQUATION   QVXKN_ES(YYY,IRRRE,IRRRI)  'Equation to ensure the proper capacity when using SOS2 variables';
EQUATION   QXLAMBDA_ES(YYY,IRRRE,IRRRI) 'Equation to ensure all the Lambda variables are equal to 1 for the same transmission line';
EQUATION   QVGKN_ES(YYY,AAA,G)  'Equation to ensure the proper capacity when using SOS2 variables';
EQUATION   QGLAMBDA_ES(YYY,AAA,G) 'Equation to ensure all the Lambda variables are equal to 1 for the same technology';


QVXKN_ES(IY411,IRE,IRI)$(IXKN(IY411,IRI,IRE) AND (IXKN_ES(IRE,IRI)))..
   VXKN(IY411,IRE,IRI) =E= SUM(XES,VXLAMBDA(IY411,IRE,IRI,XES)*XKN_ES(IRE,IRI,XES));

QXLAMBDA_ES(IY411,IRE,IRI)$(IXKN(IY411,IRI,IRE) AND (IXKN_ES(IRE,IRI)))..
  SUM(XES,VXLAMBDA(IY411,IRE,IRI,XES)) =E= 1;

QVGKN_ES(IY411,IA,G)$(IGKN_ES(G) AND IAGKNY(IY411,IA,G))..
   VGKN(IY411,IA,G) =E= SUM(XES,VGLAMBDA(IY411,IA,G,XES)*GKN_ES(G,XES));


QGLAMBDA_ES(IY411,IA,G)$(IGKN_ES(G) AND IAGKNY(IY411,IA,G))..
    SUM(XES,VGLAMBDA(IY411,IA,G,XES)) =E= 1;


EQUATION QHUBSIZE(YYY,RRR,S,T) 'The size of the hub is delimited by the export and electricity consumption in the hub';

QHUBSIZE(IY411,IR,IS3,T)$SUM((IA,IGHUB)$RRRAAA(IR,IA),IAGK_HASORPOT(IY411,IA,IGHUB))..
SUM((IA,IGHUB)$(RRRAAA(IR,IA) AND IAGK_HASORPOT(IY411,IA,IGHUB)),
     (GKFX(IY411,IA,IGHUB)
$ifi %DECOM%==yes   -VDECOM_EXO_ACCUM(IY411,IA,IGHUB)$IGDECOMEXOPOT(IY411,IA,IGHUB)
      + VGKNACCUMNET(IY411,IA,IGHUB)$SUM(Y$(YVALUE(Y) LE YVALUE(IY411)),IAGKNY(Y,IA,IGHUB))
)*(1$(NOT IGKRATE(IA,IGHUB,IS3,T)) + IGKRATE(IA,IGHUB,IS3,T)))
 =G=
  SUM(IRI$IXK_HASORPOT(IY411,IR,IRI),VX_T(IY411,IR,IRI,IS3,T))

$ifi %HYDROGEN%==yes  +SUM(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GETOH2)$(RRRAAA(IR,IA)), VGE_T(IY411,IA,IHYDROGEN_GETOH2,IS3,T)/(1-DISLOSS_E_AG(IA,IHYDROGEN_GETOH2)))
$ifi %HYDROGEN%==yes  +SUM(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GETOHH2)$(RRRAAA(IR,IA)), VGE_T(IY411,IA,IHYDROGEN_GETOHH2,IS3,T)/(1-DISLOSS_E_AG(IA,IHYDROGEN_GETOHH2)))
$ifi %HYDROGEN%==yes  +SUM(IAGK_HASORPOT(IY411,IA,IHYDROGEN_GEHTOH2)$(RRRAAA(IR,IA)), VGE_T(IY411,IA,IHYDROGEN_GEHTOH2,IS3,T)/(1-DISLOSS_E_AG(IA,IHYDROGEN_GEHTOH2)))
;



