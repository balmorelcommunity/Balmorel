* File bb123.sim to the Balmorel model.
* This file is part of the Balmorel model, version 3.02, /302.20110525/.
* (Internal version id BC07)  

* This file contains simulation structure for models BALBASE1, BALBASE2 and BALBASE3.
* BALBASE1: optimisation simultaneuosly for all (S,T) in a year, exogenous investments.
* BALBASE2: optimisation simultaneuosly for all (S,T) in a year, exogenous and endogenous investments.
* BALBASE3: optimisation of one season at a time, forwards through S, exogenous investments.

* This file is $included at the end of Balmorel.gms (no other *.sim files included).
* The set S holds the seasons to be simulated in Balbase1 and Balbase2,
* and set SSS holds the seasons to be simulated in Balbase3.

* In Balbase1 and Balbase2 the linkage between seasons within a year
* is handled endogenously since a complete year is simulated,
* while in Balbase3 linkage has to be established exogeneously.
* Two main linkage versions for this are:
* (1) Linkage provided by quantities of hydro power with reservoir;
* the quantities are specified in TABLE HYFXRW(RRR,SSS);
* the values in HYFXRW may be provided by a simulation using Balbase1.
* (2) Linkage provided by water values.


*------------------------------------------------------------------------------
* INITIALISATIONS FOR THE ANNUAL UPDATING PART:
*------------------------------------------------------------------------------

* Initialize the first year in the simulation:

* IGKVACCTOY are the capacities (MW), for accumulated endogeneously
* found generation capacities with decommisioning subtracted.
* IXKVACCTOY are the capacities for accumulated endogenously found transmission
* capacities.

* Quickfixes
IXKVACCTOY(IRE,IRI)       =0;
XKACC(Y,IRE,IRI)          =0;

$ifi %BB2%==yes XKACC(Y,IRE,IRI)          =0;
$ifi %BB2%==yes GKVACC(Y,IA,G)            =0;
$ifi %BB2%==yes GKVACCDECOM(Y,IA,G)       =0;
$ifi %BB2%==yes GVKGN(YYY,IA,G)           =0;
$ifi %BB2%==yes IXKVACCTOY(IRE,IRI)       =0;
$ifi %BB2%==yes IGKVACCEOY(IA,G)          =0;

IGKVACCTOY(IA,G) = 0;

VGKN.L(IA,G)     = 0;
VXKN.l(IR,IRI)   = 000;
VDECOM.l(IA,G)   = 0;

* Investments in new generation technology are excluded
* by the following statement:

$ifi %bb1%==yes IAGKN(IA,G)=NO;
$ifi %bb3%==yes IAGKN(IA,G)=NO;

IGKFXYMAX(IA,G)=SMAX(Y,GKFX(Y,IA,G));


*------------------------------------------------------------------------------
* INITIALISATIONS FOR addons:
*------------------------------------------------------------------------------

$ifi %GAS%==yes $INCLUDE '../../base/addons/gas/gasinit.inc';
$ifi %TSP%==yes $INCLUDE '../../base/addons/Transport/TSPInit.inc';
$ifi %HEATTRANS%==yes $INCLUDE '../../base/addons/heattrans/model/htinit.inc';

*----- End of initialisation for the addons  ---------------------------------
*----- End of initialisation for annual updating part -------------------------


*-------------------------------------------------------------------------------
* INITIALISATIONS FOR THE SCENARIO SETTINGS:
*-------------------------------------------------------------------------------



*----- End of initialisation for the scenario setting  -------------------------


*-------------------------------------------------------------------------------
* Here begins the scenario LOOP.
*-------------------------------------------------------------------------------


LOOP(SCENARIO$SCENARSIM(SCENARIO),


*-------------------------------------------------------------------------------
* Overwrite with scenario specific data:
*-------------------------------------------------------------------------------

IF((ORD(SCENARIO) GT 1),  /* Except base scenario */
* Note the use of SSS rather than S to ensure consistency also for Balbase3.


);
*-------------------------------------------------------------------------------
* End: Overwrite with scenario specific data that is independent on the year
*-------------------------------------------------------------------------------


*------------------------------------------------------------------------------
* Loop through all the years:
LOOP(Y,

*-------------------------------------------------------------------------------
* START SIMULATION FOR CURRENT YEAR
*-------------------------------------------------------------------------------

* -----------------------------------------------------------------------------
* Update parameters and bounds for current simulation year:
* -----------------------------------------------------------------------------

$ifi %REShareE%==yes  DE_Y(IR)=DE(Y,IR);
$ifi %REShareEH%==yes DE_Y(IR)=DE(Y,IR);
$ifi %REShareEH%==yes DH_Y(IA)=DH(Y,IA);

*---- Generation technologies: ------------------------------------------------


* Update generation and transmission capacities:
* Externally specified capacities:
  IGKFX_Y(IA,G)= GKFX(Y,IA,G);
  IXKINI_Y(IR,IRI) = XKINI(Y,IR,IRI);
* Externally specified capacities the year before (used in QGMAXINVEST2):
  IGKFX_Y_1(IA,G)= GKFX(Y-1,IA,G) +GKFX(Y,IA,G)$(ord(Y)=1);


* Update generation capacities according to stored investments from a bb2 calculation.
$ifi %ADDINVEST%==yes IGKVACCTOY(IA,IGKFIND)=GKVACCDECOM(Y,IA,IGKFIND);

* Generation capacities for those technologies for which investments
* in new capacity may be internally found is equal to accumulated investments:
$ifi %BB2%==yes IGKVACCTOY(IA,IGKFIND) = GKVACC(Y-1,IA,IGKFIND) - SUM(IYALIAS$(YVALUE(IYALIAS) LE (YVALUE(Y)-GDATA(IGKFIND,'GDLIFETIME'))),GVKGN(IYALIAS,IA,IGKFIND));;

$ifi %ADDINVEST%==yes IXKVACCTOY(IRE,IRI)=XKACC(Y,IRE,IRI);

IAGK_Y(IA,G)=NO;
IAGK_Y(IA,G)=YES$(IGKVACCTOY(IA,G) + IGKFX_Y(IA,G));

$ifi %COMBTECH%==YES  LOOP((IA,IGCOMB1)$IAGK_Y(IA,IGCOMB1), IAGK_Y(IA,IGCOMB2)$GGCOMB(IGCOMB1,IGCOMB2)=YES);


$ifi not %bb2%==yes $goto bb2_label1

IGKNMAX_Y(IA,IGKFIND)=GKNMAX(Y,IA,IGKFIND);

$ifi %AGKNDISC%==yes  * Discrete investments:
$ifi %AGKNDISC%==yes  IAGKNDISCAG(IA,G) = YES$AGKNDISCAG(IA,G);
$ifi %AGKNDISC%==yes  * No new capacity if capacity is not specified to be found endogeneously:
$ifi %AGKNDISC%==yes  IAGKNDISCAG(IA,IGKKNOWN) = NO;

IAGKN(IA,G) = YES$AGKN(IA,G);

* No new capacity allowed and no associated generation
* if technology is not available this year:

IAGKN(IA,G)$(GDATA(G,'GDFROMYEAR') GT YVALUE(Y)) = NO;

* No new capacity if capacity is not specified to be found endogeneously:
IAGKN(IA,IGKKNOWN) = NO;
$ifi %COMBTECH%==yes  * No new capacity on secondary combination technologies:
$ifi %COMBTECH%==yes    IAGKN(IA,IGCOMB2) = NO;

$label  bb2_label1


* Generation on existing plants
* is restricted by the capacity (MW) adjusted by GKDERATE:
* (For storages overwritten later.)


VGE_T.UP(IA,IGKE,S,T)$IAGK_Y(IA,IGKE)=
 (IGKVACCTOY(IA,IGKE) + IGKFX_Y(IA,IGKE))*GKDERATE(IA,IGKE,S);
VGH_T.UP(IA,IGKH,S,T)$IAGK_Y(IA,IGKH)=
 (IGKVACCTOY(IA,IGKH) + IGKFX_Y(IA,IGKH))*GKDERATE(IA,IGKH,S);


$ifi %DECOMPEFF%==yes VDECOM.UP(IA,IGKE) = IGKVACCTOY(IA,IGKE) + IGKFX_Y(IA,IGKE);
$ifi %DECOMPEFF%==yes VDECOM.UP(IA,IGKH) = IGKVACCTOY(IA,IGKH) + IGKFX_Y(IA,IGKH);

$ifi %COMBTECH%==YES  $INCLUDE '../../base/addons/combtech/combSIM.inc';
$ifi %H2%==yes $include '../../base/addons/Hydrogen/H2Sim.inc';

* Storage loading (MW) and unloading (MW) do not exceed capacity:

* Modified to account for storage investment options. Simple bounds
* can only be set if no investment options exist.
* Altered to include accumulated capacity. (In case investments are transfered from other simulation)
$ifi not %BB2%==yes VHSTOLOADT.UP(IA,S,T)=
$ifi not %BB2%==yes   SUM(IGHSTO$(IGKFX_Y(IA,IGHSTO) GT 0),
$ifi not %BB2%==yes (IGKFX_Y(IA,IGHSTO)+IGKVACCTOY(IA,IGHSTO))/GDATA(IGHSTO,'GDSTOHLOAD'));

$ifi not %BB2%==yes VESTOLOADT.UP(IA,S,T)=
$ifi not %BB2%==yes   SUM(IGESTO$(IGKFX_Y(IA,IGESTO) GT 0),
$ifi not %BB2%==yes (IGKFX_Y(IA,IGESTO)+IGKVACCTOY(IA,IGESTO))/GDATA(IGESTO,'GDSTOHLOAD'));

* For efficiency reasons simple bounds are generated if no storage technology is available for investment in BB2:
$ifi %BB2%==yes Loop(IA$(0=Sum(IGHSTO$IAGKN(IA,IGHSTO),1)),
$ifi %BB2%==yes  VHSTOLOADT.UP(IA,S,T)=
$ifi %BB2%==yes          SUM(IGHSTO$(IGKFX_Y(IA,IGHSTO) GT 0),
$ifi %BB2%==yes  (IGKFX_Y(IA,IGHSTO)+IGKVACCTOY(IA,IGHSTO))/GDATA(IGHSTO,'GDSTOHLOAD'));
$ifi %BB2%==yes );
$ifi %BB2%==yes Loop(IA$(0=Sum(IGESTO$IAGKN(IA,IGESTO),1)),
$ifi %BB2%==yes  VESTOLOADT.UP(IA,S,T)=
$ifi %BB2%==yes          SUM(IGESTO$(IGKFX_Y(IA,IGESTO) GT 0),
$ifi %BB2%==yes  (IGKFX_Y(IA,IGESTO)+IGKVACCTOY(IA,IGESTO))/GDATA(IGESTO,'GDSTOHLOAD'));
$ifi %BB2%==yes );

* Altered to include accumulated capacity. Includes only generation on existing or accumulated capacity, as new capacity is covered by VGEN_T and VGHN_T
VGH_T.UP(IA,IGHSTO,S,T)$(IGKFX_Y(IA,IGHSTO) GT 0)=
 (IGKFX_Y(IA,IGHSTO)+IGKVACCTOY(IA,IGHSTO))/GDATA(IGHSTO,'GDSTOHUNLD');   /* GKDERATE taken into account later */

VGE_T.UP(IA,IGESTO,S,T)$(IGKFX_Y(IA,IGESTO) GT 0)=
 (IGKFX_Y(IA,IGESTO)+IGKVACCTOY(IA,IGESTO))/GDATA(IGESTO,'GDSTOHUNLD');   /* GKDERATE taken into account later */

* Storage contents does not exceed upper limit (MWh):
* Altered to include accumulated capacity. (In case investments are transfered from other simulation)
* Note: This code format may be simplified by using IAGKN for all model types, however for now this solution is more robust against coding errors.
$ifi not %BB2%==yes VHSTOVOLT.UP(IA,S,T)=SUM(IGHSTO$(IGKFX_Y(IA,IGHSTO) GT 0),
$ifi not %BB2%==yes    IGKFX_Y(IA,IGHSTO)+IGKVACCTOY(IA,IGHSTO));

* Altered to include accumulated capacity. (In case investments are transfered from other simulation)
$ifi not %BB2%==yes VESTOVOLT.UP(IA,S,T)=SUM(IGESTO$(IGKFX_Y(IA,IGESTO) GT 0),
$ifi not %BB2%==yes    IGKFX_Y(IA,IGESTO)+IGKVACCTOY(IA,IGESTO));

* For efficiency reasons simple bounds are generated if no storage technology is available for investment in BB2:
$ifi %BB2%==yes Loop(IA$(0=Sum(IGHSTO$IAGKN(IA,IGHSTO),1)),
$ifi %BB2%==yes VHSTOVOLT.UP(IA,S,T)=SUM(IGHSTO$(IGKFX_Y(IA,IGHSTO) GT 0),
$ifi %BB2%==yes                          IGKFX_Y(IA,IGHSTO)+IGKVACCTOY(IA,IGHSTO));
$ifi %BB2%==yes );
$ifi %BB2%==yes Loop(IA$(0=Sum(IGESTO$IAGKN(IA,IGESTO),1)),
$ifi %BB2%==yes VESTOVOLT.UP(IA,S,T)=SUM(IGESTO$(IGKFX_Y(IA,IGESTO) GT 0),
$ifi %BB2%==yes                          IGKFX_Y(IA,IGESTO)+IGKVACCTOY(IA,IGESTO));
$ifi %BB2%==yes );


* Existing  hydro-run-of-river, wind power, solar power and heat can not be dispatched:

VGE_T.FX(IA,IGHYRR,S,T)$IAGK_Y(IA,IGHYRR)=
    ((WTRRRFLH(IA) * (IGKVACCTOY(IA,IGHYRR) + IGKFX_Y(IA,IGHYRR))
           * WTRRRVAR_T(IA,S,T)) / IWTRRRSUM(IA))$IWTRRRSUM(IA);
VGE_T.FX(IA,IGHYRR,S,T)$(NOT IWTRRRSUM(IA)) = 0;

$ifi not %WNDSHUTDOWN%==yes VGE_T.FX(IA,IGWND,S,T)$IAGK_Y(IA,IGWND) =  ((WNDFLH(IA) * (IGKVACCTOY(IA,IGWND) + IGKFX_Y(IA,IGWND))   * WND_VAR_T(IA,S,T)) / IWND_SUMST(IA))$IWND_SUMST(IA);
$ifi     %WNDSHUTDOWN%==yes VGE_T.up(IA,IGWND,S,T)$IAGK_Y(IA,IGWND) =  ((WNDFLH(IA) * (IGKVACCTOY(IA,IGWND) + IGKFX_Y(IA,IGWND))   * WND_VAR_T(IA,S,T)) / IWND_SUMST(IA))$IWND_SUMST(IA);
VGE_T.FX(IA,IGWND,S,T)$(NOT IWND_SUMST(IA)) = 0;


VGE_T.FX(IA,IGSOLE,S,T)$IAGK_Y(IA,IGSOLE) =
    ((SOLEFLH(IA) * (IGKVACCTOY(IA,IGSOLE) + IGKFX_Y(IA,IGSOLE))
           * SOLE_VAR_T(IA,S,T)) / ISOLESUMST(IA))$ISOLESUMST(IA);
VGE_T.FX(IA,IGSOLE,S,T)$(NOT ISOLESUMST(IA)) = 0;

VGH_T.FX(IA,IGSOLH,S,T)$IAGK_Y(IA,IGSOLH) =
    ((SOLHFLH(IA) * (IGKVACCTOY(IA,IGSOLH) + IGKFX_Y(IA,IGSOLH))
           * SOLH_VAR_T(IA,S,T)) / ISOLHSUMST(IA))$ISOLHSUMST(IA);
VGH_T.FX(IA,IGSOLH,S,T)$(NOT ISOLHSUMST(IA)) = 0;

* New hydro-run-of-river, wind power, solar power and heat can not generate if the production variation profile is not defined, 
* the $-conditions complement the $-conditions on equations QGKNHYRR, QGKNWND, QGKNSOLE, QGKNSOLH:
VGEN_T.FX(IA,IGHYRR,S,T)$(NOT IWTRRRSUM(IA))  = 0;
VGEN_T.FX(IA,IGWND,S,T)$(NOT IWND_SUMST(IA))  = 0;
VGEN_T.FX(IA,IGSOLE,S,T)$(NOT ISOLESUMST(IA)) = 0;
VGHN_T.FX(IA,IGSOLH,S,T)$(NOT ISOLHSUMST(IA)) = 0;


* Calculate hydro inflow to reservoirs in MWh/MW installed capacity (new and old):

$ifi %bb1%==yes  IHYINF_S(IA,S)=
$ifi %bb1%==yes   ((WTRRSFLH(IA) * WTRRSVAR_S(IA,S) * IDAYSIN_S(S)) / IWTRRSSUM(IA))$IWTRRSSUM(IA);

$ifi %bb2%==yes  IHYINF_S(IA,S)=
$ifi %bb2%==yes   ((WTRRSFLH(IA) * WTRRSVAR_S(IA,S) * IDAYSIN_S(S)) / IWTRRSSUM(IA))$IWTRRSSUM(IA);


$ifi not %bb2%==yes $goto bb2_label2
* Fuel limitations: No new capacity nor new generation if zero fuel potential:

LOOP((FFF,G)$IGF(G,FFF),
     LOOP((IA,C)$ICA(C,IA),
     IF( (not (not FKPOT(C,FFF) or (FKPOT(C,FFF) GE 0))),
     IAGKN(IA,G) = NO; ););

     LOOP((IA,IR)$RRRAAA(IR,IA),
     IF( (not (not FKPOT(IR,FFF) or (FKPOT(IR,FFF) GE 0))),
     IAGKN(IA,G) = NO; ););

     LOOP(IA,
     IF( (not (not FKPOT(IA,FFF) or (FKPOT(IA,FFF) GE 0))),
     IAGKN(IA,G) = NO; ););
);

VGKN.UP(IA,IGKFIND)$IGKNMAX_Y(IA,IGKFIND)=
         IGKNMAX_Y(IA,IGKFIND)-IGKVACCTOY(IA,IGKFIND);

$label bb2_label2


*---- Transmission: -----------------------------------------------------------

* For balbase1 and balbase3 the transmission capacities are by default the same for all years.
* For balbase2 the transmission capacities may be increased endogeneously.

* Default: Overwrite any previous information to ensure no new transmission lines:
  IXKN(IRE,IRI)=NO;

$ifi not %bb2%==yes $goto bb2_label3
* Update transmission capacities (MW)
* (recall that new transmission is symmetric):

  IXKVACCTOY(IRE,IRI) = IXKVACCTOY(IRE,IRI) + VXKN.L(IRE,IRI) + VXKN.L(IRI,IRE);

* Observe: the following assumes that XINVCOST is nonsymmetric:
  IXKN(IRRRE,IRRRI)$(IRE(IRRRE) AND IRI(IRRRI))=
   YES$((XINVCOST(IRRRE,IRRRI) LT INF) AND (ORD(IRRRE) GT ORD(IRRRI)));
$label bb2_label3

* Updates transmission capacity according to investments from a BB2 execution
* ADDINVEST is automatically set to 'no' if BB2==yes (see balopt.opt).
$ifi %ADDINVEST%==yes IXKVACCTOY(IRE,IRI)=XKACC(Y,IRE,IRI);


* Set the fixed exchange with third regions:

IX3FX_T_Y(IR,S,T)= ((X3FX(Y,IR) * X3FX_VAR_T(IR,S,T)) / IX3FXSUMST(IR))$(IX3FXSUMST(IR) NE 0);

* Annual updates wrt electricity exchange with 3. countries.
$ifi %X3V%==yes $include '../../base/addons/x3v/model/x3vannual.inc';

*---- Demands: ----------------------------------------------------------------

* Set the demand for electricity and heat for the current simulation year:
* Note that IDE_SUMST is the total load expressed according to DE_VAR_T:

  IDE_T_Y(IR,S,T) =
    ((DE(Y,IR) * DE_VAR_T(IR,S,T)) / IDE_SUMST(IR))$(IDE_SUMST(IR) GT 0);

  IDH_T_Y(IA,S,T) =
    ((DH(Y,IA) * DH_VAR_T(IA,S,T)) / IDH_SUMST(IA))$(IDH_SUMST(IA) GT 0);


* Elastic demands, upper bounds on the quantities of the individual steps.


VDEF_T.UP(IR,S,T,DEF_D1)$(ORD(DEF_D1) LT CARD(DEF_D1))=
   IDE_T_Y(IR,S,T)*
 (DEF_STEPS(IR,S,T,'DF_QUANT',DEF_D1+1)-DEF_STEPS(IR,S,T,'DF_QUANT',DEF_D1));

VDEF_T.UP(IR,S,T,DEF_D1)$((ORD(DEF_D1) EQ CARD(DEF_D1)) AND (1 LT CARD(DEF_D1)))=
   IDE_T_Y(IR,S,T)*(1-DEF_STEPS(IR,S,T,'DF_QUANT',DEF_D1));

VDEF_T.UP(IR,S,T,DEF_U1)$((ORD(DEF_U1) EQ 1) AND (1 LT CARD(DEF_U1)))=
   IDE_T_Y(IR,S,T)*(DEF_STEPS(IR,S,T,'DF_QUANT',DEF_U1)-1);

VDEF_T.UP(IR,S,T,DEF_U1)$(1 LT ORD(DEF_U1))=
  IDE_T_Y(IR,S,T)*
(DEF_STEPS(IR,S,T,'DF_QUANT',DEF_U1)-DEF_STEPS(IR,S,T,'DF_QUANT',DEF_U1-1));


VDEF_T.UP(IR,S,T,DEF_D2)$(ord(def_d2) eq card(def_d2)) = DEF_STEPS(IR,S,T,'DF_QUANT',DEF_D2);
VDEF_T.UP(IR,S,T,DEF_D2)$(ord(def_d2) lt card(def_d2)) = DEF_STEPS(IR,S,T,'DF_QUANT',DEF_D2)- DEF_STEPS(IR,S,T,'DF_QUANT',DEF_D2+1);

VDEF_T.UP(IR,S,T,DEF_U2) = DEF_STEPS(IR,S,T,'DF_QUANT',DEF_U2);

VDEF_T.UP(IR,S,T,DEF_U3) = IDE_T_Y(IR,S,T)*DEF_STEPS(IR,S,T,'DF_QUANT',DEF_U3);
VDEF_T.UP(IR,S,T,DEF_D3) = IDE_T_Y(IR,S,T)*DEF_STEPS(IR,S,T,'DF_QUANT',DEF_D3);


VDHF_T.UP(IA,S,T,DHF_D1)$(ORD(DHF_D1) EQ 1)= INF;

VDHF_T.UP(IA,S,T,DHF_D1)$((1 LT ORD(DHF_D1)) AND (ORD(DHF_D1) LT CARD(DHF_D1)))=
   IDH_T_Y(IA,S,T)*
 (DHF_STEPS(IA,S,T,'DF_QUANT',DHF_D1+1)-DHF_STEPS(IA,S,T,'DF_QUANT',DHF_D1));

VDHF_T.UP(IA,S,T,DHF_D1)$((ORD(DHF_D1) EQ CARD(DHF_D1)) AND (1 LT CARD(DHF_D1)))=
   IDH_T_Y(IA,S,T)*(1-DHF_STEPS(IA,S,T,'DF_QUANT',DHF_D1));

VDHF_T.UP(IA,S,T,DHF_U1)$((ORD(DHF_U1) EQ 1) AND (1 LT CARD(DHF_D1)))=
   IDH_T_Y(IA,S,T)*(DHF_STEPS(IA,S,T,'DF_QUANT',DHF_U1)-1);

VDHF_T.UP(IA,S,T,DHF_U1)$((1 LT ORD(DHF_U1)) AND (ORD(DHF_U1) LT CARD(DHF_U1)))=
  IDH_T_Y(IA,S,T)*
(DHF_STEPS(IA,S,T,'DF_QUANT',DHF_U1)-DHF_STEPS(IA,S,T,'DF_QUANT',DHF_U1-1));

VDHF_T.UP(IA,S,T,DHF_U1)$(ORD(DHF_U1) EQ CARD(DHF_U1))= INF;


* ---- Fuel prices: -----------------------------------------------------------

* Set the fuelprice for the current simulation year:
  IFUELP_Y(IA,FFF)= FUELPRICE(Y,IA,FFF);


*---- Environmental policies: -------------------------------------------------

* Set the national environmental policies for the current simulation year:

  ITAX_CO2_Y(C)  = M_POL(Y,'TAX_CO2',C);
  ITAX_SO2_Y(C)  = M_POL(Y,'TAX_SO2',C);
  ITAX_NOX_Y(C)  = M_POL(Y,'TAX_NOX',C);

* Note: only balbase1 and balbase2 will handle annual constraints directly:

$ifi %bb1%==yes    ILIM_CO2_Y(C)  = M_POL(Y,'LIM_CO2',C);
$ifi %bb1%==yes    ILIM_SO2_Y(C)  = M_POL(Y,'LIM_SO2',C);
$ifi %bb1%==yes    ILIM_NOX_Y(C)  = M_POL(Y,'LIM_NOX',C);

$ifi %bb2%==yes    ILIM_CO2_Y(C)  = M_POL(Y,'LIM_CO2',C);
$ifi %bb2%==yes    ILIM_SO2_Y(C)  = M_POL(Y,'LIM_SO2',C);
$ifi %bb2%==yes    ILIM_NOX_Y(C)  = M_POL(Y,'LIM_NOX',C);

*---- Annual updating of add-on parameters: -------------------------------------------------

$ifi %GAS%==yes $INCLUDE '../../base/addons/gas/annualupdate.inc';
$ifi %RENEWTARGET%==yes $INCLUDE '../../base/addons/renewtarget/renewupdate.inc';
$ifi %WINDTARGET%==yes $INCLUDE '../../base/addons/windtarget/wndannualupdate.inc';
$ifi %CAES%==yes $INCLUDE '../../base/addons/CAES/CAESinit.inc';
$ifi %HEATTRANS%==yes $INCLUDE '../../base/addons/heattrans/model/htannualupdate.inc'
$ifi %FOSSILTARGET%==yes $INCLUDE '../../base/addons/fossiltarget/fossilupdate.inc';

* Always MAKEWATER p.t.
$ifi %bb3%==yes  IHYFXRW(IR,S)          = HYFXRW(Y,IR,S);
$ifi %bb3%==yes  IWATERVAL(IA,S)        = WATERVAL(Y,IA,S);
$ifi %H2%==yes $INCLUDE '../../base/addons/Hydrogen/H2AnnualUpdate.inc';



* 
*------------------------------------------------------------------------------
* The following equations can exclude various elements of the model.
*------------------------------------------------------------------------------

* Electricity transmission between regions within countries and between
* countries may be excluded by the following statement:

* VX_T.FX(IRE,IRI,S,T)=0;

* Electricity transmission between countries may be excluded
* by the following statement (Note: the statement above must be comment out):

* LOOP(C, VX_T.FX(IRE,IRI,S,T)$(NOT(CCCRRR(C,IRE) AND CCCRRR(C,IRI)))=0);

* Investments in new generation technology are excluded by the following statement:

$ifi %bb1%==yes IAGKN(IA,G)=NO;
$ifi %bb3%==yes IAGKN(IA,G)=NO;

*----- End of: The following equations can exclude various elements ....-------

*----- End of update parameters and bounds for current simulation year --------


* Before solving the model, certain checks may be made on the input data:
$INCLUDE '../../base/logerror/logerinc/error3.inc';

*-------------------------------------------------------------------------------
* The following lines can be commented in, if transmission conditions
* are irrelevant, e.g. for calculation of "system price":
*XLOSS(IRRRE,IRRRI)= 0;
*IXKINI_Y(IRRRE,IRRRI)=INF;
*-------------------------------------------------------------------------------



*------------------------------------------------------------------------------
$ifi %bb3%==yes * Loop through all the seasons:
$ifi %bb3%==yes LOOP(IS3LOOPSET,
$ifi %bb3%==yes * Start simulation for current season:
*------------------------------------------------------------------------------

$ifi %bb3%==yes * Set the current season IS3:
$ifi %bb3%==yes IS3(S)=NO;
$ifi %bb3%==yes IS3(S)$(ORD(S) EQ ORD(IS3LOOPSET))=YES;

*Start level of hydrogen storage in season S fixed to value found in yearly simulation
$ifi not %H2%==yes $goto noh2store
$ifi %bb3%==yes VH2STOVOL_T.FX(IA,IGH2STO,IS3,'T001')=IH2STOVOL_START(IA,IGH2STO,IS3);

* End level of hydrogen storage in season S fixed to start level in season S+1
*$ifi %bb3%==yes VH2STOVOL_T.FX(IA,IGH2STO,IS3,'T168')=IH2STOVOL_END(IA,IGH2STO,IS3);
*$ifi %bb3%==yes VH2STOVOL_T.LO(IA,IGH2STO,IS3,'T168')=IH2STOVOL_END(IA,IGH2STO,IS3)-1;
$label noh2store


$ifi %bb1%==yes IS3(S)   = YES;
$ifi %bb2%==yes IS3(S)   = YES;

$ifi %bb3%==yes * Set <MODELNAME>.HOLDFIXED = 1 to save some computation time,
$ifi %bb3%==yes * unless you need the associated marginal values:
*$ifi %bb1%==yes BALBASE1.HOLDFIXED = 1; !
*$ifi %bb2%==yes BALBASE2.HOLDFIXED = 1;
*$ifi %bb3%==yes BALBASE3.HOLDFIXED = 1; !
*-------------------------------------------------------------------------------

*-------------------------------------------------------------------------------
*-------------------------------------------------------------------------------
*IAGKN(IA,G)$(not IWND_SUMT(IA))=no;
*PEME:

* Disable investments in electricity transmission capacity
$ifi %NOELTRANSINV%==yes IXKN(IRI,IRE)=NO;


$ifi not %GAS%==yes $goto gas_quickfix;
$ifi %QUICKFIX%==yes  if(YVALUE(Y) LE 2010,
$ifi %QUICKFIX%==yes                       VGINFINV.fx(NGINV)$SUM(SOURCE,SUPEFFECT(NGINV,SOURCE))=0;
$ifi %QUICKFIX%==yes                       STOREINV(STORE, STORETYPE)= NO;
$ifi %QUICKFIX%==yes                       ELSE
$ifi %QUICKFIX%==yes                       VGINFINV.up(NGINV)$SUM(SOURCE,SUPEFFECT(NGINV,SOURCE))=1;
$ifi %QUICKFIX%==yes                       STOREINV(STORE, STORETYPE)= YES$MAXINVEST(STORE,STORETYPE);
$ifi %QUICKFIX%==yes  );
$label gas_quickfix;


* This option must be active especially for BB1 and BB2 since this outputs the
* solution basis, which is used for generating results database.
OPTION Savepoint=%SAVEPOINTVALUE%;
$ifi %bb1%==yes SOLVE BALBASE1 USING LP MINIMIZING VOBJ;


$ifi not %bb2%==yes $goto not_bb2;
$ifi %SOLVEMIP%==yes SOLVE BALBASE2 USING MIP MINIMIZING VOBJ;
$ifi not %SOLVEMIP%==yes SOLVE BALBASE2 USING LP MINIMIZING VOBJ;
$label not_bb2;

$ifi %bb3%==yes  SOLVE BALBASE3 USING LP MINIMIZING VOBJ;


* This file will print the solver status for each season in the simulation:
$ifi %bb1%==yes $INCLUDE '../../base/logerror/logerinc/balbase1.mss';
$ifi %bb2%==yes $INCLUDE '../../base/logerror/logerinc/balbase2.mss';
$ifi %bb3%==yes $INCLUDE '../../base/logerror/logerinc/balbase3.mss';

$ifi %NAP%==yes $INCLUDE '../../base/addons/NAP/NAPstore.inc';


$ifi %MERGESAVEPOINTRESULTS%==yes
$ifi %bb3%==yes putclose  fileMERGESAVEPOINTRESULTSbat 'move  ' '%relpathModel%''BALBASE3_p.gdx '  '%relpathModel%'IS3LOOPSET.tl:0:0 '.gdx';
$ifi %MERGESAVEPOINTRESULTS%==yes
$ifi %bb3%==yes  execute  "%batfileMERGESAVEPOINTRESULTS%";


*------------------------------------------------------------------------------
$ifi %bb3%==yes * End of loop over seasons:
$ifi %bb3%==yes ); /* LOOP(IS3LOOPSET,...) */



*-------------------------------------------------------------------------------
*-------------------------------------------------------------------------------

* bb1 and bb2:
* Save yearly basis (as saved by SAVEPOINT) and move to gdx directory:

$ifi %MERGESAVEPOINTRESULTS%==yes
$ifi %bb1%==yes  putclose  fileMERGESAVEPOINTRESULTSbat 'move  ' '%relpathModel%''BALBASE1_p.gdx '  '%relpathModel%..\output\temp\'Y.tl:0:0 '.gdx';
$ifi %MERGESAVEPOINTRESULTS%==yes
$ifi %bb1%==yes  execute   "%batfileMERGESAVEPOINTRESULTS%";


$ifi %MERGESAVEPOINTRESULTS%==yes
$ifi %bb2%==yes  putclose  fileMERGESAVEPOINTRESULTSbat 'move  ' '%relpathModel%''BALBASE2_p.gdx '  '%relpathModel%..\output\temp\'Y.tl:0:0 '.gdx';
$ifi %MERGESAVEPOINTRESULTS%==yes
$ifi %bb2%==yes  execute  "%batfileMERGESAVEPOINTRESULTS%";


$ifi %bb3%==yes execute_unload 'BALBASE3.gdx'
$ifi %bb3%==yes $INCLUDE '../../base/model/symlist.inc';
$ifi %bb3%==yes execute 'rm BALBASE3_p*';
$ifi %bb3%==yes putclose  fileMERGESAVEPOINTRESULTSbat 'move  ' '%relpathModel%''BALBASE3.gdx '  '%relpathModel%..\output\temp\'Y.tl:0:0 '.gdx';
$ifi %bb3%==yes execute "%batfileMERGESAVEPOINTRESULTS%";


*-------------------------------------------------------------------------------
*-------------------------------------------------------------------------------
$ifi %X3V%==yes $INCLUDE '../../base/addons/x3v/model/x3vgeninput.inc';



* Certain checks may be made on the output:  /*Skal den flyttes ned eller andres?*/
$INCLUDE '../../base/logerror/logerinc/error4.inc';

* Various results from the simulation of current year may be printed out:
* Calculate and store intermediate results from current year:
$ifi %PRINTFILES%==yes $INCLUDE '../../base/output/printout/printinc/prt3-bb1.inc';


* Print for current year,
* according to the specifications given in the following file:

$ifi %PRINTFILES%==yes
$ifi %bb1%==yes   $INCLUDE '../../base/output/printout/printinc/prt4-bb1.inc';

$ifi %PRINTFILES%==yes
$ifi %bb2%==yes   $INCLUDE '../../base/output/printout/printinc/prt4-bb2.inc';


* The following file includes instructions for processing output to be used in
* the Economic module.
$ifi %ECONOMIC%==yes $INCLUDE '../../base/processing/economic\eco_process.inc';

* Case specific output printing:
$ifi EXIST 'caseprint.inc' $include 'caseprint.inc';

*------------------------------------------------------------------------------
*                GENERATE INPUT FILES FOR BB3 FROM RESULTS OF BB1
*------------------------------------------------------------------------------
$ifi %GAS%==yes $INCLUDE '../../base/addons/gas/gasgeninput.inc';
$ifi %GAS%==yes $INCLUDE '../../base/addons/gas/gentariffs.inc';

$ifi %bb1%==yes HYFXRW(Y,IR,S) = -7*365/364/IDAYSIN_S(S)*SUM(IA$RRRAAA(IR,IA), QHYRSSEQ.l(IA,S));
$ifi %bb2%==yes HYFXRW(Y,IR,S) = -7*365/364/IDAYSIN_S(S)*SUM(IA$RRRAAA(IR,IA), QHYRSSEQ.l(IA,S));
$ifi %bb1%==yes WATERVAL(Y,IA,S)   = IOF1_*QHYRSSEQ.m(IA,S);
$ifi %bb2%==yes WATERVAL(Y,IA,S)   = IOF1_*QHYRSSEQ.m(IA,S);
$ifi %H2%==yes $INCLUDE '../../base/addons/Hydrogen/H2geninput.inc';


* Update generation capacities according to stored investments from a bb2 calculation.
$ifi %BB2%==yes GVKGN(Y,IA,IGKFIND)  = VGKN.L(IA,IGKFIND);
$ifi %BB2%==yes GKVACC(Y,IA,IGKFIND) = GKVACC(Y-1,IA,IGKFIND) + VGKN.L(IA,IGKFIND);
$ifi %BB2%==yes GKVACCDECOM(Y,IA,IGKFIND) = GKVACC(Y,IA,IGKFIND) - SUM(IYALIAS$(YVALUE(IYALIAS) LE (YVALUE(Y)-GDATA(IGKFIND,'GDLIFETIME'))),GVKGN(IYALIAS,IA,IGKFIND)) - VDECOM.l(IA,IGKFIND);

* Update generation capacities according to stored investments from a bb2 calculation.
$ifi %BB2%==yes IGKVACCEOY(IA,IGKFIND)= GKVACCDECOM(Y,IA,IGKFIND);

XKACC(Y,IRE,IRI) = XKACC(Y-1,IRE,IRI)+VXKN.L(IRE,IRI) + VXKN.L(IRI,IRE);



$ifi %TSP%==yes $INCLUDE '../../base/addons/Transport/TSPAfterSolve.inc';

$ifi %REShareE%==yes  $include  '../../base/addons/REShareE/printinc/RESEPrt4.inc';
$ifi %REShareEH%==yes $include  '../../base/addons/REShareEH/printinc/RESEHPrt4.inc';


*------------------------------------------------------------------------------
*                    END OF SIMULATION FOR CURRENT YEAR
*------------------------------------------------------------------------------
); /* LOOP(Y,...) */


*------------------------------------------------------------------------------
*                    END OF SCENARIO LOOP:
*-------------------------------------------------------------------------------
); /* LOOP(SCENARIO$SCENARSIM(SCENARIO),...) */


*-------------------------------------------------------------------------------

* The following permits comparison between scenarios, cf. the description. /Welfare
* The following files are included only when comparing, i.e. not in basecase.
* The code is tricky, it is based on existence of file welfare.inc only in relation to base folder:

$ifi %ECONOMIC%==yes $INCLUDE '../../base/processing/economic/eco_writetbl.inc';
* Earnings
$ifi %DECOMPEFF%==yes $include '../../base/printinc/earningsout.inc';
